#
# Reynir.grammar
#
# Grammar specification file for Reynir,
# a natural language processor for Icelandic
#
# Author: Vilhjalmur Thorsteinsson
# Copyright (C) 2016
# All rights reserved
# See the accompanying README.md file for further licensing and copyright information.
#
# This file contains an extensive (but not complete) context-free grammar for parsing
# Icelandic text.
#
# The grammar is presented in augmented Backus-Naur form, where
# repetition specifiers are allowed on right-hand-side tokens
# (* for 0..n, + for 1..n, ? for 0..1 occurrences).
#
# Rules and productions are autogenerated for all variants
# (combinations) of applicable grammatical features, i.e. genders (m, f, n),
# cases, numbers (singular/plural) and persons (1st, 2nd, 3rd).
#
# As an example, the generic nonterminal Undirsetning/tala/fall,
# having variants for /tala (singular/plural) and /fall (case),
# is automatically expanded to 8 specific nonterminals:
# Undirsetning_et_nf, Undirsetning_et_þf, etc.,
# with corresponding substitutions being made in the right-hand-side
# productions (et = singular, nf = nominative...).
# The permissible variants are defined below.
#
# Nonterminal identifiers start with uppercase letters.
# Terminals start with lowercase letters.
#
# Literals within single quotes 'xyz' are matched with the stem
# of a word token, plus eventual variants.
#
# Literals within double quotes "xyz" are matched with the
# text of a word token as it appeared in the source, albeit after
# conversion to lowercase. Variants cannot be applied to double-quoted
# literals.
#
# Terminal names generally take the form category_variant1_variant2...,
# where the most common categories are:
# * no for nouns (nafnorð)
# * so for verbs (sagnorð)
# * lo for adjectives (lýsingarorð)
# * fs for prepositions (forsetningar)
# * st for connecting words (samtengingar)
# * fn for pronouns (fornöfn)
# * ao for adverbs (atviksorð)
# * töl for number words (töluorð)
#
# The terminal names are matched against POS tags from the BIN
# database in the module binparser.py.
#
# Alternative productions are separated with the '|' (vertical bar) sign.
# If there is a priority ordering between the alternatives, so that
# a single highest priority alternative should be automatically
# selected from possible alternatives, use the '>' (greater than)
# sign between the productions instead.
#
# A syntax highlighting specification for .grammar files,
# grammar.YAML-tmLanguage, has been written for the Sublime Text
# editor. It can be found under the resources folder and its
# use is highly recommended.


# VARIANT DEFINITIONS

# Gender (m, f, n) variant
/kyn = kk kvk hk

# Case (nom, acc, dat, poss) variant
/fall = nf þf þgf ef
/aukafall = þf þgf ef # All cases except nominative

# Number (singular/plural) variant
/tala = et ft

# Person (1st, 2nd, 3rd) variant
/pers = p1 p2 p3


# GRAMMAR RULES

# Note: all comments from this point are in Icelandic

# Setningargerðir

# Merkið > gefur til kynna forgangsröðun.
# Ef báðar koma til greina er Málsgrein tekin fram yfir Grein o.s.frv.
S0 → Málsgrein > Grein > Fyrirsögn

Málsgrein →
    MgrInnihald
    | '(' MgrInnihald ')'
    | '[' MgrInnihald ']'

MgrInnihald → Forskeyti? Yfirsetning StLiður* Lokatákn?

Forskeyti →
    Samtenging '-'? # 'En - þegar allt er skoðað, var þetta fínt.'
    | raðnr # Raðnúmer málsgreinar
    | '-'   # Þankastrik á undan setningu (allar gerðir strika varpast í '-')

Lokatákn → '.' | '?' | ':' | '!' | '(' '!' ')'
LokatáknEðaKomma → Lokatákn | ','

Fyrirsögn →
    # Nafnliður í nefnifalli
    Nl_nf Atviksliður? Eignun? Lokatákn?
    | SetningÁnF Lokatákn?

Eignun →
    # Mynd: Jón Stefánsson
    # Erlendur: Friðgeir er frábær
    ':' Nl_nf

SetningÁnF →
    # Setning án frumlags: 'Stefndi í átt til Bláfjalla', 'Aftengdi sjálfstýringu'
    SetningÁnF_et_p3/kyn
    | SetningÁnF_ft_p3/kyn

Grein →
    # Greinarnúmer, e.t.v. skammstafað, og e.t.v. með fyrirsögn
    raðnr 'grein' Fyrirsögn? '.'? # 17. gr. Störf Alþingis

SpurnarorðAtv →
    "hve" | "hversu"

StLiður →
    # Samtengingar yfirsetninga
    Tengimerki? Samtenging HreinYfirsetning
    | Tengimerki Yfirsetning
    # 'Það eru bara tveir kostir í boði: Selja bankann eða láta hann fara í þrot'
    | ':' SagnRuna
    # 'Aftur er umræðan sú sama: um rétt kirkjunnar þegna til að neita að gefa saman samkynhneigða'
    | ':' NhFylling
    # '(Ég spurði) hversu hrikalega stór (hann væri orðinn)'
    | SpurnarorðAtv Lo_nf/tala/kyn Setning/tala/pers/kyn

Tengimerki →
    ':' | ';' | ',' | '-'

SamtengingStök →
    # Ein samtenging, sem þó getur verið samsett, sbr. skilgreiningar í Reynir.conf
    st

Samtenging →
    # Flóknari samtenging úr mörgum orðum
    SamtengingStök+
    | "þannig" "að"
    | "það" "er" | 'það_er'
    | "áður" "en" "að"?
    | "fyrr" "en" "að"?
    | 'á_meðan' "að"?
    | "þangað" "til" "að"?
    | "allt" "frá" "því" "að"?
    | "þar" "á" "meðal" | 'þar_á_meðal'
    | "þar" "sem"
    | SpurnarSamtenging

SpurnarSamtenging →

    HverHvor/kyn SpurnarFs/kyn?                 # '(Ég spurði) hvert af börnunum (þarna væri á ferðinni)'

    # af hverju, í hverju, í hvern, í hverja, til hvers, frá hverjum...
    | fs/fall 'hver'/fall/tala/kyn FsRuna?      # '(Ég vildi vita) af hverju í ósköpunum (Jón hætti við)'

    | "hvers" "vegna" FsRuna?                   # '(Ég spurði) hvers vegna í veröldinni (hann tók ekki símtalið)'
    | "hvar" FsRuna?                            # '(Ég spurði) hvar (matarstellið væri)'
    | "hvernig" FsRuna?                         # '(Ég spurði) hvernig (Páll ætlaði að fara að þessu)'
    | "hvort" "og" "hvernig" FsRuna?            # '(Tekjustofnar voru til umræðu og) hvort og hvernig (þeir skyldu nýttir)'

    | fs/fall 'hvaða'/fall Nl/fall FsRuna?      # '(Ég spurði) í hvaða yfirmann (þeir hefðu hringt)'
    | fs/fall SpurnarorðAtv Nl/fall FsRuna?     # '(Ég spurði) í hversu miklum mæli utan heimilda (þeir hefðu urðað sorpið)'

    | SpurnarorðAtv SpurnarBotn FsRuna?         # '(Ég spurði) hversu miklu bensíni af þessari tegund (hann ætlaði að skila)'

    # hvort og hvenær eru samtengingar í BÍN og því þarf ekki reglu fyrir þær einar og sér
    # heldur aðeins með forsetningarrunu (hvort )
    | "hvort" FsRuna
    | "hvenær" FsRuna

HverHvor/kyn →
    'hver'/kyn | 'hvor'/kyn

SpurnarFs/kyn →
     Nl_ef_ft/kyn FsRuna?                       # hvert barnanna, hver kvennanna, hvor þeirra
     | FsRuna                                   # hvern í bænum, hvorn af þeim

SpurnarBotn →
    LoLiður/tala/fall/kyn Nl/fall/tala/kyn      # (hversu) miklu bensíni (af þessari tegund)
    | Atviksliður                               # (hversu) mikið (af hveiti þyrfti í uppskriftina)

Yfirsetning →
    # Yfirsetning getur hafist á runu upphrópana
    Upphrópun* HreinYfirsetning

Upphrópun →
    uh+ ',' # Jú jú, ég fór út í dag / Nei, hann kemur ekki.

HreinYfirsetning →
    Setning
    | SetningAukafall
    | SetningLo
    | SetningSo
    | SetningSkilyrði

$score(-2) SetningAukafall
$score(-2) SetningLo
$score(-2) SetningSo
$score(+3) Setning

# Skilyrði: EfÞegar eitthvað þá eitthvað

SetningSkilyrði →
    AtviksliðurKomma? EfÞegar OgEfÞegar? Skilyrði "þá"? Afleiðing FsAtv?

$score(+5) SetningSkilyrði

EfÞegar →
    "ef" | "þegar" | "þótt" | "þó" | 'jafnvel_þótt' | "þar" "sem" | "eftir" "að" | 'eins_og' | 'líkt_og'
    | 'á_meðan' | "þangað" "til"

OgEfÞegar →
    "og" EfÞegar              # 'Ef og þegar eitthvað gerist læt ég þig vita'

Skilyrði →
    HreinYfirsetning StLiður* # (Ef) Jón fer með hundinn til dýralæknis og allt er í lagi
    | SagnliðurÁnF            # (Þegar) kemur að því að útdeila gæðum

# Afleiðing í "ef"-setningu byrjar alltaf á sögn

Afleiðing →
    SagnHluti/tala/pers/kyn   # (Ef hann gæti) opinberaði Hannes niðurstöðu sína (í dag)
    | SagnliðurÁnF            # [Ef góða veðrið helst] þyrfti að vökva grasið (fyrir myrkur)

# Setningar með frumlagi

Setning → Setning/tala/pers/kyn

# Tilvik þar sem einu frumlagi fylgja nokkrir sagnliðir:
# 'Anna tók bílinn, keyrði hann sjálf í bæinn [,] og keypti í matinn'

SagnRuna → SagnRuna/tala/pers/kyn

SagnRuna/tala/pers/kyn →
    Sagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskot?
        SagnFramhald/tala/pers/kyn?

SagnFramhald/tala/pers/kyn →
    SagnKomma/tala/pers/kyn* KommaOgEðaEn Sagnliður/tala/pers/kyn
        LoTengtSögn_nf/tala/kyn? SagnInnskot?

SagnKomma/tala/pers/kyn →
    ',' Sagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskot?

# Hvetja til framhalds sömu sagnrunu, fremur en að skipta niður í fleiri setningar
$score(+2) SagnKomma/tala/pers/kyn

SagnInnskot →
    FsAtv

SagnViðhengi/tala/pers/kyn →
    KommaOgEðaEn SagnRuna/tala/pers/kyn

# Setningarhluti sem byrjar á sögn

SagnHluti/tala/pers/kyn →
    SagnliðurMeðF/tala/pers/kyn SagnInnskot? SagnViðhengi/tala/pers/kyn?

FrumlagsInnskot/tala/pers/kyn →
    ',' SetningÁnF/tala/pers/kyn ','
    | ',' Samtenging HreinYfirsetning ','  # Lögheimili á Íslandi, þegar kosning fer fram, er skilyrði
    | InnskotsSamtenging HreinYfirsetning # Lögheimili á Íslandi þegar kosning fer fram er skilyrði
    | FrumlagsSkýring # Tálmanir, til dæmis ritskoðun, má aldrei í lög leiða

# Þröngt mengi samtenginga sem nota má án kommu í frumlagsinnskoti
# (sé mengið of vítt eykst margræðni verulega)
InnskotsSamtenging →
    "þegar"
    | "meðan" | 'á_meðan'

FrumlagsSkýring →
    ',' Skýring Skýringarliður ','

$score(+6) FrumlagsSkýring

Skýringarliður →
    Nl | FsRuna

Skýring →
    "svo" "sem" | 'svo_sem'
    | "sem" "sagt" | 'sem_sagt'
    | "til" "dæmis" | 'til_dæmis'
    | "þar" "á" "meðal" | 'þar_á_meðal'
    | "meðal" "annars" | 'meðal_annars'
    | "það" "er" | 'það_er'
    | "með" "öðrum" "orðum" | 'með_öðrum_orðum'

Frumlag/pers/tala/kyn →
    Nl_nf/tala/pers/kyn

# Hakk til að láta 1.p. og 2.p. virka með fornafninu "allur",
# sem annars er aðeins brúklegt í 3.p.

Frumlag_p1/tala/kyn →
    'allur'_nf/tala/kyn # Öll erum við jöfn fyrir lögum

Frumlag_p2/tala/kyn →
    'allur'_nf/tala/kyn # Allar eruð þið jafnar fyrir lögum

Setning/tala/pers/kyn →
    # Venjuleg setningarskipan með frumlagi (í nafnhætti) fremst:
    # 'Jón sterki fór bersýnilega í bága við reglurnar með þessu'
    # 'Katrín formaður félagsins hefur augljóslega verið alveg hnífskörp í gær'
    # 'Rúmlega helmingur tvítugra karlmanna vill flugvöll í Vatnsmýri'
    # 'Ég bara spyr'
    # 'Allir mega koma til landsins sem það vilja'
    Frumlag/pers/tala/kyn FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
        SagnRuna/tala/pers/kyn KommaTengiliður/tala/pers/kyn?

    # Frumlag fyrir aftan sögn:
    # 'Gengu þau galvösk á Esjuna þrátt fyrir veðurútlitið'
    # 'Bersýnilega fór Jón sterki í bága við reglurnar með þessu'
    # 'Undir húsinu voru illa farnar skolpleiðslur'
    # 'Í skólanum borðaði Gunna aldrei matinn eftir þetta'
    # 'Í grein sinni opinberaði Hannes niðurstöðu sína'
    # Ath. að frumlag 3p. et. getur verið falið:
    # (? Það virtist mér í dag kettinum líða betur)
    # 'Í dag virtist mér kettinum líða betur'
    | FsAtv? SagnHluti/tala/pers/kyn

    # 'Það fara 40 mínútur í pappírsvinnuna'
    | "það" SagnHluti/tala/pers/kyn

Setning_et_p3_hk →
    # 'Að raða frímerkjum er góð skemmtun'
    NhLiður FsAtv? SagnRuna_et_p3_hk
    # 'Það hefði kostað Pál vinnuna að kvarta yfir verkstjóranum'
    | "það" SagnRuna_et_p3_hk NhFylling FsAtv?

# Setning sem byrjar á lýsingarorði í stað "það"
# 'Óheimilt er (að fangelsa mann fyrir engar sakir)'
# 'Óhætt má telja (að flokkurinn vinni kosningarnar)'
# 'Öruggt hefur verið talið [að fara Vatnaleiðina í þurru veðri]'
# 'Ljóst er (að Brandur var fallegasti kötturinn)'
SetningLo →
    LoTengtSögn_nf_et_hk SetningLoSögn NhFyllingEðaAtv
    # Kjörgengur við Alþingiskosningar skal vera hver sá ríkisborgari (sem dvalið hefur 5 ár í landinu)
    | LoTengtSögn_nf/tala/kyn FsRuna? HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs?

SetningLoSögn →
    HjSögnLhÞtSM_et_p3 so_0_lhþt_et_hk? # er/skal/mun/hefur verið talið/þótt...
    | HjSögnMegiSkuli_et_p3? so_0_mm_p3_et # sýnist/virðist/telst, má teljast
    | HjSögnMegiSkuli_et_p3 so_0_nh # má telja, skal ætla
    | 'þykja'_et_p3 # '(öruggt) þykir (að Jón vinni mótið)'

# Ekkert frumlag í nefnifalli, sögn sem sagnbót (oft í miðmynd):
# 'Í dag er búist við vondu veðri eftir hádegi'
# (í stað 'Það er búist við vondu veðri eftir hádegi í dag')
# 'Á morgun verður lent á neyðarbrautinni'
# 'Fylgst verður með kosningunum á RÚV næstu daga'
# (í stað 'Það verður fylgst með kosningunum á RÚV næstu daga')
# 'Í dag má sækja tölvuna'
# (í stað 'Það má sækja tölvuna í dag')

SetningSo →
    so_0_sagnb HjSögnLhÞtSM_et_p3 NhFyllingEðaAtv             # Leitað verður/skal/mun til sérfræðinga / Verið er í snatri að ráða hæfasta umsækjandann

    | so_0_mm_nh HjSögnMegiSkuliÞurfi_et_p3 FsAtv             # Búast má/skal/þarf/hefur mátt við töfum á veginum

    | FsAtv? SagnliðurÁnF NhFyllingEðaAtv                      # [Með lögum] skal tryggja rétt almennings til að fara um landið

    | FsAtv VeraSkulu_et_p3 HreinSögn_sagnb_et_hk NhFyllingEðaAtv # Með lögum er/verður/skal/mun skipað rétti útlendinga til að dveljast hér

    | VeraSkulu_et_p3 FsRuna LoTengtSögn_nf/tala/kyn Nl_nf/tala/kyn AtvFs? # Skal í frumvarpinu fólgin greinargerð um tekjur ríkisins og gjöld

    | So_1_nh/fall HjSögnMegiSkuliÞurfi_et_p3 FsRuna? Nl/fall AtvFs?  # Endurnýja þarf/skal/má leigusamninginn í ár
    | So_1_nh/fall HjSögnNhMeðSo/tala/pers Nl_nf/tala/pers/kyn Nl/fall AtvFs?  # Gæta eiga/skulu/verða þeir þingskapa

    | EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn* HjSögnLhÞt_p3/tala Nl_nf/tala/kyn AtvFs? # Teknar og skráðar hafa verið ákvarðanir í málinu

    | Sögn_bh FsAtv?                                          # Farðu galvaskur að sækja kaffi strax

    | HjSögnMegiSkuli_et_p3 FsAtv? EinSögn_nh_et_hk FsAtv?    # Má með sanni segja (að þeir hafi sloppið með skrekkinn)

    | 'þurfa'_et_p3 FsRuna? Nl_þf NhFyllingEðaAtv             # Þarf til þess lagafyrirmæli að svipta mann réttindum

    | SagnliðurVh FsAtv?                                      # Haldi þessi þróun áfram er voðinn vís

VeraSkulu_et_p3 →
    'vera'_et_p3 | 'verða'_et_p3 | 'skulu'_et_p3 | 'munu'_et_p3

# Setning þar sem aðalnafnliður er í aukafalli
# en frumlag í 3p. et. ('það') er falið.
# Sögnin tekur viðfang (subject) í aukafalli eða er í miðmynd
# 'Mér blöskrar athæfið' (? Það blöskrar mér athæfið),
# 'Mér sýnist að öllu sé óhætt' (? Það sýnist mér að öllu sé óhætt),
# 'Helmingi ungmenna finnst skólinn skemmtilegur' (? Það finnst helmingi ungmenna skólinn skemmtilegur)

SetningAukafall →

    # Manninn langaði mjög í ferðina / að fara til útlanda strax
    # Köttum í landinu fjölgar
    Nl/aukafall so_subj_op/aukafall MiðmyndarBotn?
    # Mér blöskraði mjög vitleysan (í Páli), Mig þraut örendið
    | Nl/aukafall Atviksliður? so_subj_op/tala/aukafall FsRuna? Nl_nf_þf/tala AtvFs?

    # Öfug orðaröð: 'Eignarrétti fylgja skyldur' (í stað 'Skyldur fylgja eignarrétti')
    | Nl/aukafall so_1/aukafall/tala FsRuna? Nl_nf/tala AtvFs?

    # Þessar skuldir hefur 365 ekki greitt
    # Þessa bók hef ég aldrei skilið
    | Nl/aukafall HjSögn/tala/pers Nl_nf/tala/pers/kyn AtvFs? So_1_sagnb/aukafall FsAtv?

    # Köttum hefur [ekki] fjölgað á árinu
    | Nl/aukafall HjSögn_et_p3 FsAtv? So_1_sagnb/aukafall FsAtv?

    # Hælisleitendum fer/hefur farið fækkandi
    | Nl_þgf HjSögnLhNt so_1_þgf_lh_nt FsAtv?

    # Miðmynd sagna: Mér áskotnaðist, mér fyrirgefst, mér gremst...
    | Nl_þgf so_0_mm MiðmyndarBotn?
    # Íslandsbanka hefur/hafði/hefði tekist að halda í horfinu
    | Nl_þgf HjSögnMM so_0_mm_sagnb MiðmyndarBotn?

    # Öllum er frjálst/fyrir bestu að ákveða snarlega að flytja úr landi
    | Nl_þgf HjSögnLhÞt_et_p3 FsRuna? LhÞtÞgfEinkunn NhFyllingEðaAtv

    # Öllum hefur verið/skal/mun tryggð heilbrigðisþjónusta / réttur til að lifa með reisn
    | Nl_þgf HjSögnLhÞtSM_p3/tala FsAtv? so_2_þgf_þf_lhþt/tala/kyn FsRuna? AukafallLhÞtAndlag/tala/pers/kyn NhFyllingEðaAtv
    # ESB eru þröngar skorður settar í málinu
    | Nl_þgf HjSögnLhÞtSM_p3/tala FsAtv? AukafallLhÞtAndlag/tala/pers/kyn FsRuna? so_2_þgf_þf_lhþt/tala/kyn NhFyllingEðaAtv

    # Stóru skipunum hefur verið/skal/mun stýrt í land
    # Hannesi var í fyrra falið að stýra rannsóknaverkefni
    | Nl_þgf HjSögnLhÞtSM_et_p3 FsAtv? so_1_þgf_sagnb NhFyllingEðaAtv

    # Þess skal gætt að fara varlega með sprengiefnið
    | Nl_ef HjSögnLhÞtSM_et_p3 FsAtv? so_1_ef_sagnb NhFyllingEðaAtv

    # Þátttakendum er veitt of mikið svigrúm
    | Nl_þgf HjSögnLhÞtSM_et_p3 FsAtv? so_2_þgf_þf_sagnb FsRuna? Nl_þf AtvFs?

    # Öllum hefur verið/skal/mun óskað góðs bata
    | Nl_þgf HjSögnLhÞtSM_et_p3 FsAtv? so_2_þgf_ef_sagnb FsRuna? Nl_ef AtvFs?

    # Öllum hefur verið/skal/mun skilað bílunum
    | Nl_þgf HjSögnLhÞtSM_et_p3 FsAtv? so_2_þgf_þgf_sagnb FsRuna? Nl_þgf AtvFs?

    # Ákvörðunar var/er/verður/hefur verið/mun að vænta strax í dag
    | Nl_ef HjSögnLhÞtSM_et_p3 nhm so_1_ef_nh FsAtv?

    # Köttum í landinu mun [ekki/enn] fjölga á árinu
    | Nl/aukafall HjSögnNh_et_p3 FsAtv? So_1_nh/aukafall FsAtv?

    # Þá ályktun má strax draga í dag (að Páll sé ekki með öllum mjalla)
    # Engan má skylda til að láta eign sína af hendi
    | Nl/aukafall HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall NhFyllingEðaAtv

    # Nauðungarvinnu skal engum gert að leysa af hendi
    | Nl/aukafall HjSögnMegiSkuli_et_p3 FsAtv? AukafallÖfugSögn nhm So_1_nh/aukafall FsAtv?

    # Engan þarf/ætti að undra (að mig langi í ís)
    | Nl/aukafall HjSögnÞurfaEiga_et_p3 FsAtv? nhm So_1_nh/aukafall NhFyllingEðaAtv

    # Engan má svipta íslenskum ríkisborgararétti að ósekju
    | Nl_þf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þf/fall Nl/fall AtvFs?

    # Öllum má tryggja heilbrigðisþjónustu í dag
    | Nl_þgf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þgf/fall Nl/fall AtvFs?

    # Mér er/var [ekki] sama [um málið] / bumbult [í gær] / misboðið
    # | Nl_þgf 'vera'_gm_fh_3p_et FsAtv? lo_sb_hk_nf_et FsAtv?

AukafallÖfugSögn →
    Nl/fall AtvFs? So_1_nh/fall         # (skal) engum gera
    | Nl/fall AtvFs? So_1_sagnb/fall    # (skal) engum gert

# Tilvik þar sem sögn í lhþt eintölu fylgir andlag í fleirtölu:
# 'Öllum skal tryggð mannhelgi og vernd gegn hvers kyns ofbeldi'

AukafallLhÞtAndlag/tala/pers/kyn →
    Nl_nf/tala/pers/kyn

AukafallLhÞtAndlag_et/pers/kyn →
    Nl_nf_ft/pers/kyn

AukafallsBotn →
    FsAtv                    # (Mér virtist ykkur ekki hungra) mjög undir ræðunni
    | FsRuna? Nl_nf AtvFs?   # (Mér virtist ykkur ekki falla) í geð ræðan í kvöld
    | NhFyllingAtv           # (Mér sýndist ykkur langa) gríðarlega að fara til útlanda strax

MiðmyndarBotn →

    # (Mér sýndist) kötturinn [ekki] [vera að] lepja mjólkina og borða matinn í gær
    Nl_nf AtvFs? VeraInnskot? NhRuna FsAtv?

    # (Mér finnst) kettirnir [ekki] [vera] fallegir
    | MiðmyndarBotnLo

    # (Mér virtist) ykkur [ekki] blöskra undir ræðunni
    | Nl/aukafall AtvFs? so_subj_nh/aukafall AukafallsBotn?

    # (Mér virtist) þeim [ekki] sýnast börnin vera falleg
    | Nl_þgf AtvFs? so_nh_mm MiðmyndarBotn

    # (Mér leyfist) vonandi að fara út
    | NhFyllingAtv

    # (Páli fyrirgefst) örugglega á morgun
    | FsAtv

MiðmyndarBotnLo →
    Nl_nf/tala/pers/kyn AtvFs? HjSögnVera? LoTengtSögn_nf/tala/kyn FsAtv?

$score(+4) MiðmyndarBotnLo # Gefa plús fyrir heildstætt samhengi lýsingarorðs

VeraInnskot →
    HjSögnVera nhm    # vera að / geta hafa verið að / verðir að / hafa orðið að...

Nl_nf_þf/tala → Nl_nf/tala | Nl_þf/tala

# Setningar án frumlags (notaðar t.d. í tengisetningum eftir 'sem').
# Í setningum án frumlags getur sagnliðurinn verið "öfugur",
# þ.e. "konurnar sem borðað höfðu matinn" í stað "höfðu borðað".
# Þá má atviksliður ekki koma inn á milli sagnanna, þ.e.
# "höfðu ekki borðað" er gilt, en "borðað ekki höfðu" ógilt.

SetningÁnF/tala/pers/kyn →
    EinSetningÁnF/tala/pers/kyn FramhaldÁnF/tala/pers/kyn*

EinSetningÁnF/tala/pers/kyn →
    Atviksliður? ÖfugurSagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? FsAtv? ÖfugtFramhald/tala/pers/kyn*
    # Lýsingarorð á undan sögn:
    # (Ráðherrar taka þær ákvarðanir sem) nauðsynlegar eru/þykja/teljast til rækslu starfa þeirra
    | LoTengtSögn_nf/tala/kyn Sögn_0/tala/pers/kyn NhFyllingEðaAtv

ÖfugtFramhald/tala/pers/kyn →
    KommaOgEðaEn ÖfugurSagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? FsAtv?

FramhaldÁnF/tala/pers/kyn →
    ','? Samtenging HreinYfirsetning                # (sem borðað höfðu matinn) þegar hann var framreiddur fyrr um kvöldið
    | ','? Samtenging EinSetningÁnF/tala/pers/kyn  # (sem borðað höfðu matinn) og þakkað auðmjúkar fyrir sig

# Sagnliðir

Sagnliður/tala/pers/kyn →

    Sögn_0/tala/pers/kyn       # Sögn með engu viðfangi: (veðrið) skánaði
    | Sögn_1/tala/pers/kyn     # Sögn með einu viðfangi: (Jón) sá köttinn
    | Sögn_2/tala/pers/kyn     # Sögn með tveimur viðföngum: (Kata) skrifaði Önnu bréf

    | Sögn_sagnb/tala/pers/kyn # Sagnbót: (Jón) hefur borðað matinn og farið að bóna bílinn
    | Sögn_nh/tala/pers/kyn    # Sagnir í nafnhætti: (Jón) mun/hyggst bóna bílinn og fara að kaupa inn
    | Sögn_lhþt/tala/pers/kyn  # Sagnir í lh. þt.: (Kata) er vöknuð og farin að sækja Pál
    | Sögn_lhnt/tala/pers      # Sagnir í lh. nt.: (Guðrún) var hringjandi og sannfærandi í allan dag

# Sagnliður með venjulegri eða 'öfugri' orðaröð: (Konan sem) matinn borðaði

ÖfugurSagnliður/tala/pers/kyn →

    Sagnliður/tala/pers/kyn

    | ÖfugSögn_0/tala/pers/kyn
    | ÖfugSögn_1/tala/pers/kyn
    | ÖfugSögn_2/tala/pers/kyn

    # !!! Bæta við ÖfugSögn_sagnb, _nh, _lhþt hér

# Sagnliður án sýnilegs frumlags: Í dag þarf að vökva grasið (=Það þarf að vökva grasið í dag)
# Sagnliður án frumlags þarf að geta staðið sem afleiðing í ef-þegar setningu:
# '(Ef hann hringir) má sækja tölvuna og byrja að vinna'

SagnliðurÁnF →
    so_subj_gm_none NhFylling?                          # (Í dag) þarf [sannarlega] (að vökva grasið)
    | so_0_mm_p3_et NhFylling?                          # (Í dag) dregst [sennilega] (að vökva grasið)

    | HjSögn_et_p3 FsAtv? so_subj_sagnb_none NhFylling? # (Í ár) hefur [í sumarhitanum] þurft (að vökva grasið)
    | HjSögn_et_p3 FsAtv? so_mm_sagnb NhFylling?        # (Í ár) hefur [vegna leti] dregist (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_subj_nh_none NhFylling?  # (Í ár) mun [í sumarhitanum] þurfa (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_mm_nh NhFylling?         # (Í ár) mun [í sumarhitanum] dragast (að vökva grasið)

    | HjSögnLhÞtSM_et_p3 FsAtv? so_0_sagnb NhFylling?   # (í dag) verður/skal/mun [líklega] lent á brautinni (eftir hádegi)
    | HjSögnLhÞtSM_et_p3 FsRuna? LoTengtSögn_et_nf_hk NhLiðir? # (Í dag) hefur verið býsna erfitt (að aka heiðina)

    | HjSögnMegiSkuli_et_p3 HreinSögn_nh_et_hk          # [Í dag] má sækja tölvuna (og byrja að vinna)
    | EinSögn_nh_et_hk HjSögnMegiSkuli_et_p3 HreinSögnAtv_nh? # [hvort] leggja skuli [á skatt, breyta honum eða afnema hann]

    | HjSögnNh_p3/tala FsAtv? so_mm_nh FsRuna? Nl_nf/tala  # [Í kröfunum] skulu felast tillögur (um nýtt fyrirkomulag)

    | 'þurfa'_et_p3 FsRuna? Nl_þf                       # Þarf til þess lagafyrirmæli

    | StökSögn_et_p3_gm                                 # (Í dag) segir
    | so_0_et_p3_gm FsAtv                               # (Í dag) fýkur í fólk / kreppir að / losnar um stífluna / segir

StökSögn_et_p3_gm →
    so_0_et_p3_gm

# Óalgengt að sagnorð standi ein og sér án frumlags
$score(-4) StökSögn_et_p3_gm

HreinSögnAtv_nh →
    FsAtv? HreinSögn_nh_et_hk

# Sagnliður sem hefst á viðtengingarhætti:
# 'Haldi þessi þróun áfram er voðinn vís / þarf að grípa til aðgerða'

SagnliðurVh →

    # Haldir þú [áfram að borða] (er voðinn vís)
    # Aki hann ölvaður (skal sekta hann um tíu þúsund krónur)
    so_0_vh/pers Nl_nf/tala/pers/kyn LoTengtSögn_nf/tala/kyn? NhFyllingEðaAtv? Afleiðing

    # Borði börnin [ekki] matinn (getum við farið í sund)
    | So_1_vh/fall/pers Nl_nf/pers AtvFs? Nl/fall AtvFs? Afleiðing

    # Verði þessi tillaga felld (geng ég úr flokknum)
    # Verði karlarnir byrjaðir að grilla (sæki ég sósuna)
    | VeraVerða_vh/tala/pers Nl_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn FsAtv? Afleiðing

    # Hafi maður verið sviptur frelsi að ósekju (skal hann eiga rétt til skaðabóta)
    | 'hafa'_vh/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera'_sagnb HreinSögn_lhþt/tala/kyn FsAtv? Afleiðing

    # Hafi manni verið hallmælt í útvarpi (skal hann fá kaffi og kleinu)
    | 'hafa'_vh_et_p3 Nl_þgf AtvFs? 'vera'_sagnb so_1_þgf_sagnb FsAtv? Afleiðing

    # Hafi manni verið tekið blóð að óþörfu (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa'_vh_et_p3 Nl_þgf AtvFs? 'vera'_sagnb so_2_þgf_þf_lhþt/tala/kyn Nl_nf/tala/kyn AtvFs? Afleiðing

    # Hafi manns verið leitað að óþörfu (skal hann fá axlarnudd)
    | 'hafa'_vh_et_p3 Nl_ef AtvFs? 'vera'_sagnb so_2_þgf_ef_sagnb FsAtv? Afleiðing

    # (Því) hafi þurft að senda alla farþegana (á hótel)
    | HjSögnVh so_0_sagnb NhFylling?

VeraVerða_vh/tala/pers →
    'vera'_vh/tala/pers | 'verða'_vh/tala/pers

# Sagnliður með inniföldu frumlagi:
# '(Í dag) borðaði Jón matinn'
# '(Með því) var svæðinu stefnt í hættu'
# '(Í kjölfar þess) varð ég ósáttur'
# '(Nú) mega allir fara sem vilja'
# '(Í gær) skrifuðu þær konur þingmönnum bréf sem voru heitastar í málinu'

SagnliðurMeðF/tala/pers/kyn →
    SögnMeðF/tala/pers/kyn Tengiliður/tala/pers/kyn?

SögnMeðF/tala/pers/kyn →
    SögnMeðF_0/tala/pers/kyn   # Sögn með engu viðfangi: (Í dag) skánaði veðrið
    | SögnMeðF_1/tala/pers/kyn # Sögn með einu viðfangi: (Í dag) sá Jón köttinn
    | SögnMeðF_2/tala/pers/kyn # Sögn með tveimur viðföngum: (Í dag) skrifaði Kata Önnu bréf

OgSögn_0/tala/pers →
    FsAtv? OgEða FsAtv? so_0/tala/pers
    | FsAtv? ',' OgEða FsAtv? so_0/tala/pers FsAtvKomma?

OgSögn_mm/tala/pers →
    FsAtv? OgEða FsAtv? so_mm/tala/pers
    | FsAtv? ',' OgEða FsAtv? so_mm/tala/pers FsAtvKomma?

OgSögn_mm_nh →
    FsAtv? OgEða FsAtv? so_mm_nh
    | FsAtv? ',' OgEða FsAtv? so_mm_nh FsAtvKomma?

OgSögn_mm_sagnb →
    FsAtv? OgEða FsAtv? so_mm_sagnb
    | FsAtv? ',' OgEða FsAtv? so_mm_sagnb FsAtvKomma?

OgSögn_sagnb/tala/kyn →
    # (hefur staðið) keikur í brúnni og mótmælt yfirgangi Rússa
    LoTengtSögn_nf/tala/kyn? FsAtv? OgEðaEn FsAtv? EinSögn_sagnb/tala/kyn
    | LoTengtSögn_nf/tala/kyn? FsAtv? ',' OgEðaEn FsAtv? EinSögn_sagnb/tala/kyn FsAtvKomma?

OgSögn_nh/tala/kyn →
    FsAtv? OgEðaEnÞrátt FsAtv? EinSögn_nh/tala/kyn
    | FsAtv? ',' OgEðaEnÞrátt FsAtv? EinSögn_nh/tala/kyn FsAtvKomma?

# Þá segist hún styðja trúfrelsi, þrátt fyrir að vera andsnúin moskubyggingum
OgEðaEnÞrátt →
    OgEðaEn | 'þrátt_fyrir_að'

OgSögn_lhþt/tala/kyn →
    FsAtv? OgEðaEn FsAtv? EinSögnFrh_lhþt/tala/kyn
    | FsAtv? ',' OgEðaEn? FsAtv? EinSögnFrh_lhþt/tala/kyn FsAtvKomma?

OgSögn_lhnt →
    FsAtv? OgEðaEn FsAtv? so_0_lh_nt
    | FsAtv? ',' OgEðaEn FsAtv? so_0_lh_nt FsAtvKomma?

OgSögn_1_þf_nh/tala/pers →
    FsAtv? OgEða FsAtv? So_1_þf_nh/tala/pers
    | FsAtv? ',' OgEða FsAtv? So_1_þf_nh/tala/pers FsAtvKomma?

# Ekki enda á FsAtv nema innan kommu
FsAtvKomma → FsAtv? ','

# Lýsingarháttur þátíðar (Við erum byrjaðir)

LhÞtBotn/tala/kyn →
    # (Mennirnir eru byrjaðir) galvaskir að grilla
    # (Mennirnir eru sagðir) fara herskáir að brenna þorp
    so_0_nh? NhFylling/tala/kyn
    | NhRuna                                # (Mennirnir eru sagðir) fara sjaldan í bað, og raka sig aldrei
    | LoTengtSögn_nf/tala/kyn               # (Mennirnir eru taldir) ritfærir

EinSögn_lhþt/tala/kyn →
    so_0_lhþt/tala/kyn LhÞtBotn/tala/kyn?   # (Mennirnir eru) lentir
    > so_2_þf_þgf_lhþt/tala/kyn Nl_þgf      # (Maðurinn var) sviptur frelsi
    > so_2_þgf_þf_lhþt/tala/kyn Nl_þgf      # (Ákvörðunin var) tilkynnt Páli
    > so_subj_lhþt/tala/kyn Nl_nf/tala      # (Katrín var) valin besti þjálfarinn
    > so_subj_lhþt_ft/kyn Nl_nf_et          # (Þau voru) valin efnilegasta sundfólkið

EinSögnFrh_lhþt/tala/kyn →
    EinSögn_lhþt/tala/kyn                   # (Mennirnir) eru lentir og byrjaðir að grilla
    | EinSögnFrh_lhþt
    | Pfn_þgf/kyn/tala so_1_þgf_sagnb       # honum/henni/því/þeim breytt

# !!! Hér mætti bæta við möguleika á pfn í nefnifalli með so_lhþt,
# !!! sbr. 'Starfsemi slíkra stofnana verður ekki lögð niður, henni breytt að verulegu leyti
# !!! eða *hún* fengin öðrum stofnunum nema með samþykki Alþingis'

Pfn_þgf_kk/tala → 'hann'_þgf/tala
Pfn_þgf_kvk/tala → 'hún'_þgf/tala
Pfn_þgf_hk/tala → 'það'_þgf/tala

EinSögnFrh_lhþt →
    Nl_nf/tala/pers/kyn so_0_lhþt/tala/kyn  # (og) veð tekið

HreinSögn_lhþt/tala/kyn →
    FsAtv? EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn*

Sögn_lhþt/tala/pers/kyn →
    HjSögnLhÞtSM/tala/pers HreinSögn_lhþt/tala/kyn  # er farið, hefur verið farin, hafa verið farnar, skal farin

LhÞtÞgfBotn → "fyrir" "bestu"               # Það sem er börnum fyrir bestu

LhÞtÞgfEinkunn → LhÞtÞgfBotn | LoTengtSögn_nf_et_hk

Sögn_lhþt_et_p3_hk →
    # Sértilfelli fyrir 'Það er okkur mikilvægt/fyrir bestu [í þessu máli] að fara/gera...'
    HjSögnLhÞt/tala/pers Nl_þgf LhÞtÞgfEinkunn NhFylling

# Lýsingarháttur nútíðar ('við vorum hringjandi', 'hún hefur verið kvartandi')

Sögn_lhnt/tala/pers →
    HjSögnLhÞt/tala/pers HreinSögn_lhnt

HreinSögn_lhnt →
    FsAtv? so_0_lh_nt OgSögn_lhnt*

# Sagnbót ('konurnar [segjast/þykjast/virðast] hafa borðað matinn')

Sögn_sagnb/tala/pers/kyn →
    HjSögn/tala/pers HreinSögn_sagnb/tala/kyn

HreinSögn_sagnb/tala/kyn →
    FsAtv? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*

EinSögn_sagnb/tala/kyn →
    so_0_sagnb NhFylling/tala/kyn? # Konurnar hafa farið að grilla
    | Sögn_1_sagnb                 # Konurnar hafa [ekki] borðað matinn
    | Sögn_2_sagnb

# Nafnháttur

Sögn_nh/tala/pers/kyn →
    HjSögnNh/tala/pers HreinSögn_nh/tala/kyn # Páll mun/skal skrifa bréfið
    | Sögn_nh_mm/tala/pers/kyn

# Miðmynd + nafnháttur

Sögn_nh_mm/tala/pers/kyn →
    so_mm/tala/pers OgSögn_mm/tala/pers* HreinSögn_nh/tala/kyn # Páll hyggst/þykist/telst fara að...

# Hvetja til samhengis með miðmynd: (hún) segist ekki ætla (að fara í framboð)
$score(+2) Sögn_nh_mm/tala/pers/kyn

HreinSögn_nh/tala/kyn →
    FsAtv? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

EinSögn_nh/tala/kyn →
    so_0_nh NhFylling/tala/kyn?                    # Konurnar munu fara að grilla
    | 'vera'_nh 'sá'/tala/kyn NhFylling/tala/kyn   # '(Eini valkosturinn virðist) vera sá að fara út að grilla'
    | Sögn_1_nh                                    # Konurnar munu [ekki] borða matinn
    | Sögn_2_nh

    # (Stjórnsýsla skal) vera gegnsæ og halda til haga gögnum
    | HjSögnVeraVerða FsRuna? LoTengtSögn_nf/tala/kyn

# Boðháttur

Sögn_bh →
    # Farðu út að þvo bílinn
    so_0_bh NhFylling?
    # Förum að þvo bílinn
    | so_0_gm_fh_p1_ft_nt NhFylling?
    # Borðið matinn
    | Sögn_1_bh
    # Skrifaðu jólasveininum bréf
    | Sögn_2_bh

# Boðháttur er sjaldgæfur - viljum aðra kosti frekar
$score(-4) Sögn_bh

# Sögn með engu viðfangi ('lenti')
# ('Maðurinn lenti')
Sögn_0/tala/pers/kyn →
    # (Mennirnir) lenda og fara [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* NhFylling/tala/kyn?

# Sögn með engu viðfangi ('lenti') sem stendur í undirsetningu
# ('Maðurinn sem lenti')
ÖfugSögn_0/tala/pers/kyn →

    # Mennirnir sem nennt hafa [að þvo bílinn]
    so_0_sagnb HjSögn/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lenda munu
    | so_0_nh HjSögnNh/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lentir eru
    | so_0_lhþt/tala/kyn HjSögnLhÞt/tala/pers NhFylling/tala/kyn?

    # (Páll gerði allt sem) í hans valdi stóð
    # (...þau réttindi sem) í þessari stjórnarskrá felast
    | FsAtv so_0/tala/pers

# Sögn með inniföldu frumlagi en engu viðfangi ('lenti')
# ('Í dag lenti maðurinn')
SögnMeðF_0/tala/pers/kyn →

    # (Í dag) lenda og fara mennirnir [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* Nl_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) tókst og heppnaðist Seðlabankanum [galvöskum að lækka vexti]
    # !!! Ath: ekki _fh hér þar sem _vh er leyfilegt (takist Seðlabankanum...)
    | so_mm/tala/pers OgSögn_mm/tala/pers* Nl_þgf/tala/pers/kyn NhFylling_þgf/tala/kyn?

    # (Í dag) hefur [ekki] gerst það sama
    | HjSögn/tala/pers FsAtv? so_0_sagnb Nl_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) hafa mennirnir [ekki/eftir seinkun gærdagsins] lent
    | HjSögn/tala/pers Nl_nf/tala/pers/kyn HreinSögn_sagnb/tala/kyn

    # (Í dag) eiga mennirnir [ekki/eftir seinkun gærdagsins] að hafa/geta lent
    | HjSögnNhMeðSagnb/tala/pers Nl_nf/tala/pers/kyn nhm HafaGetaFá_nh HreinSögn_sagnb/tala/kyn

    # (Í dag) munu mennirnir [ekki/innan tíðar] lenda    
    | HjSögnNh/tala/pers Nl_nf/tala/pers/kyn HreinSögn_nh/tala/kyn
    # (Í dag) hyggjast mennirnir [ekki/innan tíðar] lenda
    # (Í dag) segist Illugi ekki hafa fengið laun
    | so_mm/tala/pers Nl_nf/tala/pers/kyn SögnMeðF_mm/tala/kyn

    # (Í dag) eru mennirnir [ekki/í millitíðinni] lentir
    # (Í dag) skulu mennirnir látnir lausir
    | HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn

    # (Í ár) hefur krönunum [í borginni] fjölgað
    | HjSögn_et_p3 Nl/fall/tala/pers/kyn AtvFs? so_subj_sagnb/fall NhFylling/tala/kyn?
    # (Í dag) er þingmönnunum [í flokknum] stefnt (til þings)
    | HjSögnLhÞt_et_p3 Nl_þgf/tala/pers/kyn AtvFs? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í kvöld) er viðurgjörningsins notið (í skíðaskálanum)
    | HjSögnLhÞt_et_p3 Nl_ef/tala/pers/kyn AtvFs? so_1_ef_sagnb NhFylling/tala/kyn?

    # (Í ár) mun krönunum [í borginni] fjölga
    | HjSögnNh_et_p3 Nl/fall/tala/pers/kyn AtvFs? so_subj_nh/fall NhFylling/tala/kyn?

    # (Í dag) hafa fjöllin verið klifin og sigruð
    | 'hafa'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera'_sagnb HreinSögn_lhþt/tala/kyn
    # (Í dag) hefur flugvélinni verið lent    
    | 'hafa'_et_p3 Nl_þgf/tala/pers/kyn AtvFs? 'vera'_sagnb FsAtv? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í dag) hefur konunnar verið gætt
    | 'hafa'_et_p3 Nl_ef/tala/pers/kyn AtvFs? 'vera'_sagnb FsAtv? so_1_ef_sagnb NhFylling/tala/kyn?

    # Skal sú ákvörðun [vera] rökstudd og tilkynnt forseta Alþingis
    | HjSögnSkuluMunu/tala/pers Nl_nf/tala/pers/kyn VeraVerðaAtv? HreinSögn_lhþt/tala/kyn

VeraVerðaAtv →
     FsAtv? HjSögnVeraVerða

SögnMeðF_mm/tala/kyn →
    HreinSögn_nh/tala/kyn                           # (Í dag hyggjast mennirnir) [ekki/innan tíðar] lenda
    | FsAtv? HjSögn_mm_nh HreinSögn_sagnb/tala/kyn  # (Í dag segist Illugi) ekki hafa fengið laun

OgSögn_1/fall/tala/pers →
    OgEða FsAtv? so_1/fall/tala/pers
    | ',' OgEða FsAtv? so_1/fall/tala/pers FsAtvKomma?

# Umraðanir

So_1_sagnb_nf → so_1_nf_sagnb   # (Í fyrra höfðu konurnar [ekki]) verið leikarar
So_1_sagnb_þf → so_1_þf_sagnb   # (Í dag hafa konurnar [ekki]) borðað matinn
So_1_sagnb_þgf → so_1_þgf_sagnb # (Í dag hafa konurnar [ekki]) hafnað Páli
So_1_sagnb_ef → so_1_ef_sagnb   # (Í dag hafa konurnar [ekki]) notið matarins

So_1_nh_nf → so_1_nf_nh         # (Í dag munu konurnar [ekki]) vera leikarar
So_1_nh_þf → so_1_þf_nh         # (Í dag munu konurnar [ekki]) borða matinn
So_1_nh_þgf → so_1_þgf_nh       # (Í dag munu konurnar [ekki]) hafna Páli
So_1_nh_ef → so_1_ef_nh         # (Í dag munu konurnar [ekki]) njóta matarins

So_1_bh_nf → so_1_nf_bh | so_1_nf_gm_fh_p1_ft_nt # Vertu leikari / Verum leikarar
So_1_bh_þf → so_1_þf_bh | so_1_þf_gm_fh_p1_ft_nt # Borðaðu matinn / borðum matinn
So_1_bh_þgf → so_1_þgf_bh | so_1_þgf_gm_fh_p1_ft_nt # Hafnaðu Páli / höfnum Páli
So_1_bh_ef → so_1_ef_bh | so_1_ef_gm_fh_p1_ft_nt # Njóttu matarins / njótum matarins

So_1_vh_nf/pers → so_1_nf_vh/pers         # Væri hann leikari
So_1_vh_þf/pers → so_1_þf_vh/pers         # Borðir þú matinn
So_1_vh_þgf/pers → so_1_þgf_vh/pers       # Hafni ég Páli
So_1_vh_ef/pers → so_1_ef_vh/pers         # Nyti hann matarins

# Sagnbót með einu viðfangi

Sögn_1_sagnb →
    So_1_sagnb/fall FsRuna? Nl/fall  # Þingmenn geta lagt [fyrir Alþingi] frumvörp (til laga)
    | 'enginn'/fall/tala/kyn Nl/fall/tala/kyn So_1_sagnb/fall # (Þó getur hann) enga samninga gert

# Sögn í nafnhætti með einu viðfangi

Sögn_1_nh → So_1_nh/fall FsRuna? Nl/fall

# Sögn í boðhætti með einu viðfangi

Sögn_1_bh → So_1_bh/fall FsRuna? Nl/fall

# Sagnbót með tveimur viðföngum

Sögn_2_sagnb →
    so_2_þgf_þf_sagnb Nl_þgf Nl_þf     # Konurnar höfðu [ekki] gefið Páli gjöfina
    | so_2_þf_ef_sagnb Nl_þf Nl_ef     # Konurnar höfðu [ekki] krafið Pál svara
    | so_2_þf_þf_sagnb Nl_þf Nl_þf     # Konurnar höfðu [ekki] kostað Pál skildinginn
    | so_2_þf_þgf_sagnb Nl_þf Nl_þgf   # Konurnar höfðu [ekki] kallað Pál öllum illum nöfnum
    | so_2_þgf_þgf_sagnb Nl_þgf Nl_þgf # Konurnar höfðu [ekki] skilað Páli bílnum
    | so_2_þgf_ef_sagnb Nl_þgf Nl_ef   # Konurnar höfðu [ekki] óskað Páli alls hins besta
    | so_2_þgf_nf_sagnb Nl_þgf Nl_nf   # Kristín hafði [ekki] verið Páli stoð og stytta

# Sögn í nafnhætti með tveimur viðföngum

Sögn_2_nh →
    so_2_þgf_þf_nh Nl_þgf Nl_þf        # Konurnar munu [ekki] gefa Páli gjöfina
    | so_2_þf_ef_nh Nl_þf Nl_ef        # Konurnar munu [ekki] krefja Pál svara
    | so_2_þf_þf_nh Nl_þf Nl_þf        # Konurnar munu [ekki] kosta Pál skildinginn
    | so_2_þf_þgf_nh Nl_þf Nl_þgf      # Konurnar munu [ekki] kalla Pál öllum illum nöfnum
    | so_2_þgf_þgf_nh Nl_þgf Nl_þgf    # Konurnar munu [ekki] skila Páli bílnum
    | so_2_þgf_ef_nh Nl_þgf Nl_ef      # Konurnar munu [ekki] óska Páli alls hins besta
    | so_2_þgf_nf_nh Nl_þgf Nl_nf      # Kristín mun [ekki] vera Páli stoð og stytta

So_2_nh_þf_nf → so_2_þf_nf_nh
So_2_nh_þf_þf → so_2_þf_þf_nh
So_2_nh_þf_þgf → so_2_þf_þgf_nh
So_2_nh_þf_ef → so_2_þf_ef_nh

So_2_nh_þgf_nf → so_2_þgf_nf_nh
So_2_nh_þgf_þf → so_2_þgf_þf_nh
So_2_nh_þgf_þgf → so_2_þgf_þgf_nh
So_2_nh_þgf_ef → so_2_þgf_ef_nh

# Sögn í boðhætti með tveimur viðföngum

Sögn_2_bh →
    so_2_þgf_þf_bh Nl_þgf Nl_þf        # Gefðu Páli gjöfina
    | so_2_þf_ef_bh Nl_þf Nl_ef        # Krefðu Pál svars
    | so_2_þf_þf_bh Nl_þf Nl_þf        # Kostaðu Pál skildinginn
    | so_2_þf_þgf_bh Nl_þf Nl_þgf      # Kallaðu Pál öllum illum nöfnum
    | so_2_þgf_þgf_bh Nl_þgf Nl_þgf    # Skilaðu Páli bílnum
    | so_2_þgf_ef_bh Nl_þgf Nl_ef      # Óskaðu Páli alls hins besta
    | so_2_þgf_nf_bh Nl_þgf Nl_nf      # Vertu Páli stoð og stytta

# Sögn með einu viðfangi ('borðaði matinn')
Sögn_1/tala/pers/kyn →

    # (þf:)  Konurnar borðuðu og meltu [ekki] matinn
    # (þgf:) Konurnar afneituðu, og beinlínis höfnuðu, [ekki] Páli
    # (ef:)  Konurnar nutu [ekki] matarins
    # (nf:)  Konurnar voru [ekki] leikarar

    # (Páll) klappaði og strauk kettinum
    # (Kjósandi) velur með persónkjöri frambjóðendur sem hann kýs
    # (Við) einsetjum okkur að vinna með öðrum þjóðum
    # (Sveitarfélögin) ráða sjálf málefnum sínum
    # (Hún) sagði lafmóð söguna
    so_1/fall/tala/pers OgSögn_1/fall/tala/pers* FsRuna? LoTengtSögn_nf/tala/kyn? Nl/fall NhFylling?

    # (Ég tel/segi/álít) Pál vera dagfarsprúðan mann
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* SagnarBotn

    | so_mm/tala/pers OgSögn_mm/tala/pers* Nl_þgf                    # (Bankar) reynast og gagnast mér (vel)
    | HjSögn/tala/pers FsAtv? so_mm_sagnb OgSögn_mm_sagnb* Nl_þgf    # (Bankar) hafa reynst og gagnast mér (vel)

    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn Nl_þgf # Konan hefur verið/skal/mun gefin Páli
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn Nl_ef   # Konan hefur verið/skal/mun spurð álits
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn Nl_þgf # Konan hefur verið/skal/mun kölluð öllum illum nöfnum
    # HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_ef_lhþt/tala/kyn Nl_ef  # Konan hefur verið/skal/mun óska alls hins besta
    # HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þf_lhþt/tala/kyn Nl_þf   # Konan hefur verið/skal/mun kosta skildinginn

# Sagnir sem geta staðið með þf + nh
# '(Ég) álít (Pál vera dagfarsprúðan mann)'
# '(Guðrún) lét (soninn hafa bílinn)'
# '(Við) sáum (strákinn vaxa úr grasi)'
So_1_þf_nh/tala/pers →
    'segja'/tala/pers
    | 'telja'/tala/pers
    | 'láta'/tala/pers
    | 'álíta'/tala/pers
    | 'sjá'/tala/pers

So_1_þf_nh →
    'segja'_nh
    | 'telja'_nh
    | 'láta'_nh
    | 'álíta'_nh
    | 'sjá'_nh

SagnarBotn →
    # (Páll segir) mistökin strax valda vanda
    Nl_þf AtvFs? NhRuna
    # (Þingmaðurinn telur) niðurstöðuna vera ranga / (Verkfræðingurinn segir) útkomuna verða tvísýna
    | Nl_þf/tala/kyn AtvFs? HjSögnVeraVerða LoLiður_nf/tala/kyn

    # (Páll segir) mistökin [ekki] hafa verið gerð (á vakt Jóns)
    # (Páll segir) mistökin [ekki] hafa verið (í uppleggi Friðgeirs)
    # Hér er notað HjSögnEkkiVera í stað HjSögnVera til að forðast margræðni
    # gagnvart NhRunu
    | Nl_þf/tala/kyn AtvFs? HjSögnEkkiVera so_lhþt/tala/kyn?

    # (Hann segir) hvern farþega [strax] hafa fengið tíu evrur / hafa farið að grilla
    | Nl_þf/tala/kyn AtvFs? 'hafa'_nh HreinSögn_sagnb/tala/kyn # !!! Hér vantar að senda þolfall: 'galvaskan að grilla'

    # (Páll segir) Ásu [ekki] vera/hafa verið góðan stjórnanda
    | Nl_þf/tala AtvFs? HjSögnEkkiVera Nl_þf/tala

    # (Páll segir) skorta [á] (að farið sé að lögum)
    | so_subj_nh_none NhFylling?
    # (Páll segir) tefjast (að hefja framkvæmdir)
    | so_mm_nh NhFylling?
    # (Már segir) misskilnings gæta (hjá Hannesi)
    | Nl_ef AtvFs? so_subj_nh_ef
    # (Anna telur) fólki fækka í landinu
    | Nl_þgf AtvFs? so_subj_nh_þgf
    # (Már segir) [í grundvallaratriðum] gæta misskilnings (hjá Hannesi)
    | FsAtv? so_subj_nh_ef Nl_ef
    # !!! Ekki virðist þörf fyrir öfuga orðaröð í þgf eða þf

# Sögn með einu viðfangi sem stendur í undirsetningu
# ('Konurnar sem borðuðu matinn')
ÖfugSögn_1/tala/pers/kyn →

    # (þf:)  Konurnar sem borðuðu [ekki] matinn
    # (þgf:) Konurnar sem höfnuðu [ekki] Páli
    # (ef:)  Konurnar sem nutu [ekki] matarins
    # (nf:)  Konurnar sem voru [ekki] leikarar

    So_1_sagnb/fall HjSögn/tala/pers Nl/fall         # Konurnar sem borðað höfðu matinn
    | So_1_nh/fall HjSögnNh/tala/pers Nl/fall        # Konurnar sem borða munu matinn

    | Nl/fall AtvFs? so_1/fall/tala/pers             # Konurnar sem matinn [snarlega] borðuðu

    | so_2_þgf_þf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_þgf        # Konan sem gefin hafði verið/skal/mun Páli
    | Nl_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn # Konan sem Páli hafði verið/skal/mun gefin

    | so_2_þf_þgf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_þgf        # Konan sem kölluð hafði verið/skal/mun illum nöfnum
    | Nl_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn # Konan sem illum nöfnum hafði verið/skal/mun kölluð

    | so_2_þf_ef_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_ef          # Konan sem spurð hafði verið/skal/mun álits
    | Nl_ef HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn   # Konan sem álits hafði verið/skal/mun spurð

    | Nl_nf So_1_þf_nh/tala/pers FsAtv? NhRuna        # Orðin sem Páll lét [ekki] falla
    # so_2_þgf_ef, so_2_þgf_þgf og so_2_þf_þf virðast ekki geta staðið
    # í lhþt heldur aðeins sem sagnbót

SögnMeðF_1/tala/pers/kyn →

    # (þf:)  (Í dag) borðuðu konurnar [ekki] matinn
    # (þgf:) (Í dag) afneituðu og höfnuðu konurnar [ekki] Páli
    # (ef:)  (Í gærkvöldi) nutu konurnar [ekki] matarins
    # (nf:)  (Í fyrra) voru konurnar [ekki] leikarar

    so_1/fall/tala/pers OgSögn_1/fall/tala/pers* Nl_nf/tala/pers/kyn FsRuna? Nl/fall NhFylling?

    | HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs? so_2_þgf_þf_lhþt/tala/kyn Nl_þgf # !!! (Í dag) hefur konan verið/skal/mun gefin Páli
    | HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs? so_2_þf_þgf_lhþt/tala/kyn Nl_þgf # !!! (Í dag) hefur konan verið/skal/mun kölluð illum nöfnum
    | HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs? so_2_þf_ef_lhþt/tala/kyn Nl_ef   # !!! (Í dag) hefur konan verið/skal/mun spurð álits
    | HjSögn/tala/pers Nl_þgf/tala/pers/kyn AtvFs? so_2_þgf_ef_sagnb Nl_ef   # !!! (Í dag) hefur konunni verið óskað bata
    | HjSögn/tala/pers Nl_þgf/tala/pers/kyn AtvFs? so_2_þgf_þgf_sagnb Nl_þgf   # !!! (Í dag) hefur konunni verið skilað bílnum
    | HjSögn/tala/pers Nl_þf/tala/pers/kyn AtvFs? so_2_þf_þf_sagnb Nl_þf   # !!! (Í dag) hefur konuna kostað skildinginn

    # (Í dag) segir hann (köttinn vera svangan)
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* Nl_nf/tala/pers/kyn SagnarBotn

# Sögn með tveimur viðföngum ('gaf Páli gjöfina')
Sögn_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_þf     # Konurnar gáfu Páli [sjálfar] gjöfina

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers Nl_þf LoTengtSögn_nf/tala/kyn? Nl_ef     # Konurnar kröfðu Pál [herskáar] svars

    # Þágufall + eignarfall ('að óska einhverjum góðs bata')
    | so_2_þgf_ef/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_ef   # Konurnar óskuðu Páli [allar] góðs bata

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_þgf # Konurnar skiluðu Páli [sjálfar] bílnum

    # Þágufall + nefnifall ('að vera einhverjum eitthvað')
    | so_2_þgf_nf/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_nf   # Konurnar voru Páli [allar] stoð og stytta

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers Nl_þf LoTengtSögn_nf/tala/kyn? Nl_þf     # Konurnar kostuðu Pál [allar] skildinginn

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers Nl_þf LoTengtSögn_nf/tala/kyn? Nl_þgf   # Konurnar kölluðu Pál [æstar] illum nöfnum

# Sögn með tveimur viðföngum sem stendur í undirsetningu
# ('Konan sem gaf Páli gjöfina')
ÖfugSögn_2/tala/pers/kyn →

    so_2_þgf_þf_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_þf                          # Konurnar sem gefið hafa Páli gjöfina
    | so_2_þgf_þf_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_þf                          # Konurnar sem gefa munu Páli gjöfina

    | so_2_þf_ef_sagnb HjSögn/tala/pers 
        Nl_þf Nl_ef                           # Konurnar sem spurt hafa Pál spurningarinnar
    | so_2_þf_ef_nh HjSögnNh/tala/pers 
        Nl_þf Nl_ef                           # Konurnar sem spyrja munu Pál spurningarinnar

    | so_2_þgf_ef_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_ef                          # Konurnar sem óskað hafa Páli góðs bata
    | so_2_þgf_ef_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_ef                          # Konurnar sem óska munu Páli góðs bata

    | so_2_þgf_þgf_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_þgf                         # Konurnar sem skilað hafa Páli bílnum
    | so_2_þgf_þgf_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_þgf                         # Konurnar sem skila munu Páli bílnum

    | so_2_þgf_nf_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_nf                          # Konurnar sem verið hafa Páli stoð og stytta
    | so_2_þgf_nf_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_nf                          # Konurnar sem vera munu Páli stoð og stytta

    | so_2_þf_þf_sagnb HjSögn/tala/pers 
        Nl_þf Nl_þf                           # Konurnar sem kostað hafa Pál skildinginn
    | so_2_þf_þf_nh HjSögnNh/tala/pers 
        Nl_þf Nl_þf                           # Konurnar sem kosta munu Pál skildinginn

    | so_2_þf_þgf_sagnb HjSögn/tala/pers 
        Nl_þf Nl_þgf                          # Konurnar sem kallað hafa Pál illum nöfnum
    | so_2_þf_þgf_nh HjSögnNh/tala/pers 
        Nl_þf Nl_þgf                          # Konurnar sem kalla munu Pál illum nöfnum

SögnMeðF_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_þf      # (Í dag) gáfu konurnar [bersýnilega] Páli [ekki] gjöfina

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers Nl_nf/tala/pers/kyn Nl_þf Nl_ef      # (Í gær) kröfðu konurnar Pál [ekki] svars

    # Þágufall + eignarfall ('að óska einhverjum einhvers')
    | so_2_þgf_ef/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_ef    # (Í gær) óskuðu konurnar Páli [ekki] góðs bata

    # Þágufall + nefnifall ('að vera einhverjum eitthvap')
    | so_2_þgf_nf/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_nf    # (Í gær) voru konurnar Páli [ekki] stoð og stytta

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers Nl_nf/tala/pers/kyn Nl_þf Nl_þf      # (Í gær) kostuðu konurnar Pál [ekki] skildinginn

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_þgf  # (Í gær) skiluðu konurnar Páli [ekki] bílnum

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers Nl_nf/tala/pers/kyn Nl_þf Nl_þgf    # (Í gær) kölluðu konurnar Pál [ekki] illum nöfnum

    # Sagnbót og nafnháttur    
    | HjSögn/tala/pers Nl_nf/tala/pers/kyn AtvFs? Sögn_2_sagnb  # (Í morgun) höfðu konurnar [ekki] krafið Pál svara
    | HjSögnNh/tala/pers Nl_nf/tala/pers/kyn AtvFs? Sögn_2_nh   # (Á morgun) munu konurnar [ekki] krefja Pál svara

# Hjálparsagnasambönd: vera, hafa, geta, skulu, munu...
# Aðalsögnin er sagnbót (farið/gert/viljað/borðað...)
HjSögn/tala/pers →
    'hafa'/tala/pers                            # hafðir farið
    | 'geta'/tala/pers                          # gast farið
    | 'fá'/tala/pers                            # (allir) fái notið (þeirra réttinda)
    | 'geta'/tala/pers FsAtv? 'hafa'_nh         # gast [ekki] hafa farið
    | 'vilja'/tala/pers FsAtv? HafaGetaFá_nh    # vildir [ekki] geta/hafa farið
    | 'mega'/tala/pers FsAtv? HafaGetaFá_nh     # máttir [ekki] geta/hafa farið
    | 'þykja'/tala/pers FsAtv? HafaGetaFá_nh    # þóttir [ekki] geta/hafa farið
    | 'skulu'/tala/pers FsAtv? HafaGetaFá_nh    # skulir [ekki] geta/hafa farið
    | 'munu'/tala/pers FsAtv? HafaGetaFá_nh     # munir [ekki] geta/hafa farið
    | 'eiga'/tala/pers FsAtv? nhm HafaGetaFá_nh # átt [ekki] að geta/hafa farið
    | so_mm/tala/pers FsAtv? HjSögn_mm_nh       # segist [ekki] hafa farið

HafaGetaFá_nh →
    'hafa'_nh | 'geta'_nh | 'fá'_nh

HjSögn_mm_nh →
    'hafa'_nh                # telst hafa farið
    | 'geta'_nh 'hafa'_nh?   # sýnist geta hafa farið
    | 'vilja'_nh 'hafa'_nh   # segist vilja hafa farið
    | 'mega'_nh 'hafa'_nh    # virðist mega hafa farið
    | 'munu'_nh 'hafa'_nh    # segist munu hafa farið
    | 'skulu'_nh 'hafa'_nh   # segjast skulu hafa farið(?)
    | 'fá'_nh                # þykist fá notið

HjSögnVh →
    'hafa'_et_p3_vh                         # hafi þurft
    | 'geta'_et_p3_vh                       # gæti þurft
    | 'geta'_et_p3_vh FsAtv? 'hafa'_nh      # gæti [ekki] hafa þurft
    | 'mega'_et_p3_vh FsAtv? 'hafa'_nh      # mætti [ekki] hafa þurft
    | 'þykja'_et_p3_vh FsAtv? 'hafa'_nh     # þætti [ekki] hafa þurft
    | 'skulu'_et_p3_vh FsAtv? 'hafa'_nh     # skyldi [ekki] hafa þurft
    | 'munu'_et_p3_vh FsAtv? 'hafa'_nh      # myndi [ekki] hafa þurft

# Sögnin stendur í nafnhætti með annarri nafnháttarsögn,
# sbr. 'Gæta verða/skulu/eiga þeir þingskapa'
HjSögnNhMeðSo/tala/pers →
    'verða'/tala/pers       # gæta verða þeir / verður þú
    | 'skulu'/tala/pers     # gæta skulu þeir / skal ég
    | 'munu'/tala/pers      # gæta munu þeir / mun hann
    | 'mega'/tala/pers      # gæta mega þeir
    | 'eiga'/tala/pers      # gæta eiga þeir
    | 'þurfa'/tala/pers     # gæta þurfa þeir / þurfið þið

HjSögnNhMeðSagnb/tala/pers →
    'verða'/tala/pers       # verða mennirnir að hafa/geta... lent
    | 'eiga'/tala/pers      # eiga mennirnir að hafa/geta... lent
    | 'þurfa'/tala/pers     # þurfa mennirnir að hafa/geta... lent

# Sögnin stendur í nafnhætti
HjSögnNh/tala/pers →
    'vilja'/tala/pers       # vilja fara
    | 'skulu'/tala/pers     # skulu fara
    | 'munu'/tala/pers      # munu fara
    | 'mega'/tala/pers      # mega fara
    | 'þykja'/tala/pers     # þykja setja (leiðinlegan svip á bæinn)
    | 'skulu'/tala/pers FsAtv? 'þykja'_nh   # (flegni kjóllinn) skuli [ekki] þykja vera (í lagi)
    | 'skulu'/tala/pers FsAtv? 'mega'_nh    # skyldu [ekki] mega fara
    | 'skulu'/tala/pers FsAtv? 'vilja'_nh   # skyldu [ekki] vilja fara
    | 'munu'/tala/pers FsAtv? 'þykja'_nh    # (flegni kjóllinn) myndi [ekki] þykja vera (siðsamlegur)
    | 'munu'/tala/pers FsAtv? 'mega'_nh     # myndu [ekki] mega fara
    | 'munu'/tala/pers FsAtv? 'vilja'_nh    # myndu [ekki] vilja fara
    | 'hafa'/tala/pers FsAtv? 'þykja'_sagnb # hafa [ekki] þótt fara
    | 'hafa'/tala/pers FsAtv? 'mega'_sagnb  # hafa [ekki] mátt fara
    | 'hafa'/tala/pers FsAtv? 'vilja'_sagnb # hafa [ekki] viljað fara

HjSögnNh_p3_et → HjSögnNh_et_p3
HjSögnNh_p3_ft → HjSögnNh_ft_p3

HjSögnÞurfaEiga_et_p3 →
    'þurfa'_et_p3                   # Engan þarf að undra
    | 'eiga'_et_p3                  # Engan ætti að undra
    | 'hafa'_et_p3 'þurfa'_sagnb    # Engan hefur þurft að undra
    | 'hafa'_et_p3 'eiga'_sagnb     # Engan hefði átt að undra
    | 'munu'_et_p3 'þurfa'_nh       # Engan mun þurfa að undra
    | 'skulu'_et_p3 'þurfa'_nh      # Engan skal þurfa að undra

HjSögnMegiSkuli_et_p3 →
    'mega'_et_p3                    # (Þann lærdóm) megi/mætti/má (draga)
    | 'skulu'_et_p3                 # (Þann lærdóm) skuli/skyldi/skal (draga)
    | 'hafa'_et_p3 'mega'_sagnb     # (Þann lærdóm) hefur/hefði mátt (draga)

HjSögnMegiSkuliÞurfi_et_p3 →
    'mega'_et_p3                    # (Endurnýja) megi/mætti/má (leigusamninginn)
    | 'eiga'_et_p3                  # (Endurnýja) á/eigi/átti (leigusamninginn)
    | 'þurfa'_et_p3                 # (Endurnýja) þyrfti/þarf/þurfti (leigusamninginn)
    | 'skulu'_et_p3                 # (Endurnýja) skuli/skyldi/skal (leigusamninginn)
    | 'verða'_et_p3                 # (Endurnýja) verður/yrði/varð (leigusamninginn)
    | 'hafa'_et_p3 'mega'_sagnb     # (Endurnýja) hefur/hafði/hefði mátt (leigusamninginn)
    | 'hafa'_et_p3 'þurfa'_sagnb    # (Endurnýja) hefur/hafði/hefði þurft (leigusamninginn)
    | 'hafa'_et_p3 'eiga'_sagnb     # (Endurnýja) hefur/hafði/hefði átt (leigusamninginn)

# Vera/verða sambönd, öll nema sjálfar sagnirnar 'vera' og 'verða'
HjSögnEkkiVera →
    'vilja'_nh 'vera'_nh              # (Mér sýndist Anna) vilja vera (ein)
    | 'mega'_nh 'vera'_nh               # (Mér sýndist Anna) mega vera (ein)
    | 'hafa'_nh 'vera'_sagnb            # (Mér sýndist Anna) hafa verið (valin)
    | 'geta'_nh 'hafa'_nh? 'vera'_sagnb # (Mér sýndist Anna) geta [hafa] verið (farin)
    | 'vilja'_nh 'hafa'_nh 'vera'_sagnb # (Mér sýndist Anna) vilja hafa verið (ein)
    | 'mega'_nh 'hafa'_nh 'vera'_sagnb  # (Mér sýndist Anna) mega hafa verið (ánægð)
    | 'þykja'_nh 'hafa'_nh 'vera'_sagnb # (Félögin) þykja hafa verið (illa rekin)
    | 'vilja'_nh 'verða'_nh
    | 'mega'_nh 'verða'_nh
    | 'hafa'_nh 'verða'_sagnb
    | 'geta'_nh 'hafa'_nh? 'verða'_sagnb
    | 'vilja'_nh 'hafa'_nh 'verða'_sagnb
    | 'mega'_nh 'hafa'_nh 'verða'_sagnb
    | 'þykja'_nh 'hafa'_nh 'verða'_sagnb # (Félögin) þykja hafa orðið (illa rekin)

HjSögnVeraVerða →
    'vera'_nh | 'verða'_nh

# Vera/verða í nafnhætti ('(Mér sýndist kötturinn) vera (gulbrúnn)')
HjSögnVera →
    HjSögnEkkiVera | HjSögnVeraVerða

# Lýsingarháttur þátíðar: vera farinn, hafa verið farinn, skal vera farinn, mun vera farinn
HjSögnLhÞt/tala/pers →
    'vera'/tala/pers                            # eru farnar
    | 'vilja'/tala/pers FsAtv? 'vera'_nh        # vildu [ekki] vera farnar
    | 'þykja'/tala/pers FsAtv? 'vera'_nh        # þóttu [ekki] vera farnar
    | 'mega'/tala/pers FsAtv? 'vera'_nh         # máttu [ekki] vera farnar
    | 'skulu'/tala/pers FsAtv? 'vera'_nh        # skulu [ekki] vera farnar
    | 'munu'/tala/pers FsAtv? 'vera'_nh         # munu [ekki] vera farnar
    | 'hafa'/tala/pers FsAtv? 'vera'_sagnb      # hafa [ekki] verið farnar
    | 'geta'/tala/pers FsAtv? 'hafa'_nh? 'vera'_sagnb # geta [ekki] verið farnar / geta hafa verið farnar
    | 'vilja'/tala/pers FsAtv? 'hafa'_nh 'vera'_sagnb # vildu [ekki] hafa verið farnar
    | 'þykja'/tala/pers FsAtv? 'hafa'_nh 'vera'_sagnb # þóttu [ekki] hafa verið farnar
    | 'mega'/tala/pers FsAtv? 'hafa'_nh 'vera'_sagnb # máttu [ekki] hafa verið farnar
    | 'skulu'/tala/pers FsAtv? 'hafa'_nh 'vera'_sagnb # skulu [ekki] hafa verið farnar
    | 'munu'/tala/pers FsAtv? 'hafa'_nh 'vera'_sagnb  # munu [ekki] hafa verið farnar
    | 'verða'/tala/pers                          # verða farnar
    | 'vilja'/tala/pers FsAtv? 'verða'_nh        # vildu [ekki] verða farnar
    | 'þykja'/tala/pers FsAtv? 'verða'_nh        # þóttu [ekki] verða farnar
    | 'mega'/tala/pers FsAtv? 'verða'_nh         # máttu [ekki] verða farnar
    | 'skulu'/tala/pers FsAtv? 'verða'_nh        # skulu [ekki] verða farnar
    | 'munu'/tala/pers FsAtv? 'verða'_nh         # munu [ekki] verða farnar
    | 'hafa'/tala/pers FsAtv? 'verða'_sagnb      # hafa [ekki] orðið farnar
    | 'geta'/tala/pers FsAtv? 'hafa'_nh? 'verða'_sagnb # geta [ekki] orðið farnar / geta hafa orðið farnar
    | 'vilja'/tala/pers FsAtv? 'hafa'_nh 'verða'_sagnb # vildu [ekki] hafa orðið farnar
    | 'þykja'/tala/pers FsAtv? 'hafa'_nh 'verða'_sagnb # þóttu [ekki] hafa orðið farnar
    | 'mega'/tala/pers FsAtv? 'hafa'_nh 'verða'_sagnb  # máttu [ekki] hafa orðið farnar
    | 'skulu'/tala/pers FsAtv? 'hafa'_nh 'verða'_sagnb # skulu [ekki] hafa orðið farnar
    | 'munu'/tala/pers FsAtv? 'hafa'_nh 'verða'_sagnb  # munu [ekki] hafa orðið farnar

HjSögnLhÞt/pers/tala → HjSögnLhÞt/tala/pers

# 'Konan skal/mun gefin Páli'
HjSögnSkuluMunu/tala/pers →
    'skulu'/tala/pers | 'munu'/tala/pers

# Sameiginleg regla fyrir LhÞt og SkuluMunu

HjSögnLhÞtSM/tala/pers →
    HjSögnLhÞt/tala/pers | HjSögnSkuluMunu/tala/pers

HjSögnLhÞtSM_p3_et →
    HjSögnLhÞt_et_p3 | HjSögnSkuluMunu_et_p3

HjSögnLhÞtSM_p3_ft →
    HjSögnLhÞt_ft_p3 | HjSögnSkuluMunu_ft_p3

# 'Hælisleitendum fer/fór/færi/hefur farið/mun fara fækkandi'

HjSögnLhNt →
    'fara'_gm_p3_et
    | 'hafa'_gm_p3_et FsAtv? 'geta'_sagnb? 'fara'_sagnb # hefur/hafði/hefði [ekki] [getað] farið (fækkandi)
    | 'geta'_gm_p3_et FsAtv? 'hafa'_nh? 'fara'_sagnb    # getur/gat/gæti [hafa] farið (fækkandi)
    | 'munu'_gm_p3_et FsAtv? 'fara'_nh                  # mun/myndi [ekki] fara (fækkandi)
    | 'þykja'_gm_p3_et FsAtv? 'fara'_nh                 # þykir/þótti/þætti [ekki] fara (fækkandi)
    | 'munu'_gm_p3_et FsAtv? 'hafa'_nh 'fara'_sagnb     # mun/myndi [ekki] hafa farið (fækkandi)
    | 'þykja'_gm_p3_et FsAtv? 'hafa'_nh 'fara'_sagnb    # þykir/þótti/þætti [ekki] hafa farið (fækkandi)

# 'Íslandsbanka hefur tekist að halda í horfinu'

HjSögnMM →
    'hafa'_gm_p3_et FsAtv?               # hefur [ekki] tekist
    | 'geta'_gm_p3_et FsAtv? 'hafa'_nh?  # getur [ekki] [hafa] tekist
    | 'munu'_gm_p3_et FsAtv? 'hafa'_nh   # mun [ekki] hafa tekist
    | 'skulu'_gm_p3_et FsAtv? 'hafa'_nh  # skal [ekki] hafa tekist
    | 'þykja'_gm_p3_et FsAtv? 'hafa'_nh  # þykir [ekki] hafa tekist
    | 'mega'_gm_p3_et FsAtv? 'hafa'_nh   # má [ekki] hafa tekist

# Nafnháttafylling
# - tengd frumlagi
# 'sannarlega galvaskir að þvo bílinn í gær'
# '(Þar hættir landið) að vera jarðneskt'
NhFylling/tala/kyn →
    NhFylling_nf/tala/kyn    # Nefnifall sjálfgefið

# Aðeins nefnifall og þágufall eru notuð
NhFylling/fall/tala/kyn → EinNhFylling/fall/tala/kyn OgNhFylling/fall/tala/kyn*

EinNhFylling/fall/tala/kyn →
    # '(Hann ætlar) strax að fara að millilenda'
    FsAtv? NhLiðir
    # '(Hann er) í reynd strax búinn að hafna þessu'
    | FsRuna? LoTengtSögn/tala/fall/kyn NhLiðir
    # 'að þykja og vera fallegt'
    | FsAtv? nhm so_1_nf_nh OgNhSögn_nf* LoTengtSögn/tala/fall/kyn
    # 'að sýnast og þykjast fallegt'
    | FsAtv? nhm so_mm_nh OgSögn_mm_nh* LoTengtSögn/tala/fall/kyn

OgNhFylling/fall/tala/kyn →
    OgEða EinNhFylling/fall/tala/kyn

# - ótengd frumlagi: lýsingarliður ekki mögulegur
# (nema þá hugsanlega í 3p. et.?)
NhFylling →
    FsAtv? NhLiðir

NhFyllingAtv →
    NhFylling FsAtv?

NhFyllingEðaAtv →
    NhFylling? FsAtv?

# Nafnháttarliður
# 'að lenda snarlega á flugvellinum'
# 'að sigla skipinu í land strax'
NhLiður →
    nhm NhRuna

NhLiðurFrh →
    FsAtv? NhLiður

# 'að fara strax að athuga málið'
NhLiðir →
    NhLiður NhLiðurFrh*

NhRuna → NhSögn OgNhLiður*

OgNhLiður →
    FsAtv? KommaOgEðaEn FsAtv? NhSögn # (að selja skyldi hestinn) snarlega, en ekki hætta baráttunni

NhSögn →
    so_0_nh so_0_gm_vh? OgNhSögn*                          # að kjósa [þurfi] og telja (upp á nýtt)
    | so_1_nf_nh so_0_gm_vh? OgNhSögn_nf* FsRuna? Nl_nf    # að hrjósa [muni] hugur
    | so_1_þf_nh so_0_gm_vh? OgNhSögn_þf* FsRuna? Nl_þf    # að selja [skyldi] í snatri hestinn
    | so_1_þgf_nh so_0_gm_vh? OgNhSögn_þgf* FsRuna? Nl_þgf # að hætta [ætti] baráttunni
    | so_1_ef_nh so_0_gm_vh? OgNhSögn_ef* FsRuna? Nl_ef    # að freista [eigi] gæfunnar
    | so_2_þgf_þf_nh so_0_gm_vh? OgNhSögn_þgf_þf* FsRuna? Nl_þgf Nl_þf # að skrifa [skuli] og senda Karólínu bréfið
    | so_2_þf_ef_nh so_0_gm_vh? OgNhSögn_þf_ef* FsRuna? Nl_þf Nl_ef    # að spyrja og krefja Pál svars
    | so_2_þf_þgf_nh so_0_gm_vh? OgNhSögn_þf_þgf* FsRuna? Nl_þf Nl_þgf # að kalla [ætti] Pál illum nöfnum
    | so_2_þgf_ef_nh so_0_gm_vh? OgNhSögn_þgf_ef* FsRuna? Nl_þgf Nl_ef # að óska [eigi] og óska Páli góðs bata
    | so_2_þgf_nf_nh so_0_gm_vh? OgNhSögn_þgf_nf* FsRuna? Nl_þgf Nl_nf # að vera [eigi] og vera Páli stoð og stytta
    #| so_2_þf_þf_nh so_0_gm_vh? OgNhSögn_þf_þf* Nl_þf Nl_þf    # !!! þarf?
    #| so_2_þgf_þgf_nh so_0_gm_vh? OgNhSögn_þgf_þgf* Nl_þgf Nl_þgf    # !!! þarf?

    # að reistar yrðu og byggðar íbúðir / að barinn skyldi fiskurinn og flakaður
    # !!! Ath: sennilega geta aðeins hjálparsagnir staðið í viðtengingarhætti hér,
    # !!! ekki allar sagnir
    | so_0_lhþt/tala/kyn so_0_gm_vh/tala OgNhSögn_lhþt/tala/kyn* Nl_nf/tala/kyn OgNhSögn_lhþt/tala/kyn*

    # láta sjúklinginn ná eðlilegum líkamshita
    # segja Pál fara með rangt mál
    # telja verkefnið verða óviðráðanlegt
    # sjá drenginn fara í hundana
    | So_1_þf_nh Nl_þf NhSögn

    # betur megi fara / strax skuli tilkynna
    | FsAtv? HjSögnMegiSkuli_et_p3 NhSögn

# Samtengingar sagna

OgNhSögn →
    OgEða so_0_nh so_0_gm_vh?
OgNhSögn_þf →
    OgEða so_1_þf_nh so_0_gm_vh?
OgNhSögn_þgf →
    OgEða so_1_þgf_nh so_0_gm_vh?
OgNhSögn_ef →
    OgEða so_1_ef_nh so_0_gm_vh?
OgNhSögn_nf →
    OgEða so_1_nf_nh so_0_gm_vh?
OgNhSögn_þgf_þf →
    OgEða so_2_þgf_þf_nh so_0_gm_vh?
OgNhSögn_þf_ef →
    OgEða so_2_þf_ef_nh so_0_gm_vh?
OgNhSögn_þf_þgf →
    OgEða so_2_þf_þgf_nh so_0_gm_vh?
OgNhSögn_þgf_ef →
    OgEða so_2_þgf_ef_nh so_0_gm_vh?
OgNhSögn_þgf_nf →
    OgEða so_2_þgf_nf_nh so_0_gm_vh?
OgNhSögn_lhþt/tala/kyn →
    OgEða so_0_lhþt/tala/kyn

# Nafnliðir og frumlög (nafnliðir í nefnifalli)

# Nafnliðarkjarni

Einkunn/tala/fall/kyn →

    LoLiður/tala/fall/kyn       # glæsileg (kona), bókfært (virði) (so_lhþt), leiðandi (fyrirtæki) (so_lh_nt)
    # AtviksliðurEinkunn undanskilur atviksorð sem geta líka verið forsetningar;
    # það dregur verulega úr margræðni
    | AtviksliðurEinkunn        # rúmlega (helmingur)
    | Fornafn/tala/fall/kyn     # Sá (stóri maður), allt þetta (víkjandi lán)
    | Töluorð/tala/fall/kyn     # þrjá (gangandi menn) / 26

Einkunn/fall/tala/kyn →
    Einkunn/tala/fall/kyn

# Lýsingarorð eða eignarfallsliður sem kemur á eftir nafnlið

NlViðbót/tala/fall/kyn →
    LoEftirNl/tala/fall/kyn     # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið,
    | EfLiður                   # (forstöðumaður) fræðaseturs (sunnlendinga)

# Eintala

NlKjarni_et/fall →
    NlKjarni_et/fall/kyn

NlKjarni_et/fall/kyn →

    # 'fullorðni sprúðlandi sonur Íslands'

    Einkunn_et/fall/kyn*        # leiðandi (fyrirtæki) / bókfært (virði)
    Skst?                       # FIH (bankinn)
    Fyrirbæri/fall/kyn          # Már Guðmundsson seðlabankastjóri
    NlViðbót_et/fall/kyn?       # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið, / (sonur) skósmiðsins
    Númer?                      # (ákvæði laga) nr. 33

    # Eignarfallsliður á undan: 'Íslands fullorðni sprúðlandi sonurinn sterki'
    | EfLiðurForskeyti Einkunn_et/fall/kyn* Skst? Fyrirbæri/fall/kyn LoEftirNl_et/fall/kyn?

NlKjarni_et_kk/fall →
    tala 'hluti'_et/fall EfLiður? # 1/3 hluti Alþingismanna (getur krafist atkvæðagreiðslu)

NlKjarni_et_nf_kk → NlKjarni_et_kk_nf
NlKjarni_et_þf_kk → NlKjarni_et_kk_þf
NlKjarni_et_þgf_kk → NlKjarni_et_kk_þgf
NlKjarni_et_ef_kk → NlKjarni_et_kk_ef

# Fleirtala

NlKjarni_ft/fall/kyn →

    Einkunn_ft/fall/kyn*
    Skst?
    Fyrirbæri_ft/fall/kyn
    NlViðbót_ft/fall/kyn?
    Númer?

    | EfLiðurForskeyti Einkunn_ft/fall/kyn* Skst? Fyrirbæri_ft/fall/kyn LoEftirNl_ft/fall/kyn?

Fyrirbæri_ft/fall/kyn →
    no_ft/fall/kyn
    | no_ft_gr/fall/kyn Fjöldi_ft/fall/kyn  # Mennina tíu / félögin tvö
    | raðnr OgRaðnr+ no/tala/fall/kyn       # 2. og 3. grein laganna (eru fallnar úr gildi)

OgRaðnr → "og" raðnr

Númer → 'númer' tala

Fjöldi/tala/fall/kyn → to/tala/fall/kyn | töl

# Persónufornöfn þriðju persónu eintölu, karl- og kvenkyn
# Þessi orð eru ekki merkt með kyni í BÍN;
# þess vegna þurfa þau sérstaka meðhöndlun hér

NlKjarni_et_nf_kk → 'hann'_et_nf
NlKjarni_et_þf_kk → 'hann'_et_þf
NlKjarni_et_þgf_kk → 'hann'_et_þgf
NlKjarni_et_ef_kk → 'hann'_et_ef

NlKjarni_et_nf_kvk → 'hún'_et_nf
NlKjarni_et_þf_kvk → 'hún'_et_þf
NlKjarni_et_þgf_kvk → 'hún'_et_þgf
NlKjarni_et_ef_kvk → 'hún'_et_ef

# Fleirtölumyndir 3. persónu eru í BÍN undir 'sá'

# Afturbeygt fornafn, eins í tölum og kynjum, aðeins til í aukaföllum 3p.

# sjálfan sig / sjálfa sig
# sjálfum sér / sjálfum sér
# sjálfs sín  / sjálfra sín

NlKjarni/tala/aukafall/kyn → 'sjálfur'/aukafall/tala/kyn? abfn/aukafall

# Umraðanir

NlStak → NlStak/tala/pers/fall/kyn

NlStak/fall → NlStak/tala/pers/fall/kyn

NlStak/fall/kyn → NlStak/tala/pers/fall/kyn

NlStak/fall/pers → NlStak/tala/pers/fall/kyn

NlStak/pers/fall/kyn → NlStak/tala/pers/fall/kyn

NlStak/fall/tala/kyn → NlStak/tala/pers/fall/kyn

NlStak/fall/tala/pers/kyn → NlStak/tala/pers/fall/kyn

NlStak_et_ef/kyn → NlStak_ef_et/pers/kyn
NlStak_ft_ef/kyn → NlStak_ef_ft/pers/kyn

# Nafnliðir

NlStak_et_p1/fall/kyn →
    'ég'_et/fall FsRunaEftirNl? # Ég í öllu mínu veldi

NlStak_et_p2/fall/kyn →
    'þú'_et/fall FsRunaEftirNl?

NlStak_p1_ft/fall/kyn →
    'ég'_ft/fall NlStak_ft_p3/fall/kyn? | 'vér'_ft/fall NlStak_ft_p3/fall/kyn? # Við gömlu hjónin, vér fóstbræður
    | NlEind/fall KommaOgEða 'ég'_et/fall # Þú og ég fórum, Jón og ég fórum, stelpurnar og ég fórum
    | 'ég'_et/fall KommaOgEða NlEind/fall # Ég og þú fórum, ég og Jón fórum, ég og stelpurnar fórum

NlStak_ft_p1/fall/kyn → NlStak_p1_ft/fall/kyn FsRunaEftirNl?

NlStak_p2_ft/fall/kyn →
    'þú'_ft/fall NlStak_ft_p3/fall/kyn? | 'þér'_ft/fall NlStak_ft_p3/fall/kyn? # Þið duglegu strákarnir, þér eplin
    | NlEind/fall KommaOgEða 'þú'_et/fall # Jón og þú fóruð, stelpurnar og þú fóruð
    | 'þú'_et/fall KommaOgEða NlEind/fall # Þú og Jón fóruð, þú og stelpurnar fóruð

NlStak_ft_p2/fall/kyn → NlStak_p2_ft/fall/kyn FsRunaEftirNl?

# !!! ATH: reglurnar hér fyrir ofan leyfa 'ég og þú fóruð til Frakklands'

# Sameiginlegar reglur fyrir 3p. eintölu og fleirtölu

NlStak_p3/tala/fall/kyn →

    NlKjarni/tala/fall/kyn Fornafn/tala/fall/kyn* # (stjórnarframkvæmdum) þessum öllum
    | NlKjarni/tala/fall/kyn EignarFornafn/tala/fall/kyn KommaOgEðaEn EfLiður # skattskýrslur mínar og eiginkonu minnar
    | NlFornafn/tala/fall/kyn

# Eignarfornöfn standa með nafnlið í sama falli og hann,
# þannig að gera þarf undantekningu fyrir þau þegar þau
# tengjast eignarfallslið ('Ég seldi ekki íbúðina þína, heldur frænda þíns')
EignarFornafn/tala/fall/kyn →
    'minn'/tala/fall/kyn
    | 'þinn'/tala/fall/kyn

NlFornafn/tala/fall/kyn →
    # Stakt fornafn, e.t.v. með einkunn og töluorði
    # 'augljóslega sá vondi' / 'einhverjir tveir góðir' / 'ekkert uppbyggilegt' / 'fjórir aðrir'
    # 'þeir báðir' / 'hún öll' / 'þær sjálfar'
    AtviksliðurEinkunn* Töluorð/tala/fall/kyn? Fornafn/tala/fall/kyn LoFornafn/fall/tala/kyn? LoLiður/tala/fall/kyn*
    # 'okkar besta' / 'óumdeilanlega ykkar versta'
    | AtviksliðurEinkunn* OkkarYkkar LoLiður/tala/fall/kyn

OkkarYkkar → "okkar" | "ykkar"

# Gefa stökum fornöfnum mínusstig - við viljum frekar sjá þau sem hluta af stærri nafnlið
$score(-4) NlFornafn/tala/fall/kyn

# Lýsingarorð sem stendur sem nafnliður:
# 'Margir/fjölmargir/allmargir/heilmargir komu og aðstoðuðu Pál'
# 'Páll aðstoðaði töluvert marga'
# 'Óleyfilegt er að mismuna samkynhneigðum'

NlStak_p3/tala/fall/kyn →
    AtviksliðurEinkunn* LoSemNafnliður/tala/fall/kyn

# 'Fleiri komu í veisluna en ég bjóst við'
# 'Fleirum fannst kvikmyndin leiðinleg'
NlStak_p3_ft/fall/kyn →
    'margur'_mst_ft/fall/kyn

# Unnt er að hafa nafnlið í þágufalli á undan lýsingarorðinu:
# 'sem varðar hagsmuni hans eða honum nákominna'
# Að svo komnu máli er slíkt aðeins leyft með lýsingarorðum í eignarfalli
# enda óljóst að þörf sé fyrir önnur föll.

NlStak_p3_et_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_et_ef/kyn

NlStak_p3_ft_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_ft_ef/kyn

LoSemNafnliður/tala/fall/kyn →
    lo_sb/tala/fall/kyn

# Gefa lýsingarorðum sem nafnliðum mínusstig
$score(-6) LoSemNafnliður/tala/fall/kyn

NlStak_p3/tala/fall/kyn →
    'hver'/tala/fall/kyn 'sá'/tala/fall/kyn # Hver sá, hver sú, hvert það, hverjir þeir, hverjar þær, hver þau
    | 'hvor'/tala/fall/kyn 'tveir'_ef_ft/kyn  # (eða) hvor/hvort tveggja
    # '(Sýslumenn sinna) sama hlutverki og prestar'
    # '(Ríkissaksóknari) nýtur sömu verndar í starfi og dómarar'
    | 'sami'/tala/fall/kyn Nl/fall/tala/kyn "og" Nl_nf

# 'Sú útskýring, að Samfylkingin sé óalandi og óferjandi, (dugir ekki lengur)'
NlStak_p3/tala/fall/kyn →
    'sá'/tala/fall/kyn Nl/fall/tala/kyn Útskýring

Útskýring →
    ',' "að" HreinYfirsetning ','
    | "að" HreinYfirsetning

$score(+2) Útskýring

# Töluorð sem sjálfstæðir nafnliðir

NlStak_p3/tala/fall/kyn →
    NlTöluorð/tala/fall/kyn

NlTöluorð/tala/fall/kyn →
    # '(Ég talaði við) tvær þeirra', 'fjórum þeirra (var kalt)'
    Töluorð/tala/fall/kyn 'sá'_ft_ef/kyn?

# Gefa mínusstig fyrir stök töluorð - viljum helst sjá þau sem einkunn í stærri nafnlið
$score(-8) NlTöluorð/tala/fall/kyn

# '1100 manns (voru á skemmtuninni)', '(Ég pantaði mat fyrir) sautján manns'
# Virðist ekki ganga upp í eignarfalli
# ('Ég lagði bílum 18 manns'?)
NlStak_p3_ft_nf_kk → Manns
NlStak_p3_ft_þf_kk → Manns
NlStak_p3_ft_þgf_kk → Manns

# Hér ganga beygjanleg töluorð ekki (?'fjórir manns')
Manns →
    töl "manns"
    | tala_ft "manns"

# 3p. eintala

NlStak_et_p3/fall/kyn →

    NlStak_p3_et/fall/kyn FsRunaEftirNl?                         # fulltrúi bankans í nefndinni
    | NlStak_et_p3/fall/kyn "eða" NlStak_p3_et/fall       # Þingnefnd eða hluti Alþingismanna getur lagt fram frumvarp
    | 'hver'_et/fall/kyn "og" 'einn'_et/fall/kyn FsRuna?  # 'hver og einn [í sinni sveit] (ber ábyrgð)'

# Meðhöndla "árið 2012" og "árin 2013 og 2014" með háum forgangi

NlStak_et_p3_nf_hk → Árið_nf
NlStak_et_p3_þf_hk → Árið_þf
NlStak_et_p3_þgf_hk → Árið_þgf
NlStak_et_p3_ef_hk → Árið_ef

Árið_nf → "árið" tala
Árið_þf → "árið" tala
Árið_þgf → "árinu" tala
Árið_ef → "ársins" tala

$score(+8) Árið/fall

NlStak_ft_p3_nf_hk → Árin_nf
NlStak_ft_p3_þf_hk → Árin_þf
NlStak_ft_p3_þgf_hk → Árin_þgf
NlStak_ft_p3_ef_hk → Árin_ef

Árin_nf → "árin" MörgÁr
Árin_þf → "árin" MörgÁr
Árin_þgf → "árunum" MörgÁr
Árin_ef → "áranna" MörgÁr

MörgÁr →
    tala '-' tala
    | tala "til" tala
    | tala ÁraRuna* OgEða tala

ÁraRuna → ',' tala

$score(+8) Árin/fall

# Leyfa sérnöfn (skrifuð sem runa orða með stórum staf)
# sem nafnliði 3p. eintölu hvorugkyns, í öllum föllum
# Orka Energy, OG Capital

NlStak_et_p3_nf_hk → Sérnafn
NlStak_et_p3_þf_hk → Sérnafn
NlStak_et_p3_þgf_hk → Sérnafn
NlStak_et_p3_ef_hk → Sérnafn

# Hvetja til skilnings á löngum sérnöfnum en ekki endilega stökum orðum

Sérnafn →
    sérnafn | LangtSérnafn

LangtSérnafn →
    sérnafn "-"? sérnafn+   # Glass-Steagall lögin

$score(-6) Sérnafn
$score(+6) LangtSérnafn

NlStak_p3_et/fall → NlStak_p3_et/fall/kyn   # Samtenging tveggja eintöluliða með 'eða' krefst ekki sama kyns báðum megin

# 3p. fleirtala

NlStak_ft_p3/fall/kyn →

    NlStak_p3_ft/fall/kyn FsRunaEftirNl?           # fulltrúar bankans í nefndinni
    | 'allur'_ft/fall/kyn 'sá'_ft/fall/kyn  # Allir þeir, allar þær, öll þau

# Hvorki X né Y
# Bæði X og Y

NlStak_ft_p3/fall/kyn →
    "bæði" Nl/fall/kyn "og" Nl/fall/kyn     # (Við þurftum að lóga) bæði hestinum og kettinum
    | "hvorki" Nl/fall/kyn "né" Nl/fall/kyn # Hvorki hestinum né kettinum (var lógað)

NlStak_ft_p3_nf_hk → NlStak_ft_p3_hk_nf
NlStak_ft_p3_þf_hk → NlStak_ft_p3_hk_þf
NlStak_ft_p3_þgf_hk → NlStak_ft_p3_hk_þgf
NlStak_ft_p3_ef_hk → NlStak_ft_p3_hk_ef

Nl/kyn/fall → Nl/fall/kyn

NlStak_ft_p3_hk/fall →
    "bæði" Nl_kk/fall "og" Nl_kvk/fall
    | "hvorki" Nl_kk/fall "né" Nl_kvk/fall
    | "bæði" Nl_kvk/fall "og" Nl_kk/fall
    | "hvorki" Nl_kvk/fall "né" Nl_kk/fall
    | "bæði" Nl_kk/fall "og" Nl_hk/fall
    | "hvorki" Nl_kk/fall "né" Nl_hk/fall
    | "bæði" Nl_hk/fall "og" Nl_kk/fall
    | "hvorki" Nl_hk/fall "né" Nl_kk/fall

NlStak_ft_p1/fall/kyn →
    "bæði" Nl_p1/fall/kyn "og" Nl_p2/fall/kyn     # bæði mér og þér þykir þetta óásættanlegt
    | "hvorki" Nl_p1/fall/kyn "né" Nl_p2/fall/kyn # hvorki ég né þú höfum vit á þessu
    | "bæði" Nl_p2/fall/kyn "og" Nl_p1/fall/kyn   # bæði þér og mér þykir þetta óásættanlegt
    | "hvorki" Nl_p2/fall/kyn "né" Nl_p1/fall/kyn # hvorki þú né ég erum fallegar/falleg/fallegir

Skst → no_abbrev '-'?
    | sérnafn '-'

# !!! ATH: Ráða við "Bernhöfts bakaríið"
Fyrirbæri/fall/kyn →
    no_et/fall/kyn
    | Manneskja/fall/kyn
    | raðnr no_et/fall/kyn
    | no_gr_et/fall/kyn Nl/fall    # (Ég hef áhuga á) hugtakinu 'þjóðmenningu'

Fyrirbæri/aukafall/kyn →
    FyrirbæriMeðGreini/aukafall/kyn

FyrirbæriMeðGreini/aukafall/kyn →
    no_gr_et/aukafall/kyn Nl_nf    # (Ég hef áhuga á) hugtakinu 'þjóðmenning'

$score(-4) FyrirbæriMeðGreini/aukafall/kyn

# [séra] Jóns Dalbú Hróbjartssonar [, dómkirkjuprests]
# [frú] Vigdís Finnbogadóttir [fyrrverandi forseti]
# [stórvesír] Ásbjörn Friðriksson [, alþingismann]
# [hún] Kristín (okkar)
Manneskja/fall/kyn → Ávarp/kyn/fall? person/fall/kyn Titill/fall?

Ávarp/kyn/fall →
    no_et/fall | ÞekktÁvarp/kyn/fall

$score(-6) Ávarp/kyn/fall

ÞekktÁvarp_kk/fall → 'herra'/fall | 'séra'/fall | 'hann'/fall
ÞekktÁvarp_kvk/fall → 'frú'/fall | 'séra'/fall | 'hún'/fall
ÞekktÁvarp_hk/fall → 0 # Ekkert hvorugkyn

$score(+10) ÞekktÁvarp/kyn/fall

# 'Már Guðmundsson, bankastjóri [Seðlabanka Íslands]'
# Reynt er að láta titil ekki enda á FsRuna/FsAtv eða á EfLiður
# því það skapar margræðni við nafnliðinn sjálfan
Titill/fall →
    ','? NlTitill/fall            # [Jón], formaður [félagsins]
    | KommuTitill/fall            # [Jón], formaður námsmannafélagsins í Árósum og talsmaður þess,
    | EinnTitill/fall OgTitill/fall* ','? "og" NlTitill/fall # Gunnlaugur H Jónsson eðlisfræðingur og fyrrverandi starfsmaður Orkustofnunar

NlTitill/fall →
    TitilEinkunn/fall/kyn? no_et/fall/kyn      # (Jón) glæsilegur fulltrúi (félagsins)

TitilEinkunn/fall/kyn →
    Einkunn_et/fall/kyn

# Titileinkunn er frekar fátíð
$score(-2) TitilEinkunn/fall/kyn

EinnTitill/fall →
    NlTitill/fall EfLiður? FsRuna?

OgTitill/fall →
    KommaEðaOg EinnTitill/fall

KommaEðaOg →
    ',' | "og" | ',' "og"

KommuTitill/fall →
    ',' EinnTitill/fall ','
    | ',' EinnTitill/fall OgTitill/fall* ','? "og" EinnTitill/fall ','

# Gefa plús fyrir titla
$score(+6) Titill/fall

# Gefa enn meiri plús fyrir heila titla innan kommu
$score(+4) KommuTitill/fall

# Eignarfallsliður á eftir nafnlið
# ('formaður Gimlis, félags eldri borgara')
# ('amma Jóns, föður Bjarna')
# karlsins langa, konunnar löngu, barnsins langa
# stóra karlsins, stóru konunnar, stóra barnsins
# karlanna löngu, kvennanna löngu, barnanna löngu

EfLiður → EfLiðurStakur EfLiðurFramhald? # Jóns, föður Bjarna sterka

# Eignarfallsliður getur verið í eintölu eða fleirtölu, og í
# öllum kynjum, óháð nafnliðnum sem hann stendur með
EfLiðurStakur → Nl_ef

EfLiðurFramhald → KommaEfLiður+ ','?

KommaEfLiður →
    ',' EfLiðurStakur

# Eignarfallsliður sem stendur á undan nafnlið
# ('Íslands fullorðnu synir', 'allra uppáhald')
# Þessi liður er einfaldari en EfLiður til að
# minnka margræðni; leyfir til dæmis ekki einkunn
# (?'Litla Íslands fullorðnu synir'), né að
# lýsingarorð sé notað sem nafnorð

EfLiðurForskeytiTala/tala/kyn →
    eo* Töluorð_ef/tala/kyn       # (vel rúmlega) þriggja (klukkutíma bið) / 30 (daga frestur)

EfLiðurForskeyti →
    Fornafn_ef/tala/kyn* EfLiðurForskeytiTala/tala/kyn? EfLiðurForskeytiKjarni/tala/kyn
    | Fornafn_ef/tala/kyn+ EfLiðurForskeytiTala/tala/kyn?

# EfLiðurForskeyti er ólíklegri en aðrar samsetningar
$score(-4) EfLiðurForskeyti

EfLiðurForskeytiKjarni_et/kyn → Fyrirbæri_ef/kyn
EfLiðurForskeytiKjarni_ft/kyn → no_ef_ft/kyn

# Leyfa 'okkar'/'ykkar' í eignarfallsforskeyti (er aðeins kynlaust pfn í BÍN)
Fornafn_ft_ef/kyn → OkkarYkkar

Fornafn/tala/fall/kyn → fn/tala/fall/kyn
Fornafn/fall/tala/kyn → Fornafn/tala/fall/kyn

# Persónufornöfnin (pfn) "hans" og "hennar" eru án kyns í BÍN
Fornafn_et_ef_kk → "hans"
Fornafn_et_ef_kvk → "hennar"

Töluorð/tala/fall/kyn → to/tala/fall/kyn | tala/tala

# Öll töluorð (merkt 'töl' í BÍN) tákna fleirtölu
Töluorð_ft/fall/kyn → töl

Töluorð/fall/tala/kyn → Töluorð/tala/fall/kyn

# Undirsetningar

# 1p: 'ég sem alltaf elda matinn samviskusamlega (nenni þessu ekki)'
# 2p: 'þú sem ég elska (vilt ekki giftast mér)'
# 3p: 'sjóarinn sem hafið hafnaði í fyrra (kom hér í gær og fékk sér kaffi)'

# Gefa undirsetningum afmörkuðum með kommum forgang,
# en forðast verri kosti

TengiliðurMeðKommu/tala/pers/kyn →
      ',' Tengiliður/tala/pers/kyn ','
    >     Tengiliður/tala/pers/kyn
    >     Tengiliður/tala/pers/kyn ','
    > ',' Tengiliður/tala/pers/kyn

KommaTengiliður/tala/pers/kyn →
    ','? Tengiliður/tala/pers/kyn

Tengiliður/tala/pers/kyn →
    Tengiorð Tengisetning/tala/pers/kyn

Tengiorð →
    TengiorðSem | TengiorðEr

TengiorðSem → "eða"? "sem"
TengiorðEr → "er"

$score(-6) TengiorðEr # Sjaldgæft - ekki velja nema það þurfi

Tengisetning/tala/pers/kyn →
    HreinYfirsetning            # (Maðurinn sem) konan kyssti í gær / konurnar kysstu í gær
    | SetningÁnF/tala/pers/kyn  # (Maðurinn sem) kyssti konuna í gær / (Maðurinn sem) var fallegur

NlEind/fall →
    NlEind/tala/pers/fall/kyn

NlEind/fall/kyn →
    NlEind/tala/pers/fall/kyn

NlEind/pers/fall/kyn →
    NlEind/tala/pers/fall/kyn

NlEind/tala/pers/fall/kyn →
    NlStak/tala/pers/fall/kyn Sem/tala/fall/kyn? TengiliðurMeðKommu/tala/pers/kyn? Þó/tala/pers/kyn? NlSkýring/tala/pers/fall/kyn?

# (vegna nauðasamninganna) sem slíkra
Sem/tala/fall/kyn →
    ',' "sem" 'slíkur'/tala/fall/kyn ','
    | "sem" 'slíkur'/tala/fall/kyn

# (að endurskapa sig) sem þjóðhetju
# 'sem' með nefnifalli er talið vera forsetning
Sem/tala/aukafall/kyn →
    "sem" Nl/aukafall/tala

NlEind_et_p3_nf_hk →
    # Það hvernig kerfið virkar (er óskiljanlegt flestum)
    'það'_nf_et ÞaðTenging

NlEind_et_p3_þf_hk →
    # (Gunnar lagði áherslu á) það að vægi atkvæða hafi aldrei verið jafnt
    'það'_þf_et ÞaðTenging

NlEind_et_p3_þgf_hk →
    # (Ekkert var gert í) því að Jón hefur áreitt Önnu lengi (eða því að hún vildi segja upp)
    'það'_þgf_et ÞaðTenging

NlEind_et_p3_ef_hk →
    # (Þetta varð til) þess að Anna fór í fússi (og uppsagnar í kjölfarið)
    'það'_ef_et ÞaðTenging

ÞaðTenging →
    "að" HreinYfirsetning
    | SpurnarSamtenging HreinYfirsetning
    | NhLiðir  # Það að fara til tannlæknis (reynist mörgum þyrnir í augum)

Þó/tala/pers/kyn →
    ÞóTenging ÞóBotn/tala/pers/kyn
    | ',' ÞóTenging ÞóBotn/tala/pers/kyn ','

ÞóTenging → "þó" | "þótt" | "þó" "að"

ÞóBotn/tala/pers/kyn →
    FsRuna? LoLiður_nf/tala/kyn VeraVerða_vh/tala/pers

NlSkýring/tala/pers/fall/kyn →
    '(' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ')'
    | '[' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ']'
    | '-' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn '-'
    | ',' SvigaInnihald/tala/pers/fall/kyn ','

# Gefa aukastig fyrir skýringar á nafnliðum
$score(+2) NlSkýring/tala/pers/fall/kyn

SvigaInnihald/tala/pers/fall/kyn →
    Tengiliður/tala/pers/kyn
    > Nl/fall/tala      # 'Ég spurði forsætisráðherra, Sigmund Davíð, um málið'
    > Nl                # Ef ekki finnst sama fall og tala, prófa almennan nafnlið
    > HreinYfirsetning

SvigaForskeyti →
    "dæmi" ':'    # Ráðamenn (dæmi: forsetinn) hafa tjáð sig fjálglega
    | "les" ':'   # Hann sagði að óþjóðalýður (les: kommúnistar) hefði ráðist á húsið

# Runur nafnliða

OgEða → "og" | "eða" | "ellegar" | "eður" | "svo" "og" | "sem" "og" | "né"
    | "þar" "á" "meðal" | 'þar_á_meðal' | "meðal" "annars" | 'meðal_annars'
    | "það" "er" | 'það_er'
    | 'eins_og' | "en"
    | "þar" "með" "talið"? | "svo" "sem" | 'svo_sem'

KommaOgEða → ','? OgEða
    | '-' # 'Ég tók tvö tonn af eldsneyti - tvö þúsund lítra'
    | ',' "ekki" # Við vildum gin, ekki viskí

# (Í fyrra barði Hannes Guðmund) og (særði Pál í dag)
# (Í fyrra barði Hannes Guðmund), (særði Pál), og (neitaði sök)
# (Í gær börðu strákarnir ekki Guðmund), heldur (stríddu Páli)

OgEðaEn → "og" | "svo" "og" | "eða" | "ellegar" | "en" | "heldur"

KommaOgEðaEn → ',' | ','? OgEðaEn

# Allir liðir af sama kyni

NlRuna/fall/kyn →
    KommuSamtenging/fall/kyn* Atviksliður? KommaOgEða FsRuna? NlEind/fall/kyn AtvKomma?
    | ',' FsRuna? NlEind/fall/kyn AtvKomma    # Konurnar, sérstaklega Anna, voru óánægðar

KommuSamtenging/fall/kyn → ',' FsRuna? NlEind/fall/kyn

# Hvetja til eins langra lista og mögulegt er
$score(+3) KommuSamtenging/fall/kyn

# Blandað kyn

NlRuna/fall →
    KommuSamtenging/fall* Atviksliður? KommaOgEða FsRuna? NlEind/fall AtvKomma?
    | ',' FsRuna? NlEind/fall AtvKomma        # Þingmenn, sérstaklega Jóhanna, voru óánægðir

KommuSamtenging/fall → ',' FsRuna? NlEind/fall

# Hvetja til eins langra lista og mögulegt er
$score(+3) KommuSamtenging/fall

# Nafnliðir sem geta verið stakar eindir eða runur einda

Nl →
    Nl/tala/pers/fall/kyn

Nl/fall →
    Nl/tala/pers/fall/kyn

Nl/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala →
    Nl/tala/pers/fall/kyn

Nl/fall/pers →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/kyn →
    Nl/tala/pers/fall/kyn

Nl/pers/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers/kyn →
    Nl/tala/pers/fall/kyn

Nl/tala/pers/fall/kyn →
    NlEind/tala/pers/fall/kyn

# Gefa nefnifalli forskot ef mismunandi föll eru í boði
$score(+4) Nl_nf

# Fleirtölu-nafnliður í 3p sem leyfir runu í sama falli og kyni
# Leyfir einnig 'þeir Jón og Gunnar [sendu bréfið]' eða 'þær Elísabet og mamma hennar [byggðu húsið]'
Nl_ft_p3/fall/kyn →
    NlSamaKyn/fall/kyn

NlSamaKyn/fall/kyn →
    'sá'_ft/fall/kyn? NlEind_p3/fall/kyn NlRuna/fall/kyn+ TengiliðurMeðKommu_ft_p3/kyn?

# Fleirtölunafnliðir af sama kyni eru betri en blandaðir
$score(+2) NlSamaKyn/fall/kyn

# Blandaðir fleirtölu-nafnliðir eru í hvorugkyni

# Leyfa 'maðurinn og konan' og 'kötturinn, hundurinn og bráðlega músin',
# og einnig 'sumir, þar á meðal Jón,'

Nl_ft_p3_nf_hk →
    NlEind_nf NlRuna_nf+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þf_hk →
    NlEind_þf NlRuna_þf+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þgf_hk →
    NlEind_þgf NlRuna_þgf+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_ef_hk →
    NlEind_ef NlRuna_ef+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

# Lýsingarorðaliður

# Lýsingarorð með nafnlið

Lo/fall/tala/kyn →
    AtviksliðurEinkunn* lo/fall/tala/kyn # LoViðhengi? # (Þingmenn) kosnir leynilegri kosningu
    | "mun" lo_mst/fall/tala/kyn # LoViðhengi? # (Schengen rekur) mun öflugri gagnagrunn / (þau voru) mun frjálsari skoðana sinna
    | "sem" lo_esb/fall/tala/kyn # (hvernig skuli stuðla að) sem jöfnustu hlutfalli

# Leyfa sagnorðum að standa í stað lýsingarorða ef þau
# eru í lýsingarhætti þátíðar, sterkri eða veikri beygingu
# (_vb leyfir veika beygingu ásamt sterkri)
# ('Við litum til bókfærðs hagnaðar')
Lo/fall/tala/kyn → AtviksliðurEinkunn* so_lhþt/fall/tala/kyn
Lo/fall/tala/kyn → AtviksliðurEinkunn* so_lh_nt

# 'honum er og heimilt að merkja við fleiri en einn frambjóðanda'
LoAtviksliður → AtviksliðurEinkunn* | "og"

# Lýsingarorð í sterkri beygingu
# ('Hún fór frjáls skoðana sinna í útlegð')

Lo_sb/tala/fall/kyn → LoAtviksliður lo_sb/tala/fall/kyn LoViðhengi?
Lo_sb_nf/tala/kyn → LoAtviksliður lo_sb_nf/tala/kyn LoViðhengi?

# Miðstig
Lo_mst/tala/fall/kyn → AtviksliðurEinkunn* lo_mst/tala/fall/kyn LoViðhengi? # Miðstig
Lo_mst_nf/tala/kyn → AtviksliðurEinkunn* lo_mst_nf/tala/kyn LoViðhengi? # Miðstig

# Lýsingarorð sem stendur með sögn getur haft eignarfalls- eða
# þágufalls-viðhengi, sbr.:
# ef.: 'Þau voru frjáls skoðana sinna'
# þgf.: 'Sýningin var opin almenningi'
LoViðhengi →
    EfLiður | Nl_þgf

$score(-4) LoViðhengi

# Lýsingarorð sem einkunn með nafnlið

# '(Jón), sultuslakur eftir gufubaðið, (lék á als oddi)'
# '(Maður) alvarlega háður eiturlyfjum (bað mig um peninga)'
# '(Stjórnmálamaður) tengdur Framsóknarflokknum (hringdi í mig)'
# '(Ég ræddi við) konu tengda verkefninu'
LoEftirNl/tala/fall/kyn →
    ',' LoLiður/tala/fall/kyn
    | StaktLoEftirNl/tala/fall/kyn
    | ',' LoLiður/tala/fall/kyn FsAtv? ','
    | AtviksliðurEinkunn* LoEftirNlMeðÞgf/fall/tala/kyn Nl_þgf
    | ',' AtviksliðurEinkunn* LoEftirNlMeðÞgf/fall/tala/kyn Nl_þgf LokatáknEðaKomma

StaktLoEftirNl/tala/fall/kyn →
    LoLiður/tala/fall/kyn

# Stakt lýsingarorð eftir nafnorði gæti líklega tilheyrt næsta nafnorði
$score(-2) StaktLoEftirNl/tala/fall/kyn

# 'tengdur Framsóknarflokknum'
LoEftirNlMeðÞgf/fall/tala/kyn →
    lo_sb/fall/tala/kyn # | so_lhþt/fall/tala/kyn

$score(+6) LoEftirNlMeðÞgf/fall/tala/kyn

# '(Ég drakk) hvítu, volgu og feitu mjólkina' / '(Ég sá) hvíta, volga og feita mjólk'
# '(Íslendingar fluttu fé til annarra) og í sumum tilvikum öruggari landa'

LoLiður/tala/fall/kyn → Lo/fall/tala/kyn
    | LoLiður/tala/fall/kyn LoKomma/tala/fall/kyn* KommaOgEðaEn FsRuna? Lo/fall/tala/kyn ','?

LoLiður/fall/tala/kyn → LoLiður/tala/fall/kyn

LoKomma/tala/fall/kyn →
    ',' Lo/fall/tala/kyn

# Fornöfn sem geta hegðað sér eins og lýsingarorð:
# 'Hann komst sjálfur út úr flakinu', 'Hún fór öll inn um gluggann',
# 'Þau eru bæði viðskiptafræðingar'

LoFornafn/fall/tala/kyn →
    'allur'/fall/tala/kyn | 'sjálfur'/fall/tala/kyn | 'báðir'/fall/tala/kyn

# Lýsingarorð sem andlag sagnar
# Þar kemur til greina miðstig + 'en' + nafnliður

# '(Ég var) reiður' / '(Ég var) reiðari en þú' / '(Mér var verr við þetta) en þér'
# '(Ég var) reiðastur allra' / '(Ég talaði við þann) reiðasta þeirra allra'

# Ath.: miðstig lýsingarorða er merkt MST í BÍN og passar því ekki við lo_sb
# (en frumstig sterkrar beygingar er FSB og efsta stig er ESB)
LoSamanb/tala/fall/kyn →
    Lo_sb/tala/fall/kyn
    | "mun"? Lo_mst/tala/fall/kyn EnSamanb/fall? # Miðstig: (Ég varðveitti laxinn) [mun] betur [en bleikjuna]

EnSamanb/fall →
    "en" Nl/fall

LoSamanb_nf/tala/kyn →
    Lo_sb_nf/tala/kyn
    | "mun"? Lo_mst_nf/tala/kyn EnSamanb_nf? # Miðstig: (Ég var) [mun] reiðari [en Páll]

LoTengtSögn/tala/fall/kyn →
    LoSamanb/tala/fall/kyn
    | AtviksliðurEinkunn* LoFornafn/fall/tala/kyn # (Hann komst) [hins vegar] sjálfur (út úr flakinu)
    | LoTengtSögn/tala/fall/kyn LoKommaTengdSögn/tala/fall/kyn* KommaOgEðaEn LoSamanb/tala/fall/kyn

LoKommaTengdSögn/tala/fall/kyn →
    ',' LoSamanb/tala/fall/kyn

LoTengtSögn_nf/tala/kyn →
    LoSamanb_nf/tala/kyn
    | AtviksliðurEinkunn* LoFornafn_nf/tala/kyn # (Hann komst) sjálfur (út úr flakinu)
    | LoTengtSögn_nf/tala/kyn LoKommaTengdSögn_nf/tala/kyn* KommaOgEðaEn LoSamanb_nf/tala/kyn

LoKommaTengdSögn_nf/tala/kyn →
    ',' LoSamanb_nf/tala/kyn

# Forsetningarliðir

FsMeðFallstjórn →
    fs/fall Nl/fall
    # !!! ATH: Geta eftirfarandi form staðið með nafnlið, eða aðeins með sögn?
    | fs/fall NhLiðir # 'um að...', 'í að...', 'eftir að...'
    | "á" "hendur" Nl_þgf # 'að höfða sakamál á hendur Páli'
    | "nær" 'draga'_et_p3 Nl_þgf # 'þegar nær dró/dregur jólum'
    | 'það_sem_af_er' Nl_þgf # 'það sem af er þessu þingi'
    | "miðað" "við" Nl_þf
    | 'miðað_við' Nl_þf
    | "þegar" "kemur" "að" Nl_þgf
    | 'til_og_frá' Nl_þgf
    | "fyrir" "utan" Nl_þf
    | "hvað" FsHvaðVarðar
    | "um" 'það'_þf_et? FsSamtenging HreinYfirsetning # 'um [það] hvort Brandur sé fallegastur'
    | "um" "að" HreinYfirsetning ','? # 'um að lög um endurupptökunefnd standist ekki stjórnarskrá'
    | FsÍGegnUm
    | FsAðBaki

    | '(' FsMeðFallstjórn ')'

FsÍGegnUm →
    "í" "gegn" "um" Nl_þf
    | "í" "gegnum" Nl_þf

$score(+6) FsÍGegnUm

FsAðBaki →
    "að" "baki" Nl_þgf # 'að baki hverju bundnu sæti'

$score(+4) FsAðBaki

FsSamtenging →
    SpurnarSamtenging
    # Leyfa verður 'hvort' og 'hvenær' sérstaklega hér þar sem orðin eru samtengingar í BÍN
    # og falla ekki undir SpurnarSamtengingu (nema með forsetningarlið með sér)
    | "hvort"
    | "hvenær"

So_1_gm_fh_p3_et_nf → so_1_nf_gm_fh_p3_et
So_1_gm_fh_p3_et_þf → so_1_þf_gm_fh_p3_et
So_1_gm_fh_p3_et_þgf → so_1_þgf_gm_fh_p3_et
So_1_gm_fh_p3_et_ef → so_1_ef_gm_fh_p3_et

FsHvaðVarðar →
    Nl/fall So_1_gm_fh_p3_et/fall  # '(hvað) Pál varðar/Páli viðvíkur'
    | So_1_gm_fh_p3_et/fall Nl/fall # '(hvað) varðar Pál/viðvíkur Páli'

FsLiður → Atviksliður? FsMeðFallstjórn # snarlega í bæinn

# FsAtv getur verið runa af forsetningarliðum með atviksliðum inn á milli,
# á undan og í lok - eða einungis atviksliðir.

FsAtv →
    ','? EinFsAtv+ FsAtvFramhald* ','?

FsAtvFramhald → KommaOgEðaEn EinFsAtv+

EinFsAtv → "nema"? FsMeðFallstjórn | EinnAl

# AtvFs hefst alltaf á atvikslið en á eftir geta fylgt forsetningarliðir
# og atviksliðir.

AtvFs →
    ','? EinnAl EinFsAtv* FsAtvFramhald* ','?

# Taka sérstaklega á runu forsetningarliða sem er afmörkuð milli komma;
# það minnkar tvíræðni

FsLiðurMeðKommu →
    FsLiður
    | FsLiður KommaOgEðaEn? Atviksliður? FsLiðurMeðKommu

# FsRuna getur ekki endað á atvikslið, nema innan kommu
FsRuna → ',' FsLiðurMeðKommu Atviksliður? ','
    | FsLiðurMeðKommu

FsRunaEftirNl →
    FsRuna

# Hvetja létt til þess að forsetningar séu tengdar nafnlið
$score(+1) FsRunaEftirNl

# Atviksliður

Al → EinnAl+

EinnAl →
    ','? ao             # Strax tvöfalt til baka
    | Dagsetning        # (Ég tilkynnti þetta) strax 3. september
    | Tímasetning
    | EkkiNeitt
    | UndirEins
    | "eina" "ferðina" "enn"

EkkiNeitt →
    "ekki" "neitt"      # (Ég þekki þetta) ekki neitt

$score(-6) EkkiNeitt

UndirEins →
    "undir" "eins"    # 'Ég fór undir eins og kannaði málið'

$score(+4) UndirEins

Atviksliður →
    Al OgAtviksliður*

OgAtviksliður →
    KommaOgEðaEn Al          # Hratt, en örugglega og fumlaust

# Fanga hugsanlegan atvikslið innan kommu:
# Fyrirtækin annars vegar, og heimilin hins vegar, hafa dregið úr fjárfestingu
AtvKomma →
    Atviksliður? ','

AtviksliðurKomma →
    Atviksliður ','?

AtviksliðurEinkunn →
    eo                  # Einkunnarorð = atviksorð sem geta ekki jafnframt verið forsetningar
    | Dagsetning        # (hagnaðurinn var strax) árið 2012 (175 milljarðar króna)
    | Tímasetning

# Dagsetning og tími

Tímasetning →
    "klukkan"? tími
    | "klukkan" töl
    | "klukkan" "hálf"? to_nf_hk

Dagsetning →
    DagsFs_þf? Tímasetning 'sá'_et_kk_þf? dags # '[eftir] kl. 14 [þann] 3. júlí 2007'
    | DagsFs_ef Tímasetning 'sá'_et_kk_þf? dags # 'til kl. 14 [þann] 3. júlí 2007'
    | DagsFs_þgf Tímasetning 'sá'_et_kk_þf? dags # 'frá kl. 14 [þann] 3. júlí 2007'

    | "dags." dags # dags. 30. desember 1965
    | DagsFs_þf? 'sá'_et_kk_þf? dags 'síðastliðinn'_et_kk_þf? # '[eftir] [þann] 3. júlí 2007 [síðastliðinn]'
    | DagsFs_þgf dags # 'frá 3. júlí 2007'
    | DagsFs_ef dags # 'til 3. júlí 2007'

    | Vikudagur_þf_et dags 'næstkomandi'_et_kk_þf? # 'föstudaginn 29. júlí [næstkomandi]'
    | 'næstkomandi'_et_kk_þf Vikudagur_þf_et # 'næstkomandi fimmtudag'
    | 'síðastliðinn'_et_kk_þf Vikudagur_þf_et # 'síðastliðinn fimmtudag'

    | DagsFs_þf? 'sá'_et_kk_þf? tímapunktur # '[eftir] [þann] 3. júlí 2007 kl. 13:00'
    | DagsFs_þgf tímapunktur # 'frá 3. júlí 2007 kl. 13:00'
    | DagsFs_ef tímapunktur # 'til 3. júlí 2007 kl. 14:15'

    | DagsFs_þf? Tímasetning Vikudagur_þf_et dags # '[fyrir] kl. 13 sunnudaginn 1. janúar 2012'

    | DagsFs_þgf Tímasetning Vikudagur_þf_et dags # 'frá kl. 13 sunnudaginn 1. janúar 2012'

    | DagsFs_ef Tímasetning Vikudagur_þf_et dags # 'til kl. 13 sunnudaginn 1. janúar 2012'

    | Árstíð_þf_et ártal # 'haustið 2012'
    | Árið_þf # 'árið 2012'
    | Árin_þf # 'árin 2010-12'

    # 'Undanfarið' er atviksorð í BÍN, en 'undanfarnar vikur' o.s.frv. virkar einnig
    # eins og atviksliður (eða forsetningarliður), sbr. 'ég hef undanfarnar vikur verið að velta því fyrir mér'...
    | 'undanfarinn'_þf/tala/kyn NlStak_þf/tala/kyn # undanfarnar sex vikur, undanfarin ár, undanfarinn mánuð
    | 'undangenginn'_þf/tala/kyn NlStak_þf/tala/kyn # undangengnar sex vikur
    | 'allur'_þf/tala/kyn? gr_þf/tala/kyn? 'síðari'_þf/tala/kyn NlStak_þf/tala/kyn # síðustu sex vikur, hin síðari ár, allan síðasta mánuð
    | TímabilsEinkunn_þf/tala/kyn Tímabil_þf/kyn/tala # (Hann kom hér) alla daga, suma vikudaga, nokkur ár, eina vikuna
    | TímabilsEinkunn_þgf/tala/kyn Tímabil_þgf/kyn/tala TímaAfstæði_þgf? # (Námsönnin hefst) þremur mánuðum áður en jólafríið byrjar
    | Tímabil_þgf/kyn/tala TímaAfstæði_þgf # Klukkutíma fyrr kom ég á staðinn
    | Tímabil_ft_þgf/kyn "saman" # Nefndin fundaði ekki mánuðum saman
    | Tímabil_þf/kyn/tala TímaAfstæði_þf # Vikuna áður / Daginn eftir
    | TímaForskeyti? TímaAfstæði # áður en krónan féll / eftir að landið öðlaðist sjálfstæði

# Gefa plús fyrir dagsetningar og tímasetningar
$score(+4) Dagsetning Tímasetning

TímaAfstæði_þgf → "síðar" | "seinna" | "fyrr" TímaEn? | "áður" TímaEn?
TímaAfstæði_þf → "áður" TímaEn? | "eftir" TímaAð? | "fyrir" Nl_þf
TímaForskeyti → "löngu" | "stuttu" | "töluverðu" | "örstuttu"
TímaAfstæði → "áður" TímaEn | "eftir" TímaAð

TímaEn →
    "en" "að"? HreinYfirsetning

TímaAð →
    "að" HreinYfirsetning

TímabilsEinkunn/fall/tala/kyn →
    fn/fall/tala/kyn          # alla/suma/nokkra
    | Töluorð/fall/tala/kyn   # fjóra/4

TímabilsEinkunn_þf/tala/kyn →
    'næstur'/tala/kyn            # næsta vetur, næstu daga

Tímabil/fall/kyn/tala → Tímabil/kyn/fall/tala

Tímabil_ft_þgf_kk → Tímabil_kk_þgf_ft
Tímabil_ft_þgf_kvk → Tímabil_kvk_þgf_ft
Tímabil_ft_þgf_hk → Tímabil_hk_þgf_ft

Tímabil_hk/fall/tala →
    'ár'/fall/tala
    | 'misseri'/fall/tala
    | 'sumar'/fall/tala
    | 'vor'/fall/tala
    | 'haust'/fall/tala
    | 'kvöld'/fall/tala
    | 'hádegi'/fall/tala
    | 'kjörtímabil'/fall/tala
    | 'leikár'/fall/tala

Tímabil_kvk/fall/tala →
    'vika'/fall/tala
    | 'nótt'/fall/tala
    | 'mínúta'/fall/tala
    | 'sekúnda'/fall/tala
    | 'leiktíð'/fall/tala

Tímabil_kk/fall/tala →
    'mánuður'/fall/tala
    | 'ársfjórðungur'/fall/tala
    | 'dagur'/fall/tala
    | 'vikudagur'/fall/tala
    | 'helgidagur'/fall/tala
    | 'virkur'/fall/tala 'dagur'/fall/tala
    | 'vetur'/fall/tala
    | 'morgunn'/fall/tala
    | 'eftirmiðdagur'/fall/tala
    | Vikudagur/fall/tala
    | 'klukkutími'/fall/tala
    | 'tími'/fall/tala

Vikudagur/fall/tala →
    # !!! Do the following in the tokenizer instead?
    'sunnudagur'/fall/tala
    | 'mánudagur'/fall/tala
    | 'þriðjudagur'/fall/tala
    | 'miðvikudagur'/fall/tala
    | 'fimmtudagur'/fall/tala
    | 'föstudagur'/fall/tala
    | 'laugardagur'/fall/tala

Árstíð_þf/tala →
    'haust'_þf/tala
    | 'vor'_þf/tala
    | 'sumar'_þf/tala
    | 'vetur'_þf/tala

# Forsetningar sem hægt er að nota með dagsetningum
DagsFs_þf →
    "fyrir" | "eftir"
DagsFs_ef →
    "til"
DagsFs_þgf →
    "frá"
