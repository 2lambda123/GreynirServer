#
# Reynir.grammar
#
# Grammar specification file for Reynir,
# a natural language processor for Icelandic
#
# Copyright (C) 2017 Miðeind ehf.
#
#    This program is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see http://www.gnu.org/licenses/.
#
#
# This file contains an extensive (but not complete) context-free grammar for parsing
# Icelandic text.
#
# The grammar is presented in augmented Backus-Naur form, where
# repetition specifiers are allowed on right-hand-side tokens
# (* for 0..n, + for 1..n, ? for 0..1 occurrences).
#
# Rules and productions are autogenerated for all variants
# (combinations) of applicable grammatical features, i.e. genders (m, f, n),
# cases, numbers (singular/plural) and persons (1st, 2nd, 3rd).
#
# As an example, the generic nonterminal Undirsetning/tala/fall,
# having variants for /tala (singular/plural) and /fall (case),
# is automatically expanded to 8 specific nonterminals:
# Undirsetning_et_nf, Undirsetning_et_þf, etc.,
# with corresponding substitutions being made in the right-hand-side
# productions (et = singular, nf = nominative...).
# The permissible variants are defined below.
#
# Nonterminal identifiers start with uppercase letters.
# Terminals start with lowercase letters.
#
# Literals within single quotes 'xyz' are matched with the stem
# of a word token, plus eventual variants.
#
# Literals within double quotes "xyz" are matched with the
# text of a word token as it appeared in the source, albeit after
# conversion to lowercase. Variants cannot be applied to double-quoted
# literals.
#
# For disambiguation in POS tagging, literals may specify an optional
# word category after a colon. Thus "og:st" means that this "og" is
# a conjunction, not an adverb (which would be specified as "og:ao").
#
# This feature is occasionally used for typographic purposes, i.e.
# to distinguish between em dashes ("-:em") and regular hyphens ("-").
#
# Terminal names generally take the form category_variant1_variant2...,
# where the most common categories are:
# * no for nouns (nafnorð)
# * so for verbs (sagnorð)
# * lo for adjectives (lýsingarorð)
# * fs for prepositions (forsetningar)
# * st for connecting words (samtengingar)
# * fn for pronouns (fornöfn)
# * ao for adverbs (atviksorð)
# * töl for number words (töluorð)
# * gr for determiners (greinir)
#
# The terminal names are matched against POS tags from the BIN
# database in the module binparser.py.
#
# Alternative productions are separated with the '|' (vertical bar) sign.
# If there is a priority ordering between the alternatives, so that
# a single highest priority alternative should be automatically
# selected from possible alternatives, use the '>' (greater than)
# sign between the productions instead.
#
# For the Sublime Text editor, a syntax highlighting specification
# for .grammar files, grammar.YAML-tmLanguage, is available.
# It can be found in the resources folder and its
# use is highly recommended.


# VARIANT DEFINITIONS

# Gender (m, f, n) variant
/kyn = kk kvk hk
/nkyn = kk kvk # Natural gender (male, female)

# Case (nom, acc, dat, poss) variant
/fall = nf þf þgf ef
/aukafall = þf þgf ef # All cases except nominative

# Number (singular/plural) variant
/tala = et ft

# Person (1st, 2nd, 3rd) variant
/pers = p1 p2 p3


# GRAMMAR RULES

# Note: all comments from this point are in Icelandic

# Setningargerðir

# Merkið > gefur til kynna forgangsröðun.
# Ef báðar koma til greina er Málsgrein tekin fram yfir Grein o.s.frv.
S0 → Málsgrein > Grein > Fyrirsögn

Málsgrein →
    MgrInnihald
    | '(' MgrInnihald ')'
    | '[' MgrInnihald ']'

MgrInnihald → Forskeyti? Yfirsetning StLiður* Tilvitnun? Lokatákn?

Forskeyti →
    Samtenging "-:em"? # 'En - þegar allt er skoðað, var þetta fínt.'
    | Samtenging? uh TengimerkiVh Samtenging? # Og já, þetta var ekki gott / Nei - en hann átti ekkert svar
    | raðnr_nf_kvk # Raðnúmer málsgreinar
    | "-:em"   # Þankastrik á undan setningu (allar gerðir strika varpast í '-')
    | 'mynd:kvk'_nf_et tala_nf_kvk AfTala? ':'? # Mynd 2 af 10
    | 'dæmi:hk'_et_nf tala_nf_hk? ':'
    | 'sjá:so'_gm_nh "einnig"? ':'
    | "semsagt:ao" ':'
    | "sem:st" 'segja:so'_lhþt_sb_hk_nf_et ':'
    | 'aðferð:kvk'_nf_et ':'
    | 'með_öðrum_orðum:ao' ':' | "með:fs" 'annar:lo'_hk_þgf_ft "orðum:hk" ':'
    | Greinarnúmer ':'

AfTala → "af:fs" tala_þgf_kvk

$score(-6) Forskeyti # Ekki freistast í forskeyti ef annað er í boði

Tilvitnun →
    ',' SögnSegja/tala/pers Nl_nf/tala/pers AtvFs? # , segir/fullyrðir Jón Ásmundsson, fulltrúi samtakanna, í dag í útvarpinu
    | ',' 'segja:so'_gm_fh_p3_et "í:fs" Nl_þgf AtvFs?  # , segir í tilkynningu ráðuneytisins í gær

SögnSegja/tala/pers →
    'segja:so'_gm_fh/tala/pers
    | 'flissa:so'_gm_fh/tala/pers
    | 'fullyrða:so'_gm_fh/tala/pers
    | 'staðhæfa:so'_gm_fh/tala/pers

$score(+32) Tilvitnun # Þarf að vera meiri plús en t.d. SagnFramhald

Lokatákn → '.' | '?' | ':' | '!' | '(' '!' ')'
LokatáknEðaKomma → Lokatákn | ','

Fyrirsögn →
    Forskeyti? FyrirsögnInnihald

FyrirsögnInnihald →
    # Nafnliður í nefnifalli
    Nl_nf Atviksliður? Eignun? Lokatákn?
    | SetningÁnF Lokatákn?

Eignun →
    # Mynd: Jón Stefánsson
    # Erlendur: Friðgeir er frábær
    ':' Nl_nf

SetningÁnF →
    # Setning án frumlags: 'Stefndi í átt til Bláfjalla', 'Aftengdi sjálfstýringu'
    SetningÁnF_et_p3/kyn
    | SetningÁnF_ft_p3/kyn

Greinarnúmer →
    # Greinarnúmer, e.t.v. skammstafað, og e.t.v. með fyrirsögn
    raðnr_nf_kvk 'grein:kvk' # 'grein:kvk'_nf_et !!! Ef tilbrigðin eru með þá passar skammstöfunin gr. ekki við laufið

Grein →
    # Sjálfstæð fyrirsögn greinar
    Greinarnúmer ':'? Fyrirsögn? # 17. gr. Störf Alþingis [.]

$score(+6) Grein

SpurnarorðAtv →
    "hve:ao" | "hversu:ao"

StLiður →
    # Samtengingar yfirsetninga
    Tengimerki? Samtenging HreinYfirsetning
    | Tengimerki Yfirsetning
    # 'Það eru bara tveir kostir í boði: Selja bankann eða láta hann fara í þrot'
    | ':' SagnRuna
    # 'Aftur er umræðan sú sama: um rétt kirkjunnar þegna til að neita að gefa saman samkynhneigða'
    | ':' NhFylling
    # '(Ég spurði) hversu hrikalega stór (hann væri orðinn)'
    | SpurnarorðAtv Lo_nf/tala/kyn Setning/tala/pers/kyn
    # '(Ekki er ljóst) hverjir [það voru sem] borðuðu kökuna og fóru í sund'
    | StSpurn
    # Flokkurinn stefnir á stjórnarþátttöku, fái hann til þess umboð
    | StViðtenging

StViðtenging →
    TengimerkiVh? SagnliðurVh

$score(+4) StViðtenging

StSpurn →
    'hver:fn'_nf/tala/kyn ÞaðVarSem/tala? SagnRuna_p3/tala/kyn

ÞaðVarSem/tala →
    'það:fn'_nf_et VeraVerða_gm_p3/tala "sem:st"

Tengimerki →
    ':' | ';' | ',' | "-:em"

TengimerkiVh →
    ';' | ',' | "-:em"

SamtengingStök →
    # Ein samtenging, sem þó getur verið samsett, sbr. skilgreiningar í Reynir.conf
    st

Samtenging →
    # Flóknari samtenging úr mörgum orðum
    SamtengingStök+
    | "þannig:ao" "að:st"
    | 'það:fn'_nf_et "er:so" | 'það_er:ao'
    | 'áðan:ao'_mst "en:st" "að:st"? # áður en [að]
    | 'snemma:ao'_mst "en:st" "að:st"? # fyrr en [að]
    | 'á_meðan:ao' "að:st"?
    | "allt:fn" "frá:fs" "því:fn" "að:st"?
    | "þar:ao" "á:fs" "meðal:ao" | 'þar_á_meðal:ao'
    | "þar:ao" "sem:st"
    | 'auk_þess:ao' "sem:st" "að:st"?
    | "eins:ao" "og:st"
    | "vegna:fs" "þess:pfn" "að:st"?
    | "á_sama_tíma:ao" "og:st"
    | "eða:st"? 'allar_götur_síðan:ao'
    | 'það_er_að_segja:ao' Tengimerki "að:st"?
    | ÞangaðTil
    | OgEðaEn? SpurnarSamtenging

$tag(apply_length_bonus) Samtenging

SpurnarSamtenging →

    HverHvor/kyn SpurnarFs/kyn?                 # '(Ég spurði) hvert af börnunum (þarna væri á ferðinni)'

    # af hverju, í hverju, í hvern, í hverja, til hvers, frá hverjum...
    | fs/fall 'hver:fn'/fall/tala/kyn FsRuna?   # '(Ég vildi vita) af hverju í ósköpunum (Jón hætti við)'
    | "afhverju:ao" FsRuna?

    | 'hver:fn'_hk_ef_et "vegna:fs" FsRuna?     # '(Ég spurði) hvers vegna í veröldinni (hann tók ekki símtalið)'
    | "hví:ao" FsRuna?
    | "hvar:ao" FsRuna?                            # '(Ég spurði) hvar (matarstellið væri)'
    | "hvernig:ao" FsRuna?                         # '(Ég spurði) hvernig (Páll ætlaði að fara að þessu)'
    | "hvort:st" "og:st" "hvernig:ao" FsRuna?      # '(Tekjustofnar voru til umræðu og) hvort og hvernig (þeir skyldu nýttir)'

    | fs/fall 'hvaða:fn'/fall/tala/kyn Nl/fall/tala/kyn AtvFs? # '(Ég spurði) í hvaða yfirmann (þeir hefðu hringt)'
    | fs/fall SpurnarorðAtv Nl/fall AtvFs?      # '(Ég spurði) í hversu miklum mæli utan heimilda (þeir hefðu urðað sorpið)'

    | SpurnarorðAtv SpurnarBotn FsRuna?         # '(Ég spurði) hversu miklu bensíni af þessari tegund (hann ætlaði að skila)'

    # hvort og hvenær eru samtengingar í BÍN og því þarf ekki reglu fyrir þær einar og sér
    # heldur aðeins með forsetningarrunu (hvenær í ósköpunum hann ætlaði að koma)
    # | "hvort" FsRuna # ?hvort í ósköpunum hann ætlaði að hætta
    | "hvenær:st" FsRuna

ÞangaðTil →
    "þangað:ao" "til:st" "að:st"?

$score(+12) ÞangaðTil

HverHvor/kyn →
    'hver:fn'_nf_et/kyn | 'hvor:fn'_ft_et/kyn

SpurnarFs/kyn →
     Nl_ef_ft/kyn FsRuna?                       # hvert barnanna, hver kvennanna, hvor þeirra
     | FsRuna                                   # hvern í bænum, hvorn af þeim

SpurnarBotn →
    LoLiður/tala/fall/kyn Nl/fall/tala/kyn      # (hversu) miklu bensíni (af þessari tegund)
    | Atviksliður                               # (hversu) mikið (af hveiti þyrfti í uppskriftina)

Yfirsetning →
    # Yfirsetning getur hafist á runu upphrópana
    Upphrópun* HreinYfirsetning

Upphrópun →
    uh+ ',' # Jú jú, ég fór út í dag / Nei, hann kemur ekki.

HreinYfirsetning →
    Setning
    | SetningAukafall
    | SetningLo
    | SetningSo
    | SetningSkilyrði

$score(+2) HreinYfirsetning

$score(-2) SetningAukafall
$score(+2) SetningLo
$score(-1) SetningSo
$score(+3) Setning

$tag(begin_prep_scope) HreinYfirsetning # Nýtt undirtré fyrir tengingu sagnar og forsetningar

# Skilyrði: EfÞegar eitthvað þá eitthvað

SetningSkilyrði →
    AtviksliðurKomma? EfÞegar OgEfÞegar? Skilyrði Afleiðing FsAtv?

$score(+12) SetningSkilyrði

EfÞegar →
    "ef:st" | "þegar:st" | "er:st" | "þótt:st" | "þó:st" | "þó:ao" "að:st" | "þó:ao" "svo:ao" "að:st" | 'jafnvel_þótt:st'
    | "þar:ao" "sem:st" | "eftir:ao" "að:st"
    | "eins:ao" "og:st" | 'líkt_og:st' | 'á_meðan:st' | "meðan:st"
    | "þangað:ao" "til:st" | 'um_leið:ao' "og:st" | "síðan:st" | 'þrátt_fyrir_að:st'

OgEfÞegar →
    "og:st" EfÞegar              # 'Ef og þegar eitthvað gerist læt ég þig vita'

Skilyrði →
    HreinYfirsetning StLiður* # (Ef) Jón fer með hundinn til dýralæknis og allt er í lagi
    | SagnliðurÁnF            # (Þegar) kemur að því að útdeila gæðum

# Afleiðing í "ef"-setningu byrjar alltaf á sögn

Afleiðing →
    ","? "þá:st"? SagnHluti/tala/pers/kyn   # (Ef hann gæti) [,] [þá] opinberaði Hannes niðurstöðu sína (í dag)
    | ","? "þá:st"? SagnliðurÁnF            # [Ef góða veðrið helst] [,] [þá] þyrfti að vökva grasið (fyrir myrkur)

# Setningar með frumlagi

Setning → Setning/tala/pers/kyn

# Tilvik þar sem einu frumlagi fylgja nokkrir sagnliðir:
# 'Anna tók bílinn, keyrði hann sjálf í bæinn [,] og keypti í matinn'

SagnRuna → SagnRuna/tala/pers/kyn

SagnRuna_p3_et/kyn → SagnRuna_et_p3/kyn
SagnRuna_p3_ft/kyn → SagnRuna_ft_p3/kyn

SagnRuna/tala/pers/kyn →
    Sagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskot?
        SagnFramhald/tala/pers/kyn*

SagnFramhald/tala/pers/kyn →
    SagnKomma/tala/pers/kyn* KommaOgEðaEn FsAtv? Sagnliður/tala/pers/kyn
        LoTengtSögn_nf/tala/kyn? SagnInnskot?

SagnKomma/tala/pers/kyn →
    ',' Sagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskot?

# Hvetja til framhalds sömu sagnrunu, fremur en að skipta niður í fleiri setningar
$score(+5) SagnFramhald/tala/pers/kyn
$score(+2) SagnKomma/tala/pers/kyn

# Sagnir innan undirtrés geta ekki orsakað bónus á forsetningar utan þess
$tag(purge_verb) SagnRuna/tala/pers/kyn

SagnInnskot →
    FsAtv

# Gefa aukabónus fyrir forsetningar innan SagnInnskots ef þær passa við umlykjandi sögn
$tag(enable_prep_bonus) SagnInnskot

SagnViðhengi/tala/pers/kyn →
    KommaOgEðaEn SagnRuna/tala/pers/kyn

# Setningarhluti sem byrjar á sögn

SagnHluti/tala/pers/kyn →
    SagnliðurMeðF/tala/pers/kyn SagnInnskot? SagnViðhengi/tala/pers/kyn?

# Sagnir innan undirtrés geta ekki orsakað bónus á forsetningar utan þess
$tag(purge_verb) SagnHluti/tala/pers/kyn

FrumlagsInnskot/tala/pers/kyn →
    ',' SetningÁnF/tala/pers/kyn ','
    | ',' Samtenging HreinYfirsetning ','  # Lögheimili á Íslandi, þegar kosning fer fram, er skilyrði
    | InnskotsSamtenging HreinYfirsetning # Lögheimili á Íslandi þegar kosning fer fram er skilyrði

# Þröngt mengi samtenginga sem nota má án kommu í frumlagsinnskoti
# (sé mengið of vítt eykst margræðni verulega)
InnskotsSamtenging →
    "þegar:st" | "meðan:st" | 'á_meðan:st' | "þar:ao" "sem:st"

Frumlag/pers/tala/kyn →
    Nl_nf/tala/pers/kyn

# Hakk til að láta 1.p. og 2.p. virka með fornafninu "allur",
# sem annars er aðeins brúklegt í 3.p.

Frumlag_p1/tala/kyn →
    'allur:fn'_nf/tala/kyn # Öll erum við jöfn fyrir lögum

Frumlag_p2/tala/kyn →
    'allur:fn'_nf/tala/kyn # Allar eruð þið jafnar fyrir lögum

Samanburðarmerki → ',' | ';' | "-:em"

Samanburður →
    SamanburðarTenging Nl_nf AtvFs? # þar af einn alvarlega
    | Samanburðarmerki? "þar:ao" "með:fs" 'telja:so'_lhþt_sb_nf/tala/kyn Nl_nf/tala/kyn AtvFs? # þar með talinn allur þingflokkur Framsóknar
    | Samanburðarmerki "öðru:fn" "nær:ao"   # Viðskiptablaðið styður ekki jafnaðarstefnuna; öðru nær.

SamanburðarTenging →
    "heldur:st" "en:st"             # 'Forseti á ekki að gera meiri breytingu á sér heldur en forsætisráðherra'
    | ','? "eins:ao" "og:st"        # 'Hann á ekki andstæðinga eins og aðrir kjörnir fulltrúar'
    | ','? 'líkt_og:st'
    | Samanburðarmerki? "þar:ao" "af:ao"  # 'Fjöldi barna var í vélinni, þar af tvö kornabörn'
    | Samanburðarmerki? 'þar_á_meðal:ao'
    | Samanburðarmerki? "þar:ao" "á:fs" "meðal:ao"
    | Samanburðarmerki? 'þar_með_talið:ao' # Skammstöfunin 'þ.m.t.'
    | Samanburðarmerki? "nema:st"   # Allir vita það, nema ég.
    | ','? "enda:st"                # 'Réttindi í almennum sjóðum breyttust, enda lægri fjárhæð en áður'
    | SamanburðarTengingOg

SamanburðarTengingOg →
    ','? "og:st"               # Erfitt fjarskiptasamband er á staðnum og mikil snjóþyngsli

$score(-4) Samanburður
$score(-8) SamanburðarTengingOg

Sjálfur/tala/kyn → 'sjálfur:fn'_nf/tala/kyn

# 'Þrír þingmenn greiddu atkvæði gegn tillögunni: Jón, Sigríður og Guðrún.'
# 'Aðeins einn nemandi var með þetta rétt; strákurinn í rauðu skyrtunni.'
Formerki → ':' | ';' | "-:em"

Upptalning/tala → Formerki Nl_nf/tala

Setning/tala/pers/kyn →
    # Venjuleg setningarskipan með frumlagi (í nafnhætti) fremst:
    # 'Jón sterki fór bersýnilega í bága við reglurnar með þessu'
    # 'Katrín formaður félagsins hefur augljóslega verið alveg hnífskörp í gær'
    # 'Rúmlega helmingur tvítugra karlmanna vill flugvöll í Vatnsmýri'
    # 'Ég bara spyr [sjálf]'
    # 'Allir mega koma til landsins [sjálfir] sem það vilja'
    Frumlag/pers/tala/kyn FrumlagsInnskot/tala/pers/kyn? AtviksliðurEinkunn*
        SagnRuna/tala/pers/kyn Sjálfur/tala/kyn? KommaTengiliður/tala/pers/kyn?
        Samanburður? Upptalning/tala?

    # Frumlag fyrir aftan sögn:
    # 'Gengu þau galvösk á Esjuna þrátt fyrir veðurútlitið'
    # 'Bersýnilega fór Jón sterki í bága við reglurnar með þessu'
    # 'Undir húsinu voru illa farnar skolpleiðslur'
    # 'Í skólanum borðaði Gunna aldrei matinn eftir þetta'
    # 'Í grein sinni opinberaði Hannes niðurstöðu sína'
    # Ath. að frumlag 3p. et. getur verið falið:
    # (? Það virtist mér í dag kettinum líða betur)
    # 'Í dag virtist mér kettinum líða betur'
    # 'Eins og Páll er ég mjög ósáttur við þetta'
    | SetningForskeyti? SagnHluti/tala/pers/kyn Upptalning/tala?

    # 'Það fara 40 mínútur í pappírsvinnuna'
    # (40 mínútur fara í pappírsvinnuna)
    | SetningÞað/tala/pers/kyn

Setning_et_p3_hk →
    SetningAð
    # 'Það hefði kostað Pál vinnuna að kvarta yfir verkstjóranum í dag'
    # 'Nú þarf/þyrfti að hugsa út fyrir kassann'
    # 'Ætíð verður að gæta hagsmuna almennings'
    | Upphaf_p3_nh SagnRuna_et_p3_hk NhFyllingAtv
    # Um er/var að ræða þriggja ára stelpu
    # (-> Það er um að ræða þriggja ára stelpu)
    | SetningUmAðRæða

$tag(begin_prep_scope) Setning/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

Upphaf_p3_nh → 'það:pfn'_nf_et | ao+

SetningÞað/tala/pers/kyn →
    'það:pfn'_nf_et SagnHluti/tala/pers/kyn
    # Það er/var/væri verið að reka fólk burt af jörðum sínum
    # ('verið' er ekki til sem LHÞT, en það er hins vegar 'búið' o.fl.)
    | 'það:pfn'_nf_et 'vera:so'_gm_p3_et 'vera:so'_sagnb NhFyllingAtv

$score(-2) SetningÞað/tala/pers/kyn # Gefa lægri forgang en setningum með "það" sem frumlag

SetningAð →
    # 'Að raða frímerkjum er góð skemmtun'
    NhLiður FsAtv? SagnRuna_et_p3_hk

$score(-2) SetningAð # Óalgengt form

SetningUmAðRæða →
    "um:fs" 'vera:so'_gm_p3_et "að:nhm" 'ræða:so'_gm_nh Nl_þf AtvFs?

$score(+20) SetningUmAðRæða # Mjög líklega réttur skilningur á þessari orðaröð

SetningForskeyti →
    FsAtv
    | "eins:ao" "og:st" Nl_nf       # 'Eins og Páll borða ég ekki smjör'
    | 'líkt_og:st' Nl_nf            # 'Líkt og bakarar eru smiðir óhressir með þetta'

# Setning sem byrjar á lýsingarorði í stað "það"
# 'Óheimilt er (að fangelsa mann fyrir engar sakir)'
# 'Óhætt má telja (að flokkurinn vinni kosningarnar)'
# 'Öruggt hefur verið talið [að fara Vatnaleiðina í þurru veðri]'
# 'Ljóst er (að Brandur var fallegasti kötturinn)'
SetningLo →
    LoTengtSögn_nf_et_hk SetningLoSögn NhFyllingEðaAtv
    # Kjörgengur við Alþingiskosningar skal vera hver sá ríkisborgari (sem dvalið hefur 5 ár í landinu)
    | LoTengtSögn_nf/tala/kyn FsRuna? HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs?
    # Aðspurður sagði Jón framburð Gunnu vera uppspuna frá rótum
    # Örvingluð eftir atburði næturinnar leitaði Anna til lögreglunnar
    | LoTengtSögn_nf/tala/kyn FsAtv? SagnHluti/tala/pers/kyn
    | SetningLoMeðSpurningu
    # Frábært að heyra [í þér]
    # Yndislegt að sjá og heyra
    | LoTengtSögn_nf_et_hk NhLiðir FsAtv?
    # Gaman að sjá til þín
    | "gaman:ao" NhLiðir FsAtv?

SetningLoMeðSpurningu →
    # Aðspurð [um] hvort Jón væri edrú sagði Gunna að hann væri það
    lo_sb_nf/tala/kyn "um:fs"? SpurnarSamtenging HreinYfirsetning SagnHluti/tala/pers/kyn

$score(+4) SetningLoMeðSpurningu

SetningLoSögn →
    HjSögnLhÞtSM_et_p3 HreinSögn_lhþt_et_hk? # er/skal/mun/hefur verið talið/þótt...
    | HjSögnMegiSkuli_et_p3? so_0_mm_p3_et # sýnist/virðist/telst, má teljast
    | HjSögnMegiSkuli_et_p3 so_0_nh # má telja, skal ætla
    | 'þykja:so'_et_p3 # '(öruggt) þykir (að Jón vinni mótið)'
    | 'vera:so'_gm_p3_et 'vera:so'_sagnb # Hægt og bítandi er/var/væri verið (að ýta fólki burt)

# Ekkert frumlag í nefnifalli, sögn sem sagnbót (oft í miðmynd):
# 'Í dag er búist við vondu veðri eftir hádegi'
# (í stað 'Það er búist við vondu veðri eftir hádegi í dag')
# 'Á morgun verður lent á neyðarbrautinni'
# 'Fylgst verður með kosningunum á RÚV næstu daga'
# (í stað 'Það verður fylgst með kosningunum á RÚV næstu daga')
# 'Í dag má sækja tölvuna'
# (í stað 'Það má sækja tölvuna í dag')

SetningSo →

    So_1_p3/fall/tala Nl/fall Nl_nf/tala AtvFs?                # Fylgir honum mikil úrkoma [í nótt]

    # Leitað verður/skal/mun til sérfræðinga
    # Verið er í snatri að ráða hæfasta umsækjandann
    # Komið hefur/hafi til átaka milli þeirra
    # Farið hefur verið ofan í saumana á málinu
    | Sagnliður_sagnb

    | So_1_sagnb/fall HjSögnLhÞtSM_et_p3 FsAtv? Nl/fall AtvFs? # Horft hefur verið [til hennar] lostafullum augum

    | so_0_mm_sagnb HjSögnLhÞtSM_et_p3 NhFyllingEðaAtv         # Búist er við (að Sigmundur Davíð segi af sér)

    | so_0_mm_nh HjSögnMegiSkuliÞurfi_et_p3 SagnInnskot        # Búast má/skal/þarf/hefur mátt við töfum á veginum

    | so_0_mm/pers/tala FsAtv? Nl_nf/tala/pers AtvFs?          # Fórust með honum átján menn

    | FsAtv? SagnliðurÁnF # NhFyllingEðaAtv                      # [Með lögum] skal tryggja rétt almennings til að fara um landið

    | FsAtv VeraSkulu_et_p3 HreinSögn_sagnb_et_hk NhFyllingEðaAtv # Með lögum er/verður/skal/mun skipað rétti útlendinga til að dveljast hér

    | VeraSkulu_et_p3 FsRuna LoTengtSögn_nf/tala/kyn Nl_nf/tala/kyn AtvFs? # Skal í frumvarpinu fólgin greinargerð um tekjur ríkisins og gjöld

    # !!! Ath.: So_1_nh leyfir miðmynd nafnháttar, sem ætti ekki að vera leyfilegur hér
    # !!! sbr. '?Endurnýjast á leigusamninginn í ár'
    | So_1_nh/fall HjSögnMegiSkuliÞurfi_et_p3 FsRuna? Nl/fall AtvFs?  # Endurnýja þarf/skal/má leigusamninginn í ár
    | So_1_nh/fall HjSögnNhMeðSo/tala/pers Nl_nf/tala/pers/kyn Nl/fall AtvFs?  # Gæta eiga/skulu/verða þeir þingskapa

    | Sagnliður_lhþt

    | Sögn_bh SögnFrh_bh?                                       # Farðu galvaskur að sækja kaffi strax

    | HjSögnMegiSkuli_et_p3 FsAtv? EinSögn_nh_et_hk SagnInnskot? # Má með sanni segja (að þeir hafi sloppið með skrekkinn)

    | 'þurfa:so'_gm_et_p3 FsRuna? Nl_þf NhFyllingEðaAtv         # Þarf til þess lagafyrirmæli að svipta mann réttindum

    | SagnliðurVh Afleiðing FsAtv?                              # Haldi þessi þróun áfram er voðinn vís

    # (Því) hafi þurft að senda alla farþegana (á hótel)
    # (Í dag) gæti hafa rignt
    | FsAtv? HjSögnVh so_0_sagnb NhFylling?


Sagnliður_lhþt →
    EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn* HjSögnLhÞt_p3/tala Nl_nf/tala/kyn AtvFs? # Teknar og skráðar hafa verið ákvarðanir í málinu

$score(-4) Sagnliður_lhþt # Knúsað orðalag - velja annað ef í boði

# Talið er (að Jón sé horfinn) / Verið er að ráða hæfasta umsækjandann
# Sagt er algerlega óhætt [að fara út]
Sagnliður_sagnb →
    so_0_sagnb HjSögnSagnb_et_p3 LoTengtSögn_nf_et_hk? SagnliðurBotn_sagnb

SagnliðurBotn_sagnb →
    NhFylling
    | SagnInnskot
    | SagnliðurBotnSetning

SagnliðurBotnSetning →
    "að:st" HreinYfirsetning

$score(+8) Sagnliður_sagnb
$score(+8) SagnliðurBotnSetning

VeraSkulu_et_p3 →
    'vera:so'_et_p3 | 'verða:so'_et_p3 | 'skulu:so'_et_p3 | 'munu:so'_et_p3

# Setning þar sem aðalnafnliður er í aukafalli
# en frumlag í 3p. et. ('það') er falið.
# Sögnin tekur viðfang (subject) í aukafalli eða er í miðmynd
# 'Mér blöskrar athæfið' (? Það blöskrar mér athæfið),
# 'Mér sýnist að öllu sé óhætt' (? Það sýnist mér að öllu sé óhætt),
# 'Helmingi ungmenna finnst skólinn skemmtilegur' (? Það finnst helmingi ungmenna skólinn skemmtilegur)

SetningAukafall →

    # Manninn langaði mjög í ferðina / að fara til útlanda strax
    # Köttum í landinu fjölgar
    Nl/aukafall so_subj_op/aukafall MiðmyndarBotn?
    # Mér blöskraði mjög vitleysan (í Páli), Mig þraut örendið
    | Nl/aukafall Atviksliður? so_subj_op/tala/aukafall FsRuna? Nl_nf_þf/tala AtvFs?

    # Öfug orðaröð: 'Eignarrétti fylgja skyldur' (í stað 'Skyldur fylgja eignarrétti')
    # 'Þetta skrifar Bjarni á Facebook-síðu sinni og bendir [galvaskur] á frammistöðu stjórnarinnar'
    | Nl/aukafall So_1/aukafall/tala/pers FsRuna? Nl_nf/tala/pers/kyn SagnInnskot? SagnFramhald/tala/pers/kyn*

    # Þessar skuldir hefur 365 ekki greitt
    # Þessa bók hef ég aldrei skilið
    | Nl/aukafall HjSögn/tala/pers Nl_nf/tala/pers/kyn AtvFs? So_1_sagnb/aukafall FsAtv?

    # Köttum hefur [ekki] fjölgað á árinu
    | Nl/aukafall HjSögn_et_p3 FsAtv? So_1_sagnb/aukafall FsAtv?

    # Mér hafa ofboðið lætin í hundinum [alla daga]
    | Nl/aukafall HjSögn/tala/pers FsAtv? so_subj_sagnb/aukafall Nl_nf/tala/pers AtvFs?

    # Hælisleitendum fer/hefur farið fækkandi
    | Nl_þgf HjSögnLhNt so_1_þgf_lh_nt FsAtv?

    # Miðmynd sagna: Mér áskotnaðist, mér fyrirgefst, mér gremst, okkur finnst [öllum]...
    | Nl_þgf/tala/kyn so_0_mm LoÞgf/tala/kyn? MiðmyndarBotn?
    # Viðreisn hefur [ekki] borist/hlotnast/áskotnast öflugur liðsauki [í dag]
    | Nl_þgf HjSögnMM FsAtv? so_0_mm_sagnb Nl_nf/tala/pers AtvFs?
    # Bönkunum hefur/hafði/hefði [flestum] tekist að halda í horfinu
    | Nl_þgf/tala/kyn HjSögnMM LoÞgf/tala/kyn? so_0_mm_sagnb MiðmyndarBotn?

    # Öllum er frjálst/fyrir bestu að ákveða snarlega að flytja úr landi
    | Nl_þgf HjSögnLhÞt_et_p3 FsRuna? LhÞtÞgfEinkunn NhFyllingEðaAtv

    # Öllum hefur verið/skal/mun tryggð heilbrigðisþjónusta / réttur til að lifa með reisn
    | Nl_þgf HjSögnLhÞtSM_p3/tala FsAtv? so_2_þgf_þf_lhþt/tala/kyn FsRuna? AukafallLhÞtAndlag/tala/pers/kyn NhFyllingEðaAtv
    # ESB eru þröngar skorður settar í málinu
    | Nl_þgf HjSögnLhÞtSM_p3/tala FsAtv? AukafallLhÞtAndlag/tala/pers/kyn FsRuna? so_2_þgf_þf_lhþt/tala/kyn NhFyllingEðaAtv

    # Hjónunum var [í fyrra] [báðum] (falið að stýra rannsóknaverkefni)
    | Nl_þgf/tala/kyn HjSögnLhÞtSM_et_p3 FsAtv? LoÞgf/tala/kyn? AukafallÞgfAndlag

    # Þess skal [ekki] gætt (að fara varlega með sprengiefnið)
    | Nl_ef HjSögnLhÞtSM_et_p3 FsAtv? AukafallEfAndlag

    # Köttum í landinu mun [ekki/enn] fjölga á árinu
    | Nl/aukafall HjSögnNh_et_p3 FsAtv? So_1_nh/aukafall FsAtv?

    # Þá ályktun má strax draga í dag (að Páll sé ekki með öllum mjalla)
    # Engan má skylda til að láta eign sína af hendi
    | Nl/aukafall HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall NhFyllingEðaAtv

    # Nauðungarvinnu skal engum gert að leysa af hendi
    | Nl/aukafall HjSögnMegiSkuli_et_p3 FsAtv? AukafallÖfugSögn nhm So_1_nh/aukafall FsAtv?

    # Engan þarf/ætti að undra (að mig langi í ís)
    # Greinarmun verður [ekki] að gera (á Jóni og séra Jóni)
    | Nl/aukafall HjSögnÞurfaEiga_et_p3 FsAtv? nhm So_1_nh/aukafall NhFyllingEðaAtv

    # Engan má svipta íslenskum ríkisborgararétti að ósekju
    | Nl_þf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þf/fall Nl/fall AtvFs?

    # Öllum má tryggja heilbrigðisþjónustu í dag
    | Nl_þgf HjSögnMegiSkuli_et_p3 FsAtv? So_2_nh_þgf/fall Nl/fall AtvFs?

    # Öllum skal [ekki] standa [til boða] menntun (Menntun skal standa öllum til boða)
    | Nl/aukafall HjSögnMegiSkuli_et_p3 FsAtv? So_1_nh/aukafall FsAtv? Nl_nf AtvFs?

    # Mér/okkur/ykkur/börnunum finnst/þykir [mörgum [það]] [ekki] skemmtilegt [að borða góðan mat / hérna í kvöld]
    | Nl_þgf/tala/kyn HjSögnFinnst FinnstÞað/tala/kyn? FinnstAndlag NhFyllingEðaAtv

    # Mér hefur [ekki] þótt [það] vont að sofa á nýju dýnunni
    | Nl_þgf HjSögn_et_p3 FsAtv? HjSögnFinnst_sagnb "það:pfn"? FinnstAndlag NhFyllingEðaAtv

    # Þann auð eigum við [ekki] að nýta (í þágu lands og þjóðar)
    | Nl/aukafall so_0/tala/pers Nl_nf/tala/pers AtvFs? nhm So_1_nh/aukafall FsAtv?

    # Þann auð munum/skulum við [ekki] nýta (í þágu lands og þjóðar)
    | Nl/aukafall HjSögnNh/tala/pers Nl_nf/tala/pers AtvFs? So_1_nh/aukafall FsAtv?

    # Þann auð ber okkur að nýta (til góðra verka)
    | Nl/aukafall so_subj_op/fall Nl/fall AtvFs? nhm So_1_nh/aukafall FsAtv?

    # Nöfn ráðherra var [ekki] að finna [í Panamaskjölunum]
    | Nl_þf 'vera:so'_gm_p3_et FsAtv? nhm 'finna:so'_gm_nh FsAtv?

    # Mér er/var [ekki] sama [um málið] / bumbult [í gær] / misboðið
    # | Nl_þgf 'vera:so'_gm_fh_p3_et FsAtv? lo_sb_hk_nf_et FsAtv?

FinnstÞað/tala/kyn →
    'það:pfn'_et_nf LoÞgf/tala/kyn  # Bílstjórum finnst það mörgum leiðinlegt
    | LoÞgf/tala/kyn 'það:pfn'_et_nf?      # Bílstjórum finnst mörgum [það] leiðinlegt

FinnstAndlag →
    LoTengtSögn_nf_et_hk    # skemmtilegt
    | ao* so_lh_nt          # mjög endurnærandi, afskaplega frískandi

AukafallÞgfAndlag →
    # (Stóru skipunum hefur verið/skal/mun) stýrt í land
    # (Hannesi var í fyrra) falið að stýra rannsóknaverkefni
    so_1_þgf_sagnb NhFyllingEðaAtv

    # (Þátttakendum er) veitt of mikið svigrúm
    | so_2_þgf_þf_sagnb FsRuna? Nl_þf AtvFs?

    # (Þátttakendum er) gert kleift að matast með reisn
    | 'gera:so'_sagnb LoTengtSögn_nf_et_hk NhFyllingAtv

    # (Öllum hefur verið/skal/mun) óskað góðs bata
    | so_2_þgf_ef_sagnb FsRuna? Nl_ef AtvFs?

    # (Öllum hefur verið/skal/mun) skilað bílunum
    | so_2_þgf_þgf_sagnb FsRuna? Nl_þgf AtvFs?

AukafallEfAndlag →
    # Þess skal [ekki] gætt að fara varlega með sprengiefnið
    so_1_ef_sagnb NhFyllingEðaAtv

    # Ákvörðunar var/er/verður/hefur verið/mun [ekki] að vænta [strax í dag]
    | nhm so_1_ef_nh FsAtv?

AukafallÖfugSögn →
    Nl/fall AtvFs? So_1_nh/fall         # (skal) engum gera
    | Nl/fall AtvFs? So_1_sagnb/fall    # (skal) engum gert

# Tilvik þar sem sögn í lhþt eintölu fylgir andlag í fleirtölu:
# 'Öllum skal tryggð mannhelgi og vernd gegn hvers kyns ofbeldi'

AukafallLhÞtAndlag/tala/pers/kyn →
    Nl_nf/tala/pers/kyn

AukafallLhÞtAndlag_et/pers/kyn →
    Nl_nf_ft/pers/kyn

AukafallsBotn →
    FsAtv                    # (Mér virtist ykkur ekki hungra) mjög undir ræðunni
    | FsRuna? Nl_nf AtvFs?   # (Mér virtist ykkur ekki falla) í geð ræðan í kvöld
    | NhFyllingAtv           # (Mér sýndist ykkur langa) gríðarlega að fara til útlanda strax

MiðmyndarBotn →

    # (Mér sýndist) kötturinn [ekki] [vera að] lepja mjólkina og borða matinn í gær
    Nl_nf AtvFs? VeraInnskot? NhRuna FsAtv?

    # (Mér finnst) kettirnir [ekki] [vera] fallegir
    | MiðmyndarBotnLo

    # (Mér virtist) ykkur [ekki] blöskra undir ræðunni
    | Nl/aukafall AtvFs? so_subj_nh/aukafall AukafallsBotn?

    # (Mér virtist) þeim [ekki] sýnast börnin vera falleg
    | Nl_þgf AtvFs? so_nh_mm MiðmyndarBotn

    # (Mér leyfist) vonandi að fara út
    | NhFyllingAtv

    # (Páli fyrirgefst) örugglega á morgun
    | FsAtv

MiðmyndarBotnLo →
    Nl_nf/tala/pers/kyn AtvFs? HjSögnVera? LoTengtSögn_nf/tala/kyn FsAtv?

$score(+4) MiðmyndarBotnLo # Gefa plús fyrir heildstætt samhengi lýsingarorðs

VeraInnskot →
    HjSögnVera nhm    # vera að / geta hafa verið að / verðir að / hafa orðið að...

Nl_nf_þf/tala → Nl_nf/tala | Nl_þf/tala

# Setningar án frumlags (notaðar t.d. í tengisetningum eftir 'sem').
# Í setningum án frumlags getur sagnliðurinn verið "öfugur",
# þ.e. "konurnar sem borðað höfðu matinn" í stað "höfðu borðað".
# Þá má atviksliður ekki koma inn á milli sagnanna, þ.e.
# "höfðu ekki borðað" er gilt, en "borðað ekki höfðu" ógilt.

SetningÁnF/tala/pers/kyn →
    EinSetningÁnF/tala/pers/kyn FramhaldÁnF/tala/pers/kyn*

EinSetningÁnF/tala/pers/kyn →
    Atviksliður? ÖfugurSagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskot? ÖfugtFramhald/tala/pers/kyn*
    # Lýsingarorð á undan sögn:
    # (Ráðherrar taka þær ákvarðanir sem) nauðsynlegar eru/þykja/teljast til rækslu starfa þeirra
    | LoTengtSögn_nf/tala/kyn Sögn_0/tala/pers/kyn NhFyllingEðaAtv

$tag(begin_prep_scope) EinSetningÁnF/tala/pers/kyn # Nýtt undirtré fyrir tengingu sagnar og forsetningar

ÖfugtFramhald/tala/pers/kyn →
    KommaOgEðaEn ÖfugurSagnliður/tala/pers/kyn LoTengtSögn_nf/tala/kyn? SagnInnskot?

FramhaldÁnF/tala/pers/kyn →
    ','? Samtenging HreinYfirsetning               # (sem borðað höfðu matinn) þegar hann var framreiddur fyrr um kvöldið
    | ','? Samtenging EinSetningÁnF/tala/pers/kyn  # (sem borðað höfðu matinn) og þakkað auðmjúkar fyrir sig

# Sagnliðir

Sagnliður/tala/pers/kyn →

    Sögn_0/tala/pers/kyn       # Sögn með engu viðfangi: (veðrið) skánaði
    | Sögn_1/tala/pers/kyn     # Sögn með einu viðfangi: (Jón) sá köttinn
    | Sögn_2/tala/pers/kyn     # Sögn með tveimur viðföngum: (Kata) skrifaði Önnu bréf

    | Sögn_sagnb/tala/pers/kyn # Sagnbót: (Jón) hefur borðað matinn og farið að bóna bílinn
    | Sögn_nh/tala/pers/kyn    # Sagnir í nafnhætti: (Jón) mun/hyggst bóna bílinn og fara að kaupa inn
    | Sögn_lhþt/tala/pers/kyn  # Sagnir í lh. þt.: (Kata) er vöknuð og farin að sækja Pál
    | Sögn_lhnt/tala/pers      # Sagnir í lh. nt.: (Guðrún) var hringjandi og sannfærandi í allan dag

# Sagnliður með venjulegri eða 'öfugri' orðaröð: (Konan sem) matinn borðaði

ÖfugurSagnliður/tala/pers/kyn →

    Sagnliður/tala/pers/kyn

    | ÖfugSögn_0/tala/pers/kyn
    | ÖfugSögn_1/tala/pers/kyn
    | ÖfugSögn_2/tala/pers/kyn

    # !!! Bæta við ÖfugSögn_sagnb, _nh, _lhþt hér

# Sagnliður án sýnilegs frumlags: Í dag þarf að vökva grasið (=Það þarf að vökva grasið í dag)
# Sagnliður án frumlags þarf að geta staðið sem afleiðing í ef-þegar setningu:
# '(Ef hann hringir) má sækja tölvuna og byrja að vinna'

SagnliðurÁnF →
    so_subj_gm_none NhFyllingAtv?                          # (Í dag) þarf [sannarlega] (að vökva grasið)
    | so_0_mm_p3_et NhFyllingAtv?                          # (Í dag) dregst [sennilega] (að vökva grasið)

    | HjSögn_et_p3 FsAtv? so_subj_sagnb_none NhFyllingAtv? # (Í ár) hefur [í sumarhitanum] þurft (að vökva grasið)
    | HjSögn_et_p3 FsAtv? so_mm_sagnb NhFyllingAtv?        # (Í ár) hefur [vegna leti] dregist (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_subj_nh_none NhFyllingAtv?  # (Í ár) mun [í sumarhitanum] þurfa (að vökva grasið)
    | HjSögnNh_et_p3 FsAtv? so_mm_nh NhFyllingAtv?         # (Í ár) mun [í sumarhitanum] dragast (að vökva grasið)

    | HjSögnLhÞtSM_et_p3 FsAtv? so_0_sagnb NhFyllingEðaAtv # (í dag) verður/skal/mun [líklega] lent á brautinni (eftir hádegi)
    | HjSögnLhÞtSM_et_p3 FsRuna? LoTengtSögn_et_nf_hk NhFylling? # (Í dag) hefur verið býsna erfitt (að aka heiðina)

    | HjSögnMegiSkuli_et_p3 HreinSögn_nh_et_hk SagnInnskot? # [Í dag] má sækja tölvuna (og byrja að vinna)
    | EinSögn_nh_et_hk HjSögnNhMeðSo_et_p3 HreinSögnAtv_nh? # [hvort] leggja skuli/eigi/þarf/verður [á skatt, breyta honum eða afnema hann]

    | HjSögnNh_p3/tala FsAtv? so_mm_nh FsRuna? Nl_nf/tala  # [Í kröfunum] skulu felast tillögur (um nýtt fyrirkomulag)

    | 'þurfa:so'_et_p3 FsRuna? Nl_þf                       # Þarf til þess lagafyrirmæli

    | StökSögn_et_p3_gm                                 # (Í dag) segir
    | so_0_et_p3_gm SagnInnskot                         # (Í dag) fýkur í fólk / kreppir að / losnar um stífluna / segir

$tag(begin_prep_scope) SagnliðurÁnF # Nýtt undirtré fyrir tengingu sagnar og forsetningar

StökSögn_et_p3_gm →
    so_0_et_p3_gm

# Óalgengt að sagnorð standi ein og sér án frumlags
$score(-8) StökSögn_et_p3_gm

HreinSögnAtv_nh →
    FsAtv? HreinSögn_nh_et_hk

# Sagnliður sem hefst á viðtengingarhætti:
# 'Haldi þessi þróun áfram er voðinn vís / þarf að grípa til aðgerða'

SagnliðurVh →

    # Haldir þú [áfram að borða] (er voðinn vís)
    # Aki hann ölvaður (skal sekta hann um tíu þúsund krónur)
    so_0_vh/tala/pers Nl_nf/tala/pers/kyn LoTengtSögn_nf/tala/kyn? NhFyllingEðaAtv

    # Borði börnin [ekki] matinn (getum við farið í sund)
    | So_1_vh/fall/tala/pers Nl_nf/tala/pers AtvFs? Nl/fall AtvFs?

    # Verði þessi tillaga felld (geng ég úr flokknum)
    # Verði karlarnir byrjaðir að grilla (sæki ég sósuna)
    | VeraVerða_vh/tala/pers Nl_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn FsAtv?

    # Verði þeim [ekki] vísað [úr landi] geta afleiðingarnar orðið hörmulegar
    # Sé börnunum sagt [að fara í háttinn] grenja þau stanslaust
    | VeraVerða_vh_et_p3 Nl_þgf AtvFs? so_1_þgf_sagnb NhFyllingEðaAtv

    # Hafi maður verið sviptur frelsi að ósekju (skal hann eiga rétt til skaðabóta)
    | 'hafa:so'_vh/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_sagnb HreinSögn_lhþt/tala/kyn FsAtv?

    # Hafi manni verið hallmælt í útvarpi (skal hann fá kaffi og kleinu)
    | 'hafa:so'_vh_et_p3 Nl_þgf AtvFs? 'vera:so'_sagnb so_1_þgf_sagnb FsAtv?

    # Hafi manni verið tekið blóð að óþörfu (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa:so'_vh_et_p3 Nl_þgf AtvFs? 'vera:so'_sagnb so_2_þgf_þf_lhþt/tala/kyn Nl_nf/tala/kyn AtvFs?

    # Hafi manni verið gert kleift að matast með reisn (skal hann liggja á bekk í stundarfjórðung)
    | 'hafa:so'_vh_et_p3 Nl_þgf AtvFs? 'vera:so'_sagnb 'gera:so'_lhþt/tala/kyn LoTengtSögn_nf_et_hk NhFyllingAtv

    # Hafi manns verið leitað að óþörfu (skal hann fá axlarnudd)
    | 'hafa:so'_vh_et_p3 Nl_ef AtvFs? 'vera:so'_sagnb so_2_þgf_ef_sagnb FsAtv?

    # Fari [það] svo/þannig að ég nái ekki kjöri til forseta [,] [þá] getið þið sótt fyrirlestur um þetta hjá mér
    | 'fara:so'_vh_et_p3 "það:pfn"? FsAtv "að:st" HreinYfirsetning

$score(+4) SagnliðurVh

VeraVerða_vh/tala/pers →
    'vera:so'_vh/tala/pers | 'verða:so'_vh/tala/pers

VeraVerða_gm_p3/tala →
    'vera:so'_gm_p3/tala | 'verða:so'_gm_p3/tala

OgVeraVerða_gm_p3/tala →
    FsAtv? OgEðaEn FsAtv? VeraVerða_gm_p3/tala
    | FsAtv? ',' OgEðaEn FsAtv? VeraVerða_gm_p3/tala FsAtvKomma?

# Sagnliður með inniföldu frumlagi:
# '(Í dag) borðaði Jón matinn'
# '(Með því) var svæðinu stefnt í hættu'
# '(Í kjölfar þess) varð ég ósáttur'
# '(Nú) mega allir fara sem vilja'
# '(Í gær) skrifuðu þær konur þingmönnum bréf sem voru heitastar í málinu'

SagnliðurMeðF/tala/pers/kyn →
    SögnMeðF/tala/pers/kyn Tengiliður/tala/pers/kyn?

SögnMeðF/tala/pers/kyn →
    SögnMeðF_0/tala/pers/kyn   # Sögn með engu viðfangi: (Í dag) skánaði veðrið
    | SögnMeðF_1/tala/pers/kyn # Sögn með einu viðfangi: (Í dag) sá Jón köttinn
    | SögnMeðF_2/tala/pers/kyn # Sögn með tveimur viðföngum: (Í dag) skrifaði Kata Önnu bréf

$score(+2) SögnMeðF_1/tala/pers/kyn
$score(+4) SögnMeðF_2/tala/pers/kyn

OgSögn_0/tala/pers →
    FsAtv? OgEðaEn FsAtv? so_0/tala/pers
    | FsAtv? ',' OgEðaEn FsAtv? so_0/tala/pers FsAtvKomma?

OgSögn_mm/tala/pers →
    FsAtv? OgEðaEn FsAtv? so_mm/tala/pers
    | FsAtv? ',' OgEðaEn FsAtv? so_mm/tala/pers FsAtvKomma?

OgSögn_mm_nh →
    FsAtv? OgEðaEn FsAtv? so_mm_nh
    | FsAtv? ',' OgEðaEn FsAtv? so_mm_nh FsAtvKomma?

OgSögn_mm_sagnb →
    FsAtv? OgEðaEn FsAtv? so_mm_sagnb
    | FsAtv? ',' OgEðaEn FsAtv? so_mm_sagnb FsAtvKomma?

OgSögn_sagnb/tala/kyn →
    # (hefur staðið) keikur í brúnni og mótmælt yfirgangi Rússa
    LoTengtSögn_nf/tala/kyn? FsAtv? OgEðaEn FsAtv? EinSögn_sagnb/tala/kyn
    | LoTengtSögn_nf/tala/kyn? FsAtv? ',' OgEðaEn FsAtv? EinSögn_sagnb/tala/kyn FsAtvKomma?

$score(+6) OgSögn_sagnb/tala/kyn # Enn sterkari plús fyrir runu sagnb

OgSögn_nh/tala/kyn →
    SagnInnskot? OgEðaEnÞrátt FsAtv? EinSögn_nh/tala/kyn
    | SagnInnskot? ',' OgEðaEnÞrátt FsAtv? EinSögn_nh/tala/kyn FsAtvKomma?

# Þá segist hún styðja trúfrelsi, þrátt fyrir að vera andsnúin moskubyggingum
OgEðaEnÞrátt →
    OgEðaEn | 'þrátt_fyrir_að:st'

OgSögn_lhþt/tala/kyn →
    # Leyfa að 'og' sé sleppt, sbr. "Tveir menn voru stöðvaðir í morgun [sterklega] grunaðir um ölvun við akstur"
    FsAtv? OgEðaEnAtv? EinSögnFrh_lhþt/tala/kyn
    | FsAtv? ',' OgEðaEn? FsAtv? EinSögnFrh_lhþt/tala/kyn FsAtvKomma?

OgSögn_lhnt →
    FsAtv? OgEðaEn FsAtv? so_0_lh_nt
    | FsAtv? ',' OgEðaEn FsAtv? so_0_lh_nt FsAtvKomma?

OgSögn_1_þf_nh/tala/pers →
    FsAtv? OgEðaEn FsAtv? So_1_þf_nh/tala/pers
    | FsAtv? ',' OgEðaEn FsAtv? So_1_þf_nh/tala/pers FsAtvKomma?

# Ekki enda á FsAtv nema innan kommu
FsAtvKomma → FsAtv? ','

# Lýsingarháttur þátíðar (Við erum byrjaðir)

LhÞtBotn/tala/kyn →
    # (Mennirnir eru byrjaðir) galvaskir að grilla
    # (Mennirnir eru sagðir) fara herskáir að brenna þorp
    so_0_nh? NhFylling/tala/kyn
    | NhRuna                                # (Mennirnir eru sagðir) fara sjaldan í bað, og raka sig aldrei
    | LoTengtSögn_nf/tala/kyn               # (Mennirnir eru taldir) ritfærir
    | Aldur                                 # (Mennirnir eru orðnir) fimmtán ára

EinSögn_lhþt/tala/kyn →
    so_0_lhþt/tala/kyn LhÞtBotn/tala/kyn?   # (Mennirnir eru) lentir
    > so_2_þf_þgf_lhþt/tala/kyn Nl_þgf      # (Maðurinn var) sviptur frelsi
    > so_2_þgf_þf_lhþt/tala/kyn Nl_þgf      # (Ákvörðunin var) tilkynnt Páli
    > so_subj_lhþt/tala/kyn Nl_nf/tala      # (Katrín var) valin besti þjálfarinn
    > so_subj_lhþt_ft/kyn Nl_nf_et          # (Þau voru) valin efnilegasta sundfólkið

EinSögnFrh_lhþt/tala/kyn →
    EinSögn_lhþt/tala/kyn                   # (Mennirnir) eru lentir og byrjaðir að grilla
    > Nl_nf/tala/kyn so_0_lhþt/tala/kyn     # veð tekið / veðin tekin / kötturinn kembdur
    > EinSögnFrh_lhþt_sagnb

# Framhald lýsingarháttar þátíðar sem fer yfir í sagnbót
# ('leyfisveiting yrði einfölduð og fólki leyft að leigja út eignir, og heimagisting flokkuð á annan hátt')
# Í dæminu er lhþt og sagnb, og lhþt
EinSögnFrh_lhþt_sagnb →
    Nl_þf/tala/kyn so_1_þf_sagnb NhFylling/tala/kyn?     # fólki leyft (að fara sársvöngu um borð)
    | Nl_þgf/tala/kyn so_1_þgf_sagnb NhFylling/tala/kyn? # miðunum breytt / fólkinu sagt (að fara um borð)
    | Nl_ef/tala/kyn so_1_ef_sagnb NhFylling/tala/kyn?   # matarins neytt / þess beðið (að fara um borð)

HreinSögn_lhþt/tala/kyn →
    FsAtv? EinSögn_lhþt/tala/kyn OgSögn_lhþt/tala/kyn*

# Hvetja til 'verði bókfærðar'
$score(+12) HreinSögn_lhþt/tala/kyn

Sögn_lhþt/tala/pers/kyn →
    # er farið, hefur verið farin, hafa verið farnar, skal farin
    HjSögnLhÞtSM/tala/pers HreinSögn_lhþt/tala/kyn
    # er leiðinlegur, hefur verið frábær, munu vera einstök
    | SögnErLo/tala/pers/kyn
    | SögnErEinsOg/tala/pers
    # talin er/hefur verið [liggja undir grun]
    | so_0_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers LhÞtBotn/tala/kyn?

$score(+8) Sögn_lhþt/tala/pers/kyn

SögnErLo/tala/pers/kyn →
    # (Jón er orðinn) duglegur / tólf ára
    HjSögnLhÞt/tala/pers SögnErLoBotn/tala/kyn

SögnErLoBotn/tala/kyn →
    LoTengtSögn_nf/tala/kyn NhFylling?  # (Guðrún er) snögg [að svara fyrir sig]
    | Aldur                             # (Páll var) tíu ára

SögnErEinsOg/tala/pers →
    # er [í laginu alveg] eins og krakki í sælgætisbúð
    HjSögnLhÞt/tala/pers FsAtv? "eins:ao" "og:st" Nl_nf

# Hvetja til 'er/var/hefur verið...' + lýsingarorð í nefnifalli og sterkri beygingu
$score(+8) SögnErLo/tala/pers/kyn
$score(+6) SögnErEinsOg/tala/pers

LhÞtÞgfBotn → "fyrir:fs" 'góður:lo'_esb_hk_þgf_et  # Það sem er börnum fyrir bestu

LhÞtÞgfEinkunn → LhÞtÞgfBotn | LoTengtSögn_nf_et_hk

Sögn_lhþt_et_p3_hk →
    # Sértilfelli fyrir 'Það er okkur mikilvægt/fyrir bestu [í þessu máli] að fara/gera...'
    HjSögnLhÞt/tala/pers Nl_þgf LhÞtÞgfEinkunn NhFylling

# Lýsingarháttur nútíðar ('við vorum hringjandi', 'hún hefur verið kvartandi')

Sögn_lhnt/tala/pers →
    HjSögnLhÞt/tala/pers HreinSögn_lhnt
    | 'þykja:so'/tala/pers HreinSögn_lhnt  # Almenningur þykir uggandi / ég þykist vitandi

HreinSögn_lhnt →
    FsAtv? so_0_lh_nt OgSögn_lhnt*

# Sagnbót ('konurnar [segjast/þykjast/virðast] [galvaskar/sumar/allar] hafa borðað matinn')

Sögn_sagnb/tala/pers/kyn →
    HjSögn/tala/pers LoTengtSögn_nf/tala/kyn? HreinSögn_sagnb/tala/kyn

HreinSögn_sagnb/tala/kyn →
    FsAtv? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*

# Útgáfa til brúks strax á eftir nafnlið
# (sem getur endað á FsRunu og er því tvíræður við FsAtv)
HreinSögnAtv_sagnb/tala/kyn →
    AtvFs? EinSögn_sagnb/tala/kyn OgSögn_sagnb/tala/kyn*

EinSögn_sagnb/tala/kyn →
    so_0_sagnb NhFylling/tala/kyn?      # Konurnar hafa farið að grilla
    | So_1_þf_nh_sagnb SagnarBotn       # Konurnar hafa [ekki] látið Pál borða matinn / sagt niðurstöðuna vera ranga
    | Sögn_1_sagnb NhFylling/tala/kyn?  # Konurnar hafa [ekki] lofað Páli [að fara út]
    | Sögn_2_sagnb
    | so_0_mm_sagnb LhÞtBotn/tala/kyn   # Konurnar hafa reynst vera bíræfnir þjófar / þóst ritfærar / sagst fara að drífa sig

# Gefa auka stig hér enda smella þá fleiri en eitt púsl saman
$score(+8) EinSögn_sagnb/tala/kyn

# Nafnháttur

Sögn_nh/tala/pers/kyn →
    HjSögnNh/tala/pers LoTengtSögn_nf/tala/kyn? HreinSögn_nh/tala/kyn # Páll mun/skal [galvaskur/allur] skrifa bréfið
    | HjSögnNhMeðSagnb/tala/pers FsAtv? LoTengtSögn_nf/tala/kyn? nhm HreinSögn_nh/tala/kyn # Páll á/ætti/verður/þarf [ekki] [galvaskur/allur] að skrifa bréfið
    | Sögn_nh_mm/tala/pers/kyn

# Gefa auka stig hér (enda er oft tvíræðni milli sagna í nafnhætti og nafnorða)
$score(+2) Sögn_nh/tala/pers/kyn

# Miðmynd + nafnháttur
# TODO skgr. hvaða sagnir ganga sem "hjálparsagnir" hér?
Sögn_nh_mm/tala/pers/kyn →
    so_mm/tala/pers OgSögn_mm/tala/pers* Botn_nh_mm/tala/kyn
    # Hann segist munu borða kökuna
    # Ég telst vilja kjósa flokkinn
    # Þau segjast geta látið henda Jóni út
    | So_1_þf_mm HjSögnNh_ft_p3 HreinSögn_nh/tala/kyn

Botn_nh_mm/tala/kyn →
     HreinSögn_nh/tala/kyn # Páll hyggst/þykist/telst skrifa söguna
     | HreinSögn_lhþt/tala/kyn # Kvennalistakonur virðast [ekki] taldar sigurstranglegar
     # (Jón sagðist) þeirrar skoðunar [að múslimar væru óalandi og óferjandi]
     # (Maðurinn reyndist) þeirrar innréttingar að ekki var á hann treystandi
     | Botn_nh_ef

Botn_nh_ef →
     'sá:fn'_ef/tala/kyn Nl_ef/tala/kyn Útskýring?

# Hvetja til samhengis með miðmynd: (hún) segist ekki ætla (að fara í framboð)
$score(+4) Sögn_nh_mm/tala/pers/kyn

HreinSögn_nh/tala/kyn →
    FsAtv? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

EinSögn_nh/tala/kyn →
    so_0_nh NhFylling/tala/kyn?                    # Konurnar munu fara að grilla
    | Sögn_1_nh NhFylling/tala/kyn?                # Konurnar munu [ekki] lofa Páli [að fara út]
    | Sögn_2_nh

    # (Stjórnsýsla skal) vera gegnsæ og halda til haga gögnum
    | HjSögnVeraVerða FsRuna? LoTengtSögn_nf/tala/kyn

# Boðháttur

Sögn_bh →
    # Farðu [út] að þvo bílinn [strax]
    so_0_bh BhBotn
    # Förum [út] að þvo bílinn [strax]
    | so_0_gm_fh_p1_ft_nt BhBotn
    # Lofið barninu [að borða] [í friði]
    | Sögn_1_bh BhBotn
    # Skrifaðu jólasveininum bréf
    | Sögn_2_bh BhBotn
    # Láttu/látið það ekki slá ykkur (út af laginu) og fara (með ykkur í gönur)
    | 'láta:so'_bh Nl_þf AtvFs? EinSögn_nh/tala/kyn OgSögn_nh/tala/kyn*

# Boðháttur er sjaldgæfur - viljum aðra kosti frekar
$score(-5) Sögn_bh
$tag(begin_prep_scope) Sögn_bh

BhBotn →
    # að borða í friði
    NhFylling? SagnInnskot?
    # í skálinni meðan þið brjótið eggin
    | FsAtv? BhMeðan HreinYfirsetning

# (Hrærið [í deiginu]) uns (súkkulaðið bráðnar)
BhMeðan → 'á_meðan:st' | "meðan:st" | "uns:st" | "þar:st" "til:st"

SögnFrh_bh →
    KommuSögn_bh* OgSögn_bh+

KommuSögn_bh →
    ',' FsAtv? Sögn_bh

OgSögn_bh →
    ','? OgEðaEn FsAtv? Sögn_bh

# ...en þegar eru komnar tvær eða fleiri sagnir í röð þá er það líklegri lausn
$score(+10) KommuSögn_bh
$score(+10) OgSögn_bh

# Sögn með engu viðfangi ('lenti')
# ('Maðurinn lenti')
Sögn_0/tala/pers/kyn →
    # (Mennirnir) lenda og fara [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* NhFylling/tala/kyn?

# Sögn með engu viðfangi ('lenti') sem stendur í undirsetningu
# ('Maðurinn sem lenti')
ÖfugSögn_0/tala/pers/kyn →

    # Mennirnir sem nennt hafa [að þvo bílinn]
    so_0_sagnb HjSögn/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lenda munu
    | so_0_nh HjSögnNh/tala/pers NhFylling/tala/kyn?

    # Mennirnir sem lentir eru
    | so_0_lhþt/tala/kyn HjSögnLhÞt/tala/pers NhFylling/tala/kyn?

    # (Páll gerði allt sem) í hans valdi stóð
    # (...þau réttindi sem) í þessari stjórnarskrá felast
    | FsAtv so_0/tala/pers

# Sögn með inniföldu frumlagi en engu viðfangi ('lenti')
# ('Í dag lenti maðurinn')
SögnMeðF_0/tala/pers/kyn →

    # (Í dag) lenda og fara mennirnir [galvaskir að þvo bílinn]
    so_0/tala/pers OgSögn_0/tala/pers* FsRuna? Nl_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) tókst og heppnaðist Seðlabankanum [galvöskum að lækka vexti]
    # !!! Ath: ekki _fh hér þar sem _vh er leyfilegt (takist Seðlabankanum...)
    | so_mm/tala/pers OgSögn_mm/tala/pers* Nl_þgf/tala/pers/kyn NhFylling_þgf/tala/kyn?

    # (Í dag) ber stjórnvöldum að taka hætturnar alvarlega
    | so_subj_gm_op_p3_et_þgfnh Nl_þgf/tala/pers/kyn NhFylling/tala/kyn

    # (Í dag) líður mér [vel að hafa ekki farið þá leið]
    # (Í gær) dreymdi mig [um að fara alsæll í sólarlandaferð]
    # (Eftir matinn) svimaði mig
    | so_subj_op/aukafall Nl/aukafall/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) hefur [ekki] gerst það sama
    # (Um árabil) hefur verið [í gildi] yfirlýsing
    | HjSögn/tala/pers FsAtv? so_0_sagnb FsRuna? Nl_nf/tala/pers/kyn NhFylling/tala/kyn?

    # (Í dag) hafa mennirnir [ekki getað] lent
    | HjSögn/tala/pers Nl_nf/tala/pers/kyn GetaAtv_sagnb? HreinSögnAtv_sagnb/tala/kyn

    # (Í dag) eiga mennirnir [ekki/eftir seinkun gærdagsins] að hafa/geta lent
    | HjSögnNhMeðSagnb/tala/pers Nl_nf/tala/pers/kyn nhm HafaGetaFá_nh HreinSögn_sagnb/tala/kyn

    # (Í dag) munu mennirnir [ekki/innan tíðar] lenda
    | SögnMeðF_nh/tala/pers/kyn

    # (Í dag) hyggjast mennirnir [ekki/innan tíðar] lenda
    # (Í dag) segist Illugi ekki hafa fengið laun
    | so_mm/tala/pers Nl_nf/tala/pers/kyn SögnMeðF_mm/tala/kyn

    # (Í dag) eru mennirnir [ekki/í millitíðinni] lentir
    # (Í dag) skulu mennirnir látnir lausir
    | HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn HreinSögn_lhþt/tala/kyn

    # (Hér) væri [ennfremur] komin mikil framleiðsluspenna
    # Ath: Hjálparsögnin er í 3p þó frumlagið sé það ekki, sbr.
    # 'Hér er ennfremur kominn þú' (ekki 'ert')
    | HjSögnLhÞtSM_p3/tala FsAtv? so_0_lhþt/tala/kyn Nl_nf/tala/kyn

    # (Í ár) hefur krönunum [í borginni] fjölgað
    | HjSögn_et_p3 Nl/fall/tala/pers/kyn AtvFs? so_subj_sagnb/fall NhFylling/tala/kyn?
    # (Í dag) er þingmönnunum [í flokknum] stefnt (til þings)
    | HjSögnLhÞt_et_p3 Nl_þgf/tala/pers/kyn AtvFs? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í kvöld) er viðurgjörningsins notið (í skíðaskálanum)
    | HjSögnLhÞt_et_p3 Nl_ef/tala/pers/kyn AtvFs? so_1_ef_sagnb NhFylling/tala/kyn?

    # (Í ár) mun krönunum [í borginni] fjölga
    | HjSögnNh_et_p3 Nl/fall/tala/pers/kyn AtvFs? so_subj_nh/fall NhFylling/tala/kyn?

    # (Í dag) hafa fjöllin verið klifin og sigruð
    | 'hafa:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_sagnb HreinSögn_lhþt/tala/kyn
    # (Í dag) hefur flugvélinni verið lent    
    | 'hafa:so'_et_p3 Nl_þgf/tala/pers/kyn AtvFs? 'vera:so'_sagnb FsAtv? so_1_þgf_sagnb NhFylling/tala/kyn?
    # (Í dag) hefur konunnar verið gætt
    | 'hafa:so'_et_p3 Nl_ef/tala/pers/kyn AtvFs? 'vera:so'_sagnb FsAtv? so_1_ef_sagnb NhFylling/tala/kyn?

    # Skal sú ákvörðun [vera] rökstudd og tilkynnt forseta Alþingis
    | HjSögnSkuluMunu/tala/pers Nl_nf/tala/pers/kyn VeraVerðaAtv? HreinSögn_lhþt/tala/kyn

SögnMeðF_nh/tala/pers/kyn →
    HjSögnNh/tala/pers Nl_nf/tala/pers/kyn HreinSögn_nh/tala/kyn # (Í stað takka) vill Apple koma fyrir snertiskynjurum

$score(+4) SögnMeðF_nh/tala/pers/kyn

VeraVerðaAtv →
     FsAtv? HjSögnVeraVerða

SögnMeðF_mm/tala/kyn →
    HreinSögn_nh/tala/kyn                           # (Í dag hyggjast mennirnir) [ekki/innan tíðar] lenda
    | FsAtv? HjSögn_mm_nh HreinSögn_sagnb/tala/kyn  # (Í dag segist Illugi) ekki hafa fengið laun

OgSögn_1/fall/tala/pers →
    OgEðaEn FsAtv? so_1/fall/tala/pers
    | ',' OgEðaEn FsAtv? so_1/fall/tala/pers FsAtvKomma?

# Umraðanir

So_1/aukafall/tala/pers → so_1/aukafall/tala/pers
$tag(pick_up_verb) So_1/aukafall/tala/pers

So_1_sagnb_nf → so_1_nf_sagnb   # (Í fyrra höfðu konurnar [ekki]) verið leikarar
So_1_sagnb_þf → so_1_þf_sagnb   # (Í dag hafa konurnar [ekki]) borðað matinn
So_1_sagnb_þgf → so_1_þgf_sagnb # (Í dag hafa konurnar [ekki]) hafnað Páli
So_1_sagnb_ef → so_1_ef_sagnb   # (Í dag hafa konurnar [ekki]) notið matarins

So_1_nh_nf → so_1_nf_nh         # (Í dag munu konurnar [ekki]) vera leikarar
So_1_nh_þf → so_1_þf_nh         # (Í dag munu konurnar [ekki]) borða matinn
So_1_nh_þgf → so_1_þgf_nh       # (Í dag munu konurnar [ekki]) hafna Páli
So_1_nh_ef → so_1_ef_nh         # (Í dag munu konurnar [ekki]) njóta matarins

So_1_bh_nf → so_1_nf_bh | so_1_nf_gm_fh_p1_ft_nt # Vertu leikari / Verum leikarar
So_1_bh_þf → so_1_þf_bh | so_1_þf_gm_fh_p1_ft_nt # Borðaðu matinn / borðum matinn
So_1_bh_þgf → so_1_þgf_bh | so_1_þgf_gm_fh_p1_ft_nt # Hafnaðu Páli / höfnum Páli
So_1_bh_ef → so_1_ef_bh | so_1_ef_gm_fh_p1_ft_nt # Njóttu matarins / njótum matarins

$tag(pick_up_verb) So_1_bh/fall

So_1_vh_nf/tala/pers → so_1_nf_vh/tala/pers    # Væri hann leikari
So_1_vh_þf/tala/pers → so_1_þf_vh/tala/pers    # Borðir þú matinn
So_1_vh_þgf/tala/pers → so_1_þgf_vh/tala/pers  # Hafni ég Páli
So_1_vh_ef/tala/pers → so_1_ef_vh/tala/pers    # Nyti hann matarins

So_1_p3_nf/tala → so_1_nf_p3/tala
So_1_p3_þf/tala → so_1_þf_p3/tala
So_1_p3_þgf/tala → so_1_þgf_p3/tala
So_1_p3_ef/tala → so_1_ef_p3/tala

# Sagnbót með einu viðfangi

Sögn_1_sagnb →
    So_1_sagnb/fall FsRuna? Nl/fall  # Þingmenn geta/hafa lagt [fyrir Alþingi] frumvörp (til laga)
    | 'enginn:fn'/fall/tala/kyn Nl/fall/tala/kyn So_1_sagnb/fall # (Þó getur hann) enga samninga gert

# Sögn í nafnhætti með einu viðfangi

Sögn_1_nh →
    So_1_nh/fall FsRuna? Nl/fall
    # sjá/láta Pál breyta sér í kött
    | So_1_þf_nh Nl_þf So_1_nh/fall FsRuna? Nl/fall

# Sögn í boðhætti með einu viðfangi

Sögn_1_bh → So_1_bh/fall FsRuna? Nl/fall

# Sagnbót með tveimur viðföngum

Sögn_2_sagnb →
    so_2_þgf_þf_sagnb Nl_þgf Nl_þf     # Konurnar höfðu [ekki] gefið Páli gjöfina
    | so_2_þf_ef_sagnb Nl_þf Nl_ef     # Konurnar höfðu [ekki] krafið Pál svara
    | so_2_þf_þf_sagnb Nl_þf Nl_þf     # Konurnar höfðu [ekki] kostað Pál skildinginn
    | so_2_þf_þgf_sagnb Nl_þf Nl_þgf   # Konurnar höfðu [ekki] kallað Pál öllum illum nöfnum
    | so_2_þgf_þgf_sagnb Nl_þgf Nl_þgf # Konurnar höfðu [ekki] skilað Páli bílnum
    | so_2_þgf_ef_sagnb Nl_þgf Nl_ef   # Konurnar höfðu [ekki] óskað Páli alls hins besta
    | so_2_þgf_nf_sagnb Nl_þgf Nl_nf   # Kristín hafði [ekki] verið Páli stoð og stytta
    | 'gera:so'_sagnb Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar höfðu [ekki] gert Páli kleift að klæða sig

# Það er gott að ná tveimur viðföngum með sögn í sagnbeygingu
$score(+6) Sögn_2_sagnb

# Sögn í nafnhætti með tveimur viðföngum

Sögn_2_nh →
    so_2_þgf_þf_nh Nl_þgf Nl_þf        # Konurnar munu [ekki] gefa Páli gjöfina
    | so_2_þf_ef_nh Nl_þf Nl_ef        # Konurnar munu [ekki] krefja Pál svara
    | so_2_þf_þf_nh Nl_þf Nl_þf        # Konurnar munu [ekki] kosta Pál skildinginn
    | so_2_þf_þgf_nh Nl_þf Nl_þgf      # Konurnar munu [ekki] kalla Pál öllum illum nöfnum
    | so_2_þgf_þgf_nh Nl_þgf Nl_þgf    # Konurnar munu [ekki] skila Páli bílnum
    | so_2_þgf_ef_nh Nl_þgf Nl_ef      # Konurnar munu [ekki] óska Páli alls hins besta
    | so_2_þgf_nf_nh Nl_þgf Nl_nf      # Kristín mun [ekki] vera Páli stoð og stytta
    | 'gera:so'_nh Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar munu [ekki] gera Páli kleift/mögulegt að klæða sig

So_2_nh_þf_nf → so_2_þf_nf_nh
So_2_nh_þf_þf → so_2_þf_þf_nh
So_2_nh_þf_þgf → so_2_þf_þgf_nh
So_2_nh_þf_ef → so_2_þf_ef_nh

So_2_nh_þgf_nf → so_2_þgf_nf_nh
So_2_nh_þgf_þf → so_2_þgf_þf_nh
So_2_nh_þgf_þgf → so_2_þgf_þgf_nh
So_2_nh_þgf_ef → so_2_þgf_ef_nh

# Sögn í boðhætti með tveimur viðföngum

Sögn_2_bh →
    so_2_þgf_þf_bh Nl_þgf Nl_þf        # Gefðu Páli gjöfina
    | so_2_þf_ef_bh Nl_þf Nl_ef        # Krefðu Pál svars
    | so_2_þf_þf_bh Nl_þf Nl_þf        # Kostaðu Pál skildinginn
    | so_2_þf_þgf_bh Nl_þf Nl_þgf      # Kallaðu Pál öllum illum nöfnum
    | so_2_þgf_þgf_bh Nl_þgf Nl_þgf    # Skilaðu Páli bílnum
    | so_2_þgf_ef_bh Nl_þgf Nl_ef      # Óskaðu Páli alls hins besta
    | so_2_þgf_nf_bh Nl_þgf Nl_nf      # Vertu Páli stoð og stytta
    | 'gera:so'_bh Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Gerðu Páli kleift/mögulegt að klæða sig í fötin

# Sögn með einu viðfangi ('borðaði matinn')
Sögn_1/tala/pers/kyn →

    # (þf:)  Konurnar borðuðu og meltu [ekki] matinn
    # (þgf:) Konurnar afneituðu, og beinlínis höfnuðu, [ekki] Páli
    # (ef:)  Konurnar nutu [ekki] matarins
    # (nf:)  Konurnar voru [ekki] leikarar

    # (Páll) klappaði og strauk kettinum
    # (Kjósandi) velur með persónkjöri frambjóðendur sem hann kýs
    # (Við) einsetjum okkur að vinna með öðrum þjóðum
    # (Sveitarfélögin) ráða sjálf málefnum sínum
    # (Hún) sagði lafmóð/sjálf söguna
    so_1/fall/tala/pers OgSögn_1/fall/tala/pers* FsRuna? LoTengtSögn_nf/tala/kyn? Nl/fall NhFylling?

    # (Ég tel/segi/álít) Pál vera dagfarsprúðan mann
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* SagnarBotn

    # | Sögn_1_mm/tala/pers/kyn  # !!! Á ekki að þurfa sértilvik lengur

    | HjSögn/tala/pers FsAtv? so_mm_sagnb OgSögn_mm_sagnb* Nl_þgf    # (Bankar) hafa reynst og gagnast mér (vel)

    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn Nl_þgf # Konan hefur verið/skal/mun gefin Páli
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn Nl_ef   # Konan hefur verið/skal/mun spurð álits
    | HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn Nl_þgf # Konan hefur verið/skal/mun kölluð öllum illum nöfnum
    # HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_ef_lhþt/tala/kyn Nl_ef  # Konan hefur verið/skal/mun óska alls hins besta
    # HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þf_lhþt/tala/kyn Nl_þf   # Konan hefur verið/skal/mun kosta skildinginn

#Sögn_1_mm/tala/pers/kyn →
#    so_mm/tala/pers OgSögn_mm/tala/pers* Nl_þgf LoTengtSögn_nf/tala/kyn? # (Ég) giftist [og tengdist] Önnu [galvaskur]

# Kjósa miðmyndarform + þgf fremur en almenn sagnform + önnur föll
#$score(+2) Sögn_1_mm/tala/pers/kyn

# Býli í landinu eru/geta verið/verða/munu vera 3.000/tuttugu [talsins]
Sögn_1_ft/pers/kyn →
    HjSögnLhÞt_ft/pers FsAtv? Töluorð_ft_nf/kyn

# Sagnir sem geta staðið með þf + nh
# '(Ég) álít (Pál vera dagfarsprúðan mann)'
# '(Guðrún) lét (soninn hafa bílinn)'
# '(Við) sáum (strákinn vaxa úr grasi)'
# '(Jón) kvað (stíginn vera öruggan)'
# '(Myndbandið) sýndi (björgunarmenn bera hann inn í sjúkrabíl)'
So_1_þf_nh/tala/pers →
    'segja:so'_gm/tala/pers
    | 'telja:so'_gm/tala/pers
    | 'láta:so'_gm/tala/pers
    | 'álíta:so'_gm/tala/pers
    | 'sjá:so'_gm/tala/pers
    | 'kveða:so'_gm/tala/pers
    | 'sýna:so'_gm/tala/pers

So_1_þf_nh →
    'segja:so'_gm_nh
    | 'telja:so'_gm_nh
    | 'láta:so'_gm_nh
    | 'álíta:so'_gm_nh
    | 'sjá:so'_gm_nh
    | 'kveða:so'_gm_nh
    | 'sýna:so'_gm_nh

# segist munu/vilja borða kökuna
# telst munu/vilja kjósa flokkinn
# læst munu/vilja virða meirihlutann
# kveðst munu/vilja virða ákvörðun ráðherrans
# sýnist munu/vilja slá metið
So_1_þf_mm →
    'segja:so'_mm
    | 'telja:so'_mm
    | 'láta:so'_mm
    | 'kveða:so'_mm
    | 'sýna:so'_mm

So_1_þf_nh_sagnb →
    'segja:so'_gm_sagnb
    | 'telja:so'_gm_sagnb
    | 'láta:so'_gm_sagnb
    | 'álíta:so'_gm_sagnb
    | 'sjá:so'_gm_sagnb
    | 'kveða:so'_gm_sagnb
    | 'sýna:so'_gm_sagnb

$score(+2) So_1_þf_nh_sagnb

SagnarBotn →
    # (Páll segir) mistökin strax valda vanda
    Nl_þf AtvFs? NhRuna
    # (Þingmaðurinn telur) niðurstöðuna vera ranga / (Verkfræðingurinn segir) útkomuna verða tvísýna
    | Nl_þf/tala/kyn AtvFs? HjSögnVeraVerða LoLiður_nf/tala/kyn

    # (Páll segir) mistökin [ekki] hafa verið gerð (á vakt Jóns)
    # (Páll segir) mistökin [ekki] hafa verið (í uppleggi Friðgeirs)
    # Hér er notað HjSögnEkkiVera í stað HjSögnVera til að forðast margræðni
    # gagnvart NhRunu
    | Nl_þf/tala/kyn AtvFs? HjSögnEkkiVera so_lhþt/tala/kyn?

    # (Hann segir) hvern farþega [strax] hafa fengið tíu evrur / hafa farið að grilla
    | Nl_þf/tala/kyn AtvFs? 'hafa:so'_nh HreinSögn_sagnb/tala/kyn # !!! Hér vantar að senda þolfall: 'galvaskan að grilla'

    # (Páll segir) Ásu [ekki] vera/hafa verið góðan stjórnanda
    | Nl_þf/tala AtvFs? HjSögnEkkiVera Nl_þf/tala

    # (Páll segir) skorta [á] (að farið sé að lögum)
    | so_subj_nh_none NhFylling?
    # (Páll segir) tefjast (að hefja framkvæmdir)
    | so_mm_nh NhFylling?
    # (Már segir) misskilnings gæta (hjá Hannesi)
    | Nl_ef AtvFs? so_subj_nh_ef
    # (Anna telur) fólki fækka í landinu
    | Nl_þgf AtvFs? so_subj_nh_þgf
    # (Már segir) [í grundvallaratriðum] gæta misskilnings (hjá Hannesi)
    | FsAtv? so_subj_nh_ef Nl_ef
    # !!! Ekki virðist þörf fyrir öfuga orðaröð í þgf eða þf

$score(+4) SagnarBotn

# Sögn með einu viðfangi sem stendur í undirsetningu
# ('Konurnar sem borðuðu matinn')
ÖfugSögn_1/tala/pers/kyn →

    # (þf:)  Konurnar sem borðuðu [ekki] matinn
    # (þgf:) Konurnar sem höfnuðu [ekki] Páli
    # (ef:)  Konurnar sem nutu [ekki] matarins
    # (nf:)  Konurnar sem voru [ekki] leikarar

    So_1_sagnb/fall HjSögn/tala/pers Nl/fall LoTengtSögn_nf/tala/kyn?  # Konurnar sem borðað höfðu matinn [sjálfar/galvaskar]
    | So_1_nh/fall HjSögnNh/tala/pers Nl/fall LoTengtSögn_nf/tala/kyn? # Konurnar sem borða munu matinn [allar/galvaskar]

    | Nl/fall AtvFs? so_1/fall/tala/pers LoTengtSögn_nf/tala/kyn?      # Konurnar sem matinn [snarlega] borðuðu [báðar/galvaskar]

    | so_2_þgf_þf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_þgf          # Konan sem gefin hafði verið/skal/mun Páli
    | Nl_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þgf_þf_lhþt/tala/kyn   # Konan sem Páli hafði verið/skal/mun gefin

    | so_2_þf_þgf_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_þgf          # Konan sem kölluð hafði verið/skal/mun illum nöfnum
    | Nl_þgf HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_þgf_lhþt/tala/kyn   # Konan sem illum nöfnum hafði verið/skal/mun kölluð

    | so_2_þf_ef_lhþt/tala/kyn HjSögnLhÞtSM/tala/pers Nl_ef            # Konan sem spurð hafði verið/skal/mun álits
    | Nl_ef HjSögnLhÞtSM/tala/pers FsAtv? so_2_þf_ef_lhþt/tala/kyn     # Konan sem álits hafði verið/skal/mun spurð

    | SoFrumlag_1_þf_nh FsAtv? NhRuna                                  # Orðin sem Páll lét [ekki] falla
    # so_2_þgf_ef, so_2_þgf_þgf og so_2_þf_þf virðast ekki geta staðið
    # í lhþt heldur aðeins sem sagnbót

SoFrumlag_1_þf_nh →
    Nl_nf/tala/pers So_1_þf_nh/tala/pers  # (Orðin sem) Páll lét (falla) / (Orðin sem) þú lést (falla)

SögnMeðF_1/tala/pers/kyn →

    # (þf:)  (Í dag) borðuðu konurnar [ekki] matinn [sjálfar/galvaskar]
    # (þgf:) (Í dag) afneituðu og höfnuðu konurnar [ekki] Páli [báðar/galvaskar]
    # (ef:)  (Í gærkvöldi) óskuðu konurnar [ekki] leyfis [allar/galvaskar] [að höfða mál]
    # (nf:)  (Í fyrra) voru konurnar [ekki] leikarar [sjálfar/galvaskar]

    so_1/fall/tala/pers OgSögn_1/fall/tala/pers* Nl_nf/tala/pers/kyn Nl/fall LoTengtSögn_nf/tala/kyn? NhFylling?

    | HjSögnLhÞtSM/tala/pers Nl_nf/tala/pers/kyn AtvFs? SagnarBotnLhÞt/tala/kyn

    # (Þegar gögnin hafa verið lesin) eru eimaðar upp úr þeim upplýsingar [á borð við nöfn]
    | HjSögnLhÞt/tala/pers FsAtv? so_lhþt_nf/tala/kyn FsAtv? Nl_nf/tala/pers/kyn

    # '(og) var Ástþór mjög ósáttur (við útvarpsstjóra)'
    | HjSögnLoMeðF/tala/kyn LoTengtSögn_nf/tala/kyn

    # (Í dag) segir hann (köttinn vera svangan)
    | So_1_þf_nh/tala/pers OgSögn_1_þf_nh/tala/pers* Nl_nf/tala/pers/kyn SagnarBotn

    # (Í dag) barst lögreglunni ábending
    | so_mm_op/tala Nl_þgf Nl_nf/tala

    # (Áður) hafði KR látið Jón og Gunnar fara / sagt niðurstöðuna vera ranga
    | HjSögn/tala/pers Nl_nf/tala/pers So_1_þf_nh_sagnb SagnarBotn

SögnMeðF_1_ft/pers/kyn →
    # (Í dag) eru og verða þetta tvítugir strákar [galvaskir] [að hella sig fulla]
    VeraVerða_gm_p3_ft OgVeraVerða_gm_p3_ft* 'þetta'_nf_et Nl_nf_ft/pers/kyn LoTengtSögn_nf_ft/kyn? NhFylling?

SagnarBotnLhÞt/tala/kyn →
    # (Í dag) verður konan gefin Páli [sjálf]
    so_2_þgf_þf_lhþt/tala/kyn Nl_þgf LoTengtSögn_nf/tala/kyn?
    # (Í dag) er/skal/mun þingmaðurinn kallaður illum nöfnum [saklaus]
    | so_2_þf_þgf_lhþt/tala/kyn Nl_þgf LoTengtSögn_nf/tala/kyn?
    # (Í dag) mun konan spurð álits [galvösk]
    | so_2_þf_ef_lhþt/tala/kyn Nl_ef LoTengtSögn_nf/tala/kyn?

# Sögn með tveimur viðföngum ('gaf Páli gjöfina')
Sögn_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_þf     # Konurnar gáfu Páli [sjálfar] gjöfina

    # gera + lo ('að gera einhverjum kleift að fara út')
    | 'gera:so'/tala/pers Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv     # Konurnar gerðu Páli kleift að matast með reisn

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers Nl_þf LoTengtSögn_nf/tala/kyn? Nl_ef     # Konurnar kröfðu Pál [herskáar] svars

    # Þágufall + eignarfall ('að óska einhverjum góðs bata')
    | so_2_þgf_ef/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_ef   # Konurnar óskuðu Páli [allar] góðs bata

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_þgf # Konurnar skiluðu Páli [sjálfar] bílnum

    # Þágufall + nefnifall ('að vera einhverjum eitthvað')
    | so_2_þgf_nf/tala/pers Nl_þgf LoTengtSögn_nf/tala/kyn? Nl_nf   # Konurnar voru Páli [allar] stoð og stytta

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers Nl_þf LoTengtSögn_nf/tala/kyn? Nl_þf     # Konurnar kostuðu Pál [allar] skildinginn

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers Nl_þf LoTengtSögn_nf/tala/kyn? Nl_þgf   # Konurnar kölluðu Pál [æstar] illum nöfnum

# Sögn með tveimur viðföngum sem stendur í undirsetningu
# ('Konan sem gaf Páli gjöfina')
ÖfugSögn_2/tala/pers/kyn →

    so_2_þgf_þf_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_þf                          # Konurnar sem gefið hafa Páli gjöfina
    | 'gera:so'_sagnb HjSögn/tala/pers
        Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # Konurnar sem gert hafa Páli kleift að matast með reisn
    | so_2_þgf_þf_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_þf                          # Konurnar sem gefa munu Páli gjöfina

    | so_2_þf_ef_sagnb HjSögn/tala/pers 
        Nl_þf Nl_ef                           # Konurnar sem spurt hafa Pál spurningarinnar
    | so_2_þf_ef_nh HjSögnNh/tala/pers 
        Nl_þf Nl_ef                           # Konurnar sem spyrja munu Pál spurningarinnar

    | so_2_þgf_ef_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_ef                          # Konurnar sem óskað hafa Páli góðs bata
    | so_2_þgf_ef_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_ef                          # Konurnar sem óska munu Páli góðs bata

    | so_2_þgf_þgf_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_þgf                         # Konurnar sem skilað hafa Páli bílnum
    | so_2_þgf_þgf_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_þgf                         # Konurnar sem skila munu Páli bílnum

    | so_2_þgf_nf_sagnb HjSögn/tala/pers 
        Nl_þgf Nl_nf                          # Konurnar sem verið hafa Páli stoð og stytta
    | so_2_þgf_nf_nh HjSögnNh/tala/pers 
        Nl_þgf Nl_nf                          # Konurnar sem vera munu Páli stoð og stytta

    | so_2_þf_þf_sagnb HjSögn/tala/pers 
        Nl_þf Nl_þf                           # Konurnar sem kostað hafa Pál skildinginn
    | so_2_þf_þf_nh HjSögnNh/tala/pers 
        Nl_þf Nl_þf                           # Konurnar sem kosta munu Pál skildinginn

    | so_2_þf_þgf_sagnb HjSögn/tala/pers 
        Nl_þf Nl_þgf                          # Konurnar sem kallað hafa Pál illum nöfnum
    | so_2_þf_þgf_nh HjSögnNh/tala/pers 
        Nl_þf Nl_þgf                          # Konurnar sem kalla munu Pál illum nöfnum

SögnMeðF_2/tala/pers/kyn →

    # Þágufall + þolfall ('að gefa einhverjum kött')
    so_2_þgf_þf/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_þf      # (Í dag) gáfu konurnar [bersýnilega] Páli [ekki] gjöfina

    # 'gera' + lo ('að gera einhverjum eitthvað kleift')
    | 'gera:so'/tala/pers Nl_nf/tala/pers/kyn Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # (Í dag) gerðu konurnar Páli kleift að matast með reisn

    # Þolfall + eignarfall ('að spyrja einhvern einhvers')
    | so_2_þf_ef/tala/pers Nl_nf/tala/pers/kyn Nl_þf Nl_ef      # (Í gær) kröfðu konurnar Pál [ekki] svars

    # Þágufall + eignarfall ('að óska einhverjum einhvers')
    | so_2_þgf_ef/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_ef    # (Í gær) óskuðu konurnar Páli [ekki] góðs bata

    # Þágufall + nefnifall ('að vera einhverjum eitthvap')
    | so_2_þgf_nf/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_nf    # (Í gær) voru konurnar Páli [ekki] stoð og stytta

    # Þolfall + þolfall ('að kosta einhvern eitthvað')
    | so_2_þf_þf/tala/pers Nl_nf/tala/pers/kyn Nl_þf Nl_þf      # (Í gær) kostuðu konurnar Pál [ekki] skildinginn

    # Þágufall + þágufall ('að skila einhverjum einhverju')
    | so_2_þgf_þgf/tala/pers Nl_nf/tala/pers/kyn Nl_þgf Nl_þgf  # (Í gær) skiluðu konurnar Páli [ekki] bílnum

    # Þolfall + þágufall ('að kalla einhvern illum nöfnum')
    | so_2_þf_þgf/tala/pers Nl_nf/tala/pers/kyn Nl_þf Nl_þgf    # (Í gær) kölluðu konurnar Pál [ekki] illum nöfnum

    # Sagnbót og nafnháttur    
    | HjSögn/tala/pers Nl_nf/tala/pers/kyn AtvFs? Sögn_2_sagnb  # (Í morgun) höfðu konurnar [ekki] krafið Pál svara
    | HjSögnNh/tala/pers Nl_nf/tala/pers/kyn AtvFs? Sögn_2_nh   # (Á morgun) munu konurnar [ekki] krefja Pál svara

# Hjálparsagnasambönd: vera, hafa, geta, skulu, munu...
# Aðalsögnin er sagnbót (farið/gert/viljað/borðað...)
HjSögn/tala/pers →
    'hafa:so'/tala/pers Geta_sagnb?                # hafðir [í fátinu ekki getað] farið
    | 'geta:so'/tala/pers                          # gast farið
    | 'fá:so'/tala/pers                            # (allir) fái notið (þeirra réttinda)
    | 'geta:so'/tala/pers FsAtv? 'hafa'_nh         # gast [ekki] hafa farið
    | 'vilja:so'/tala/pers FsAtv? HafaGetaFá_nh    # vildir [ekki] geta/hafa farið
    | 'mega:so'/tala/pers FsAtv? HafaGetaFá_nh     # máttir [ekki] geta/hafa farið
    | 'þykja:so'/tala/pers FsAtv? HafaGetaFá_nh    # þóttir [ekki] geta/hafa farið
    | 'skulu:so'/tala/pers FsAtv? HafaGetaFá_nh    # skulir [ekki] geta/hafa farið
    | 'munu:so'/tala/pers FsAtv? HafaGetaFá_nh     # munir [ekki] geta/hafa farið
    | 'eiga:so'/tala/pers FsAtv? nhm HafaGetaFá_nh # átt [ekki] að geta/hafa farið
    | so_mm/tala/pers FsAtv? HjSögn_mm_nh       # segist [ekki] hafa farið

HafaGetaFá_nh →
    'hafa:so'_nh | 'geta:so'_nh | 'fá:so'_nh

Geta_sagnb →
    FsAtv? 'geta:so'_sagnb

GetaAtv_sagnb →
    AtvFs? 'geta:so'_sagnb

$score(+3) HafaGetaFá_nh     # Gefa plús fyrir flókin sagnform

HjSögn_mm_nh →
    'hafa:so'_nh                # telst hafa farið
    | 'geta:so'_nh 'hafa:so'_nh?   # sýnist geta hafa farið
    | 'vilja:so'_nh 'hafa:so'_nh   # segist vilja hafa farið
    | 'mega:so'_nh 'hafa:so'_nh    # virðist mega hafa farið
    | 'munu:so'_nh 'hafa:so'_nh    # segist munu hafa farið
    | 'skulu:so'_nh 'hafa:so'_nh   # segjast skulu hafa farið(?)
    | 'fá:so'_nh                # þykist fá notið

HjSögnVh →
    'hafa:so'_et_p3_vh                         # hafi þurft
    | 'geta:so'_et_p3_vh                       # gæti þurft
    | 'geta:so'_et_p3_vh FsAtv? 'hafa:so'_nh      # gæti [ekki] hafa þurft
    | 'mega:so'_et_p3_vh FsAtv? 'hafa:so'_nh      # mætti [ekki] hafa þurft
    | 'þykja:so'_et_p3_vh FsAtv? 'hafa:so'_nh     # þætti [ekki] hafa þurft
    | 'skulu:so'_et_p3_vh FsAtv? 'hafa:so'_nh     # skyldi [ekki] hafa þurft
    | 'munu:so'_et_p3_vh FsAtv? 'hafa:so'_nh      # myndi [ekki] hafa þurft

# Sögnin stendur í nafnhætti með annarri nafnháttarsögn,
# sbr. 'Gæta verða/skulu/eiga/vilja þeir þingskapa'
HjSögnNhMeðSo/tala/pers →
    'verða:so'/tala/pers       # gæta verða þeir / verður þú
    | 'skulu:so'/tala/pers     # gæta skulu þeir / skal ég
    | 'munu:so'/tala/pers      # gæta munu þeir / mun hann
    | 'mega:so'/tala/pers      # gæta mega þeir
    | 'eiga:so'/tala/pers      # gæta eiga þeir
    | 'þurfa:so'/tala/pers     # gæta þurfa þeir / þurfið þið
    | 'vilja:so'/tala/pers     # gæta vilja þeir / viljið þið

HjSögnNhMeðSagnb/tala/pers →
    'verða:so'/tala/pers       # verða mennirnir að hafa/geta... lent
    | 'eiga:so'/tala/pers      # eiga mennirnir að hafa/geta... lent
    | 'þurfa:so'/tala/pers     # þurfa mennirnir að hafa/geta... lent

# Sögnin stendur í nafnhætti
HjSögnNh/tala/pers →
    'vilja:so'/tala/pers       # vilja fara
    | 'láta:so'/tala/pers      # læt/læst fara
    | 'vilja:so'/tala/pers FsAtv? 'láta:so'_nh # vill láta fara
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'láta:so'_sagnb # vildir [gjarnan] hafa látið fara
    | 'skulu:so'/tala/pers     # skulu fara
    | 'munu:so'/tala/pers      # munu fara
    | 'mega:so'/tala/pers      # mega fara
    | 'þykja:so'/tala/pers     # þykja/þykjast setja (leiðinlegan svip á bæinn)
    | 'mega:so'/tala/pers FsAtv? 'láta:so'_nh     # mætti láta fara
    | 'skulu:so'/tala/pers FsAtv? 'þykja:so'_nh   # (flegni kjóllinn) skuli [ekki] þykja vera (í lagi)
    | 'skulu:so'/tala/pers FsAtv? 'mega:so'_nh    # skyldu [ekki] mega fara
    | 'skulu:so'/tala/pers FsAtv? 'vilja:so'_nh   # skyldu [ekki] vilja fara
    | 'skulu:so'/tala/pers FsAtv? 'láta:so'_nh    # skuli [ekki] láta fara
    | 'munu:so'/tala/pers FsAtv? 'þykja:so'_nh    # (flegni kjóllinn) myndi [ekki] þykja vera (siðsamlegur)
    | 'munu:so'/tala/pers FsAtv? 'mega:so'_nh     # myndu [ekki] mega fara
    #| 'munu'/tala/pers FsAtv? 'hafa'_nh? 'vilja'_nh 'láta'_nh? # myndu [ekki] vilja [láta] fara   - GÖLLUÐ REGLA, leyfir líka "munu hafa vilja láta fara" TODO laga
    | 'munu:so'/tala/pers FsAtv? 'láta:so'_nh     # myndu [ekki] láta fara
    | 'hafa:so'/tala/pers FsAtv? 'þykja:so'_sagnb # hafa [ekki] þótt fara
    | 'hafa:so'/tala/pers FsAtv? 'mega:so'_sagnb  # hafa [ekki] mátt fara
    | 'hafa:so'/tala/pers FsAtv? 'vilja:so'_sagnb 'láta:so'_nh? # hafa [ekki] viljað [láta] fara
    | 'hafa:so'/tala/pers FsAtv? 'láta:so'_sagnb  # hafa [ekki] látið fara
    | 'geta:so'/tala/pers FsAtv? 'láta:so'_sagnb  # getur [ekki] látið fara
    | 'geta:so'/tala/pers FsAtv? 'þykja:so'_sagnb # getur [ekki] þótt fara
    | 'geta:so'/tala/pers FsAtv? 'mega:so'_sagnb  # getur [ekki] mátt fara
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'vilja:so'_sagnb 'láta:so'_nh? # getur [ekki] viljað fara / gæti viljað láta fara

HjSögnNh_p3_et → HjSögnNh_et_p3
HjSögnNh_p3_ft → HjSögnNh_ft_p3

HjSögnÞurfaEiga_et_p3 →
    'þurfa:so'_et_p3                   # Greinarmun þarf/þyrfti að gera
    | 'eiga:so'_et_p3                  # Greinarmun á/ætti að gera
    | 'verða:so'_et_p3                 # Greinarmun verður/varð/yrði að gera
    | 'hafa:so'_et_p3 'þurfa:so'_sagnb    # Engan hefur þurft að undra
    | 'hafa:so'_et_p3 'eiga:so'_sagnb     # Engan hefði átt að undra
    | 'hafa:so'_et_p3 'verða:so'_sagnb    # Greinarmun hefði orðið að gera
    | 'munu:so'_et_p3 'þurfa:so'_nh       # Engan mun þurfa að undra
    | 'skulu:so'_et_p3 'þurfa:so'_nh      # Engan skal þurfa að undra
    | 'munu:so'_et_p3 'eiga:so'_nh        # Greinarmun mun eiga að gera
    | 'skulu:so'_et_p3 'eiga:so'_nh       # Greinarmun skal eiga að gera
    | 'munu:so'_et_p3 'verða:so'_nh       # Greinarmun mun verða að gera
    | 'skulu:so'_et_p3 'verða:so'_nh      # Greinarmun skal verða að gera

HjSögnMegiSkuli_et_p3 →
    'mega:so'_et_p3                    # (Þann lærdóm) megi/mætti/má (draga)
    | 'skulu:so'_et_p3                 # (Þann lærdóm) skuli/skyldi/skal (draga)
    | 'hafa:so'_et_p3 'mega:so'_sagnb     # (Þann lærdóm) hefur/hefði mátt (draga)

HjSögnMegiSkuliÞurfi_et_p3 →
    'mega:so'_et_p3                    # (Endurnýja) megi/mætti/má (leigusamninginn)
    | 'eiga:so'_et_p3                  # (Endurnýja) á/eigi/átti (leigusamninginn)
    | 'þurfa:so'_et_p3                 # (Endurnýja) þyrfti/þarf/þurfti (leigusamninginn)
    | 'skulu:so'_et_p3                 # (Endurnýja) skuli/skyldi/skal (leigusamninginn)
    | 'verða:so'_et_p3                 # (Endurnýja) verður/yrði/varð (leigusamninginn)
    | 'hafa:so'_et_p3 'mega:so'_sagnb     # (Endurnýja) hefur/hafði/hefði mátt (leigusamninginn)
    | 'hafa:so'_et_p3 'þurfa:so'_sagnb    # (Endurnýja) hefur/hafði/hefði þurft (leigusamninginn)
    | 'hafa:so'_et_p3 'eiga:so'_sagnb     # (Endurnýja) hefur/hafði/hefði átt (leigusamninginn)

# Vera/verða sambönd, öll nema sjálfar sagnirnar 'vera' og 'verða'
HjSögnEkkiVera →
    'vilja:so'_nh 'vera:so'_nh              # (Mér sýndist Anna) vilja vera (ein)
    | 'mega:so'_nh 'vera:so'_nh               # (Mér sýndist Anna) mega vera (ein)
    | 'hafa:so'_nh 'vera:so'_sagnb            # (Mér sýndist Anna) hafa verið (valin)
    | 'geta:so'_nh 'hafa:so'_nh? 'vera:so'_sagnb # (Mér sýndist Anna) geta [hafa] verið (farin)
    | 'vilja:so'_nh 'hafa:so'_nh 'vera:so'_sagnb # (Mér sýndist Anna) vilja hafa verið (ein)
    | 'mega:so'_nh 'hafa:so'_nh 'vera:so'_sagnb  # (Mér sýndist Anna) mega hafa verið (ánægð)
    | 'þykja:so'_nh 'hafa:so'_nh 'vera:so'_sagnb # (Félögin) þykja hafa verið (illa rekin)
    | 'vilja:so'_nh 'verða:so'_nh
    | 'mega:so'_nh 'verða:so'_nh
    | 'hafa:so'_nh 'verða:so'_sagnb
    | 'geta:so'_nh 'hafa:so'_nh? 'verða:so'_sagnb
    | 'vilja:so'_nh 'hafa:so'_nh 'verða:so'_sagnb
    | 'mega:so'_nh 'hafa:so'_nh 'verða:so'_sagnb
    | 'þykja:so'_nh 'hafa:so'_nh 'verða:so'_sagnb # (Félögin) þykja hafa orðið (illa rekin)

HjSögnVeraVerða →
    'vera:so'_nh | 'verða:so'_nh

# Vera/verða í nafnhætti ('(Mér sýndist kötturinn) vera (gulbrúnn)')
HjSögnVera →
    HjSögnEkkiVera | HjSögnVeraVerða

# Lýsingarháttur þátíðar: vera farinn, hafa verið farinn, skal vera farinn, mun vera farinn
HjSögnLhÞt/tala/pers →
    'vera:so'/tala/pers                            # eru farnar
    | 'vilja:so'/tala/pers FsAtv? 'vera:so'_nh        # vildu [ekki] vera farnar
    | 'þykja:so'/tala/pers FsAtv? 'vera:so'_nh        # þóttu [ekki] vera farnar
    | 'mega:so'/tala/pers FsAtv? 'vera:so'_nh         # máttu [ekki] vera farnar
    | 'skulu:so'/tala/pers FsAtv? 'vera:so'_nh        # skulu [ekki] vera farnar
    | 'munu:so'/tala/pers FsAtv? 'vera:so'_nh         # munu [ekki] vera farnar
    | 'hafa:so'/tala/pers FsAtv? 'vera:so'_sagnb      # hafa [ekki] verið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'vera:so'_sagnb # geta [ekki] verið farnar / geta hafa verið farnar
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # vildu [ekki] hafa verið farnar
    | 'þykja:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # þóttu [ekki] hafa verið farnar
    | 'mega:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # máttu [ekki] hafa verið farnar
    | 'skulu:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb # skulu [ekki] hafa verið farnar
    | 'munu:so'/tala/pers FsAtv? 'hafa:so'_nh 'vera:so'_sagnb  # munu [ekki] hafa verið farnar
    | 'verða:so'/tala/pers                          # verða farnar
    | 'vilja:so'/tala/pers FsAtv? 'verða:so'_nh        # vildu [ekki] verða farnar
    | 'þykja:so'/tala/pers FsAtv? 'verða:so'_nh        # þóttu [ekki] verða farnar
    | 'mega:so'/tala/pers FsAtv? 'verða:so'_nh         # máttu [ekki] verða farnar
    | 'skulu:so'/tala/pers FsAtv? 'verða:so'_nh        # skulu [ekki] verða farnar
    | 'munu:so'/tala/pers FsAtv? 'verða:so'_nh         # munu [ekki] verða farnar
    | 'hafa:so'/tala/pers FsAtv? 'verða:so'_sagnb      # hafa [ekki] orðið farnar
    | 'geta:so'/tala/pers FsAtv? 'hafa:so'_nh? 'verða:so'_sagnb # geta [ekki] orðið farnar / geta hafa orðið farnar
    | 'vilja:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # vildu [ekki] hafa orðið farnar
    | 'þykja:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # þóttu [ekki] hafa orðið farnar
    | 'mega:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb  # máttu [ekki] hafa orðið farnar
    | 'skulu:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb # skulu [ekki] hafa orðið farnar
    | 'munu:so'/tala/pers FsAtv? 'hafa:so'_nh 'verða:so'_sagnb  # munu [ekki] hafa orðið farnar

HjSögnLhÞt_p3_et → HjSögnLhÞt_et_p3
HjSögnLhÞt_p3_ft → HjSögnLhÞt_ft_p3

# Hjálparsagnir með inniföldu frumlagi þegar lýsingarorð kemur á eftir
# ('Í dag þykir Ástþór [ekki lengur] vera (sáttur)')
HjSögnLoMeðF/tala/kyn →
    'vera:so'/tala/pers Nl_nf/tala/pers/kyn                        # er Páll (sáttur)
    | so_mm/tala/pers Nl_nf/tala/pers/kyn                       # telst Páll (sáttur)

    | 'vilja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # vill Páll [ekki] vera (sáttur)
    | 'þykja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # þótti Páll [ekki] vera (sáttur)
    | 'mega:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_nh     # mátti Páll [ekki] vera (sáttur)
    | 'skulu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_nh    # skal Páll [ekki] vera (sáttur)
    | 'munu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_nh     # mun Páll [ekki] vera (sáttur)
    | so_mm/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_nh      # sagðist Páll [ekki] vera (sáttur)

    | 'hafa:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'vera:so'_sagnb  # hefur Páll [ekki] verið (sáttur)
    | 'geta:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh? 'vera:so'_sagnb # getur Páll [ekki] [hafa] verið (sáttur)
    | 'vilja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # vildi Páll [ekki] hafa verið (sáttur)
    | 'þykja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # þótt Páll [ekki] hafa verið (sáttur)
    | 'mega:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # mátti Páll [ekki] hafa verið (sáttur)
    | 'skulu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb # skal Páll [ekki] hafa verið (sáttur)
    | 'munu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb  # mun Páll [ekki] hafa verið (sáttur)
    | so_mm/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'vera:so'_sagnb  # sagðist Páll [ekki] hafa verið (sáttur)

    | 'verða:so'/tala/pers Nl_nf/tala/pers/kyn                          # verður Páll (sáttur)
    | 'vilja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_nh        # vildi Páll [ekki] verða (sáttur)
    | 'þykja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_nh        # þótti Páll [ekki] verða (sáttur)
    | 'mega:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_nh         # mátti Páll [ekki] verða (sáttur)
    | 'skulu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_nh        # skal Páll [ekki] verða (sáttur)
    | 'munu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_nh         # mun Páll [ekki] verða (sáttur)
    | so_mm/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_nh          # sagðist Páll [ekki] verða (sáttur)
    | 'hafa:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'verða:so'_sagnb      # hefur Páll [ekki] orðið (sáttur)

    | 'geta:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh? 'verða:so'_sagnb # getur Páll [ekki] orðið (sáttur) / geta hafa orðið (sáttur)
    | 'vilja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # vildi Páll [ekki] hafa orðið (sáttur)
    | 'þykja:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # þótti Páll [ekki] hafa orðið (sáttur)
    | 'mega:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb  # mátti Páll [ekki] hafa orðið (sáttur)
    | 'skulu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb # skal Páll [ekki] hafa orðið (sáttur)
    | 'munu:so'/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb  # mun Páll [ekki] hafa orðið (sáttur)
    | so_mm/tala/pers Nl_nf/tala/pers/kyn AtvFs? 'hafa:so'_nh 'verða:so'_sagnb   # sagðist Páll [ekki] hafa orðið (sáttur)

# 'Konan skal/mun gefin Páli'
HjSögnSkuluMunu/tala/pers →
    'skulu:so'/tala/pers | 'munu:so'/tala/pers

# Sameiginleg regla fyrir LhÞt og SkuluMunu

HjSögnLhÞtSM/tala/pers →
    HjSögnLhÞt/tala/pers | HjSögnSkuluMunu/tala/pers

HjSögnLhÞtSM_p3_et →
    HjSögnLhÞt_et_p3 | HjSögnSkuluMunu_et_p3

HjSögnLhÞtSM_p3_ft →
    HjSögnLhÞt_ft_p3 | HjSögnSkuluMunu_ft_p3

HjSögnSagnb_et_p3 →
    HjSögnLhÞtSM_et_p3 | 'hafa:so'_et_p3

# 'Hælisleitendum fer/fór/færi/hefur farið/mun fara fækkandi'

HjSögnLhNt →
    'fara:so'_gm_p3_et
    | 'hafa:so'_gm_p3_et FsAtv? 'geta:so'_sagnb? 'fara:so'_sagnb # hefur/hafði/hefði [ekki] [getað] farið (fækkandi)
    | 'geta:so'_gm_p3_et FsAtv? 'hafa:so'_nh? 'fara:so'_sagnb    # getur/gat/gæti [hafa] farið (fækkandi)
    | 'munu:so'_gm_p3_et FsAtv? 'fara:so'_nh                  # mun/myndi [ekki] fara (fækkandi)
    | 'þykja:so'_gm_p3_et FsAtv? 'fara:so'_nh                 # þykir/þótti/þætti [ekki] fara (fækkandi)
    | 'munu:so'_gm_p3_et FsAtv? 'hafa:so'_nh 'fara:so'_sagnb     # mun/myndi [ekki] hafa farið (fækkandi)
    | 'þykja:so'_gm_p3_et FsAtv? 'hafa:so'_nh 'fara:so'_sagnb    # þykir/þótti/þætti [ekki] hafa farið (fækkandi)

# 'Íslandsbanka hefur tekist að halda í horfinu'

HjSögnMM →
    'hafa:so'_gm_p3_et FsAtv?               # hefur [ekki] tekist
    | 'geta:so'_gm_p3_et FsAtv? 'hafa:so'_nh?  # getur [ekki] [hafa] tekist
    | 'munu:so'_gm_p3_et FsAtv? 'hafa:so'_nh   # mun [ekki] hafa tekist
    | 'skulu:so'_gm_p3_et FsAtv? 'hafa:so'_nh  # skal [ekki] hafa tekist
    | 'þykja:so'_gm_p3_et FsAtv? 'hafa:so'_nh  # þykir [ekki] hafa tekist
    | 'mega:so'_gm_p3_et FsAtv? 'hafa:so'_nh   # má [ekki] hafa tekist

HjSögnFinnst →
    'finna:so'_mm_p3_et      # Okkur finnst/fannst skemmtilegt
    | 'finna:so'_mm_vh_p3_et # Okkur finnist/fyndist skemmtilegt
    | 'þykja:so'_gm_p3_et    # Okkur þykir/þótti skemmtilegt
    | 'þykja:so'_gm_vh_p3_et # Okkur þyki/þætti skemmtilegt

HjSögnFinnst_sagnb →
    'finna:so'_mm_sagnb    # Okkur hefur fundist skemmtilegt
    | 'þykja:so'_gm_sagnb  # Okkur hefur þótt skemmtilegt

# HjSögnÞaðLo_et_p3 →
#     'vera:so'_gm_p3_et 'verða:so'_sagnb? # Það er [orðið] brýnt að...
#     | 'verða:so'_gm_p3_et  # Það verður brýnt að bæta umgengni
#     | 'hafa:so'_gm_p3_et 'vera:so'_sagnb  # Það hefur verið brýnt að...
#     | 'hafa:so'_gm_p3_et 'verða:so'_sagnb # Það hefur orðið brýnt að...
#     | 'hafa:so'_gm_p3_et 'þykja:so'_sagnb # Það hefur þótt brýnt að...
#     | 'þykja:so'_gm_p3_et 'vera:so'_nh? # Það þykir [vera] brýnt að...
#     | 'þykja:so'_gm_p3_et 'vera:so'_nh? 'verða'_sagnb # Það þykir [vera] orðið brýnt að...
#     | 'munu:so'_gm_p3_et 'vera:so'_nh 'verða:so'_sagnb? # Það mun vera [orðið] brýnt að...
#     | 'munu:so'_gm_p3_et 'hafa:so'_nh 'þykja:so'_sagnb # Það mun hafa þótt brýnt að...
#     | 'munu:so'_gm_p3_et 'þykja:so'_nh # Það mun þykja brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'vera:so'_sagnb # Það getur [hafa] verið brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'verða:so'_sagnb # Það getur [hafa] orðið brýnt að...
#     | 'geta:so'_gm_p3_et 'hafa:so'_nh? 'þykja:so'_sagnb # Það getur [hafa] þótt brýnt að...

# Nafnháttarfylling
# - tengd frumlagi
# '(Þeir ætluðu) [sannarlega galvaskir/sjálfir] að þvo bílinn í gær'
# '(Þar hættir landið) [sjálft] að vera jarðneskt'
NhFylling/tala/kyn →
    Sjálfur/tala/kyn? NhFylling_nf/tala/kyn    # Nefnifall sjálfgefið

$score(+2) NhFylling/tala/kyn # Gefa 'að borða köku' forgang sem NhFylling fremur en fs+no, til dæmis fs+no_þgf+no_ef

# Aðeins nefnifall og þágufall eru notuð
NhFylling_nf/tala/kyn → EinNhFylling_nf/tala/kyn OgNhFylling_nf/tala/kyn*
NhFylling_þgf/tala/kyn → EinNhFylling_þgf/tala/kyn OgNhFylling_þgf/tala/kyn*

EinNhFylling/fall/tala/kyn →
    # '(Hann ætlar) strax að fara að millilenda'
    SagnInnskot? NhLiðir
    # '(Hann er) í reynd strax búinn að hafna þessu'
    | FsRuna? LoTengtSögn/tala/fall/kyn NhLiðir
    # 'að þykja og vera fallegt'
    | SagnInnskot? nhm so_1_nf_nh OgNhSögn_nf* LoTengtSögn/tala/fall/kyn
    # 'að sýnast og þykjast fallegt'
    | SagnInnskot? nhm so_mm_nh OgSögn_mm_nh* LoTengtSögn/tala/fall/kyn

OgNhFylling/fall/tala/kyn →
    OgEða EinNhFylling/fall/tala/kyn

# - ótengd frumlagi: lýsingarliður ekki mögulegur
# (nema þá hugsanlega í 3p. et.?)
NhFylling →
    FsAtv? NhLiðir

NhFyllingAtv →
    NhFylling SagnInnskot?

NhFyllingÁnAtv →
    NhLiðir SagnInnskot? # Getur ekki byrjað á FsAtv

$tag(begin_prep_scope) NhFyllingAtv # Nýtt undirtré fyrir tengingu sagnar og forsetningar
$tag(begin_prep_scope) NhFyllingÁnAtv # Nýtt undirtré fyrir tengingu sagnar og forsetningar

NhFyllingEðaAtv →
    NhFyllingAtv | SagnInnskot?

# Nafnháttarliður
# 'að lenda snarlega á flugvellinum'
# 'að sigla skipinu í land strax'
NhLiður →
    nhm NhRuna

$score(+2) NhLiður

NhLiðurFrh →
    SagnInnskot? NhLiður

# 'að fara strax að athuga málið'
NhLiðir →
    NhLiður NhLiðurFrh*

NhRuna → NhSögn OgNhLiður*

OgNhLiður →
    # (að selja skyldi hestinn) snarlega, en ekki hætta baráttunni
    # (er ekki unnt að fara) nema nota allt fé ríkisins
    SagnInnskot? KommaOgEðaEnNema SagnInnskot? NhSögn

NhSögn →
    so_0_nh so_0_gm_vh_p3_et? OgNhSögn*                          # að kjósa [þurfi] og telja (upp á nýtt)
    | so_1_nf_nh so_0_gm_vh_p3_et? OgNhSögn_nf* FsRuna? Nl_nf    # að hrjósa [muni] hugur
    | so_1_þf_nh so_0_gm_vh_p3_et? OgNhSögn_þf* FsRuna? Nl_þf    # að selja [skyldi] í snatri hestinn
    | so_1_þgf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf* FsRuna? Nl_þgf # að hætta [ætti] baráttunni
    | so_1_ef_nh so_0_gm_vh_p3_et? OgNhSögn_ef* FsRuna? Nl_ef    # að freista [eigi] gæfunnar
    | so_2_þgf_þf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_þf* FsRuna? Nl_þgf Nl_þf # að skrifa [skuli] og senda Karólínu bréfið
    | so_2_þf_ef_nh so_0_gm_vh_p3_et? OgNhSögn_þf_ef* FsRuna? Nl_þf Nl_ef    # að spyrja og krefja Pál svars
    | so_2_þf_þgf_nh so_0_gm_vh_p3_et? OgNhSögn_þf_þgf* FsRuna? Nl_þf Nl_þgf # að kalla [ætti] Pál illum nöfnum
    | so_2_þgf_ef_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_ef* FsRuna? Nl_þgf Nl_ef # að óska [eigi] og óska Páli góðs bata
    | so_2_þgf_nf_nh so_0_gm_vh_p3_et? OgNhSögn_þgf_nf* FsRuna? Nl_þgf Nl_nf # að vera [eigi] og vera Páli stoð og stytta
    #| so_2_þf_þf_nh so_0_gm_vh? OgNhSögn_þf_þf* Nl_þf Nl_þf    # !!! þarf?
    #| so_2_þgf_þgf_nh so_0_gm_vh? OgNhSögn_þgf_þgf* Nl_þgf Nl_þgf    # !!! þarf?
    | 'gera:so'_nh so_0_gm_vh_p3_et? Nl_þgf LoTengtSögn_nf_et_hk NhFyllingAtv # að gera [eigi] Páli kleift að matast með reisn
    # að reistar yrðu og byggðar íbúðir / að barinn skyldi fiskurinn og flakaður
    # !!! Ath: sennilega geta aðeins hjálparsagnir staðið í viðtengingarhætti hér,
    # !!! ekki allar sagnir
    | so_0_lhþt/tala/kyn so_0_gm_vh/tala OgNhSögn_lhþt/tala/kyn* Nl_nf/tala/kyn OgNhSögn_lhþt/tala/kyn*

    # 'giftast sér' / 'tengjast henni' !!! ATH: Þetta leyfir mun fleiri sagnir en raunverulega geta tekið þgf
    # | so_mm_nh Nl_þgf

    # (Páll fór inn eftir að) hafa fært bílinn / (fyrir að) geta lyft grettistaki
    | NhSögnHafaGetaFá

    # láta sjúklinginn ná eðlilegum líkamshita
    # segja Pál fara með rangt mál
    # telja verkefnið verða óviðráðanlegt
    # sjá drenginn fara í hundana
    | So_1_þf_nh Nl_þf NhSögn

    # betur megi fara / strax skuli tilkynna
    | HjSögnMegiSkuli_et_p3 NhSögn

NhSögnHafaGetaFá →
    HafaGetaFá_nh HreinSögn_sagnb_et_hk

# Gefa þessu gott forskot til að keppa við SamstættFall með greini,
# til dæmis "hafa ráðið Jóni bana", þar sem "ráðið" er sögnin "ráða"
# en ekki nafnorðið "ráð" með greini
$score(+8) NhSögnHafaGetaFá

# Samtengingar sagna

OgNhSögn →
    OgEða so_0_nh so_0_gm_vh_p3_et?
OgNhSögn_þf →
    OgEða so_1_þf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf →
    OgEða so_1_þgf_nh so_0_gm_vh_p3_et?
OgNhSögn_ef →
    OgEða so_1_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_nf →
    OgEða so_1_nf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_þf →
    OgEða so_2_þgf_þf_nh so_0_gm_vh_p3_et?
OgNhSögn_þf_ef →
    OgEða so_2_þf_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_þf_þgf →
    OgEða so_2_þf_þgf_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_ef →
    OgEða so_2_þgf_ef_nh so_0_gm_vh_p3_et?
OgNhSögn_þgf_nf →
    OgEða so_2_þgf_nf_nh so_0_gm_vh_p3_et?
OgNhSögn_lhþt/tala/kyn →
    OgEða so_0_lhþt/tala/kyn

# Gefa til kynna hvar lesa á upp sagnir til að nota í
# tengingu sagna og forsetninga

$tag(pick_up_verb) Sagnliður/tala/pers/kyn
$tag(pick_up_verb) Sagnliður_sagnb
$tag(pick_up_verb) SögnMeðF/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_0/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_1/tala/pers/kyn
$tag(pick_up_verb) ÖfugSögn_2/tala/pers/kyn
$tag(pick_up_verb) SagnliðurÁnF
$tag(pick_up_verb) NhLiður

# Nafnliðir og frumlög (nafnliðir í nefnifalli)

# Nafnliðarkjarni

Einkunn/tala/fall/kyn →

    LoLiður/tala/fall/kyn       # glæsileg (kona), bókfært (virði) (so_lhþt), leiðandi (fyrirtæki) (so_lh_nt)
    # AtviksliðurEinkunn undanskilur atviksorð sem geta líka verið forsetningar;
    # það dregur verulega úr margræðni
    | AtviksliðurEinkunn        # rúmlega (helmingur)
    | Fornafn/tala/fall/kyn     # Sá (stóri maður), allt þetta (víkjandi lán)
    | Töluorð/tala/fall/kyn     # þrjá (gangandi menn) / 26
    | OkkarYkkar                # 'okkar skoðanir', 'ykkar félag' (okkar og ykkar eru pfn, ekki fn)
    | GreinirMeðLo/tala/fall/kyn  # Tilbrigði 2.1 (hluti)

# 'Skjölin voru mörg hundruð síður', 'Í húsinu voru fleiri þúsund hænur', 'hún saknaði mörg hundruð hálsmena'
Einkunn_ft/fall/kyn →
    HundruðForskeyti Hundruð

$score(+4) Einkunn/tala/fall/kyn

HundruðForskeyti → 'margur:lo'_sb_hk_nf_ft | 'margur:lo'_mst_hk_nf_ft # mörg, fleiri

Hundruð → 'hundrað:hk'_nf_ft | 'þúsund:hk'_nf_ft # hundruð, þúsund

# Lýsingarorð eða eignarfallsliður sem kemur á eftir nafnlið

NlViðbót/tala/fall/kyn →
    LoEftirNl/tala/fall/kyn     # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið,
    | EfLiður LoEftirNlMeðÞgf/fall/tala/kyn? # (forstöðumenn) fræðaseturs [sunnlendinga] [tengdir flokknum]

# Eintala

NlKjarni_et/fall/kyn →

    # 'fullorðni sprúðlandi sonur Íslands'

    EfLiðurForskeyti?           # 1. deildar (lið Vals)
    Einkunn_et/fall/kyn*        # leiðandi (fyrirtæki) / bókfært (virði)
    Fyrirbæri/fall/kyn          # Már Guðmundsson seðlabankastjóri
    NlViðbót_et/fall/kyn?       # (Jón) sterki / (Jón), sultuslakur eftir gufubaðið, / (sonur) skósmiðsins
    Númer?                      # (ákvæði laga) nr. 33

NlKjarni_et_kk/fall →
    tala_kk/fall 'hluti:kk'_et/fall EfLiður? # 1/3 hluti Alþingismanna (getur krafist atkvæðagreiðslu)

NlKjarni_et_nf_kk → NlKjarni_et_kk_nf
NlKjarni_et_þf_kk → NlKjarni_et_kk_þf
NlKjarni_et_þgf_kk → NlKjarni_et_kk_þgf
NlKjarni_et_ef_kk → NlKjarni_et_kk_ef

# Fleirtala

NlKjarni_ft/fall/kyn →

    EfLiðurForskeyti?
    Einkunn_ft/fall/kyn*
    Fyrirbæri_ft/fall/kyn
    NlViðbót_ft/fall/kyn?
    Númer?

Fyrirbæri_ft/fall/kyn →
    Skst? no_ft/fall/kyn                 # [Commodore PET] tölvur
    | Skst? no_ft_gr/fall/kyn Fjöldi_ft/fall/kyn  # Mennina tíu / félögin tvö
    | FyrirbæriMeðGreini_ft/fall/kyn
    | raðnr/fall/kyn OgRaðnr/fall/kyn+ no/tala/fall/kyn    # 2. og 3. grein laganna (eru fallnar úr gildi)

OgRaðnr/fall/kyn →
    "og:st" raðnr/fall/kyn

Númer → 'númer:hk' tala

Fjöldi_ft/fall/kyn → to_ft/fall/kyn | töl_ft/fall/kyn

# Persónufornöfn þriðju persónu eintölu, karl- og kvenkyn
# Þessi orð eru ekki merkt með kyni í BÍN;
# þess vegna þurfa þau sérstaka meðhöndlun hér

NlKjarni_et_nf_kk → 'hann:pfn'_et_nf
NlKjarni_et_þf_kk → 'hann:pfn'_et_þf
NlKjarni_et_þgf_kk → 'hann:pfn'_et_þgf
NlKjarni_et_ef_kk → 'hann:pfn'_et_ef

NlKjarni_et_nf_kvk → 'hún:pfn'_et_nf
NlKjarni_et_þf_kvk → 'hún:pfn'_et_þf
NlKjarni_et_þgf_kvk → 'hún:pfn'_et_þgf
NlKjarni_et_ef_kvk → 'hún:pfn'_et_ef

# Fleirtölumyndir 3. persónu eru í BÍN undir 'sá'

# Afturbeygt fornafn, eins í tölum og kynjum, aðeins til í aukaföllum 3p.

# sjálfan sig / sjálfa sig
# sjálfum sér / sjálfum sér
# sjálfs sín  / sjálfra sín

NlKjarni/tala/aukafall/kyn →
    'sjálfur:fn'/tala/aukafall/kyn abfn/aukafall/tala/kyn # Hér vitum við tölu og kyn
    | abfn/aukafall # Hér vitum við hvorki tölu né kyn

# Mörg/flest þessara mála, sum þeirra sem lentu í slysinu
# TODO bæta öðrum fornöfnum sem taka hlutaeignarfall við
NlKjarni_ft/fall/kyn →
    'margur:fn'_ft/fall/kyn EfLiður_ft/kyn
    | 'fár:lo'_ft/fall/kyn EfLiður_ft/kyn
    | 'sumur:fn'_ft/fall/kyn EfLiður_ft/kyn

# Prósentubil með eignarfalli

NlKjarni_ft/fall/kyn →
    Prósentubil EfLiður_ft/kyn # 10-25% kvenna eru galvaskar

NlKjarniPrósent_hk →
    Prósentubil EfLiður_ft/kyn # 10-25% kvenna eru galvösk

NlKjarni_ft_nf_hk → NlKjarniPrósent_hk
NlKjarni_ft_þf_hk → NlKjarniPrósent_hk
NlKjarni_ft_þgf_hk → NlKjarniPrósent_hk
NlKjarni_ft_ef_hk → NlKjarniPrósent_hk

# Umraðanir

NlStak_p3/fall/kyn → NlStak_et_p3/fall/kyn | NlStak_ft_p3/fall/kyn

NlStak_et_ef/kyn → NlStak_et_p1_ef/kyn | NlStak_et_p2_ef/kyn | NlStak_et_p3_ef/kyn
NlStak_ft_ef/kyn → NlStak_ft_p1_ef/kyn | NlStak_ft_p2_ef/kyn | NlStak_ft_p3_ef/kyn

# Nafnliðir

NlStak_et_p1/fall/kyn →
    'ég:pfn'_et/fall 'sjálfur:fn'_et/fall/kyn? FsRunaEftirNl? # Ég í öllu mínu veldi / (Hvað) mig sjálfan (varðar)
    | 'sjálfur:fn'_et/fall/kyn 'ég:pfn'_et/fall FsRunaEftirNl? # Sjálfan mig hef ég aldrei þolað

NlStak_et_p2/fall/kyn →
    'þú:pfn'_et/fall 'sjálfur:fn'_et/fall/kyn? FsRunaEftirNl? # Þú [sjálfur] ert ekki sem verstur

NlStak_p1_ft/fall/kyn →
    'ég:pfn'_ft/fall NlStakViðbót/fall/kyn?     # Við gömlu hjónin, við Sigmundur, við flest, við sjálfar
    | 'vér:pfn'_ft/fall NlStak_ft_p3/fall/kyn?  # Vér fóstbræður
    | NlEind/fall KommaOgEða 'ég:pfn'_et/fall   # Þú og ég fórum, Jón og ég fórum, stelpurnar og ég fórum
    | 'ég:pfn'_et/fall KommaOgEða NlEind/fall   # Ég og þú fórum, ég og Jón fórum, ég og stelpurnar fórum

NlStakViðbót/fall/kyn →
    NlStakViðbótNl/fall/kyn
    | 'allur:fn'_ft/fall/kyn           # við öll, okkur alla, okkar allra
    | 'sumur:fn'_ft/fall/kyn        # við sum, okkur sumra, okkar sumra
    | 'margur:lo'_ft/fall/kyn          # við mörg/flest, okkur margra/flestra, okkar margra/flestra
    | 'sjálfur:fn'_ft/fall/kyn         # við sjálf, okkur sjálfra, okkar sjálfra

$score(+8) NlStakViðbót/fall/kyn

NlStakViðbótNl/fall/kyn →
    NlStak_ft_p3/fall/kyn               # við fóstbræður, við alþingismennirnir

$score(-20) NlStakViðbótNl/fall/kyn     # Draga af stig fyrir "við fóstbræður" vegna hættu á ruglingi við forsetninguna 'við'

NlStak_ft_p1/fall/kyn → NlStak_p1_ft/fall/kyn FsRunaEftirNl?

NlStak_p2_ft/fall/kyn →
    'þú:pfn'_ft/fall NlStakViðbót/fall/kyn? # þið öll, þið flest, þið stelpurnar, þið eplin
    | 'þér:pfn'_ft/fall NlStak_ft_p3/fall/kyn? # Þér eplin
    | NlEind/fall KommaOgEða 'þú:pfn'_et/fall # Jón og þú fóruð, stelpurnar og þú fóruð
    | 'þú:pfn'_et/fall KommaOgEða NlEind/fall # Þú og Jón fóruð, þú og stelpurnar fóruð

NlStak_ft_p2/fall/kyn → NlStak_p2_ft/fall/kyn FsRunaEftirNl?

# !!! ATH: reglurnar hér fyrir ofan leyfa 'ég og þú fóruð til Frakklands'

# Sameiginlegar reglur fyrir 3p. eintölu og fleirtölu

NlStak_p3/tala/fall/kyn →

    NlKjarni/tala/fall/kyn Fornafn/tala/fall/kyn* # (stjórnarframkvæmdum) þessum öllum
    | NlKjarni/tala/fall/kyn EignarFornafn/tala/fall/kyn KommaOgEðaEn EfLiður # skattskýrslur mínar og eiginkonu minnar
    | NlFornafn/tala/fall/kyn
    | Pfn/kyn/tala/fall to/tala/fall/kyn? # 'þið tvær', 'við fjögur', 'hann einn'

# Feðgarnir séra Jón Steinsson í Víðihlíð og Arnaldur Jónsson bóndi
Skyldleiki_kk/fall → 'feðgar:kk'_gr_ft/fall | 'bróðir:kk'_gr_ft/fall | 'frændi:kk'_gr_ft/fall
# Mæðgurnar frú Jóhanna Sigurjónsdóttir og Anna Kristjánsdóttir
Skyldleiki_kvk/fall → 'mæðgur:kvk'_gr_ft/fall | 'systir:kvk'_gr_ft/fall | 'frænka:kvk'_gr_ft/fall
# Systkinin Jón og Anna, feðginin Friðmar og Jóna
Skyldleiki_hk/fall → 'systkin:hk'_gr_ft/fall | 'frændsystkin:hk'_gr_ft/fall | 'mæðgin:hk'_gr_ft/fall | 'feðgin:hk'_gr_ft/fall

Fjölskylda_ft/nkyn/fall → Skyldleiki/nkyn/fall Manneskja/fall/nkyn FsRuna? "og:st" Manneskja/fall/nkyn
Fjölskylda_ft_hk/fall → Skyldleiki_hk/fall Manneskja/fall FsRuna? "og:st" Manneskja/fall

NlStak_ft_p3/fall/kyn → Fjölskylda_ft/kyn/fall FsRunaEftirNl?

$score(+8) Fjölskylda_ft/kyn/fall

# Sértilvik fyrir "vinstri-grænn" (ath: stutt bandstrik, ekki en- eða em-dash)

NlStak_ft_p3_nf_hk → 'vinstri:lo'_ft_nf_hk "-"? 'grænn:lo'_ft_nf_hk
NlStak_ft_p3_þf_hk → 'vinstri:lo'_ft_þf_hk "-"? 'grænn:lo'_ft_þf_hk
NlStak_ft_p3_þgf_hk → 'vinstri:lo'_ft_þgf_hk "-"? 'grænn:lo'_ft_þgf_hk
NlStak_ft_p3_ef_hk → 'vinstri:lo'_ft_ef_hk "-"? 'grænn:lo'_ft_ef_hk

# 'hvorugt okkar systkinanna', 'enginn okkar', 'einhver ykkar bræðranna', 'einn liðsmanna'
NlStak_et_p3/fall/kyn → OkkarFornafn/fall/kyn OkkarFramhald/kyn

OkkarFramhald/kyn →
    'ég:pfn'_ft_ef Nl_ef_ft/kyn?
    | Nl_ef_ft/kyn

OkkarFornafn/fall/kyn → 'hvorugur:fn'_et/fall/kyn | 'enginn:fn'_et/fall/kyn | 'einhver:fn'_et/fall/kyn
    | 'einn:fn'_et/fall/kyn | 'sérhver:fn'_et/fall/kyn
    | 'annar:fn'_et/fall/kyn 'hvor:fn'_et/fall/kyn?

# Eignarfornöfn standa með nafnlið í sama falli og hann,
# þannig að gera þarf undantekningu fyrir þau þegar þau
# tengjast eignarfallslið ('Ég seldi ekki íbúðina þína, heldur frænda þíns')
EignarFornafn/tala/fall/kyn →
    'minn:fn'/tala/fall/kyn
    | 'þinn:fn'/tala/fall/kyn

NlFornafn/tala/fall/kyn →
    # Stakt fornafn, e.t.v. með einkunn og töluorði
    # 'augljóslega sá vondi' / 'einhverjir tveir góðir' / 'ekkert uppbyggilegt' / 'fjórir aðrir'
    # 'þeir báðir' / 'hún öll' / 'þær sjálfar'
    # 'öllu því' / 'neinum þeirra'
    AtviksliðurEinkunn* Töluorð/tala/fall/kyn? Fornafn/tala/fall/kyn FornafnViðskeyti/tala/fall/kyn? LoLiður/tala/fall/kyn*
    # 'okkar besta' / 'óumdeilanlega ykkar versta'
    | AtviksliðurEinkunn* OkkarYkkar LoLiður/tala/fall/kyn

Pfn_kk/tala/fall → 'hann:pfn'/tala/fall
Pfn_kvk/tala/fall → 'hún:pfn'/tala/fall
Pfn_hk/tala/fall → 'það:pfn'/tala/fall

Pfn/tala/fall/kyn → Pfn/kyn/tala/fall

FornafnViðskeyti/tala/fall/kyn →
    Atviksliður? LoFornafn/fall/tala/kyn # (hafa þeir) [þó] báðir (lýst yfir sigri)
    | Pfn/kyn/tala/fall     # öllu því, ýmsum þeim
    | Töluorð/tala/fall/kyn # öllum fjórum, þeirra tveggja

FornafnViðskeyti_et/fall/kyn →
    Pfn_ft_ef/kyn Töluorð_ft_ef/kyn? FornafnViðskeytiLo_ft_ef/kyn? # neinum þeirra þriggja, öllum þeirra, hvern þeirra fjögurra

FornafnViðskeytiLo_ft_ef/kyn →
    AtviksliðurEinkunn* lo_vb_ft_ef/kyn # hvern þeirra fjögurra langsamlega sigursælustu

#NlFornafn_et/fall/kyn →
#    # 'hvern þeirra' / 'hverja þeirra' / 'hvert þeirra'
#    'hver'_et/fall/kyn 'sá'_ft_ef/kyn

# allt annar | allt aðrir
# allt önnur | allt aðrar
# allt annað | allt önnur
NlFornafn/tala/fall/kyn →
    'allur:fn'_et_nf_hk 'annar:fn'/tala/fall/kyn

OkkarYkkar → "okkar:fn" | "ykkar:fn"

# Gefa stökum fornöfnum mínusstig - við viljum frekar sjá þau sem hluta af stærri nafnlið
$score(-4) NlFornafn/tala/fall/kyn

# TODO ný regla - Tilbrigði 2.1
GreinirMeðLo/tala/fall/kyn →
    Greinir/tala/fall/kyn LoLiður/tala/fall/kyn

# Tilbrigði 2.1
Greinir/tala/fall/kyn →
    'hinn:gr'/tala/fall/kyn

#TODO Nýtt - Tilbrigði 2.1
$score(+6) GreinirMeðLo/tala/fall/kyn

# Lýsingarorð sem stendur sem nafnliður:
# 'Margir/fjölmargir/allmargir/heilmargir komu og aðstoðuðu Pál'
# 'Páll aðstoðaði töluvert marga'
# 'Óleyfilegt er að mismuna samkynhneigðum'

NlStak_p3/tala/fall/kyn →
    AtviksliðurEinkunn* LoSemNafnliður/tala/fall/kyn

# 'Fleiri komu í veisluna en ég bjóst við'
# 'Fleirum fannst kvikmyndin leiðinleg'
NlStak_p3_ft/fall/kyn →
    'margur:lo'_mst_ft/fall/kyn

# Unnt er að hafa nafnlið í þágufalli á undan lýsingarorðinu:
# 'sem varðar hagsmuni hans eða honum nákominna'
# Að svo komnu máli er slíkt aðeins leyft með lýsingarorðum í eignarfalli
# enda óljóst að þörf sé fyrir önnur föll.

NlStak_p3_et_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_et_ef/kyn

NlStak_p3_ft_ef/kyn →
    Nl_þgf AtviksliðurEinkunn* LoSemNafnliður_ft_ef/kyn

LoSemNafnliður/tala/fall/kyn →
    lo_sb/tala/fall/kyn | lo_mst/tala/fall/kyn # Efsta stig (ESB) er innifalið í _sb

# Gefa lýsingarorðum sem nafnliðum mínusstig
$score(-14) LoSemNafnliður/tala/fall/kyn

NlStak_p3/tala/fall/kyn →
    'hver:fn'/tala/fall/kyn 'sá:fn'/tala/fall/kyn # Hver sá, hver sú, hvert það, hverjir þeir, hverjar þær, hver þau
    | 'hvor:fn'/tala/fall/kyn 'tveir:to'_ft_ef/kyn  # (eða) hvor/hvort tveggja
    # '(Sýslumenn sinna) sama hlutverki og prestar'
    # '(Ríkissaksóknari) nýtur sömu verndar í starfi og dómarar'
    | 'sami:fn'/tala/fall/kyn Nl/fall/tala/kyn "og:st" Nl_nf

# 'Sú útskýring, að Samfylkingin sé óalandi og óferjandi, (dugir ekki lengur)'
NlStak_p3/tala/fall/kyn →
    'sá:fn'/tala/fall/kyn Nl/fall/tala/kyn AtvFs? Útskýring

Útskýring →
    ',' "að:st" HreinYfirsetning ','
    | "að:st" HreinYfirsetning
    | NhFyllingÁnAtv # Sú ákvörðun Úkraínustjórnar [í gær] að loka á síður

$score(+12) Útskýring

# Töluorð sem sjálfstæðir nafnliðir

NlStak_p3/tala/fall/kyn →
    NlTöluorð/tala/fall/kyn

NlTöluorð/tala/fall/kyn →
    # '(Ég talaði við) tvær þeirra', 'fjórum þeirra (var kalt)'
    # 'Að minnsta kosti fjórir létust'
    # 'Um þrír af hverjum fjórum landsmönnum telja...'
    TöluorðForskeyti* TöluorðLo/tala? Töluorð/tala/fall/kyn 'sá:fn'_ft_ef/kyn?
    | TöluorðForskeyti* Töluorð/tala/fall/kyn TöluorðLo/tala # (önnur af) þremur hröðustu (sem eftir standa)

NlTöluorð_ft_nf_hk → Prósentubil
NlTöluorð_ft_þf_hk → Prósentubil
NlTöluorð_ft_þgf_hk → Prósentubil
NlTöluorð_ft_ef_hk → Prósentubil

Prósentubil → TöluorðForskeyti* tala "-:en" prósenta

$score(+4) Prósentubil

TöluorðForskeyti →
    'að_minnsta_kosti:ao' | "um:ao" | "minnst:ao" | "einungis:ao" | "aðeins:ao" | "ekki:ao"
    | "svo:st" "sem:st" | "tæplega:ao" | "rúmlega:ao" | "ríflega:ao" | "einnig:ao" | "samtals:ao"
    | "yfir:ao" | "hátt:ao" "í:ao" | 'allur:fn'_et_nf_hk "að:ao" | "næstum:ao" | "nálægt:ao"

TöluorðLo/tala →
    lo_evb_nf_hk/tala # efstu tvo / efstu tvö / efstu tvær
    | lo_mst_nf_hk/tala # lægri tvo / lægri tvö / lægri tvær

# Gefa mínusstig fyrir stök töluorð - viljum helst sjá þau sem einkunn í stærri nafnlið
$score(-10) NlTöluorð/tala/fall/kyn
$score(+4) TöluorðForskeyti # Rétta mínusinn af ef fleira fylgir

# '1100 manns (voru á skemmtuninni)', '(Ég pantaði mat fyrir) sautján manns'
# Virðist ekki ganga upp í eignarfalli
# ('Ég lagði bílum 18 manns'?)
NlStak_p3_ft_nf_kk → Manns_nf
NlStak_p3_ft_þf_kk → Manns_þf
NlStak_p3_ft_þgf_kk → Manns_þgf

# Hér ganga beygjanleg töluorð ekki (?'fjórir manns')
Manns/fall →
    TöluorðForskeyti? töl_ft_kk/fall "manns:kk"
    | TöluorðForskeyti? tala_ft_kk/fall "manns:kk"
    | Talnabil "manns:kk" # 50-100 manns

# 3p. eintala

NlStak_et_p3/fall/kyn →

    NlStak_p3_et/fall/kyn VeikSögnLhÞt_et/fall/kyn? FsRunaEftirNl?  # fulltrúi bankans í nefndinni
    | NlStak_et_p3/fall/kyn "eða:st" NlStak_p3_et/fall              # Þingnefnd eða hluti Alþingismanna getur lagt fram frumvarp
    | 'hver:fn'_et/fall/kyn "og:st" 'einn:fn'_et/fall/kyn FsRuna?   # 'hver og einn [í sinni sveit] (ber ábyrgð)'

# '(svör við bréfi sínu) dagsettu (13. mars sl.)'
VeikSögnLhÞt/tala/fall/kyn →
    Atviksliður? EinVeikSögnLhÞt/tala/fall/kyn                      # (Stúlkan sá pabba sinn) [vel og snyrtilega] rakaðan
    | ',' so_lhþt/tala/fall/kyn FsAtv? LokatáknEðaKomma

EinVeikSögnLhÞt/tala/fall/kyn → so_lhþt/tala/fall/kyn

$score(-4) EinVeikSögnLhÞt/tala/fall/kyn

# Meðhöndla "árið 2012" og "árin 2013 og 2014" með háum forgangi

NlStak_et_p3_nf_hk → Árið_nf
NlStak_et_p3_þf_hk → Árið_þf
NlStak_et_p3_þgf_hk → Árið_þgf
NlStak_et_p3_ef_hk → Árið_ef

Árið/fall →
    'ár:hk'_gr_et/fall ártal
    | 'rekstrarár:hk'_gr_et/fall ártal
    | 'skólaár:hk'_gr_et/fall ártal

$score(+16) Árið/fall

NlStak_ft_p3_nf_hk → Árin_nf
NlStak_ft_p3_þf_hk → Árin_þf
NlStak_ft_p3_þgf_hk → Árin_þgf
NlStak_ft_p3_ef_hk → Árin_ef

Árin/fall →
    'ár:hk'_gr_ft/fall MörgÁr
    | 'rekstrarár:hk'_gr_ft/fall MörgÁr
    | 'skólaár:hk'_gr_ft/fall MörgÁr

MörgÁr →
    ártal Til tala
    | ártal ÁraRuna* OgEða tala

ÁraRuna → ',' tala

$score(+20) Árin/fall

# Leyfa frístandandi sérnöfn (skrifuð sem runa orða með stórum staf)
# sem nafnliði 3p. eintölu hvorugkyns, í öllum föllum
# Orka Energy, OG Capital

NlStak_p3_et_nf_hk → NlSérnafn
NlStak_p3_et_þf_hk → NlSérnafn
NlStak_p3_et_þgf_hk → NlSérnafn
NlStak_p3_et_ef_hk → NlSérnafn

# Sérnöfn þurfa einnig að geta staðið í fleirtölu,
# sbr. 'Valsmenn skoruðu flest mörk', 'Dodgers áttu sigurinn vísan',
# 'Essenar voru trúflokkur'

NlStak_p3_ft_nf_hk → NlSérnafn_ft
NlStak_p3_ft_þf_hk → NlSérnafn_ft
NlStak_p3_ft_þgf_hk → NlSérnafn_ft
NlStak_p3_ft_ef_hk → NlSérnafn_ft

NlSérnafn_ft → NlSérnafn

# Gefa mínus fyrir fleirtölu m.v. eintölu
$score(-2) NlSérnafn_ft

# !!! no_ef er notað í stað EfLiður til að minnka margræðni þegar
# !!! mörg sérnöfn standa í röð
NlSérnafn → eo* Sérnafn NlSérnafnEf? # ekki Nakamoto, örugglega Nelson Mandela, Hong Kong norðursins

NlSérnafnEf → no_ef

# Forðast að nota sérnöfn sem nafnliði, ef aðrir kostir eru í boði
$score(-16) NlSérnafn

Sérnafn →
    sérnafn tala? | entity tala? | LangtSérnafn

# ATH: eitt 'entity' getur verið fleiri en eitt samliggjandi orð.
# Ekki ætti að hafa samliggjandi 'entity' í málfræðireglu.
# Hér eru höfð stutt bandstrik milli nafna, ekki en- eða em-dash.

LangtSérnafn →
    sérnafn "-"? sérnafn+ tala?             # Glass-Steagall lögin
    | sérnafn+ "-"? entity+ sérnafn* tala?  # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity+ "-"? sérnafn+ tala?           # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity "-" entity tala?               # Villeneuve-Loubet
    | entity+ "-"? sérnafn* tala? fyrirtæki # Standard Oil Corp.
    | entity entity+ tala?                  # !!! Til prófunar

Sérnafn/fall →
    sérnafn/fall tala? | LangtSérnafn/fall tala?

LangtSérnafn/fall →
    sérnafn/fall "-"? sérnafn/fall+ tala?               # Stóra-Felli
    | sérnafn/fall+ "-"? entity+ sérnafn/fall* tala?    # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra
    | entity+ "-"? sérnafn/fall+ tala?                  # Eining með nafni sem þekkt er úr gagnagrunni eða runu slíkra

# Leyfa löngu sérnafni í nefnifalli að passa við langt sérnafn án falls
LangtSérnafn_nf →
    LangtSérnafn

# Löng sérnöfn, þ.e. runur orða með upphafsstöfum, eru mjög líklega rétt greind

$score(+6) LangtSérnafn
$score(+10) LangtSérnafn/fall

# Heimilisfang (götuheiti + tala)

NlStak_et_p3/fall/kyn → Heimilisfang/fall/kyn

Heimilisfang/fall/kyn → gata/fall/kyn tala # !!! TODO: húsnúmer (7a, 13C)

$score(+4) Heimilisfang/fall/kyn

NlStak_p3_et/fall → NlStak_p3_et/fall/kyn   # Samtenging tveggja eintöluliða með 'eða' krefst ekki sama kyns báðum megin

# 3p. fleirtala

NlStak_ft_p3/fall/kyn →

    NlStak_p3_ft/fall/kyn VeikSögnLhÞt_ft/fall/kyn? FsRunaEftirNl?  # fulltrúar bankans í nefndinni
    | 'allur:fn'_ft/fall/kyn 'sá:fn'_ft/fall/kyn                    # Allir þeir, allar þær, öll þau

# Hvorki X né Y
# Bæði X og Y
# Annaðhvort X eða Y

# Sama kyn á X og Y

NlStak_ft_p3/fall/kyn → NlBæðiHvorki/fall/kyn

NlBæðiHvorki/fall/kyn →
    "bæði:st" Nl/fall/kyn "og:st" Nl/fall/kyn          # (Við þurftum að lóga) bæði hestinum og kettinum
    | "hvorki:ao" Nl/fall/kyn "né:st" Nl/fall/kyn         # Hvorki hestinum né kettinum (var lógað)
    | "annaðhvort:fn" Nl/fall/kyn "eða:st" Nl/fall/kyn # Annaðhvort hestinum eða kettinum (var lógað)
    | "annað:fn" "hvort:fn" Nl/fall/kyn "eða:st" Nl/fall/kyn # Annaðhvort hestinum eða kettinum (var lógað)

$score(+4) NlBæðiHvorki/fall/kyn

NlStak_ft_p3_nf_hk → NlStak_ft_p3_hk_nf
NlStak_ft_p3_þf_hk → NlStak_ft_p3_hk_þf
NlStak_ft_p3_þgf_hk → NlStak_ft_p3_hk_þgf
NlStak_ft_p3_ef_hk → NlStak_ft_p3_hk_ef

Nl/kyn/fall → Nl/fall/kyn

# Mismunandi kyn á X og Y, útkoman er hvorugkyns

NlStak_ft_p3_hk/fall → NlBæðiHvorki_p3_hk/fall

NlBæðiHvorki_p3_hk/fall →

    "bæði:st" Nl_kk/fall "og:st" Nl_kvk/fall
    | "bæði:fn" Nl_kvk/fall "og:st" Nl_kk/fall
    | "bæði:fn" Nl_kk/fall "og:st" Nl_hk/fall
    | "bæði:fn" Nl_hk/fall "og:st" Nl_kk/fall
    | "bæði:fn" Nl_kvk/fall "og:st" Nl_hk/fall
    | "bæði:fn" Nl_hk/fall "og:st" Nl_kvk/fall

    | "hvorki:ao" Nl_kk/fall "né:st" Nl_kvk/fall
    | "hvorki:ao" Nl_kvk/fall "né:st" Nl_kk/fall
    | "hvorki:ao" Nl_kk/fall "né:st" Nl_hk/fall
    | "hvorki:ao" Nl_hk/fall "né:st" Nl_kk/fall
    | "hvorki:ao" Nl_kvk/fall "né:st" Nl_hk/fall
    | "hvorki:ao" Nl_hk/fall "né:st" Nl_kvk/fall

    | "annaðhvort:fn" Nl_kk/fall "eða:st" Nl_kvk/fall
    | "annaðhvort:fn" Nl_kvk/fall "eða:st" Nl_kk/fall
    | "annaðhvort:fn" Nl_kk/fall "eða:st" Nl_hk/fall
    | "annaðhvort:fn" Nl_hk/fall "eða:st" Nl_kk/fall
    | "annaðhvort:fn" Nl_kvk/fall "eða:st" Nl_hk/fall
    | "annaðhvort:fn" Nl_hk/fall "eða:st" Nl_kvk/fall

    | "annað:fn" "hvort:fn" Nl_kk/fall "eða:st" Nl_kvk/fall
    | "annað:fn" "hvort:fn" Nl_kvk/fall "eða:st" Nl_kk/fall
    | "annað:fn" "hvort:fn" Nl_kk/fall "eða:st" Nl_hk/fall
    | "annað:fn" "hvort:fn" Nl_hk/fall "eða:st" Nl_kk/fall
    | "annað:fn" "hvort:fn" Nl_kvk/fall "eða:st" Nl_hk/fall
    | "annað:fn" "hvort:fn" Nl_hk/fall "eða:st" Nl_kvk/fall

$score(+4) NlBæðiHvorki_p3_hk/fall

NlStak_ft_p1/fall/kyn → NlBæðiHvorki_p1_hk/fall

NlBæðiHvorki_p1_hk/fall →
    "bæði:fn" Nl_p1/fall/kyn "og:st" Nl_p2/fall/kyn    # bæði mér og þér þykir þetta óásættanlegt
    | "hvorki:ao" Nl_p1/fall/kyn "né:st" Nl_p2/fall/kyn   # hvorki ég né þú höfum vit á þessu
    | "annaðhvort:fn" Nl_p1/fall/kyn "eða:st" Nl_p2/fall/kyn   # annaðhvort ég eða þú þurfum að fara
    | "annað:fn" "hvort:fn" Nl_p1/fall/kyn "eða:st" Nl_p2/fall/kyn   # annaðhvort ég eða þú þurfum að fara
    | "bæði:fn" Nl_p2/fall/kyn "og:st" Nl_p1/fall/kyn  # bæði þér og mér þykir þetta óásættanlegt
    | "hvorki:ao" Nl_p2/fall/kyn "né" Nl_p1/fall/kyn   # hvorki þú né ég erum fallegar/falleg/fallegir
    | "annaðhvort:fn" Nl_p2/fall/kyn "eða:st" Nl_p1/fall/kyn   # annaðhvort þú eða ég erum fallegar/falleg/fallegir
    | "annað:fn" "hvort:fn" Nl_p2/fall/kyn "eða:st" Nl_p1/fall/kyn   # annaðhvort þú eða ég erum fallegar/falleg/fallegir

$score(+4) NlBæðiHvorki_p1_hk/fall

# Skammstafanir með bandstriki nota stutt bandstrik, ekki en- eða em-dash

Skst →
    Skst_abbrev
    | Sérnafn "-"?              # Barclays-bankann, Al Thani-málið

Skst_abbrev → no_abbrev "-"?    # FIH bankann

$score(+8) Skst
$score(+4) Skst_abbrev

Fyrirbæri/fall/kyn →
    Skst? no_et/fall/kyn        # Microsoft-hugbúnaðarrisinn, Nikon myndavél
    | Raðnr/fall/kyn no_et/fall/kyn
    | FyrirbæriMeðGreini/fall/kyn
    | Úrslit/fall/kyn

# Raðnúmer

Raðnr/fall/kyn →
    raðnr/fall/kyn
    | raðnr/fall/kyn Til raðnr/fall/kyn           # 2.-3. sæti listans / 2. til 3. sæti listans

# Úrslit í leikjum (32:27 sigur, 0:2-tap, 8:0 burst)

Úrslit →
    tala ':' tala

Úrslit/fall/kyn →
    Úrslit "-:en"? no_et/fall/kyn

$score(+2) Úrslit/fall/kyn

# nkyn = náttúrulegt kyn (karlkyn eða kvenkyn)
Fyrirbæri/fall/nkyn →
    Manneskja/fall/nkyn

# Fyrirtækisnöfn - hvorugkyn
# !!! Ath: Eiga fyrirtækisnöfn e.t.v. að vera NlKjarni, ekki Fyrirbæri?

Fyrirbæri_nf_hk →
    Fyrirtæki_nf
Fyrirbæri_þf_hk →
    Fyrirtæki_þf
Fyrirbæri_þgf_hk →
    Fyrirtæki_þgf
Fyrirbæri_ef_hk →
    Fyrirtæki_ef

Fyrirtæki/fall →
    Sérnafn/fall no/fall* fyrirtæki # hf., ehf., Inc., Corporation, SA o.s.frv.

# Fyrirtæki eru næstum örugglega rétt greind
$score(+12) Fyrirtæki/fall

# '(Danska) byggingavörukeðjan Bygma'
# '(Jón vann hjá) verkfræðistofunni Afli'
# 'Tryggingafélagið Vörður (hagnaðist um 300 milljónir)'

FyrirbæriMeðGreini/fall/kyn →
    SamstættFall/fall/kyn

FyrirbæriMeðGreini/aukafall/kyn →
    ÓsamstættFall/aukafall/kyn

# !!! ATH: Ef þessu er breytt gæti þurft að breyta processors/entities.py
SamstættFall/fall/kyn →
    # (Ég versla við) norsku herrafataverslunina Smekkmann
    # Lagahöfundurinn góðkunni Jónas Friðrik (hefur glatt marga)
    VeiktLo_et/fall/kyn* no_gr_et/fall/kyn VeiktLo_et/fall/kyn? SérnafnEðaManneskja/fall

SamstættFall_nf/kyn →
    # Enska dagblaðið The Mirror
    VeiktLo_et_nf/kyn* no_gr_et_nf/kyn VeiktLo_et_nf/kyn? SérnafnEðaManneskja

# !!! ATH: Ef þessu er breytt gæti þurft að breyta processors/entities.py
ÓsamstættFall/aukafall/kyn →
    VeiktLo_et/aukafall/kyn* no_gr_et/aukafall/kyn VeiktLo_et/aukafall/kyn? SérnafnEðaManneskja # (Ég versla við) herrafataverslunina Smekkmaður

SérnafnEðaManneskja/fall →
    # (danska húsgagnahönnuðinn) Arne Jacobsen
    # (suður-kóreska símaframleiðandann) Samsung
    # (kvikmyndina umtöluðu) 50 gráa skugga
    Sérnafn/fall | Persóna/fall | Fyrirtæki/fall | Nafn/fall

Persóna/fall →
    person/fall

$score(+8) Persóna/fall # Velja Persónu frekar en Sérnafn

SérnafnEðaManneskja →
    # (kvikmyndina umtöluðu) 50 gráir skuggar
    Sérnafn | Persóna | Fyrirtæki/fall | Nafn_nf

$score(-2) SérnafnEðaManneskja # Velja form með falli frekar en án falls

Nafn/fall →
    Nl_p3/fall/kyn

# Ekki sækja í að nota nafnlið sem nafn í kjölfar annars nafnliðar með greini
$score(-4) Nafn/fall

Persóna →
    person

$score(+8) Persóna

VeiktLo/tala/fall/kyn →
    SkstAtv? lo_vb/tala/fall/kyn

# suður-kóreski farsímaframleiðandinn Samsung

SkstAtv → SkstAtvOrð "-" # Stutt bandstrik

SkstAtvOrð →
    "norður:ao" | "suður:ao" | "vestur:ao" | "austur:ao"

# Hvetja til runa af lýsingarorðum í veikri beygingu með skilgreiningu sérnafna
$score(+8) VeiktLo/tala/fall/kyn
$score(+2) SkstAtv

$score(+5) SamstættFall/fall/kyn
$score(-1) ÓsamstættFall/aukafall/kyn

# 'Frumkvöðlarnir Jón og Guðrún'
# 'bandarísku sjóðirnir Eaton Vance Corp. og Autonomy Capital LP'

FyrirbæriMeðGreini_ft/fall/kyn →
    Skilgreining/fall/kyn Eindalisti/fall/kyn

Skilgreining/fall/kyn →
    VeiktLo_ft/fall/kyn* no_gr_ft/fall/kyn

Eindalisti/fall/kyn →
    SérnafnEðaManneskja/fall KommaSérnafnEðaManneskja/fall* "og:st" SérnafnEðaManneskja/fall
    | Töluorð_ft/fall/kyn # skipverjanna tveggja, börnin tvö, stúlkunum tveimur

KommaSérnafnEðaManneskja/fall →
    "," SérnafnEðaManneskja/fall

$score(+8) FyrirbæriMeðGreini_ft/fall/kyn

# [séra] Jóns Dalbú Hróbjartssonar [, dómkirkjuprests]
# [frú] Vigdís Finnbogadóttir [fyrrverandi forseti]
# [stórvesír] Ásbjörn Friðriksson [, alþingismann]
# [hún] Kristín (okkar)
Mannsnafn/fall/nkyn → person/fall/nkyn

Manneskja/fall/nkyn → Ávarp/nkyn/fall? Mannsnafn/fall/nkyn Titill/fall?
Manneskja/fall → Manneskja/fall/nkyn

Ávarp/nkyn/fall →
    no_et/fall | ÞekktÁvarp/nkyn/fall

$score(-6) Ávarp/nkyn/fall

ÞekktÁvarp_kk/fall →
    'herra:kk'_et/fall | 'séra:kk'_et/fall | 'hann:pfn'_et/fall
    | 'doktor:kk'_et/fall
    | 'prófessor:kk'_et/fall
ÞekktÁvarp_kvk/fall →
    'frú:kvk'_et/fall | 'séra:kk'_et/fall | 'hún:pfn'_et/fall
    | 'doktor:kk'_et/fall
    | 'prófessor:kk'_et/fall

$score(+10) ÞekktÁvarp/nkyn/fall

# 'Már Guðmundsson, bankastjóri [Seðlabanka Íslands]'
# Reynt er að láta titil ekki enda á FsRuna/FsAtv eða á EfLiður
# því það skapar margræðni við nafnliðinn sjálfan
Titill/fall →
    ',' NlTitill/fall             # [Jón], formaður [félagsins]
    | KommulausTitill/fall        # [Jón] formaður [félagsins]
    | KommuTitill/fall            # [Jón], formaður námsmannafélagsins í Árósum og talsmaður þess,
    # Gunnlaugur H Jónsson eðlisfræðingur og fyrrverandi starfsmaður Orkustofnunar
    | EinnTitill/fall OgTitill/fall* ','? "og:st" NlTitill/fall

NlTitill/fall →
    TitilEinkunn/fall/kyn? no_et/fall/kyn      # (Jón) glæsilegur fulltrúi (félagsins)

TitilEinkunn/fall/kyn →
    Einkunn_et/fall/kyn

# Titileinkunn er frekar fátíð
$score(-2) TitilEinkunn/fall/kyn

EinnTitill/fall →
    NlTitill/fall EfLiður? FsRuna?

OgTitill/fall →
    KommaEðaOg EinnTitill/fall

KommaEðaOg →
    ',' | "og:st" | ',' "og:st"

KommuTitill/fall →
    ',' EinnTitill/fall LokatáknEðaKomma
    | ',' EinnTitill/fall OgTitill/fall* ','? "og:st" EinnTitill/fall LokatáknEðaKomma

KommulausTitill/fall →
    NlTitill/fall

# Gefa plús fyrir titla
$score(+5) Titill/fall

# ...en ekki eins mikinn fyrir kommulausan titil
$score(-5) KommulausTitill/fall

# Gefa enn meiri plús fyrir heila titla innan kommu
$score(+8) KommuTitill/fall

# Eignarfallsliður á eftir nafnlið
# ('formaður Gimlis, félags eldri borgara')
# ('amma Jóns, föður Bjarna')
# karlsins langa, konunnar löngu, barnsins langa
# stóra karlsins, stóru konunnar, stóra barnsins
# karlanna löngu, kvennanna löngu, barnanna löngu

EfLiður → EfLiðurStakur EfLiðurFramhald? # Jóns, föður Bjarna sterka
EfLiður_ft/kyn → Nl_ef_ft/kyn # margar þessara kvenna, suma þeirra sem lentu í slysinu

# Eignarfallsliður getur verið í eintölu eða fleirtölu, og í
# öllum kynjum, óháð nafnliðnum sem hann stendur með
EfLiðurStakur → Nl_ef

EfLiðurFramhald → KommaEfLiður+ ','?

KommaEfLiður →
    ',' EfLiðurStakur

$score(+3) EfLiður
$score(+3) EfLiður_ft/kyn

# Eignarfallsliður sem stendur á undan nafnlið
# ('Íslands fullorðnu synir', 'allra uppáhald')
# Þessi liður er einfaldari en EfLiður til að
# minnka margræðni; leyfir til dæmis ekki einkunn
# (?'Litla Íslands fullorðnu synir'), né að
# lýsingarorð sé notað sem nafnorð

EfLiðurForskeytiTala/tala/kyn →
    Töluorð_ef/tala/kyn TilTölu/tala/kyn?  # (vel rúmlega) þriggja (klukkutíma bið) / 30 þúsund (króna tjón)

TilTölu/tala/kyn →
    "til:st" Töluorð_ef/tala/kyn+   # (á) tveggja til þriggja metra dýpi

EfLiðurForskeyti →
    Fornafn_ef/tala/kyn* eo* EfLiðurForskeytiTala/tala/kyn? EfLiðurForskeytiKjarni/tala/kyn
    | Fornafn_ef/tala/kyn+ eo* EfLiðurForskeytiTala/tala/kyn?
    | eo* EfLiðurForskeytiTala/tala/kyn ÓbeygðEining? # 'rúmlega sjö prósent (hækkun frá fyrra ári)'
    | 'meiri:lo'_mst_kk_ef_et "háttar:kk"
    | 'minni:lo'_mst_kk_ef_et "háttar:kk"
    | Prósentubil           # (Spáð er) a.m.k. 10-20% (aukningu)
    | eo* Prósenta          # (eigi) nú 50% (hlut í fjarskiptafyrirtækinu)

Prósenta →
    prósenta

$score(+8) Prósenta

ÓbeygðEining →
    'prósent:hk'_ef_ft
    | 'tomma:kvk'_ef_et # '45 tommu flatskjá' (í stað '45 tomma flatskjá', sem er málfræðilega rétt)

EfLiðurForskeytiKjarni_et/kyn → Fyrirbæri_ef/kyn
EfLiðurForskeytiKjarni_ft/kyn → no_ef_ft/kyn

# Ef hægt er að nota t.d. fornafn í stað eignarfalls-forskeytis er það betra
$score(-1) EfLiðurForskeyti
# Eignarfalls-forskeyti sem eru ekki tölur eða fornöfn eru sjaldgæf
$score(-4) EfLiðurForskeytiKjarni/tala/kyn

# Leyfa 'okkar'/'ykkar' í eignarfallsforskeyti (er aðeins kynlaust pfn í BÍN)
Fornafn_ft_ef/kyn → OkkarYkkar

Fornafn/tala/fall/kyn → fn/tala/fall/kyn
Fornafn/fall/tala/kyn → Fornafn/tala/fall/kyn

# Persónufornöfnin (pfn) "hans" og "hennar" eru án kyns í BÍN
Fornafn_et_ef_kk → "hans"
Fornafn_et_ef_kvk → "hennar"

# Einn og sami veiðimaðurinn, eitt og sama fyrirbæri
Fornafn_et/fall/kyn → 'einn:fn'_et/fall/kyn "og:st" 'sami:lo'_et/fall/kyn

Töluorð/tala/fall/kyn → to/tala/fall/kyn | tala/tala/fall/kyn

# Öll töluorð (merkt 'töl' í BÍN) tákna fleirtölu
# Talnabil eru líka alltaf í fleirtölu
Töluorð_ft/fall/kyn → töl_ft/fall/kyn | Talnabil

Talnabil → tala "-:en" tala
$score(+4) Talnabil

Töluorð/fall/tala/kyn → Töluorð/tala/fall/kyn

# Undirsetningar

# 1p: 'ég sem alltaf elda matinn samviskusamlega (nenni þessu ekki)'
# 2p: 'þú sem ég elska (vilt ekki giftast mér)'
# 3p: 'sjóarinn sem hafið hafnaði í fyrra (kom hér í gær og fékk sér kaffi)'

# Gefa undirsetningum afmörkuðum með kommum forgang,
# en forðast verri kosti

TengiliðurMeðKommu/tala/pers/kyn →
      ',' Tengiliður/tala/pers/kyn ','
    >     Tengiliður/tala/pers/kyn
    >     Tengiliður/tala/pers/kyn ','
    > ',' Tengiliður/tala/pers/kyn

# KommaTengiliður er einungis notað í lok venjulegrar
# setningar með frumlagi í nefnifalli, sbr.
# Allir mega koma með okkur sem eru fullklæddir
KommaTengiliður/tala/pers/kyn →
    ','? "sem:stt" SetningÁnF/tala/pers/kyn

$score(-8) KommaTengiliður/tala/pers/kyn

Tengiliður/tala/pers/kyn →
    Tengiorð Tengisetning/tala/pers/kyn OgTengisetning/tala/pers/kyn*

Tengiorð →
    TengiorðSem | TengiorðEr

TengiorðSem → "eða:st"? "sem:stt"
    | "þar:ao" "sem:st"         # ??? Er þetta rétt?

TengiorðEr → "er:stt"

$score(-6) TengiorðEr # Sjaldgæft - ekki velja nema það þurfi

Tengisetning/tala/pers/kyn →
    TengiYfirsetning            # (Maðurinn sem) konan kyssti í gær / konurnar kysstu í gær
    | SetningÁnF/tala/pers/kyn  # (Maðurinn sem) kyssti konuna í gær / (Maðurinn sem) var fallegur

TengiYfirsetning →
    HreinYfirsetning

# Nota frekar SetningÁnF heldur en HreinYfirsetning, ef völ er á hvoru tveggja
$score(-4) TengiYfirsetning

# ...sem hætta gæti stafað af og eru kílómetri í þvermál
OgTengisetning/tala/pers/kyn →
    ","? OgSem Tengisetning/tala/pers/kyn

$score(+2) OgTengisetning/tala/pers/kyn

NlEind/fall →
    NlEind/tala/pers/fall/kyn

NlEind/fall/kyn →
    NlEind/tala/pers/fall/kyn

NlEind_p3/fall/kyn →
    NlEind_et_p3/fall/kyn | NlEind_ft_p3/fall/kyn

NlEind/tala/pers/fall/kyn →
    NlStak/tala/pers/fall/kyn Sem/tala/fall/kyn? TengiliðurMeðKommu/tala/pers/kyn?
        Þó/tala/pers/kyn? NlSkýring/tala/pers/fall/kyn?

# (vegna nauðasamninganna) sem slíkra
Sem/tala/fall/kyn →
    ',' "sem:st" 'slíkur:lo'/tala/fall/kyn LokatáknEðaKomma
    | "sem:st" 'slíkur:lo'/tala/fall/kyn

# (að endurskapa sig) sem þjóðhetju
# 'sem' með nefnifalli er talið vera forsetning
Sem/tala/aukafall/kyn →
    SemAukafall/aukafall/tala

SemAukafall/aukafall/tala →
    "sem:st" Nl/aukafall/tala

$score(-4) SemAukafall/aukafall/tala

NlEind_et_p3_nf_hk →
    # Það [eitt] hvernig kerfið virkar (er óskiljanlegt flestum)
    'það:pfn'_nf_et 'einn:fn'_nf_et_hk? ÞaðTenging

NlEind_et_p3_þf_hk →
    # (Gunnar lagði áherslu á) það [eitt] [í dag] að vægi atkvæða hafi aldrei verið jafnt
    'það:pfn'_þf_et 'einn:fn'_þf_et_hk? ÞaðTenging

NlEind_et_p3_þgf_hk →
    # (Ekkert var gert í) því [einu] [strax] að Jón hefur áreitt Önnu lengi (eða því að hún vildi segja upp)
    'það:pfn'_þgf_et 'einn:fn'_þgf_et_hk? ÞaðTenging

NlEind_et_p3_ef_hk →
    # (Þetta varð til) þess [eins] að Anna fór í fússi (og uppsagnar í kjölfarið)
    'það:pfn'_ef_et 'einn:fn'_ef_et_hk? ÞaðTenging

ÞaðTenging →
    FsAtv? "að:st" HreinYfirsetning
    | SpurnarSamtenging HreinYfirsetning
    | NhLiðir  # Það að fara til tannlæknis (reynist mörgum þyrnir í augum)

$score(+32) ÞaðTenging

# Aðdróttanir, þó sannar séu, varða fangelsisvist

Þó/tala/pers/kyn →
    ÞóTenging ÞóBotn/tala/pers/kyn
    | ',' ÞóTenging ÞóBotn/tala/pers/kyn LokatáknEðaKomma

ÞóTenging → "þó:st" | "þótt:st" | "þó:ao" "að:st"

ÞóBotn/tala/pers/kyn →
    FsRuna? LoLiður_nf/tala/kyn VeraVerða_vh/tala/pers

NlSkýring/tala/pers/fall/kyn →
    '(' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ')'
    | '[' SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn ']'
    | "-:em" SvigaForskeyti? SvigaInnihald/tala/pers/fall/kyn "-:em"
    | ',' SvigaInnihald/tala/pers/fall/kyn LokatáknEðaKomma
    | ',' Skýring Skýringarliður LokatáknEðaKomma
    # (Verslunin þjónar fjölbreyttum markhópi), allt frá börnum til aldraðra, (og er vinsæl í hverfinu)
    | Samanburðarmerki 'allur:fn'_nf_et_hk "frá:fs" Nl_þgf "til:fs" Nl_ef LokatáknEðaKomma

# Gefa aukastig fyrir skýringar á nafnliðum
$score(+6) NlSkýring/tala/pers/fall/kyn

SvigaInnihald/tala/pers/fall/kyn →
    Árabil
    > Tengiliður/tala/pers/kyn # (og sem ég er almennt fylgjandi)
    > Nl/fall/tala      # 'Ég spurði forsætisráðherra, Sigmund Davíð, um málið'
    > SvigaInnihaldFsRuna
    > SvigaInnihaldNl   # Ef ekki finnst sama fall og tala, prófa almennan nafnlið
    > HreinYfirsetning

# Árabil nota en-dash

Árabil →
    ártal "-:en" ártal   # 1951-1983
    > ártal "-:en" tala  # 2001-8

SvigaInnihaldNl →
    Nl

# Nafnliður í öðru falli en það sem verið er að skýra er ekki spennandi kostur
$score(-8) SvigaInnihaldNl

SvigaInnihaldFsRuna →
    FsRuna

# FsRuna innan kommu er oftast margræð við FsRunu eftir nafnlið
$score(-4) SvigaInnihaldFsRuna

Skýring →
    "svo:st" "sem:st" | 'svo_sem:ao'
    | "sem:st" 'segja:so'_lhþt_sb_hk_nf_et | 'sem_sagt:ao'
    | "til:fs" "dæmis:hk" | 'til_dæmis:ao'
    | "þar:ao" "á:fs" "meðal:ao" | 'þar_á_meðal:ao'
    | "meðal:ao" "annars:ao" | 'meðal_annars:ao'
    | 'það:pfn'_nf_et "er:so" | 'það_er:ao'
    # | "þar:ao" "sem:st" "er:so"
    | "með:fs" 'annar:lo'_hk_þgf_ft "orðum:hk" | 'með_öðrum_orðum:ao'

SvigaForskeyti →
    Skýring
    | 'dæmi:hk'_hk_et_nf ':'    # Ráðamenn (dæmi: forsetinn) hafa tjáð sig fjálglega
    | "les:so" ':'     # Hann sagði að óþjóðalýður (les: kommúnistar) hefði ráðist á húsið
    # TODO: "lesist"?
Skýringarliður →
    Nl | FsAtv      # , meðal annars Jón í Holti, / , oft snarlega,

# Runur nafnliða

# !!! TODO: Skilja á milli samtenginga sem mynda fleirtölu úr eintöluliðum
# !!! (strákurinn og stelpan eru...), og þeirra sem halda eintölu
# !!! (hópurinn, þar á meðal ég, var... / Össur, ekki síður en þingflokkurinn, var...)

OgEða → "og:st" | "eða:st" | "ellegar:st" | "eður:st" | "svo:st" "og:st" | "sem:st" "og:st" | "né:st"
    | ÞarÁMeðal | "meðal:ao" "annars:ao" | 'meðal_annars:ao'
    | 'það'_nf_et "er:st" | 'það_er:ao'
    | "eins:ao" "og:st" | 'líkt_og:st' | "jafnt:ao" "sem:st"
    | "þar:ao" "með:ao" 'telja:so'_lhþt_sb_hk_nf_et? | "svo:st" "sem:st" | 'svo_sem:st'
    | EnÍOgEða

EnÍOgEða →
    "en:st"

$score(-24) EnÍOgEða # Frekar að líta á 'en' sem samtengingu ef það er hægt

OgSem → "og:st" "sem:stt"? | "eða:st" "sem:stt"? | "ellegar:st" | "en:st" "sem:stt"?

ÞarÁMeðal → "þar:ao" "á:fs" "meðal:ao"

$score(+16) ÞarÁMeðal # Líta frekar á "þar á meðal" sem samtengingu en skýringu

KommaOgEða → ','? OgEða
    | "-:em" "eða:st" # 'Ég tók tvö tonn af eldsneyti - eða tvö þúsund lítra'
    | KommaOgEðaStrik
    | ',' "ekki:ao" # Við vildum gin, ekki viskí
    | ',' "núna:ao" "síðast:ao" # Tóku á móti mörgum flóttamönnum, núna síðast þremur þúsundum

KommaOgEðaStrik → "-:em"    # 'Ég tók tvö tonn af eldsneyti - tvö þúsund lítra'

$score(-8) KommaOgEðaStrik

# (Í fyrra barði Hannes Guðmund) og (særði Pál í dag)
# (Í fyrra barði Hannes Guðmund), (særði Pál), og (neitaði sök)
# (Í gær börðu strákarnir ekki Guðmund), heldur (stríddu Páli)

OgEðaEn →
    "og:st" | "svo:st" "og:st" | "eða:st" | "ellegar:st" | "en:st" | "heldur:st"
    | "eins:ao" "og:st"

OgEðaEnNema → OgEðaEn | "nema:st"

OgEðaEnAtv → OgEðaEn FsAtv?

KommaOgEðaEn → ',' | ','? OgEðaEn | "-:em" OgEðaEn
KommaOgEðaEnNema → ',' | ';' | ','? OgEðaEnNema

# Allir liðir af sama kyni

NlRuna/fall/kyn →
    KommuSamtenging/fall/kyn* KommuEndir/fall/kyn
    | ',' KommuStForskeyti? NlEind/fall/kyn AtvKomma # Konurnar, sérstaklega Anna, voru óánægðar

KommuStForskeyti →
    FsRuna
    | "þar:ao" "af:ao"    # fjórir þingmenn, þar af einn nýútskrifaður, 

KommuSamtenging/fall/kyn → ',' KommuStForskeyti? NlEind/fall/kyn

KommuEndir/fall/kyn →
    Atviksliður? KommaOgEða KommuStForskeyti? NlEind/fall/kyn AtvKomma?
    | 'og_fleira:Ao' ','?
    | 'og_margt_fleira:ao' ','?
    | 'og_svo_framvegis:ao' ','?
    | 'og_þess_háttar:ao' ','?

# Hvetja til eins langra lista og mögulegt er
$score(+6) KommuSamtenging/fall/kyn

# Blandað kyn

NlRuna/fall →
    KommuSamtenging/fall* KommuEndir/fall
    | ',' KommuStForskeyti? NlEind/fall AtvKomma # Þingmenn, sérstaklega Jóhanna, voru óánægðir

KommuSamtenging/fall → ',' KommuStForskeyti? NlEind/fall

KommuEndir/fall →
    Atviksliður? KommaOgEða KommuStForskeyti? NlEind/fall AtvKomma?
    | 'og_fleira:ao' ','?
    | 'og_margt_fleira:ao' ','?
    | 'og_svo_framvegis:ao' ','?
    | 'og_þess_háttar:ao' ','?

# Hvetja til eins langra lista og mögulegt er
$score(+6) KommuSamtenging/fall

# Nafnliðir sem geta verið stakar eindir eða runur einda

Nl →
    Nl/tala/pers/fall/kyn

Nl/fall →
    Nl/tala/pers/fall/kyn

Nl/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala →
    Nl/tala/pers/fall/kyn

Nl/fall/pers →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers →
    Nl/tala/pers/fall/kyn

Nl/pers/fall/kyn →
    Nl/tala/pers/fall/kyn

Nl/fall/tala/pers/kyn →
    Nl/tala/pers/fall/kyn

Nl/tala/pers/fall/kyn →
    NlEind/tala/pers/fall/kyn

# Gefa nefnifalli forskot ef mismunandi föll eru í boði
$score(+4) Nl_nf

# Fleirtölu-nafnliður í 3p sem leyfir runu í sama falli og kyni
# Leyfir einnig 'þeir Jón og Gunnar [sendu bréfið]' eða 'þær Elísabet og mamma hennar [byggðu húsið]'
Nl_ft_p3/fall/kyn →
    NlSamaKyn/fall/kyn

NlSamaKyn/fall/kyn →
    NlSá_ft/fall/kyn? NlEind_p3/fall/kyn NlRuna/fall/kyn+ TengiliðurMeðKommu_ft_p3/kyn?

# Fleirtölunafnliðir af sama kyni eru betri en blandaðir
$score(+2) NlSamaKyn/fall/kyn

NlSá_ft/fall/kyn →
    eo* 'sá:fn'_ft/fall/kyn # einnig þau Jón og Gunna

# Blandaðir fleirtölu-nafnliðir eru í hvorugkyni

# Leyfa 'maðurinn og konan' og 'kötturinn, hundurinn og bráðlega músin',
# og einnig 'sumir, þar á meðal Jón,'

Nl_ft_p3_nf_hk →
    NlSá_ft_nf_hk? NlEind_nf NlRuna_nf+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þf_hk →
    NlSá_ft_þf_hk? NlEind_þf NlRuna_þf+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_þgf_hk →
    NlSá_ft_þgf_hk? NlEind_þgf NlRuna_þgf+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

Nl_ft_p3_ef_hk →
    NlSá_ft_ef_hk? NlEind_ef NlRuna_ef+ TengiliðurMeðKommu_ft_p3_hk? # Blandað kyn verður að hvorugkyni fleirtölu

# Lýsingarorðaliður

# Lýsingarorð með nafnlið

# !!! TODO: Hér er ekki gerður greinarmunur á sterkri og veikri
# beygingu lýsingarorða sem standa með nafnorðum, eftir því hvort
# nafnorðið er með greini eða án. (Sbr.: 'Fallega konan talaði við mig'
# vs. 'Falleg kona talaði við mig'.)
Lo/fall/tala/kyn →
    LoSemLo/fall/tala/kyn   # Tilbrigði 3.1
    | LoMun/fall/tala/kyn
    | LoSem/fall/tala/kyn
    | LoSemSo/fall/tala/kyn

# Tilbrigði 3.1
LoSemLo/fall/tala/kyn → 
    AtviksliðurEinkunn* lo/fall/tala/kyn # (Þingmenn) kosnir leynilegri kosningu

LoMun/fall/tala/kyn →
    'munur:kk'_þgf_et lo_mst/fall/tala/kyn # (Schengen rekur) mun öflugri gagnagrunn / (þau voru) mun frjálsari skoðana sinna

$score(+10) LoMun/fall/tala/kyn

LoSem/fall/tala/kyn →
    "sem:ao" lo_esb/fall/tala/kyn # (hvernig skuli stuðla að) sem jöfnustu hlutfalli
    # | "sem:ao" lo_evb/fall/tala/kyn # (hvernig skuli stuðla að) sem jafnasta hlutfallinu

$score(+4) LoSem/fall/tala/kyn

# Tilbrigði 3.1
# Leyfa sagnorðum að standa í stað lýsingarorða ef þau
# eru í lýsingarhætti þátíðar, sterkri eða veikri beygingu
# (_vb leyfir veika beygingu ásamt sterkri)
# ('Við litum til bókfærðs hagnaðar')
LoSemSo/fall/tala/kyn →
    AtviksliðurEinkunn* so_lhþt/fall/tala/kyn
    | AtviksliðurEinkunn* so_lh_nt

$score(+6) LoSemLo/fall/tala/kyn  # Tilbrigði 3.1, eina sem þarf að toggla.

LoAtviksliðir →
    LoAtviksliður+ | "og:st" # 'honum er og heimilt að merkja við fleiri en einn frambjóðanda'

# 'Ég vaknaði ekkert alltof fersk'
LoAtviksliður →
    AtviksliðurEinkunn | 'enginn:fn'_hk_et_nf

# Lýsingarorð í sterkri beygingu
# ('Hún fór frjáls skoðana sinna í útlegð')

Lo_sb/tala/fall/kyn → LoAtviksliðir? lo_sb/tala/fall/kyn LoViðhengi?
Lo_sb_nf/tala/kyn → LoAtviksliðir? lo_sb_nf/tala/kyn LoViðhengi?

# Miðstig
Lo_mst/tala/fall/kyn → LoAtviksliðir? lo_mst/tala/fall/kyn LoViðhengi? # Miðstig
Lo_mst_nf/tala/kyn → LoAtviksliðir? lo_mst_nf/tala/kyn LoViðhengi? # Miðstig

# Lýsingarorð sem stendur með sögn getur haft eignarfalls- eða
# þágufalls-viðhengi, sbr.:
# ef.: 'Þau voru frjáls skoðana sinna'
# þgf.: 'Sýningin var opin almenningi'
LoViðhengi →
    EfLiður | Nl_þgf

$score(-10) LoViðhengi

# Lýsingarorð sem einkunn með nafnlið

# '(Jón), sultuslakur eftir gufubaðið, (lék á als oddi)'
# '(Maður) alvarlega háður eiturlyfjum (bað mig um peninga)'
# '(Stjórnmálamaður) tengdur Framsóknarflokknum (hringdi í mig)'
# '(Ég ræddi við) konu tengda verkefninu'
LoEftirNl/tala/fall/kyn →
    StaktLoEftirNl/tala/fall/kyn
    | ',' LoLiður/tala/fall/kyn FsAtv? LokatáknEðaKomma
    | LoEftirNlMeðÞgf/fall/tala/kyn
    | ',' LoEftirNlMeðÞgf/fall/tala/kyn LokatáknEðaKomma

StaktLoEftirNl/tala/fall/kyn →
    ','? LoLiður/tala/fall/kyn

# Stakt lýsingarorð eftir nafnorði gæti líklega tilheyrt næsta nafnorði
$score(-6) StaktLoEftirNl/tala/fall/kyn

# Lýsingarorð sem getur tekið með sér þágufall
# !!! ATH: Til eru listar yfir slík orð

# '[sterklega] tengdur Framsóknarflokknum'
LoEftirNlMeðÞgf/fall/tala/kyn →
    AtviksliðurEinkunn* lo_sb/fall/tala/kyn Nl_þgf

$score(+4) LoEftirNlMeðÞgf/fall/tala/kyn

# '(Ég drakk) hvítu, volgu og feitu mjólkina' / '(Ég sá) hvíta, volga og feita mjólk'
# '(Íslendingar fluttu fé til annarra) og í sumum tilvikum öruggari landa'

LoLiður/tala/fall/kyn →
    Lo/fall/tala/kyn
    | LoLiður/tala/fall/kyn LoKomma/tala/fall/kyn* KommaOgEðaEn FsRuna? Lo/fall/tala/kyn ','?

LoLiður_nf/tala/kyn → LoLiður_et_nf/kyn | LoLiður_ft_nf/kyn

LoKomma/tala/fall/kyn →
    ',' Lo/fall/tala/kyn

# Fornöfn sem geta hegðað sér eins og lýsingarorð:
# 'Hann komst sjálfur út úr flakinu', 'Hún fór öll inn um gluggann',
# 'Þau eru bæði viðskiptafræðingar',  'Það reyndist vera sú sama'
# 'Þeir fóru sumir hverjir með ferjunni'
# ('báðir' er aðeins til í fleirtölu en það veldur ekki vandræðum hér)

LoFornafn/fall/tala/kyn →
    'allur:fn'/fall/tala/kyn | 'sjálfur:fn'/fall/tala/kyn | 'báðir:fn'/fall/tala/kyn
    | 'sami:fn'/fall/tala/kyn | 'sumur:fn'/fall/tala/kyn 'hver:fn'/fall/tala/kyn?

# Lýsingarorð sem andlag sagnar
# Þar kemur til greina miðstig + 'en' + nafnliður

# '(Ég var) reiður' / '(Ég var) reiðari en þú' / '(Mér var verr við þetta) en þér'
# '(Ég var) reiðastur allra' / '(Ég talaði við þann) reiðasta þeirra allra'

# Ath.: miðstig lýsingarorða er merkt MST í BÍN og passar því ekki við lo_sb
# (en frumstig sterkrar beygingar er FSB og efsta stig er ESB)
LoSamanb/tala/fall/kyn →
    Lo_sb/tala/fall/kyn
    # Miðstig: (Ég varðveitti laxinn) [mun] betur [en bleikjuna]
    | "mun:kk"? Lo_mst/tala/fall/kyn EnSamanb/fall?
    # (Hann tók við hrósinu) [,] ýmist glaður eða með fýlusvip
    | LoSamanbFleiri/fall/tala/kyn

LoSamanbFleiri/fall/tala/kyn →
    Samanburðarmerki "ýmist:st" LoEðaFs/fall/tala/kyn "eða:st" LoEðaFs/fall/tala/kyn LokatáknEðaKomma
    | "ýmist:st" LoEðaFs/fall/tala/kyn "eða:st" LoEðaFs/fall/tala/kyn
    | Samanburðarmerki "bæði:st" LoEðaFs/fall/tala/kyn "og:st" LoEðaFs/fall/tala/kyn LokatáknEðaKomma
    | "bæði:st" LoEðaFs/fall/tala/kyn "og:st" LoEðaFs/fall/tala/kyn

$score(+4) LoSamanbFleiri/fall/tala/kyn

LoEðaFs/fall/tala/kyn →
     LoTengtSögn/tala/fall/kyn | FsAtv

EnSamanb/fall →
    "en:st" Nl/fall

# Hann er reiður / [!!! ATH:] Hann er Önnu mun reiðari en Páli
LoSamanb_nf/tala/kyn →
    Lo_sb_nf/tala/kyn
    | "mun:kk"? Lo_mst_nf/tala/kyn EnSamanb_nf? # Miðstig: (Ég var) [mun] reiðari [en Páll]
    # (Hann tók við hrósinu) [,] ýmist glaður eða með fýlusvip
    | LoSamanbFleiri_nf/tala/kyn

LoTengtSögn/tala/fall/kyn →
    LoSamanb/tala/fall/kyn
    | AtviksliðurEinkunn* LoFornafn/fall/tala/kyn # (Hann komst) [hins vegar] sjálfur (út úr flakinu)
    | LoTengtSögn/tala/fall/kyn LoKommaTengdSögn/tala/fall/kyn* KommaOgEðaEn LoSamanb/tala/fall/kyn

LoKommaTengdSögn/tala/fall/kyn →
    ',' LoSamanb/tala/fall/kyn

LoTengtSögn_nf/tala/kyn →
    LoSamanb_nf/tala/kyn
    | AtviksliðurEinkunn* LoFornafn_nf/tala/kyn # (Hann komst) sjálfur (út úr flakinu)
    | LoTengtSögn_nf/tala/kyn LoKommaTengdSögn_nf/tala/kyn* KommaOgEðaEn LoSamanb_nf/tala/kyn

LoKommaTengdSögn_nf/tala/kyn →
    ',' LoSamanb_nf/tala/kyn

# Viðhengi með miðmyndarsögnum: (okkur finnst) sjálfum/mörgum/flestum [hverjum]...
LoÞgf_et/kyn →
    lo_sb_þgf_et/kyn
    | LoFornafn_þgf_et/kyn

LoÞgf_ft/kyn →
    lo_sb_þgf_ft/kyn 'hver:fn'_þgf_ft/kyn?
    | LoFornafn_þgf_ft/kyn 'hver:fn'_þgf_ft/kyn?

# Forsetningarliðir

FsMeðFallstjórn →

    fs/fall Nl/fall

    | fs_ef_ft Nl_ef_ft # 'milli' eða 'millum'

    # '(Beiðnin) um að...', '(Fíknin) í að...', '(Biðin) eftir að...', '(Þörfin) fyrir að...',
    # 'gegnum að...', 'kringum að...',
    # 'með að...', '(Löngunin) til að...', '(Áhættan) við að...', '(Hættan) á að...'
    | fs/fall FsMeðNh

    # 'Fjöldi stúlkna hefur leitað til okkar þrátt fyrir að hafa farið í heilsueftirlit í skólum'
    | 'þrátt_fyrir_að:st' NhRuna NhLiðurFrh*

    | "á:fs" "hendur:kvk" Nl_þgf    # 'að höfða sakamál á hendur Páli'
    | "á:fs" "fætur:kk" Nl_þgf      # '(Hann borðaði hverja tertusneiðina) á fætur annarri'
    | "nær:fs" 'draga:so'_op_gm_et_p3 Nl_þgf # 'þegar nær dró/dregur jólum'
    | 'það_sem_af_er:fs' Nl_þgf     # 'það sem af er þessu þingi'
    | 'fyrir_hönd:fs' Nl_ef
    | "beggja:fn" "vegna:fs" Nl_ef  # 'beggja vegna vegarins'
    | 'með_tilliti_til:fs' Nl_ef    # Skammstöfunin m.t.t.
    | 'samanbera:so'_lhþt_sb_hk_nf_et "við:fs" Nl_þf
    | 'bera:so'_lhþt_sb_hk_nf_et "saman:ao" "við:fs" Nl_þf
    | "þegar:st" 'koma:so'_gm_p3_et "að:fs" Nl_þgf
    | 'til_og_frá:fs' Nl_þgf
    | "fyrir:ao" "nú:ao"? "utan:fs" Nl_þf
    | "innan:ao" "við:fs" Nl_þf
    | "innan:ao" "sem:st" "utan:fs" Nl_ef
    | "hvað:ao" FsHvaðVarðar
    | "allt:fn" "frá:fs" Nl_þgf     # allt frá hruni, allt frá börnum til fullorðinna
    # 'um að lög um endurupptökunefnd standist ekki stjórnarskrá' - Ath: 'um það að' er höndlað í ÞaðTenging
    | "um:fs" "að:st" HreinYfirsetning ','?
    # 'er bjartsýnn á að Alfreð komi aftur í liðið' - Ath: 'á það/því að' er höndlað í ÞaðTenging
    | "á:fs" "að:st" HreinYfirsetning ','?
    | "að:fs" "nafni:hk" person_nf  # Ég ræddi við konu að nafni Aðalbjörg Jónsdóttir
    | "á:fs" Vikudagur_gr_þf_et 'koma:so'_gm_fh_p3_et  # á sunnudaginn kemur / á laugardagskvöldið kemur
    | FsMiðaðVið
    | FsÍGegnUm
    | FsAðBaki
    | FsTilÞgf
    | FsÁMeðal
    | FsÁMóti
    | Átt ÁttFs # vestur á firði, suður götuna, austur í flóa, norður að fjalli
    # 'hvorki við stjórn spítalans né ráðherrann'
    # 'hvorki til mín né stjórnarinnar'
    | "hvorki:ao" fs/fall Nl/fall "né:st" Nl/fall
    | FsÞgfTilEf

    | '(' FsMeðFallstjórn ')'

$score(+1) FsMeðFallstjórn

FsMeðNh → NhLiðir SagnInnskot?

$tag(begin_prep_scope) FsMeðNh # Nýtt samhengi fyrir samtengingu sagna og forsetninga

# Áttirnar eru aðeins forsetningar í BÍN, ekki atviksorð - þess vegna þurfa þær sérmeðhöndlun

Átt → "vestur:fs" | "austur:fs" | "suður:fs" | "norður:fs"

ÁttFs →
    Nl_þf               # suður götuna
    | "á:fs" Nl_þf      # vestur á firði 
    | "í:fs" Nl_þf      # norður í land
    | "að:fs" Nl_þf     # suður að læk
    | "á:fs" Nl_þgf     # vestur á fjörðum
    | "í:fs" Nl_þgf     # austur í dölum
    | "að:fs" Nl_þgf    # (við fórum) norður að bænum

FsÞgfTilEf →
    Nl_þgf "til:fs" Nl_ef # máli sínu til stuðnings, barninu til hróss, henni til bjargar

# Ekki velja þetta nema nauðsyn beri til
$score(-6) FsÞgfTilEf

FsMiðaðVið →
    'miða:so'_lhþt_sb_hk_nf_et "við:fs" Nl_þf
    | 'miðað_við:fs' Nl_þf # Skammstöfunin m.v.

$score(+12) FsMiðaðVið

FsÍGegnUm →
    "í:ao" "gegn:ao" "um:fs" Nl_þf
    | "í:ao" "gegnum:fs" Nl_þf

$score(+6) FsÍGegnUm

FsAðBaki →
    "að:fs" "baki:hk" Nl_þgf # 'að baki hverju bundnu sæti'

$score(+4) FsAðBaki

FsÁMeðal →
    "á:ao" "meðal:fs" Nl_ef # Sérregla hér þar sem þetta er að öðrum kosti túlkað sem meðal:hk + ef

$score(+16) FsÁMeðal

FsÁMóti →
    "á:ao" "móti:fs" Nl_þgf
    | "á:ao" "milli:fs" Nl_ef_ft

$score(+24) FsÁMóti

FsTilÞgf →
    "til:fs" FsTilÞgfOrð Nl_þgf # 'til stuðnings/heiðurs Páli'

FsTilÞgfOrð →
    "stuðnings:kk" | "heiðurs:kk" | "háðungar:kvk"

So_1_gm_fh_p3_et_nf → so_1_nf_gm_fh_p3_et
So_1_gm_fh_p3_et_þf → so_1_þf_gm_fh_p3_et
So_1_gm_fh_p3_et_þgf → so_1_þgf_gm_fh_p3_et
So_1_gm_fh_p3_et_ef → so_1_ef_gm_fh_p3_et

FsHvaðVarðar →
    Nl/fall So_1_gm_fh_p3_et/fall  # '(hvað) Pál varðar/Páli viðvíkur'
    | So_1_gm_fh_p3_et/fall Nl/fall # '(hvað) varðar Pál/viðvíkur Páli'

$score(+4) FsHvaðVarðar

FsLiður → Atviksliður? FsMeðFallstjórn # snarlega í bæinn

# FsAtv getur verið runa af forsetningarliðum með atviksliðum inn á milli,
# á undan og í lok - eða einungis atviksliðir.

FsAtv →
    # (Ég samþykkti þetta), en með semingi þó
    Samanburðarmerki? "en:st"? EinFsAtv+ FsAtvFramhald* ','?

FsAtvFramhald → KommaOgEðaEn EinFsAtv+

EinFsAtv →
    Nema? FsMeðFallstjórn
    | EinnAl
    | "hvorki:ao" EinEinföldFsAtv+ "né:st" EinEinföldFsAtv+ # 'hvorki í Frakklandi né annars staðar í Evrópusambandinu'
    | "annað:fn" "hvort:st" EinEinföldFsAtv+ "eða:st" EinEinföldFsAtv+ # 'annað hvort í Breiðholti eða við World Class'
    | "annaðhvort:fn" EinEinföldFsAtv+ "eða:st" EinEinföldFsAtv+ # 'annaðhvort í Breiðholti eða við World Class'
    | "bæði:st" EinEinföldFsAtv+ ','? "og:st" EinEinföldFsAtv+

EinEinföldFsAtv → FsMeðFallstjórn | EinnAl

Nema →
    "nema:st" "þá:ao"? "helst:ao"?

# AtvFs hefst alltaf á atvikslið en á eftir geta fylgt forsetningarliðir
# og atviksliðir.

AtvFs →
    ','? EinnAl EinFsAtv* FsAtvFramhald* ','?

# Taka sérstaklega á runu forsetningarliða sem er afmörkuð milli komma;
# það minnkar tvíræðni

FsLiðurMeðKommu →
    FsLiður
    | FsLiður KommaOgEðaEn? Atviksliður? FsLiðurMeðKommu

# FsRuna getur ekki endað á atvikslið, nema innan kommu
# 'var, líkt og í búsáhaldabyltingunni, byggð á óánægju'
FsRuna → ',' FsForskeyti? FsLiðurMeðKommu Atviksliður? LokatáknEðaKomma
    | FsLiðurMeðKommu

FsRunaEftirNl →
    FsRuna
    # Atviksorð sem geta fylgt á eftir nafnliðum
    # !!! Setja í conf?
    | "hérlendis:ao"
    | "erlendis:ao"
    | 'í_ár:ao'

# Hvetja létt til þess að forsetningar séu tengdar nafnlið
$score(+1) FsRunaEftirNl
$tag(purge_prep) FsRunaEftirNl # Ekki senda innifaldar forsetningar upp tréð

# Forskeyti sem leyfileg eru á undan forsetningarlið innan kommu
FsForskeyti →
    'líkt_og:st' | "eins:ao" "og:st" | 'það_er:ao' | "það:pfn" "er:so"

# Atviksliður

Al → EinnAl+

EinnAl →
    ','? ao                 # Strax tvöfalt til baka
    | Dagsetning            # (Ég tilkynnti þetta) strax 3. september
    | Tímasetning
    | EkkiNeitt
    | UndirEins
    | "eina:fn" "ferðina:kvk" "enn:ao"
    | "heima:ao" "fyrir:ao"
    | SemStendur
    | "hingað:ao" "til:ao"
    # | "til:ao" "að:nhm" "mynda:so"
    | "sem:ao" "sagt:so"
    | FyrirLöngu
    | Sinnum
    | Úrslit                # KR sigraði Val 6:0

$tag(no_prep) EinnAl        # Engar forsetningar hér inni - óþarfi að skoða dótturgreinar

EkkiNeitt →
    "ekki:ao" "neitt:ao"    # (Ég þekki þetta) ekki neitt

$score(-6) EkkiNeitt

FyrirLöngu →
    "fyrir:fs" 'langur:lo'_þgf_et_hk # 'fyrir löngu'

$score(+8) FyrirLöngu

# Sem stendur er hann ekki viðlátinn
SemStendur →
    "sem:ao" "stendur:so"

$score(-6) SemStendur

UndirEins →
    "undir:ao" "eins:ao"    # 'Ég fór undir eins og kannaði málið'

$score(+4) UndirEins

Sinnum →
    töl_ft_hk_þgf "sinnum:hk"          # tvisvar, þrisvar og fjórum sinnum er skilgreint í Main.conf
    | tala_ft_hk_þgf "sinnum:hk"
    | tala_et_hk_þgf "sinni:hk"

$score(+24) Sinnum

Atviksliður →
    Al OgAtviksliður*

OgAtviksliður →
    KommaOgEðaEn Al         # Hratt, en örugglega og fumlaust

# Fanga hugsanlegan atvikslið innan kommu:
# Fyrirtækin annars vegar, og heimilin hins vegar, hafa dregið úr fjárfestingu
AtvKomma →
    Atviksliður? ','

AtviksliðurKomma →
    Atviksliður ','?

AtviksliðurEinkunn →
    eo                              # Einkunnarorð = atviksorð sem geta ekki jafnframt verið forsetningar
    | Dagsetning                    # (hagnaðurinn var strax) árið 2012 (175 milljarðar króna)
    | Tímasetning
    | Aldur                         # Hin 29 ára gamla fyrirsæta
    | "allt:Fn" "að:ao"                # 'fjárfesta fyrir allt að 100 milljónir króna'
    | "svo:ao" "sem:ao"             # 'enda kemur kynlíf þeirra svo sem engum við'
    | UmEðaYfir

UmEðaYfir →
    "um:ao" "eða:st" "yfir:ao"      # Um eða yfir 100 tyrkneskum eldflaugum rigndi yfir Aleppo

$score(+8) UmEðaYfir

# Dagsetning og tími

Tímasetning →
    "klukkan:kvk"? tími OgKlukkan? Degis?
    | "klukkan:kvk" "hálf:lo"? töl_ft_hk_nf OgKlukkan? Degis?
    | "klukkan:kvk" "hálf:lo"? to_nf_hk Degis?
    | Degis

$tag(apply_prep_bonus) Tímasetning # Gefa bónus þegar tímasetning fylgir sögn frekar en nafnlið

OgKlukkan → "og:st" tala | "og:st" töl_ft_hk_nf

Degis → "árdegis:ao" | "síðdegis:ao" | "miðdegis:ao"

OgMeð → "og:st" "með:fs"

# Talnabil nota en-dash

Til → "-:en" | "til:st" | "og:st"

Dagsetning →
    DagsFs_þf? Tímasetning 'sá:fn'_et_kk_þf? dags # '[eftir] kl. 14 [þann] 3. júlí 2007'
    | DagsFs_ef Tímasetning 'sá:fn'_et_kk_þf? dags # 'til kl. 14 [þann] 3. júlí 2007'
    | DagsFs_þgf Tímasetning 'sá:fn'_et_kk_þf? dags # 'frá kl. 14 [þann] 3. júlí 2007'

    | "dags.:lo" dags # dags. 30. desember 1965
    | "dagana:kk"? raðnr_þf_kk Til dags # 1. - 10. júní / 25. til 29. ágúst
    | DagsFs_þf? 'sá:fn'_et_kk_þf? dags 'síðastliðinn:lo'_et_kk_þf? # '[eftir] [þann] 3. júlí 2007 [síðastliðinn]'
    | "hinn" dags 'síðastliðinn:lo'_et_kk_þf? # 'hinn 3. júlí [síðastliðinn]'
    | DagsFs_þgf dags # 'frá 3. júlí 2007'
    | DagsFs_ef dags # 'til 3. júlí 2007'
    | "aðfaranótt:kvk" dags 'síðastliðinn:lo'_et_kk_ef?
    | "aðfaranótt:kvk" Vikudagur_ef_et # aðfaranótt sunnudags

    | Vikudagur_þf_et dags 'næstkomandi:lo'_et_kk_þf? # 'föstudag[inn] 29. júlí [næstkomandi]'
    | Vikudagur_þf_et tímapunktur # 'föstudag[inn] 29. júlí kl. 15'
    | 'næstkomandi:lo'_et_kk_þf Vikudagur_þf_et # 'næstkomandi fimmtudag'
    | 'síðastliðinn:lo'_et_kk_þf Vikudagur_þf_et # 'síðastliðinn fimmtudag'
    | "á:fs" Vikudagur_þf_et # á föstudag / á sunnudaginn / á fimmtudagskvöld
    | "í:fs" Mánuður_þf # í febrúar
    | 'á_morgun:ao' ','? Vikudagur_þf_et # á morgun, miðvikudag

    | DagsFs_þf? 'sá:fn'_et_kk_þf? tímapunktur # '[eftir] [þann] 3. júlí 2007 kl. 13:00'
    | DagsFs_þgf tímapunktur # 'frá 3. júlí 2007 kl. 13:00'
    | DagsFs_ef tímapunktur # 'til 3. júlí 2007 kl. 14:15'

    | DagsFs_þf? Tímasetning Vikudagur_þf_et dags # '[fyrir] kl. 13 sunnudag[inn] 1. janúar 2012'

    | DagsFs_þgf Tímasetning Vikudagur_þf_et dags # 'frá kl. 13 sunnudag[inn] 1. janúar 2012'

    | DagsFs_ef Tímasetning Vikudagur_þf_et dags # 'til kl. 13 sunnudag[inn] 1. janúar 2012'

    | Árstíð_þf_et ártal # 'haustið 2012'
    | Árið_þf # 'árið 2012'
    | Árin_þf # 'árin 2010-12'
    | ártal # 2012
    | MörgÁr # '2009-13'
    | "frá:fs" OgMeð? "árinu:hk"? ártal # 'frá árinu 2009', 'frá og með 2012'
    | "til:fs" "ársins:hk"? ártal # 'til ársins 1997'
    | Síðan
    | "dagana:kk" Dagana # dagana 1. til 10. júlí
    | 'sá:fn'_kk_et_þf? raðnr_þf_kk "þessa:fn" "mánaðar:kk" # [þann] 23. þessa mánaðar
    | ÞegarLíðaTekurÁ

    | Undanfarið
    | 'allur:fn'_þf/tala/kyn? gr_þf/tala/kyn? 'síðari:lo'_þf/tala/kyn TalaTímabil_þf/tala/kyn # síðustu sex vikur, hin síðari ár, allan síðasta mánuð
    | Tímabil_þf # (Hann kom hér) alla daga, suma vikudaga, nokkur ár, eina vikuna, síðustu fimm vetur
    | TímabilsEinkunn_þgf/tala/kyn Tímabil_þgf/kyn/tala TímaAfstæði_þgf? # (Námsönnin hefst) þremur mánuðum áður en jólafríið byrjar
    | Tímabil_þgf/kyn/tala TímaAfstæði_þgf # Klukkutíma fyrr kom ég á staðinn
    | Tímabil_ft_þgf/kyn "saman:ao" # Nefndin fundaði ekki mánuðum saman
    | Tímabil_þf/kyn/tala TímaAfstæði_þf # Vikuna áður / Daginn eftir / áratugina á undan
    | TímaForskeyti? TímaAfstæði # [viku/löngu] áður en krónan féll / [ári/stuttu] eftir að landið öðlaðist sjálfstæði

$tag(apply_prep_bonus) Dagsetning # Gefa bónus þegar dagsetning fylgir sögn frekar en nafnlið
$tag(apply_length_bonus) Dagsetning # Gefa bónus eftir því hversu margar eindir (tokens) falla undir dagsetninguna

Undanfarið →
    # 'Undanfarið' er atviksorð í BÍN, en 'undanfarnar vikur' o.s.frv. virkar einnig
    # eins og atviksliður (eða forsetningarliður), sbr. 'ég hef undanfarnar vikur verið að velta því fyrir mér'...
    'undanfarinn:lo'_þf/tala/kyn TalaTímabil_þf/tala/kyn # undanfarnar sex vikur, undanfarin ár, undanfarinn mánuð
    | 'undangenginn:lo'_þf/tala/kyn TalaTímabil_þf/tala/kyn # undangengnar sex vikur

$score(+10) Undanfarið

Síðan →
    "síðan:ao" ártal # síðan 2012
    | "síðan:ao" dags # síðan 19. júlí 2006

$score(+10) Síðan

ÞegarLíðaTekurÁ →
    "þegar:st" "líða:so" 'taka:so'_gm_p3_et "á:fs" Tímabil_þf/kyn/tala

$score(+16) ÞegarLíðaTekurÁ

Dagana →
    raðnr_þf_kk Til dags # dagana 1. til 10. júlí / 9.-16. ágúst
    | raðnr_þf_kk "og:st" dags # dagana 1. og 2. júní
    | dags Til dags # dagana 29. júní til 3. júlí

# 29 ára, eins árs, tveggja vikna, margra ára

Aldur →
    Töluorð_ef/tala/kyn Tímabil_ef/kyn/tala
    | 'margur:lo'_ef_ft Tímabil_ef_ft
    | 'allmargur:lo'_ef_ft Tímabil_ef_ft
    | 'fjölmargur:lo'_ef_ft Tímabil_ef_ft

# Gefa plús fyrir dagsetningar, tímasetningar og aldur
$score(+6) Dagsetning Tímasetning Aldur

TímaAfstæði_þgf → "síðar:ao" | "seinna:ao" | "fyrr:ao" TímaEn? | "áður:ao" TímaEn?
TímaAfstæði_þf → "áður:ao" TímaEn? | "eftir:ao" TímaAð? | "fyrir:fs" Nl_þf | "á:ao" "undan:ao" | "á:ao" "eftir:ao"
TímaForskeyti → "löngu:lo" | "stuttu:lo" | "töluverðu:lo" | "örstuttu:lo"
    | "rétt:ao" | "örskömmu:lo" | Tímabil_þgf/kyn/tala
TímaAfstæði → "áður:ao" TímaEn | "eftir:ao" TímaAð

$score(+2) TímaForskeyti
$score(+4) TímaAfstæði

TímaEn →
    "en:st" "að:st"? HreinYfirsetning

TímaAð →
    "að:st" HreinYfirsetning

TímabilsEinkunn/fall/tala/kyn →
    fn/fall/tala/kyn          # alla/suma/nokkra
    | Töluorð/fall/tala/kyn   # fjóra/4

TalaTímabil_þf/tala/kyn →
    Töluorð_þf/tala/kyn? Tímabil_þf/kyn/tala # sex vikur, mánuð, tvö ár

Tímabil_þf →
    TímabilsEinkunn_þf/tala/kyn Tímabil_þf/kyn/tala Tímabil_ef? # (Hann kom hér) alla daga, suma vikudaga, nokkur ár, eina vikuna

Tímabil_ef →
    'ár:hk'_hk_ef_et_gr            # ársins
    | 'skólaár:hk'_hk_ef_et_gr     # skólaársins
    | 'misseri:hk'_hk_ef_et_gr     # misserisins
    | 'sumar:hk'_hk_ef_et_gr
    | 'vor:hk'_hk_ef_et_gr
    | 'haust:hk'_hk_ef_et_gr
    | 'kjörtímabil:hk'_hk_ef_et_gr
    | 'leikár:hk'_hk_ef_et_gr
    | 'rekstrarár:hk'_hk_ef_et_gr
    | 'almanaksár:hk'_hk_ef_et_gr
    | 'vika:kvk'_kvk_ef_et_gr
    | 'leiktíð:kvk'_ef_et_gr
    | 'öld:kvk'_kvk_ef_et_gr
    | 'mánuður:kk'_ef_et_gr
    | 'ársfjórðungur:kk'_ef_et_gr
    | 'dagur:kk'_ef_et_gr
    | 'sólarhringur:kk'_ef_et_gr
    | 'vetur:kk'_kk_ef_et_gr
    | 'morgunn:kk'_ef_et_gr
    | 'eftirmiðdagur:kk'_ef_et_gr
    | 'klukkutími:kk'_ef_et_gr
    | 'áratugur:kk'_ef_et_gr

# $score(+8) Tímabil_þf

Tímabil_ef_ft →
    Tímabil_hk_ef_ft
    | Tímabil_kk_ef_ft
    | Tímabil_kvk_ef_ft

TímabilsEinkunn_þf_ft/kyn →
    NæsturSíðastur_þf_ft/kyn Töluorð_þf_ft/kyn?  # Næstu fimm daga, síðustu þrjú sumur

TímabilsEinkunn_þf_et/kyn →
    NæsturSíðastur_þf_et/kyn # Næsta árið, fyrsta mánuðinn

TímabilsEinkunn_þgf/tala/kyn →
    "rúmlega:ao" Töluorð_þgf/tala/kyn # rúmlega sex vikum síðar
    | "tæplega:ao" Töluorð_þgf/tala/kyn # tæplega þremur árum síðar
    | 'tæpur:lo'_þgf/tala/kyn Töluorð_þgf/tala/kyn # tæpum þremur mánuðum fyrr
    | 'rúmur:lo'_þgf/tala/kyn Töluorð_þgf/tala/kyn # rúmum fjórum vikum fyrr

NæsturSíðastur_þf/tala/kyn →
    'næstur:lo'_vb_þf/tala/kyn      # næsta vetur, næstu daga
    | 'síðari:lo'_vb_þf/tala/kyn    # síðasta vetur, síðustu daga
    | 'seinn:lo'_vb_þf/tala/kyn     # seinasta vetur, seinustu daga
    | 'fyrstur:lo'_vb_þf/tala/kyn   # fyrstu þrjá vetur, fyrstu 10 daga

NæsturSíðastur_þf_et_kk →
    'síðastliðinn:lo'               # skammstöfunin sl.

NæsturSíðastur_þf_ft/kyn →
    'síðastliðinn:lo'_sb_þf_ft/kyn  # síðastliðna tvo sólarhringa

NæsturSíðastur_þf_et_kvk →
    'síðastliðinn:lo'_sb_þf_et_kvk  # síðastliðna viku

NæsturSíðastur_þf_et_hk →
    'síðastliðinn:lo'_sb_þf_et_hk  # síðastliðið sumar

$score(+6) NæsturSíðastur_þf/tala/kyn

Tímabil/fall/kyn/tala → Tímabil/kyn/fall/tala

Tímabil_ft_þgf_kk → Tímabil_kk_þgf_ft
Tímabil_ft_þgf_kvk → Tímabil_kvk_þgf_ft
Tímabil_ft_þgf_hk → Tímabil_hk_þgf_ft

Tímabil_hk/fall/tala →
    'ár:hk'_hk/fall/tala
    | 'skólaár:hk'_hk/fall/tala
    | 'misseri:hk'_hk/fall/tala
    | 'sumar:hk'_hk/fall/tala
    | 'vor:hk'_hk/fall/tala
    | 'haust:hk'_hk/fall/tala
    | 'kvöld:hk'_hk/fall/tala
    | 'hádegi:hk'_hk/fall/tala
    | 'kjörtímabil:hk'_hk/fall/tala
    | 'leikár:hk'_hk/fall/tala
    | 'rekstrarár:hk'_hk/fall/tala
    | 'almanaksár:hk'_hk/fall/tala
    | 'korter:hk'_hk/fall/tala
    | 'kortér:hk'_hk/fall/tala

Tímabil_kvk/fall/tala →
    'vika:kvk'_kvk/fall/tala
    | 'helgi:kvk'_kvk/fall/tala
    | 'nótt:kvk'_kvk/fall/tala
    | 'mínúta:kvk'/fall/tala
    | 'sekúnda:kvk'/fall/tala
    | 'leiktíð:kvk'/fall/tala
    | 'öld:kvk'_kvk/fall/tala

Tímabil_kk/fall/tala →
    'mánuður:kk'/fall/tala
    | 'ársfjórðungur:kk'/fall/tala
    | 'dagur:kk'/fall/tala
    | 'sólarhringur:kk'/fall/tala
    | 'vikudagur:kk'/fall/tala
    | 'helgidagur:kk'/fall/tala
    | 'virkur:lo'_kk/fall/tala 'dagur:kk'/fall/tala
    | 'vetur:kk'_kk/fall/tala
    | 'morgunn:kk'/fall/tala
    | 'eftirmiðdagur:kk'/fall/tala
    | Vikudagur/fall/tala
    | 'klukkutími:kk'_kk/fall/tala
    | 'hálftími:kk'_kk/fall/tala
    | 'stundarfjórðungur:kk'_kk/fall/tala
    | 'tími:kk'_kk/fall/tala
    | 'áratugur:kk'/fall/tala
    | FyrriSíðari_kk/fall/tala? 'hálfleikur:kk'/fall/tala
    | 'leikur:kk'/fall/tala

FyrriSíðari/kyn/fall/tala →
    'fyrri:lo'_mst/kyn/fall/tala
    | 'síðari:lo'_mst/kyn/fall/tala
    | 'seinn:lo'_mst/kyn/fall/tala

Vikudagur/fall/tala →
    # !!! Do the following in the tokenizer instead?
    'sunnudagur:kk'/fall/tala
    | 'mánudagur:kk'/fall/tala
    | 'þriðjudagur:kk'/fall/tala
    | 'miðvikudagur:kk'/fall/tala
    | 'fimmtudagur:kk'/fall/tala
    | 'föstudagur:kk'/fall/tala
    | 'laugardagur:kk'/fall/tala
    | 'sunnudagskvöld:hk'/fall/tala
    | 'mánudagskvöld:hk'/fall/tala
    | 'þriðjudagskvöld:hk'/fall/tala
    | 'miðvikudagskvöld:hk'/fall/tala
    | 'fimmtudagskvöld:hk'/fall/tala
    | 'föstudagskvöld:hk'/fall/tala
    | 'laugardagskvöld:hk'/fall/tala

Vikudagur_gr/fall/tala →
    'sunnudagur:kk'_gr/fall/tala
    | 'mánudagur:kk'_gr/fall/tala
    | 'þriðjudagur:kk'_gr/fall/tala
    | 'miðvikudagur:kk'_gr/fall/tala
    | 'fimmtudagur:kk'_gr/fall/tala
    | 'föstudagur:kk'_gr/fall/tala
    | 'laugardagur:kk'_gr/fall/tala
    | 'sunnudagskvöld:hk'_gr/fall/tala
    | 'mánudagskvöld:hk'_gr/fall/tala
    | 'þriðjudagskvöld:hk'_gr/fall/tala
    | 'miðvikudagskvöld:hk'_gr/fall/tala
    | 'fimmtudagskvöld:hk'_gr/fall/tala
    | 'föstudagskvöld:hk'_gr/fall/tala
    | 'laugardagskvöld:hk'_gr/fall/tala

Árstíð_þf/tala →
    'haust:hk'_hk_þf/tala
    | 'vor:hk'_hk_þf/tala
    | 'sumar:hk'_hk_þf/tala
    | 'vetur:kk'_kk_þf/tala

Mánuður_þf →
    'janúar:kk'_þf
    | 'febrúar:kk'_þf
    | 'mars:kk'_þf
    | 'apríl:kk'_þf
    | 'maí:kk'_þf
    | 'júní:kk'_þf
    | 'júlí:kk'_þf
    | 'ágúst:kk'_þf
    | 'september:kk'_þf
    | 'október:kk'_þf
    | 'nóvember:kk'_þf
    | 'desember:kk'_þf

# Forsetningar sem hægt er að nota með dagsetningum
DagsFs_þf →
    "fyrir:fs" | "eftir:fs"
DagsFs_ef →
    "til:fs"
DagsFs_þgf →
    "frá:fs"

# ----------------------------------------------
#
# Query grammar
#
# The following grammar is used for queries only
#
# ----------------------------------------------

QueryRoot →

    QPerson > QCompany > QEntity > QTitle > QWord > QSearch

# Mark the QueryRoot nonterminal as a root in the grammar
$root(QueryRoot)

QPerson →
    Manneskja_nf
    | QPersonPrefix_nf Manneskja_nf '?'?
    | QPersonPrefix_þf Manneskja_þf '?'?
    | QPersonPrefix_þgf Manneskja_þgf # '?'?
    | QPersonPrefix_ef Manneskja_ef '?'?
    | QPersonPrefixAny Sérnafn '?'?

QPersonPrefixAny →
    QPersonPrefix/fall

$score(-2) QPersonPrefixAny # Discourage entity names if person names are available

QPersonPrefix_nf →
    "hver" "er"
    | "hvað" "gerir"
    | "hvað" "starfar"
    | "hvaða" "titil" "hefur"
    | "hvaða" "starfi" "gegnir"

QPersonPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QPersonPrefix_ef →
    "hver" "er" "titill"
    | "hver" "er" "starfstitill"
    | "hvert" "er" "starf"

QPersonPrefix_þgf →
    "segðu" "mér"? "frá"

QCompany →
    QCompanyPrefix_nf Fyrirtæki_nf '?'?
    | QCompanyPrefix_þf Fyrirtæki_þf '?'?
    | QCompanyPrefix_þgf Fyrirtæki_þgf # '?'?

QCompanyPrefix_nf →
    "hvað" "er"

QCompanyPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QCompanyPrefix_þgf →
    "segðu" "mér"? "frá"

QEntity → QEntityPrefix/fall QEntityKey/fall '?'?

QEntityKey/fall →
    Sérnafn/fall > Sérnafn > no/fall > no

QEntityPrefix_nf →
    "hvað" "er"
    | "hvað" "eru"
    | 0

QEntityPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QEntityPrefix_þgf →
    "segðu" "mér"? "frá"

QEntityPrefix_ef →
    0

QTitle →
    QTitlePrefix_nf QTitleKey_nf '?'?
    | QTitlePrefix_ef QTitleKey_ef '?'?

QSegðuMér →
    "segðu" "mér"
    | "mig" "langar" "að" "vita"
    | "ég" "vil" "gjarnan"? "vita"

QTitlePrefix_nf →
    QSegðuMér? QTitlePrefixFrh_nf

QTitlePrefixFrh_nf →
    "hver" "er"
    | "hver" "var"
    | "hver" "hefur" "verið"

QTitlePrefix_ef →
    QSegðuMér? "hver" "gegnir" "starfi"

QTitleKey_nf →
    EinnTitill_nf OgTitill_nf*

QTitleKey_ef →
    EinnTitill_ef OgTitill_ef*

# Word relation query

QWord →
    QWordPerson
    > QWordEntity
    > QWordNoun
    > QWordVerb

QWordPrefix_þgf →
    "hvað" "tengist"
    | "hvað" "er" "tengt"
    | "hvaða" "orð" "tengjast"
    | "hvaða" "orð" "tengist"
    | "hvaða" "orð" "eru" "tengd"

QWordPrefix_nf →
    "hverju" "tengist"
    | "hvaða" "orðum" "tengist"

# 'Hvað tengist [orðinu/nafnorðinu] útihátíð?'

QWordNoun →
    QWordNoun_nf
    | QWordNoun_þgf

QWordNoun_þgf →
    QWordPrefix_þgf QWordNounKey_þgf '?'?

QWordNoun_nf →
    QWordNounKey_nf
    | QWordPrefix_þgf "orðinu" QWordNounKey_nf '?'?
    | QWordPrefix_þgf "nafnorðinu" QWordNounKey_nf '?'?
    | QWordPrefix_nf "orðið" QWordNounKey_nf '?'?
    | QWordPrefix_nf "nafnorðið" QWordNounKey_nf '?'?

QWordNounKey/fall → no/fall

# 'Hvaða orð tengjast Ragnheiði Ríkharðsdóttur?'
# 'Hvaða orð eru tengd nafninu Elliði Vignisson?'

QWordPerson →
    QWordPerson_nf
    | QWordPerson_þgf

QWordPerson_þgf →
    QWordPrefix_þgf QWordPersonKey_þgf '?'?

QWordPerson_nf →
    QWordPrefix_þgf "nafninu" QWordPersonKey_nf '?'?
    | QWordPrefix_nf "nafnið" QWordPersonKey_nf '?'?

QWordPersonKey/fall → person/fall

# 'Hvaða orð tengjast sögninni að teikna?'

QWordVerb →
    nhm? QWordVerbKey
    | QWordPrefix_þgf "orðinu" QWordVerbKey '?'?
    | QWordPrefix_þgf "sögninni" nhm? QWordVerbKey '?'?
    | QWordPrefix_þgf "sagnorðinu" nhm? QWordVerbKey '?'?
    | QWordPrefix_nf "orðið" QWordVerbKey '?'?
    | QWordPrefix_nf "sögnin" nhm? QWordVerbKey '?'?
    | QWordPrefix_nf "sagnorðið" nhm? QWordVerbKey '?'?

QWordVerbKey → so_nh

# 'Hvaða orð tengjast Wintris?'

QWordEntity →
    QWordEntityKey_nf
    | QWordPrefix_þgf QWordEntityKey_þgf '?'?
    | QWordPrefix_þgf "orðinu" QWordEntityKey_nf '?'?
    | QWordPrefix_þgf "nafninu" QWordEntityKey_nf '?'?
    | QWordPrefix_þgf "sérnafninu" QWordEntityKey_nf '?'?
    | QWordPrefix_þgf "heitinu" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "orðið" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "nafnið" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "sérnafnið" QWordEntityKey_nf '?'?
    | QWordPrefix_nf "heitið" QWordEntityKey_nf '?'?

QWordEntityKey/fall → Sérnafn/fall > Sérnafn

# Arbitrary search

# Try to recognize the search query first as a sentence,
# then as a noun phrase, and finally as an arbitrary sequence of tokens

QSearch →
    QSearchSentence
    | QSearchNl 
    | QSearchArbitrary

QSearchSentence →
    Málsgrein
    | SetningÁnF_et_p3/kyn Lokatákn? # 'Stefndi í átt til Bláfjalla'
    | SetningÁnF_ft_p3/kyn Lokatákn? # 'Aftengdu fjarstýringu'

$score(+4) QSearchSentence

QSearchNl →
    Nl_nf Atviksliður? Lokatákn? # 'Tobías í turninum'

QSearchArbitrary →
    QSearchToken+ Lokatákn?

$score(-100) QSearchArbitrary

QSearchToken →
    person/fall/kyn > fyrirtæki > no/fall/tala/kyn
    > fn/fall/tala/kyn > pfn/fall/tala/kyn > entity > lo > so
    > eo > ao > fs/fall > dags > tími > raðnr > to > töl > ártal > tala > sérnafn
