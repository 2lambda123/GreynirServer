# Grammar for the numbers utility module

# TODO: Decide what to do with "tólf hundrað og sjö". 
# It's a common error, i.e. should be "tólf hundruð og sjö",
# but accepting it interferes with catching "fimm, tólf, hundrað og sjö, áttatíu", 
# i.e. a list of numbers where "hundrað" is preceded by a number in the teens.
# TODO: 

UBrotaTala → UTöluðTala/fall/kyn "komma" UTöluðTala/fall/kyn+ | UTöluðTala/fall/kyn "," UTöluðTala/fall/kyn+

UHeilTala → tala | UHeilTala/fall/kyn

/grno = gr nogr

UHeilTala/fall/kyn →
    UTöluðTala/fall/kyn
    # Standalone forms (definite) that
    # don't work inside a larger spoken number,
    # e.g. hundraðið, hundraðinu, hundruðunum
    | UTöluðTalaHundrað_gr/fall/tala
    | UTöluðTalaÞúsund_gr/fall/tala
    | UTöluðTalaMilljón_gr/fall/tala
    | UTöluðTalaMilljarður_gr/fall/tala
    | UTöluðTalaBilljón_gr/fall/tala
    | UTöluðTalaBilljarður_gr/fall/tala
    | UTöluðTalaTrilljón_gr/fall/tala
    | UTöluðTalaTrilljarður_gr/fall/tala
    | UTöluðTalaKvaðrilljón_gr/fall/tala
    | UTöluðTalaKvaðrilljarður_gr/fall/tala
    | UTöluðTalaKvintilljón_gr/fall/tala
    | UTöluðTalaSextilljón_gr/fall/tala
    | UTöluðTalaSeptilljón_gr/fall/tala
    | UTöluðTalaOktilljón_gr/fall/tala
    # Zero
    | UTöluðTala0/fall/tala

# The reason we start so large (oktilljón = 10^48 = (10^6)^8) is so that a large number
# doesn't get interpreted as multiple discrete numbers side by side.
# E.g. we want 'tvö hundruð þúsund' to be one number,
# but it would be split into 'tvö' 'hundruð' 'þúsund'
# if we had shortcuts in the grammar and multiple TöluðTala
# nonterminals were used side by side in a grammar.
# This allows us (theoretically) to use constructions like: TöluðTala+
# to catch a list of numbers, like when people read telephone numbers

# Single cardinal number
UTöluðTala/fall/kyn →
    # Something smaller than an oktilljón (quindecillion in english), probably most numbers
    UTöluðTalaUndirOktilljón/fall/kyn
    # "x oktilljónir ..."
    | UTöluðTalaOktilljónir_nf UTöluðTalaUndirOktilljón/fall/kyn?
    # "x oktilljónir og ..."
    | UTöluðTalaOktilljónir_nf UTöluðTalaOgUndirOktilljón/fall/kyn

# OKTILLJÓNIR

UTöluðTalaOktilljónir/fall →
    UTöluðTalaUndirOktilljón_kvk/fall? UTöluðTalaOktilljón/fall/tala

# SEPTILLJÓNIR

UTöluðTalaUndirOktilljón/kyn/fall → UTöluðTalaUndirOktilljón/fall/kyn 
UTöluðTalaUndirOktilljón/fall/kyn →
    UTöluðTalaSeptilljónir_nf UTöluðTalaUndirSeptilljón/fall/kyn?
    | UTöluðTalaSeptilljónir_nf UTöluðTalaOgUndirSeptilljón/fall/kyn
    | UTöluðTalaUndirSeptilljón/fall/kyn

UTöluðTalaOgUndirOktilljón/fall/kyn →
    "og" UTöluðTalaSeptilljónir/fall
    | UTöluðTalaOgUndirSeptilljón/fall/kyn

UTöluðTalaSeptilljónir/fall →
    UTöluðTalaUndirSeptilljón_kvk/fall? UTöluðTalaSeptilljón/fall/tala

# SEXTILLJÓNIR

UTöluðTalaUndirSeptilljón/kyn/fall → UTöluðTalaUndirSeptilljón/fall/kyn 
UTöluðTalaUndirSeptilljón/fall/kyn →
    UTöluðTalaSextilljónir_nf UTöluðTalaUndirSextilljón/fall/kyn?
    | UTöluðTalaSextilljónir_nf UTöluðTalaOgUndirSextilljón/fall/kyn
    | UTöluðTalaUndirSextilljón/fall/kyn

UTöluðTalaOgUndirSeptilljón/fall/kyn →
    "og" UTöluðTalaSextilljónir/fall
    | UTöluðTalaOgUndirSextilljón/fall/kyn

UTöluðTalaSextilljónir/fall →
    UTöluðTalaUndirSextilljón_kvk/fall? UTöluðTalaSextilljón/fall/tala

# KVINTILLJÓNIR

UTöluðTalaUndirSextilljón/kyn/fall → UTöluðTalaUndirSextilljón/fall/kyn 
UTöluðTalaUndirSextilljón/fall/kyn →
    UTöluðTalaKvintilljónir_nf UTöluðTalaUndirKvintilljón/fall/kyn?
    | UTöluðTalaKvintilljónir_nf UTöluðTalaOgUndirKvintilljón/fall/kyn
    | UTöluðTalaUndirKvintilljón/fall/kyn

UTöluðTalaOgUndirSextilljón/fall/kyn →
    "og" UTöluðTalaKvintilljónir/fall
    | UTöluðTalaOgUndirKvintilljón/fall/kyn

UTöluðTalaKvintilljónir/fall →
    UTöluðTalaUndirKvintilljón_kvk/fall? UTöluðTalaKvintilljón/fall/tala

# KVAÐRILLJARÐAR

UTöluðTalaUndirKvintilljón/kyn/fall → UTöluðTalaUndirKvintilljón/fall/kyn 
UTöluðTalaUndirKvintilljón/fall/kyn →
    UTöluðTalaKvaðrilljarðar_nf UTöluðTalaUndirKvaðrilljarði/fall/kyn?
    | UTöluðTalaKvaðrilljarðar_nf UTöluðTalaOgUndirKvaðrilljarði/fall/kyn
    | UTöluðTalaUndirKvaðrilljarði/fall/kyn

UTöluðTalaOgUndirKvintilljón/fall/kyn →
    "og" UTöluðTalaKvaðrilljarðar/fall
    | UTöluðTalaOgUndirKvaðrilljarði/fall/kyn

UTöluðTalaKvaðrilljarðar/fall →
    UTöluðTalaUndirKvaðrilljarði_kk/fall? UTöluðTalaKvaðrilljarður/fall/tala

# KVAÐRILLJÓNIR

UTöluðTalaUndirKvaðrilljarði/kyn/fall → UTöluðTalaUndirKvaðrilljarði/fall/kyn 
UTöluðTalaUndirKvaðrilljarði/fall/kyn →
    UTöluðTalaKvaðrilljónir_nf UTöluðTalaUndirKvaðrilljón/fall/kyn?
    | UTöluðTalaKvaðrilljónir_nf UTöluðTalaOgUndirKvaðrilljón/fall/kyn
    | UTöluðTalaUndirKvaðrilljón/fall/kyn

UTöluðTalaOgUndirKvaðrilljarði/fall/kyn →
    "og" UTöluðTalaKvaðrilljónir/fall
    | UTöluðTalaOgUndirKvaðrilljón/fall/kyn

UTöluðTalaKvaðrilljónir/fall →
    UTöluðTalaUndirKvaðrilljón_kvk/fall? UTöluðTalaKvaðrilljón/fall/tala

# TRILLJARÐAR

UTöluðTalaUndirKvaðrilljón/kyn/fall → UTöluðTalaUndirKvaðrilljón/fall/kyn 
UTöluðTalaUndirKvaðrilljón/fall/kyn →
    UTöluðTalaTrilljarðar_nf UTöluðTalaUndirTrilljarði/fall/kyn?
    | UTöluðTalaTrilljarðar_nf UTöluðTalaOgUndirTrilljarði/fall/kyn
    | UTöluðTalaUndirTrilljarði/fall/kyn

UTöluðTalaOgUndirKvaðrilljón/fall/kyn →
    "og" UTöluðTalaTrilljarðar/fall
    | UTöluðTalaOgUndirTrilljarði/fall/kyn

UTöluðTalaTrilljarðar/fall →
    UTöluðTalaUndirTrilljarði_kk/fall? UTöluðTalaTrilljarður/fall/tala

# TRILLJÓNIR

UTöluðTalaUndirTrilljarði/kyn/fall → UTöluðTalaUndirTrilljarði/fall/kyn 
UTöluðTalaUndirTrilljarði/fall/kyn →
    UTöluðTalaTrilljónir_nf UTöluðTalaUndirTrilljón/fall/kyn?
    | UTöluðTalaTrilljónir_nf UTöluðTalaOgUndirTrilljón/fall/kyn
    | UTöluðTalaUndirTrilljón/fall/kyn

UTöluðTalaOgUndirTrilljarði/fall/kyn →
    "og" UTöluðTalaTrilljónir/fall
    | UTöluðTalaOgUndirTrilljón/fall/kyn

UTöluðTalaTrilljónir/fall →
    UTöluðTalaUndirTrilljón_kvk/fall? UTöluðTalaTrilljón/fall/tala
    | UTöluðTalaUndirMilljarði_kk/fall? UTöluðTalaMilljarður/fall/tala UTöluðTalaMilljarður/fall/tala

# BILLJARÐAR

UTöluðTalaUndirTrilljón/kyn/fall → UTöluðTalaUndirTrilljón/fall/kyn 
UTöluðTalaUndirTrilljón/fall/kyn →
    UTöluðTalaBilljarðar_nf UTöluðTalaUndirBilljarði/fall/kyn?
    | UTöluðTalaBilljarðar_nf UTöluðTalaOgUndirBilljarði/fall/kyn
    | UTöluðTalaUndirBilljarði/fall/kyn

UTöluðTalaOgUndirTrilljón/fall/kyn →
    "og" UTöluðTalaBilljarðar/fall
    | UTöluðTalaOgUndirBilljarði/fall/kyn

UTöluðTalaBilljarðar/fall →
    UTöluðTalaUndirBilljarði_kk/fall? UTöluðTalaBilljarður/fall/tala
    | UTöluðTalaUndirMilljón_kvk/fall? UTöluðTalaMilljón/fall/tala UTöluðTalaMilljarður/fall/tala

# BILLJÓNIR

UTöluðTalaUndirBilljarði/kyn/fall → UTöluðTalaUndirBilljarði/fall/kyn 
UTöluðTalaUndirBilljarði/fall/kyn →
    UTöluðTalaBilljónir_nf UTöluðTalaUndirBilljón/fall/kyn?
    | UTöluðTalaBilljónir_nf UTöluðTalaOgUndirBilljón/fall/kyn
    | UTöluðTalaUndirBilljón/fall/kyn

UTöluðTalaOgUndirBilljarði/fall/kyn →
    "og" UTöluðTalaBilljónir/fall
    | UTöluðTalaOgUndirBilljón/fall/kyn

UTöluðTalaBilljónir/fall →
    UTöluðTalaUndirBilljón_kvk/fall? UTöluðTalaBilljón/fall/tala
    | UTöluðTalaUndirMilljón_kvk/fall? UTöluðTalaMilljón/fall/tala UTöluðTalaMilljón/fall/tala

# MILLJARÐAR

UTöluðTalaUndirBilljón/kyn/fall → UTöluðTalaUndirBilljón/fall/kyn 
UTöluðTalaUndirBilljón/fall/kyn →
    UTöluðTalaMilljarðar_nf UTöluðTalaUndirMilljarði/fall/kyn?
    | UTöluðTalaMilljarðar_nf UTöluðTalaOgUndirMilljarði/fall/kyn
    | UTöluðTalaUndirMilljarði/fall/kyn

UTöluðTalaOgUndirBilljón/fall/kyn →
    "og" UTöluðTalaMilljarðar/fall
    | UTöluðTalaOgUndirMilljarði/fall/kyn

UTöluðTalaMilljarðar/fall →
    UTöluðTalaUndirMilljarði_kk/fall? UTöluðTalaMilljarður/fall/tala

# MILLJÓNIR

UTöluðTalaUndirMilljarði/kyn/fall → UTöluðTalaUndirMilljarði/fall/kyn 
UTöluðTalaUndirMilljarði/fall/kyn →
    UTöluðTalaMilljónir_nf UTöluðTalaUndirMilljón/fall/kyn?
    | UTöluðTalaMilljónir_nf UTöluðTalaOgUndirMilljón/fall/kyn
    | UTöluðTalaUndirMilljón/fall/kyn

UTöluðTalaOgUndirMilljarði/fall/kyn →
    "og" UTöluðTalaMilljónir/fall
    | UTöluðTalaOgUndirMilljón/fall/kyn

UTöluðTalaMilljónir/fall →
    UTöluðTalaUndirMilljón_kvk/fall? UTöluðTalaMilljón/fall/tala

# ÞÚSUND

UTöluðTalaUndirMilljón/kyn/fall → UTöluðTalaUndirMilljón/fall/kyn
UTöluðTalaUndirMilljón/fall/kyn →
    UTöluðTalaÞúsundir_nf UTöluðTalaUndirÞúsund/fall/kyn?
    | UTöluðTalaÞúsundir_nf UTöluðTalaOgUndirÞúsund/fall/kyn
    | UTöluðTala10Til19Hundruð_nf/kyn UTöluðTalaUndirHundrað/fall/kyn?
    | UTöluðTala10Til19Hundruð_nf/kyn UTöluðTalaOgUndirHundrað/fall/kyn
    | UTöluðTalaUndirÞúsund/fall/kyn

UTöluðTalaOgUndirMilljón/fall/kyn →
    "og" UTöluðTalaÞúsundir/fall
    | UTöluðTalaOgUndirÞúsund/fall/kyn

UTöluðTalaÞúsundir/fall →
    UTöluðTalaUndirÞúsund_hk/fall? UTöluðTalaÞúsundHk/fall/tala
    | UTöluðTalaUndirÞúsund_kvk/fall? UTöluðTalaÞúsundKvk/fall/tala

UTöluðTala10Til19Hundruð/fall/kyn →
    UTöluðTala10Til19/fall/kyn UTöluðTalaHundrað_ft/fall
    # | UTöluðTala10Til19/fall/kyn UTöluðTalaHundrað_et/fall

# HUNDRUÐ

UTöluðTalaUndirÞúsund/kyn/fall → UTöluðTalaUndirÞúsund/fall/kyn
UTöluðTalaUndirÞúsund/fall/kyn →
    UTöluðTalaHundruð_nf UTöluðTalaTugurOgEining/fall/kyn
    | UTöluðTalaHundruð_nf UTöluðTalaOgUndirHundrað/fall/kyn
    | UTöluðTalaHundruð/fall
    | UTöluðTalaUndirHundrað/fall/kyn

UTöluðTalaOgUndirÞúsund/fall/kyn →
    "og" UTöluðTalaHundruð/fall
    | UTöluðTalaOgUndirHundrað/fall/kyn

UTöluðTalaHundruð/fall →
    UTöluðTala2Til9_hk_nf UTöluðTalaHundrað_ft/fall
    | UTöluðTala1_hk_et_nf UTöluðTalaHundrað_et/fall
    | UTöluðTalaHundrað_et/fall

# UNDIR HUNDRAÐ

UTöluðTalaUndirHundrað/fall/kyn →
    UTöluðTalaTugurOgEining/fall/kyn
    | UTöluðTalaTugir/fall/kyn
    | UTöluðTala10Til19/fall/kyn
    | UTöluðTala2Til9/fall/kyn
    | UTöluðTala1/fall/kyn/tala

UTöluðTalaOgUndirHundrað/fall/kyn →
    "og" UTöluðTalaTugir/fall/kyn
    | "og" UTöluðTala10Til19/fall/kyn
    | "og" UTöluðTala2Til9/fall/kyn
    | "og" UTöluðTala1/fall/kyn/tala

UTöluðTalaTugurOgEining/fall/kyn →
    UTöluðTalaTugir/fall/kyn "og" UTöluðTala2Til9/fall/kyn
    | UTöluðTalaTugir/fall/kyn "og" UTöluðTala1/fall/kyn/tala

###

UTöluðTala0/fall/tala → 'núll:hk'/fall/tala | 'núll:to'

UTöluðTala1/kyn/tala/fall → UTöluðTala1/fall/kyn/tala
UTöluðTala1/fall/kyn/tala → 'einn:to'/fall/kyn/tala

UTöluðTala2Til9/kyn/fall → UTöluðTala2Til9/fall/kyn
UTöluðTala2Til9/fall/kyn →
    'tveir:to'_ft/fall/kyn
    | 'þrír:to'_ft/fall/kyn
    | 'fjórir:to'_ft/fall/kyn
    | "fimm"
    | "sex"
    | "sjö"
    | "átta"
    | "níu"

UTöluðTala10Til19/fall/kyn →
    "tíu"
    | "ellefu"
    | "tólf"
    | "þrettán"
    | "fjórtán"
    | "fimmtán"
    | "sextán"
    | "sautján"
    | "átján"
    | "nítján"

UTöluðTalaTugir/fall/kyn →
    "tuttugu"
    | "þrjátíu"
    | "fjörutíu"
    | "fimmtíu"
    | "sextíu"
    | "sjötíu"
    | "áttatíu"
    | "níutíu"

UTöluðTalaHundrað/tala/fall → UTöluðTalaHundrað/fall/tala
UTöluðTalaHundrað/fall/tala → UTöluðTalaHundrað_nogr/fall/tala
UTöluðTalaHundrað/grno/fall/tala → 'hundrað:hk'/grno/fall/tala
UTöluðTalaHundrað_nogr/fall/tala → UTöluðTalaHundrað_nogr/tala/fall
UTöluðTalaHundrað_nogr_et/fall → "hundrað"
UTöluðTalaHundrað_nogr_ft/fall → "hundruð"

# Þúsund is both hk and kvk
UTöluðTalaÞúsund/fall/tala →
    UTöluðTalaÞúsund_nogr/fall/tala
    | 'þúsund:to'
UTöluðTalaÞúsund/grno/fall/tala →
    UTöluðTalaÞúsundHk/grno/fall/tala
    | UTöluðTalaÞúsundKvk/grno/fall/tala
UTöluðTalaÞúsundHk/fall/tala → UTöluðTalaÞúsundHk_nogr/fall/tala
UTöluðTalaÞúsundKvk/fall/tala → UTöluðTalaÞúsundKvk_nogr/fall/tala
UTöluðTalaÞúsundHk/grno/fall/tala → 'þúsund:hk'/grno/fall/tala
UTöluðTalaÞúsundKvk/grno/fall/tala → 'þúsund:kvk'_ft/grno/fall

UTöluðTalaMilljón/fall/tala → UTöluðTalaMilljón_nogr/fall/tala | 'milljón:to'
UTöluðTalaMilljón/grno/fall/tala → 'milljón:kvk'/grno/fall/tala

# The kk gender words don't have a töl form in BÍN
UTöluðTalaMilljarður/fall/tala → UTöluðTalaMilljarður_nogr/fall/tala
UTöluðTalaMilljarður/grno/fall/tala → 'milljarður:kk'/grno/fall/tala | 'miljarður:kk'/grno/fall/tala

UTöluðTalaBilljón/fall/tala → UTöluðTalaBilljón_nogr/fall/tala | 'billjón:to'
UTöluðTalaBilljón/grno/fall/tala → 'billjón:kvk'/grno/fall/tala

UTöluðTalaBilljarður/fall/tala → UTöluðTalaBilljarður_nogr/fall/tala
UTöluðTalaBilljarður/grno/fall/tala → 'billjarður:kk'/grno/fall/tala

UTöluðTalaTrilljón/fall/tala → UTöluðTalaTrilljón_nogr/fall/tala | 'trilljón:to'
UTöluðTalaTrilljón/grno/fall/tala → 'trilljón:kvk'/grno/fall/tala

UTöluðTalaTrilljarður/fall/tala → UTöluðTalaTrilljarður_nogr/fall/tala
UTöluðTalaTrilljarður/grno/fall/tala → 'trilljarður:kk'/grno/fall/tala

UTöluðTalaKvaðrilljón/fall/tala → UTöluðTalaKvaðrilljón_nogr/fall/tala | 'kvaðrilljón:to'
UTöluðTalaKvaðrilljón/grno/fall/tala → 'kvaðrilljón:kvk'/grno/fall/tala

UTöluðTalaKvaðrilljarður/fall/tala → UTöluðTalaKvaðrilljarður_nogr/fall/tala
UTöluðTalaKvaðrilljarður/grno/fall/tala → 'kvaðrilljarður:kk'/grno/fall/tala

UTöluðTalaKvintilljón/fall/tala → UTöluðTalaKvintilljón_nogr/fall/tala | 'kvintilljón:to'
UTöluðTalaKvintilljón/grno/fall/tala → 'kvintilljón:kvk'/grno/fall/tala

UTöluðTalaSextilljón/fall/tala → UTöluðTalaSextilljón_nogr/fall/tala | 'sextilljón:to'
UTöluðTalaSextilljón/grno/fall/tala → 'sextilljón:kvk'/grno/fall/tala

UTöluðTalaSeptilljón/fall/tala → UTöluðTalaSeptilljón_nogr/fall/tala | 'septilljón:to'
UTöluðTalaSeptilljón/grno/fall/tala → 'septilljón:kvk'/grno/fall/tala

UTöluðTalaOktilljón/fall/tala → UTöluðTalaOktilljón_nogr/fall/tala | 'oktilljón:to'
UTöluðTalaOktilljón/grno/fall/tala → 'oktilljón:kvk'/grno/fall/tala
