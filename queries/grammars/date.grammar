Query →
    QDate

QDate →
    QDateQuery '?'?

QDateQuery →
    QDateCurrent
    | QDateNextDay
    | QDatePrevDay
    | QDateHowLongUntil
    # | QDateHowLongSince  # Disabled for now.
    | QDateWhenIs
    | QDateWhichYear
    | QDateLeapYear

QDateCurrent →
    "dagsetning" QDateNow?
    | "dagsetningin" QDateNow?
    | "hvað" "er" "dagsetningin" QDateNow?
    | "hver" "er" "dagsetningin" QDateNow?
    | "hvaða" "dagsetning" "er" QDateNow?
    | "hvaða" "dagur" "er" QDateNow?
    | "hvaða" "dagur" "er" "dagurinn" "í" "dag"
    | "hvaða" "mánaðardagur" "er" QDateNow?
    | "hvaða" "vikudagur" "er" QDateNow?
    | "hvaða" "mánuður" "er" QDateNow?
    | "hver" "er" "dagurinn" QDateNow?
    | "hver" "er" "mánaðardagurinn" QDateNow?
    | "hver" "er" "vikudagurinn" QDateNow?
    | "dagurinn" QDateNow

QDateNextDay →
    "dagsetning" QDateTomorrow
    | "dagsetningin" QDateTomorrow
    | "hvað" "er" "dagsetningin" QDateTomorrow
    | "hver" "er" "dagsetningin" QDateTomorrow
    | "hvaða" "dagsetning" "er" QDateTomorrow
    | "hvaða" "dagur" "er" QDateTomorrow
    | "hvaða" "dagur" "kemur" QDateTomorrow
    | "hvaða" "mánaðardagur" "er" QDateTomorrow
    | "hvaða" "vikudagur" "er" QDateTomorrow
    | "hvaða" "vikudagur" "kemur" QDateTomorrow
    | "hvaða" "mánuður" "er" QDateTomorrow
    | "hvaða" "mánuður" "kemur" QDateTomorrow
    | "hver" "er" "dagurinn" QDateTomorrow
    | "hver" "er" "mánaðardagurinn" QDateTomorrow
    | "hver" "er" "vikudagurinn" QDateTomorrow
    | "hver" "er" "morgundagurinn"
    | "hvað" "er" "morgundagurinn"
    | "hvaða" "dagur" "er" "morgundagurinn"
    | "morgundagurinn"

QDateTomorrow →
    "á_morgun" | "í" "fyrramálið"

QDatePrevDay →
    "dagsetning" QDateYesterday
    | "dagsetningin" QDateYesterday
    | "hvað" "var" "dagsetningin" QDateYesterday
    | "hver" "var" "dagsetningin" QDateYesterday
    | "hvaða" "dagsetning" "var" QDateYesterday
    | "hvaða" "dagur" "var" QDateYesterday
    | "hvaða" "dagur" "kom" QDateYesterday
    | "hvaða" "mánaðardagur" "var" QDateYesterday
    | "hvaða" "vikudagur" "var" QDateYesterday
    | "hvaða" "vikudagur" "kom" QDateYesterday
    | "hvaða" "mánuður" "var" QDateYesterday
    | "hvaða" "mánuður" "kom" QDateYesterday
    | "hver" "var" "dagurinn" QDateYesterday
    | "hver" "var" "mánaðardagurinn" QDateYesterday
    | "hver" "var" "vikudagurinn" QDateYesterday
    | "hver" "var" "gærdagurinn"
    | "hvað" "er" "gærdagurinn"
    | "hvaða" "dagur" "var" "gærdagurinn"
    | "gærdagurinn"

QDateYesterday →
    "í_gær" | "í_gærkvöldi" | "í_gærmorgun"

QDateNow →
    "í" "dag" | "í" "augnablikinu" | "eins" "og" "stendur"
    | "nákvæmlega"? "núna" | "einmitt" "núna" | "akkúrat" "núna"

QDateHowLongUntil →
    "hvað" "er" "langt" "í" QDateItem_þf
    | "hvað" "er" "langt" "fram" "að" QDateItem_þgf
    | "hvað" "er" "langt" "til" QDateItem_ef
    | "hversu" "langt" "er" "í" QDateItem_þf
    | "hversu" "langt" "er" "til" QDateItem_ef
    | "hvað" "eru" "margir" "dagar" "í" QDateItem_þf
    | "hvað" "eru" "margir" "dagar" "til" QDateItem_ef
    # | "hvað" "eru" "margar" "vikur" "í" QDateItem_þf
    # | "hvað" "eru" "margir" "mánuðir" "í" QDateItem_þf

QDateHowLongSince →
    # "hvað" "er" "langt" "síðan" QDateItem
    "hvað" "er" "langt" "um"? "liðið" "frá" QDateItem_þgf
    | "hvað" "er" "langur" "tími" "liðinn" "frá" QDateItem_þgf
    | "hvað" "eru" "margir" "dagar" "liðnir" "frá" QDateItem_þgf
    | "hvað" "eru" "margir" "mánuðir" "liðnir" "frá" QDateItem_þgf
    | "hvað" "eru" "margar" "vikur" "liðnar" "frá" QDateItem_þgf

QDateIsAre → "er" | "eru"
QDateIsWas → "er" | "var"
QDateCome → "koma" | "kemur"

QDateWhenIs →
    "hvenær" QDateIsAre QDateSpecialDay_nf QDateThisYear?
    | "hvenær" QDateCome QDateSpecialDay_nf QDateThisYear?
    | "hvaða" "dagur" "er" QDateSpecialDay_nf QDateThisYear?
    | "á" "hvaða" "degi" QDateIsAre QDateSpecialDay_nf QDateThisYear?

QDateThisYear →
    "núna"? "í_ár" | "þetta" "ár" | "á" "þessu" "ári" | "þetta" "árið"

QDateWhichYear →
    "hvaða" "ár" "er" QDateNow?
    | "hvaða" "ár" "er" "í" "gangi" QDateNow?
    | "hvaða" "ár" "er" "að" "líða" QDateNow?

QDateLeapYear →
    QDateIsWas "hlaupár" QDateNow?
    | QDateIsWas Árið_nf "hlaupár"
    | QDateIsWas "hlaupár" Árið_nf

QDateItem/fall →
    QDateAbsOrRel | QDateSpecialDay/fall

QDateAbsOrRel →
    dagsföst | dagsafs

# TODO: Order this by time of year
QDateSpecialDay/fall →
    QDateHalloween/fall
    | QDateWhitsun/fall
    | QDateAscensionDay/fall
    | QDateAshDay/fall
    | QDateBunDay/fall
    | QDateSovereigntyDay/fall
    | QDateFirstDayOfSummer/fall
    | QDateThorlaksMass/fall
    | QDateChristmasEve/fall
    | QDateChristmasDay/fall
    | QDateNewYearsEve/fall
    | QDateNewYearsDay/fall
    | QDateNewYear/fall
    | QDateWorkersDay/fall
    | QDateEaster/fall
    | QDateEasterSunday/fall
    | QDateMaundyThursday/fall
    | QDateGoodFriday/fall
    | QDateNationalDay/fall
    | QDateBankHoliday/fall
    | QDateCultureNight/fall
    | QDateValentinesDay/fall
    | QDateMansDay/fall
    | QDateWomansDay/fall
    | QDateMardiGrasDay/fall
    | QDatePalmSunday/fall
    | QDateMothersDay/fall
    | QDateSeamensDay/fall
    | QDateFathersDay/fall
    | QDateIcelandicTongueDay/fall
    | QDateSecondChristmasDay/fall
    # | QDateFirstDayOfWinter/fall
    # | QDateSummerSolstice/fall
    # | QDateWinterSolstice/fall

QDateWhitsun/fall →
    'hvítasunnudagur:kk'_et/fall
    | 'hvítasunna:kvk'_et/fall
    | 'hvítasunnuhelgi:kvk'_et/fall

QDateAscensionDay/fall →
    'uppstigningardagur:kk'_et/fall

QDateAshDay/fall →
    'öskudagur:kk'_et/fall

QDateBunDay/fall →
    'bolludagur:kk'_et/fall

QDateHalloween/fall →
    'hrekkjavaka:kvk'_et/fall
    | "halloween"

QDateSovereigntyDay/fall →
    'fullveldisdagur:kk'_et/fall

QDateFirstDayOfSummer/fall →
    'sumardagur:kk'_et_gr/fall 'fyrstur:lo'_et_kk/fall
    | 'fyrstur:lo'_et_kk/fall 'sumardagur:kk'_et/fall

QDateThorlaksMass/fall →
    'Þorláksmessa:kvk'_et/fall

QDateChristmasEve/fall →
    'aðfangadagur:kk'_et/fall 'jól:hk'_ef?

QDateChristmasDay/fall →
    'jól:hk'/fall
    | 'jóladagur:kk'_et/fall

QDateNewYearsEve/fall →
    'gamlárskvöld:hk'_et/fall
    | 'gamlársdagur:kk'_et/fall

QDateNewYearsDay/fall →
    'nýársdagur:kk'_et/fall

QDateNewYear/fall →
    'áramót:hk'_ft/fall

QDateWorkersDay/fall →
    'baráttudagur:kk'_et/fall 'verkalýður:kk'_et_ef

QDateEaster/fall →
    'páskar:kk'/fall

QDateEasterSundayWord_nf -> "páskasunnudagur"
QDateEasterSundayWord_þf -> "páskasunnudag"
QDateEasterSundayWord_þgf -> "páskasunnudegi"
QDateEasterSundayWord_ef -> "páskasunnudags"

QDateEasterSunday/fall →
    'páskadagur:kk'_et/fall
    | QDateEasterSundayWord/fall

QDateMaundyThursday/fall →
    'skírdagur:kk'_et/fall

QDateGoodFriday/fall →
    'föstudagur:kk'_et_gr/fall 'langur:lo'_et_kk/fall

QDateNationalDay/fall →
    'þjóðhátíðardagur:kk'_et/fall
    | 'þjóðhátíðardagur:kk'_et/fall 'Íslendingur:kk'_ft_ef
    | 'þjóðhátíðardagur:kk'_et/fall 'Ísland:hk'_et_ef
    | 'þjóðhátíð:kvk'_et/fall 'Íslendingur:kk'_ft_ef
    | 'þjóðhátíð:kvk'_et/fall 'Ísland:hk'_et_ef

QDateBankHoliday/fall →
    'verslunarmannahelgi:kvk'_et/fall
    | 'frídagur:kk'_et/fall 'verslunarmaður:kk'_ft_ef

QDateCultureNight/fall →
    'menningarnótt:kvk'_et/fall

QDateValentinesDay/fall →
    'Valentínusardagur:kk'_et/fall
    | 'Dagur'/fall 'elskandi:kk'_ft_ef
    | 'dagur:kk'_et/fall 'elskandi:kk'_ft_ef

QDateMansDay/fall →
    'bóndadagur:kk'_et/fall

QDateWomansDay/fall →
    'konudagur:kk'_et/fall

QDateMardiGrasDay/fall →
    'sprengidagur:kk'_et/fall
    | 'sprengikvöld:hk'_et/fall

QDatePalmSunday/fall →
    'pálmasunnudagur:kk'_et/fall

QDateMothersDay/fall →
    'mæðradagur:kk'_et/fall

QDateSeamensDay/fall →
    'sjómannadagur:kk'_et/fall

QDateFirstDayOfWinter/fall →
    'fyrstur:lo'_et_kk/fall 'vetrardagur:kk'_et/fall
    | 'vetrardagur:kk'_et_gr/fall 'fyrstur:lo'_et_kk/fall

QDateFathersDay/fall →
    'feðra-dagur:kk'_et/fall  # Hack because 'feðradagur' isn't in BÍN
    | 'feðradagur:kk'_et/fall

QDateIcelandicTongueDay/fall →
    'dagur:kk'_et/fall "íslenskrar" "tungu"
    | 'dagur:kk'_et/fall "íslenskrar" 'Tunga'_ef_kvk
    | 'Dagur'/fall "íslenskrar" "tungu"
    | 'Dagur'/fall "íslenskrar" 'Tunga'_ef_kvk

QDateSecondChristmasDay/fall →
    'annar:lo'_et_kk/fall "í" "jólum"

QDateSummerSolstice/fall →
    'sumarsólstöður:kvk'_ft/fall

QDateWinterSolstice/fall →
    'vetrarsólstöður:kvk'_ft/fall

$score(+55) QDate
