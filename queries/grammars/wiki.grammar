Query →
    QWiki

QWiki →
    QWikiQuery '?'? | QWikiWhatIsQuery '?'?

QWikiQuery →
    # These take the subject in the nominative case
    QWikiSubjectNf "í" QWikipedia

    # These take the subject in the accusative case
    | "hvað" "segir" QWikipedia "um"? QWikiSubjectÞf
    | "hvað" "stendur" "í" QWikipedia "um" QWikiSubjectÞf
    | "hvað" "stendur" "á" QWikipedia "um" QWikiSubjectÞf
    | "hvað" "stendur" "um" QWikiSubjectÞf "í" QWikipedia
    | "hvað" "getur" "þú" "sagt" QWikiMeOrUsÞgf? "um" QWikiSubjectÞf
    | "hvað" "geturðu" "sagt" QWikiMeOrUsÞgf? "um" QWikiSubjectÞf
    | "hvað" "getur" QWikipedia "sagt" QWikiMeOrUsÞgf? "um" QWikiSubjectÞf
    | "hvaða" "upplýsingar" "ert" "þú" "með" "um" QWikiSubjectÞf
    | "hvaða" "upplýsingar" "ertu" "með" "um" QWikiSubjectÞf
    | "hvaða" "upplýsingar" "er" QWikipedia "með" "um" QWikiSubjectÞf
    | "hvaða" "upplýsingum" "býr" QWikipedia "yfir" "varðandi" QWikiSubjectÞf
    | "hvaða" "upplýsingum" "býrðu" "yfir" "varðandi" QWikiSubjectÞf
    | "hvað" "myndi" QWikipedia "segja" QWikiMeOrUsÞgf? "um" QWikiSubjectÞf
    | "fræddu" QWikiMeOrUsÞf "um" QWikiSubjectÞf
    | "geturðu" "frætt" QWikiMeOrUsÞf "um" QWikiSubjectÞf
    | "nennirðu" "að" "fræða" QWikiMeOrUsÞf "um" QWikiSubjectÞf

    # These take the subject in the dative case
    | "segðu" QWikiMeOrUsÞgf "frá" QWikiSubjectÞgf
    | "segðu" QWikiMeOrUsÞgf "eitthvað" "um" QWikiSubjectÞf
    | "flettu" "upp" QWikiSubjectÞgf "í" QWikipedia
    | "geturðu" "flett" "upp" QWikiSubjectÞgf "í" QWikipedia
    | "nennirðu" "að" "fletta" "upp" QWikiSubjectÞgf "í" QWikipedia
    | "gætirðu" "flett" "upp" QWikiSubjectÞgf "í" QWikipedia

QWikiWhatIsQuery →
    "hvað" "er" QWikiSubjectNlNf |
    "hvað" "eru" QWikiSubjectNlNf

QWikiMeOrUsÞgf →
    "mér" | "okkur"

QWikiMeOrUsÞf →
    "mig" | "okkur"

QWikiSubjectNf →
    QWikiPrevSubjectNf | QWikiSubjectNlNf

QWikiSubjectNlNf →
    Nl_nf

QWikiSubjectÞf →
    QWikiPrevSubjectÞf | QWikiSubjectNlÞf

QWikiSubjectNlÞf →
    Nl_þf

QWikiSubjectÞgf →
    QWikiPrevSubjectÞgf | QWikiSubjectNlÞgf

QWikiSubjectNlÞgf →
    Nl_þgf

QWikiPrevSubjectNf →
    "hann" | "hún" | "það"

QWikiPrevSubjectÞf →
    "hann" | "hana" | "það"

QWikiPrevSubjectÞgf →
    "honum" | "henni" | "því"

QWikipedia →
    {wikipedia_variations}

$score(+35) QWikiPrevSubjectNf
$score(+35) QWikiPrevSubjectÞf
$score(+35) QWikiPrevSubjectÞf

$score(+35) QWikiQuery
