# A plug-in query grammar always starts with the following,
# adding one or more query productions to the Query nonterminal

Query →
    QUnitQuery

QUnitQuery →
    QUnitConversion '?'?

$score(+35) QUnitQuery

QUnitPrefix_nf →
    "hvað" "er" | "hvað" "eru"

QUnitPrefix_þgf →
    "hvað" "jafngildir" | "hvað" "jafngilda" | "hvað" "samsvarar" | "hvað" "samsvara"

QUnitMany/fall/kyn → 'margur:lo'_sb_ft/fall/kyn

QUnitConversion →
    # Hvað eru X kíló mörg pund [?]
    # Hvað eru Y pund mörg kíló [?]
    QUnitPrefix_nf QUnitFrom_nf QUnitMany_nf/kyn QUnitTo_nf/kyn
    # Hvað eru mörg pund í X kílóum [?]
    | QUnitPrefix_nf QUnitMany_nf/kyn QUnitTo_nf/kyn "í" QUnitFrom_þgf
    # Hversu mörg pund eru í X kílóum [?]
    | "hversu" QUnitMany_nf/kyn QUnitTo_nf/kyn "eru" "í" QUnitFrom_þgf
    # Hvað jafngilda X kíló mörgum pundum [?]
    | QUnitPrefix_þgf QUnitFrom_nf QUnitMany_þgf/kyn QUnitTo_þgf/kyn

QUnitFrom/fall →
    QUnitFrom/fall/kyn
    | QUnitFromPounds/fall

QUnitFrom/fall/kyn →
    QUnitNumber/kyn/fall? QUnit/kyn/fall

# Special case hack: '150 pund' is tokenized as an amount
# (Note that this will also allow '150 sterlingspund' but we let that slide ;-) )
QUnitFromPounds/fall →
    amount_gbp/fall

QUnitTo/fall/kyn →
    QUnit/kyn/fall

$tag(keep) QUnitTo/fall/kyn # Keep this from being optimized away

QUnitNumber/kyn/fall →
    # to is a declinable number word ('tveir/tvo/tveim/tveggja')
    # töl is an undeclinable number word ('sautján')
    # tala is a number ('17')
    to/kyn/fall | töl | tala
    | 'hálfur:lo'_sb/kyn/fall
    | 'Eina:kvk'

# Volume

QUnit_kk/fall →
    'lítri:kk'/fall
    | 'millilítri:kk'/fall
    | 'desilítri:kk'/fall
    | 'desílítri:kk'/fall
    | 'sentilítri:kk'/fall
    | 'sentílítri:kk'/fall
    | 'hektólítri:kk'/fall
    | 'rúmmetri:kk'/fall
    | 'rúmsentimetri:kk'/fall
    | 'rúmsentímetri:kk'/fall
    | 'bolli:kk'/fall
    | 'Bolli'/fall
    | 'pint:kk'/fall

QUnit_kvk/fall →
    'matskeið:kvk'/fall
    | 'teskeið:kvk'/fall
    | 'tunna:kvk'/fall
    | 'olíutunna:kvk'/fall
    | 'vökvaúnsa:kvk'/fall
    | "vökva" "únsa"
    | "vökva" "únsur"

QUnit_hk/fall →
    'gallon:hk'/fall
    | 'gallón:hk'/fall

# Weight

QUnit_kk/fall →
    'steinn:kk'/fall
    | 'Steinn'/fall
    | 'Steinar'/fall

QUnit_kvk/fall →
    'únsa:kvk'/fall
    | 'lest:kvk'/fall
    | 'smálest:kvk'/fall

QUnit_hk/fall →
    'kíló:hk'/fall
    | 'gramm:hk'/fall
    | 'kílógramm:hk'/fall
    | 'hektógramm:hk'/fall
    | 'tonn:hk'/fall
    | 'pund:hk'/fall
    | 'karat:hk'/fall
    | 'mörk:kvk'/fall
    | 'Mörk'/fall

# Distance

QUnit_kk/fall →
    'metri:kk'/fall
    | 'kílómetri:kk'/fall
    | 'sentimetri:kk'/fall
    | 'sentímetri:kk'/fall
    | 'millimetri:kk'/fall
    | 'yard:kk'/fall
    | 'jard:kk'/fall
    | 'þumlungur:kk'/fall
    | 'faðmur:kk'/fall

QUnit_kvk/fall →
    'míla:kvk'/fall
    | 'Míla'/fall
    | 'sjómíla:kvk'/fall
    | 'tomma:kvk'/fall

QUnit_hk/fall →
    'ljósár:hk'/fall
    | 'fet:hk'/fall

# Area

QUnit_kk/fall →
    'fermetri:kk'/fall
    | 'fersentímetri:kk'/fall
    | 'fersentimetri:kk'/fall
    | 'ferkílómetri:kk'/fall
    | 'hektari:kk'/fall
    | 'ferþumlungur:kk'/fall

QUnit_hk/fall →
    'ferfet:hk'/fall

QUnit_kvk/fall →
    'fermíla:kvk'/fall
    | 'fertomma:kvk'/fall
    | 'ekra:kvk'/fall

# Time

QUnit_kk/fall →
    'mánuður:kk'/fall
    | 'dagur:kk'/fall
    | 'Dagur'/fall
    | 'áratugur:kk'/fall
    | 'klukkutími:kk'/fall
    | 'hálftími:kk'/fall
    | 'stundarfjórðungur:kk'/fall

QUnit_hk/fall →
    'árþúsund:hk'/fall | "árþúsund" # Often incorrectly declined
    | 'ár:hk'/fall
    | 'sekúndubrot:hk'/fall
    | 'kortér:hk'/fall | "korter"

QUnit_kvk/fall →
    'nanósekúnda:kvk'/fall
    | 'míkrósekúnda:kvk'/fall
    | 'millisekúnda:kvk'/fall
    | 'sekúnda:kvk'/fall
    | 'mínúta:kvk'/fall
    | 'klukkustund:kvk'/fall
    | 'vika:kvk'/fall
    | 'öld:kvk'/fall
