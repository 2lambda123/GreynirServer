# Grammar for the ja query module

Query →
    QJaQuery '?'?

QJaQuery →
    QJaPhoneNumQuery

QJaPhoneNumQuery →
    QJaName4PhoneNumQuery | QJaPhoneNum4NameQuery

QJaPhoneNum4NameQuery →
    # "Hver er síminn hjá Jóni Jónssyni?"
    QJaWhatWhich "er" QJaTheNumber_nf "hjá" QJaSubject
    # "Hvaða síma er Jón Jónsson með?"
    | "hvaða" QJaTheNumber_þf "er" QJaSubject "með"
    # Flettu upp númerinu hjá Jóni Jónssyni?
    | "flettu" "upp" QJaTheNumber_þgf "hjá" QJaSubject
    # Hver er sími Jóns Jónssonar?
    # | QJaWhatWhich "er" QJaTheNumber_nf QJaSubject

QJaName4PhoneNumQuery →
    "hver" "er" "með" QJaTheNumber_þf QJaPhoneNum QJaInPhonebook?
    | "hverjir" "eru" "með" QJaTheNumber_þf QJaPhoneNum QJaInPhonebook?
    | "flettu" "upp" QJaTheNumber_þgf QJaPhoneNum QJaInPhonebook?
    | "flettu" "upp" QJaPhoneNum QJaInPhonebook
    | "hver" "er" "í" "síma" QJaPhoneNum

QJaThemÞgf →
    "hann" | "hana" | "þau" | "þá" | "þær" | "það" "númer"? | "þetta" "númer"?

QJaInPhonebook →
    "í" "símaskránni" | "á" "já" "punktur" "is"

QJaPhoneNum →
    Nl

QJaSubject →
    Nl

QJaTheNumber/fall →
    'númer:hk'/fall
    | 'símanúmer:hk'/fall
    | 'sími:kk'/fall

QJaWhatWhich →
    "hvert" | "hvað" | "hver"

$score(+35) QJaQuery

$tag(keep) QJaPhoneNum
$tag(keep) QJaSubject
