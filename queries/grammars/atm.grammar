# Grammar for the ATM query module

Query →
    QAtm

QAtm → QAtmQuery '?'?

QAtmQuery →
    QAtmClosest | QAtmClosestDeposit | QAtmClosestForeignExchange | QAtmFurtherInfo

QAtmClosest →
    QAtmHvarEr "næsti"? QAtmHradbankiIslandsbanka
    | "næsti" QAtmHradbankiIslandsbanka
    | "hvaða" QAtmHradbankiIslandsbanka "er" "í" "nágrenninu" 
    | QAtmHradbankiIslandsbanka "í" "nágrenninu"
    | QAtmHvarEr? QAtmHradbankiIslandsbanka "nálægt" "mér"
    | "hvar" "finn" "ég" "næsta"? QAtmHradbankaIslandsbanka
    | QAtmHvarGetEg "tekið" "út" QAtmMoney
    | QAtmHvarGetEg "fundið" "næsta"? QAtmHradbankaIslandsbanka

QAtmClosestDeposit →
    QAtmHvarGetEg "lagt" "inn" QAtmMoney
    | QAtmHvarGetEg "lagt" "inn" "á" "reikning"

QAtmClosestForeignExchange →
    QAtmHvarEr "næsti"? QAtmHradbankiIslandsbanka "sem" "leyfir" "kaup" "á" QAtmGjaldeyrir
    | QAtmHvarEr "næsti"? QAtmHradbankiIslandsbanka "þar" "sem" "ég" "get" QAtmKeyptFengidNalgast QAtmGjaldeyrir
    | QAtmHvarEr "næsti"? QAtmHradbankiIslandsbanka "sem" "selur" QAtmGjaldeyrir
    | "næsti"? QAtmHradbankiIslandsbanka "þar" "sem" "hægt" "er" "að" QAtmKaupaFaNalgastTakaUt QAtmGjaldeyrir
    | "næsti"? QAtmHradbankiIslandsbanka "með" QAtmGjaldeyrir

QAtmFurtherInfo →
    QAtmFurtherInfoDeposit
    | QAtmFurtherInfoWithdrawalLimit
    | QAtmFurtherInfoOpeningHours
    | QAtmFurtherInfoForeignExchange
#    | QAtmFurtherInfoIsItOpen

QAtmFurtherInfoDeposit →
    "er" "hægt" "að" "leggja" "inn" QAtmMoney "þar"
    | QAtmGetEg "lagt" "inn" QAtmMoney "þar"
    | QAtmGetEg "lagt" "inn" "á" "reikning" "þar"
    | "tekur" "hann" "við" "seðlum"

QAtmFurtherInfoWithdrawalLimit →
    "hver" "er" "hámarksúttektarheimildin" "þar"
    | "hver" "er" "hámarks"? "úttektarheimildin" "þar"
    | "hver" "er" "hámarksúttektin" "þar"
    | "hvað" QAtmGetEg "tekið" "út" "mikinn" "pening" "þar"
    | "hvað" QAtmGetEg "tekið" "mikið" "út" "þar"
    | "hvað" QAtmGetEg "tekið" "út" "mikið" "af" QAtmMoneyPlural "þar"

QAtmFurtherInfoOpeningHours →
    "hverjir" "eru" "opnunartímarnir" "þar"
    | "hvernig" "eru" "opnunartímarnir" "þar"
    | "er" "hann" "opinn" "allan" "daginn"
    | "hvað" "er" "hann" "opinn" "lengi"
    | "hvenær" "er" "hann" "opinn"
    | "hvenær" "er" "sá" "hraðbanki" "opinn"
    | "er" "hann" "opinn"
    | "er" "sá" QAtmHradbankiIslandsbanka "opinn"

QAtmFurtherInfoForeignExchange →
    QAtmGetEg QAtmKeyptFengidNalgast QAtmGjaldeyrir "þar"
    | "er" "hægt" "að" QAtmKaupaFaNalgastTakaUt QAtmGjaldeyrir "þar"

QAtmGetEg →
    "get" "ég"

QAtmHvarEr →
    "hvar" "er" 

QAtmHvarGetEg → 
    "hvar" "get" "ég"

QAtmHradbankiIslandsbanka →
    "hraðbanki" "íslandsbanka"?

QAtmHradbankaIslandsbanka →
    "hraðbanka" "íslandsbanka"

QAtmMoney →
    "pening"
    | "peninga"
    | "reiðufé"
    | "seðla"

QAtmMoneyPlural → 
    "peningum"
    | "reiðufé"
    | "seðlum"

QAtmKeyptFengidNalgast →
    "keypt"
    | "fengið"
    | "nálgast"
    | "tekið" "út"

QAtmKaupaFaNalgastTakaUt →
    "kaupa"
    | "fá"
    | "nálgast"
    | "taka" "út"

QAtmGjaldeyrir →
    "erlenda"? "gjaldmiðla"
    | "erlendan"? "gjaldmiðil"
    | "erlendan"? "gjaldeyri"


# Hvar er næsti hraðbanki þar sem ég get tekið út erlendan gjaldmiðil?
# Hvar er næsti hraðbanki þar sem ég get lagt inn peninga?
# Hvar er næsti hraðbanki þar sem ég get keypt erlendan gjaldeyri?
# Hvar get ég keypt erlendan gjaldeyri?
# Hvar er hraðbanki þar sem ég get ...

# Additional stuff:
# myntsöluvélar
# hvað með ef tveir hraðbankar á sama stað með mismunandi þjónustu?
# Hvar er hraðbanki sem er opinn?

# "dialogue" options
# -> er hann opinn?
# Hvar er næsti hraðbanki