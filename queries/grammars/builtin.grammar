# Grammar for the builtin query module

Query →
    BuiltinQueries

BuiltinQueries →
    QPerson > QCompany > QEntity > QTitle > QRepeat > QWord #> QSearch

QPerson →
    Mannsnafn_nf/nkyn
    | QPersonPrefix/fall QPersonKey/fall "?"?

QPersonKey/fall →
    Mannsnafn/fall/nkyn
    | QPersonPronoun/fall/kyn

QPersonKey/fall →
    > Sérnafn

QPersonPronoun/fall/kyn →
    Pfn_et/fall/kyn

$tag(keep) QPersonPronoun/fall/kyn

QPersonPrefix_nf →
    "hver" "er"
    | "hver" "var"
    | "hvað" "gerir"
    | "hvað" "gerði"
    | "hvað" "starfar"
    | "hvað" "starfaði"
    | "hvaða" "titil" "hefur"
    | "hvaða" "titil" "hafði"
    | "hvaða" "starfi" "gegnir"
    | "hvaða" "starfi" "gegndi"

QPersonPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QPersonPrefix_ef →
    "hver" "er" "titill"
    | "hver" "var" "titill"
    | "hver" "er" "starfstitill"
    | "hver" "var" "starfstitill"
    | "hvert" "er" "starf"
    | "hvert" "var" "starf"

QPersonPrefix_þgf →
    "segðu" "mér"? "frá"

QCompany →
    # Það þarf að gera ráð fyrir sérstökum punkti í
    # enda fyrirspurnarinnar þar sem punktur á eftir 'hf.'
    # eða 'ehf.' í enda setningar er skilinn frá
    # skammstöfunar-tókanum.
    QCompanyPrefix_nf Fyrirtæki_nf "."? "?"?
    | QCompanyPrefix_þf Fyrirtæki_þf "."? "?"?
    | QCompanyPrefix_þgf Fyrirtæki_þgf "."? # "?"?

QCompanyPrefix_nf →
    "hvað" "er"

QCompanyPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QCompanyPrefix_þgf →
    "segðu" "mér"? "frá"

# A question about the definition of a named entity

QEntity → QEntityPrefix/fall QEntityKey/fall "."? "?"?

QEntityKey/fall →
    Sérnafn/fall > Sérnafn > Nl/fall

QEntityPrefix_nf →
    "hvað" "er"
    | "hvað" "eru"
    # Allowing epsilon here makes this production catch too much

QEntityPrefix_þf →
    "hvað" "veistu" "um"
    | "hvað" "geturðu" "sagt" "mér"? "um"

QEntityPrefix_þgf →
    "segðu" "mér"? "frá"

QEntityPrefix_ef →
    "" # Never matches

QTitle →
    QTitlePrefix_nf QTitleKey_nf "?"?
    | QTitlePrefix_ef QTitleKey_ef "?"?

QSegðuMér →
    "segðu" "mér"
    | "mig" "langar" "að" "vita"
    | "ég" "vil" "gjarnan"? "vita"

QTitlePrefix_nf →
    QSegðuMér? QTitlePrefixFrh_nf

QTitlePrefixFrh_nf →
    "hver" "er"
    | "hver" "var"
    | "hver" "hefur" "verið"
    | "hvað" "heitir"

QTitlePrefix_ef →
    QSegðuMér? "hver" "gegnir" "starfi"

QTitleKey_nf →
    EinnTitill_nf OgTitill_nf*

QTitleKey_ef →
    EinnTitill_ef OgTitill_ef*

# Request to repeat the last answer

QRepeat →
    QRepeatQuery '?'?

QRepeatThis → "þetta" | "síðasta" "svar" | "svarið"

QRepeatQuery →
    QPlease? "endurtaktu" QRepeatThis?
    | QPlease? "segðu" "mér"? QRepeatThis? "aftur"
    | "geturðu" "endurtekið" QRepeatThis?
    | "geturðu" "sagt" QRepeatThis "aftur"
    | "gætirðu" "endurtekið" QRepeatThis?
    | "gætirðu" "sagt" QRepeatThis "aftur"
    | "hvað" "sagðir" "þú"
    | "hvað" "sagðirðu"

QPlease →
    "vinsamlega" | "vinsamlegast"

# Word relation query

QWord →
    QWordPerson
    > QWordEntity
    > QWordNoun
    > QWordVerb

QWordPrefix_þgf →
    "hvað" "tengist"
    | "hvað" "er" "tengt"
    | "hvaða" "orð" "tengjast"
    | "hvaða" "orð" "tengist"
    | "hvaða" "orð" "eru" "tengd"

QWordPrefix_nf →
    "hverju" "tengist"
    | "hvaða" "orðum" "tengist"

# 'Hvað tengist [orðinu/nafnorðinu] útihátíð?'

QWordNoun →
    QWordNoun_nf
    | QWordNoun_þgf

QWordNoun_þgf →
    QWordPrefix_þgf QWordNounKey_þgf "?"?

QWordNoun_nf →
    QWordNounKey_nf
    | QWordPrefix_þgf "orðinu" QWordNounKey_nf "?"?
    | QWordPrefix_þgf "nafnorðinu" QWordNounKey_nf "?"?
    | QWordPrefix_nf "orðið" QWordNounKey_nf "?"?
    | QWordPrefix_nf "nafnorðið" QWordNounKey_nf "?"?

QWordNounKey/fall → no/fall

# 'Hvaða orð tengjast Ragnheiði Ríkharðsdóttur?'
# 'Hvaða orð eru tengd nafninu Elliði Vignisson?'

QWordPerson →
    QWordPerson_nf
    | QWordPerson_þgf

QWordPerson_þgf →
    QWordPrefix_þgf QWordPersonKey_þgf "?"?

QWordPerson_nf →
    QWordPrefix_þgf "nafninu" QWordPersonKey_nf "?"?
    | QWordPrefix_nf "nafnið" QWordPersonKey_nf "?"?

QWordPersonKey/fall → person/fall

# 'Hvaða orð tengjast sögninni að teikna?'

QWordVerb →
    Nhm? QWordVerbKey
    | QWordPrefix_þgf "orðinu" QWordVerbKey "?"?
    | QWordPrefix_þgf "sögninni" Nhm? QWordVerbKey "?"?
    | QWordPrefix_þgf "sagnorðinu" Nhm? QWordVerbKey "?"?
    | QWordPrefix_nf "orðið" QWordVerbKey "?"?
    | QWordPrefix_nf "sögnin" Nhm? QWordVerbKey "?"?
    | QWordPrefix_nf "sagnorðið" Nhm? QWordVerbKey "?"?

QWordVerbKey → so_nh

# 'Hvaða orð tengjast Wintris?'

QWordEntity →
    QWordEntityKey_nf
    | QWordPrefix_þgf QWordEntityKey_þgf "?"?
    | QWordPrefix_þgf "orðinu" QWordEntityKey_nf "?"?
    | QWordPrefix_þgf "nafninu" QWordEntityKey_nf "?"?
    | QWordPrefix_þgf "sérnafninu" QWordEntityKey_nf "?"?
    | QWordPrefix_þgf "heitinu" QWordEntityKey_nf "?"?
    | QWordPrefix_nf "orðið" QWordEntityKey_nf "?"?
    | QWordPrefix_nf "nafnið" QWordEntityKey_nf "?"?
    | QWordPrefix_nf "sérnafnið" QWordEntityKey_nf "?"?
    | QWordPrefix_nf "heitið" QWordEntityKey_nf "?"?

QWordEntityKey/fall → Sérnafn/fall > Sérnafn

# Arbitrary search

# Try to recognize the search query first as a sentence,
# then as a noun phrase, and finally as an arbitrary sequence of tokens

QSearch →
    QSearchSentence
    | QSearchNl
    | QSearchArbitrary

# Prefer other parses to QSearch
$score(-999) QSearch

QSearchSentence →
    Málsgrein
    | SetningÁnF_et_p3/kyn Lokatákn? # 'Stefndi í átt til Bláfjalla'
    | SetningÁnF_ft_p3/kyn Lokatákn? # 'Aftengdu fjarstýringu'

$score(+4) QSearchSentence

QSearchNl →
    Nl_nf Atviksliður? Lokatákn? # 'Tobías í turninum'

QSearchArbitrary →
    QSearchToken+ Lokatákn?

$score(-100) QSearchArbitrary

QSearchToken →
    person/fall/kyn > fyrirtæki > no/fall/tala/kyn
    > fn/fall/tala/kyn > pfn/fall/tala/kyn > entity > lo > so
    > eo > ao > fs/fall
    > dags > dagsafs > dagsföst
    > tímapunkturafs > tímapunkturfast > tími
    > raðnr > to > töl > ártal > tala > sérnafn
