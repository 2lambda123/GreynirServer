# TODO: 2x of a topping. "Tvöfalt", "mikið", "extra"
# TODO: Ban more than two instances of a topping.
# TODO: Fix the toppings being a set. Doesn't handle "Ég vil skinku, ólífur og auka skinku."
# TODO: Add to PizzaRequestBare, start conversation with an order.
# TODO: Add the words for margherita to BinPackage

/þgf = þgf
/ef = ef

# Currently, the Hotword needs to be top-level, because the QPizza nonterminal is banned.
Query →
    QPizzaHotWord '?'?
    | QPizza '?'?

QPizza →
    QPizzaQuery

QPizzaQuery →
    QPizzaDialogue

# Hotwords are used to initialize the conversation.
QPizzaHotWord →
    QPizzaWord/nf # e.g. "Pítsa"
    | QPizzaRequestBare # e.g. "Ég vil panta pizzu."

# Doesn't allow for any order specification, e.g. the number of pizzas: "Ég vil panta tvær flatbökur."
QPizzaRequestBare ->
    QPizzaRequestPleasantries? QPizzaWord/þf

QPizzaDialogue →
    QPizzaNumberAnswer # Answer to the question "How many pizzas do you want?"
    | QPizzaMenuOrToppingsAnswer # Answer to the question "What do you want to order?"
    | QPizzaSizeAnswer # Answer to the question "What size do you want?"
    | QPizzaCrustAnswer # Answer to the question "What kind of crust do you want?"
    | QPizzaExtrasAnswer # Answer to the question "Do you want anything with your pizzas?"

QPizzaNumberAnswer →
    QPizzaRequestPleasantries? QPizzaNum/þf QPizzaWord/þf # e.g. "Ég vil kaupa tvær pítsur."
    | QPizzaNum/nf QPizzaWord/nf? # e.g. "Tvær pítsur."

QPizzaMenuOrToppingsAnswer ->
    QPizzaMenuAnswer # A request for a pizza off the menu.
    | QPizzaToppingsAnswer # A request for a custom pizza.

QPizzaMenuAnswer ->
    QPizzaRequestPleasantries? QPizzaMenuWords/þf # e.g. "Ég vil kjötveislu."
    | QPizzaMenuWords/nf # e.g. "Margaríta."

QPizzaToppingsAnswer ->
    QPizzaRequestPleasantries? QPizzaToppingsList/þf QPizzaAPitsunaPhrase? # e.g. "Ég vil skinku, ólífur og pepperóní á pítsuna."
    | QPizzaRequestPleasantries? QPizzaWord/þf "með" QPizzaToppingsList/þgf # e.g. "Ég vil pizzu með ólífum og ananas."

# It is common to say "miðstærð af pítsu", which is handled separately here.
QPizzaSizeAnswer ->
    QPizzaRequestPleasantries? QPizzaSize/þf QPizzaWord/þf? # e.g. "Ég vil stóra pítsu."
    | QPizzaSize/nf # e.g. "Tólf tommur."
    | QPizzaRequestPleasantries? QPizzaMediumWord/þf QPizzaAfPitsuPhrase? # e.g. "Ég vil miðstærð af pítsu."

QPizzaCrustAnswer ->
    QPizzaRequestPleasantries? QPizzaCrustPhrase/þf QPizzaAPitsunaPhrase? # e.g. "Ég vil klassískan botn."
    | QPizzaRequestPleasantries? QPizzaWord/þf "með" QPizzaCrustPhrase/þgf # e.g. "Ég vil pizzu með ítölskum botni."
    | QPizzaCrustPhrase/nf # e.g. "Ítalskur botn."

QPizzaExtrasAnswer ->
    QPizzaRequestPleasantries? QPizzaExtraWords/þf QPizzaMedPitsunni? # e.g. "Ég vil brauðstangir með pizzunum."
    | QPizzaEgVil "líka" QPizzaExtraWords/þf QPizzaMedPitsunni? # e.g. "Ég vil líka kanilgott með pizzunum."
    | QPizzaExtraWords/nf # e.g. "Kók."

QPizzaToppingsList/fall ->
    QPizzaToppingsWordWrapper/fall* 'og:st'? QPizzaToppingsWord/fall

QPizzaSize/fall ->
    QPizzaSizeLarge/fall
    | QPizzaSizeMedium/fall
    | QPizzaSizeSmall/fall

# This wrapper is necessary to prevent the module from reading toppings multiple times.
# Otherwise, it reads "ananas skinku og pepperóní" as "ananas" "skinka" "pepperóní" and "ananas skinka" (2022/07/20).
QPizzaToppingsWordWrapper/fall ->
    QPizzaToppingsWord/fall

# Toppings that are transcribed in different ways are in separate nonterminals for clarity.
# This also helps standardize the handling of each topping in the module, i.e. not reading "ólífa" and "ólíva" as separate toppings.
QPizzaToppingsWord/fall ->
    QPizzaMushroom/fall
    | QPizzaPepperoni/fall
    | 'ananas:kk'/fall
    | 'skinka:kvk'/fall
    | QPizzaOlive/fall

QPizzaMenuWords/fall ->
    'prinsessa:kvk'/fall
    | 'dóttir:kvk'/fall
    | QPizzaMargherita/fall
    | 'kjötveisla:kvk'/fall
    | 'hvítlauksbrauð:hk'/fall
    | QPizzaTokyo/fall

QPizzaExtraWords/fall ->
    QPizzaSidesWords/fall
    | QPizzaDrinksWords/fall
    | QPizzaDipsWords/fall

QPizzaSidesWords/fall ->
    QPizzaLargeBreadsticks/fall 
    | QPizzaSmallBreadsticks/fall
    | 'lítill:lo'_kvk_ft/fall 'brauðstöng:kvk'_ft/fall
    | "ostagott"
    | "kanilgott"
    | "súkkulaðigott"
    | 'kartöflubátur:kk'_ft/fall
    | 'vængur:kk'_ft/fall

QPizzaDrinksWords/fall ->
    QPizzaCoke/fall 
    | QPizzaCokeZero/fall
    | "fanta"
    | 'toppur:kk'_et/fall
    | 'sítrónutoppur:kk'_et/fall
    | 'appelsínusvali:kk'_et/fall
    | 'eplasvali:kk'_et/fall
    | "monster"

QPizzaDipsWords/fall ->
    'hvítlauksolía:kvk'_et/fall
    | 'hvítlaukssósa:kvk'_et/fall
    | 'brauðstangasósa:kvk'_et/fall
    | QPizzaBlueCheese/fall
    | 'sterkur:lo'_kvk_et/fall 'sósa:kvk'_et/fall
    | 'kokteilsósa:kvk'_et/fall
    | "súkkulaðiglassúr"
    | "glassúr"

QPizzaCrustPhrase/fall ->
    QPizzaCrustType/fall QPizzaCrustWord/fall?

QPizzaAfPitsuPhrase ->
    "af" QPizzaWord/þgf

QPizzaAPitsunaPhrase ->
    "á" QPizzaWord/þgf

QPizzaMedPitsunni ->
    "með" QPizzaWord/þgf

# A large pizza at Domino's is typically thought to be 16", some believe it to be 15".
# The actual size is 14.5".
QPizzaSizeLarge/fall ->
    'stór:lo'/fall
    | QPizzaSixteenWord 'tomma:kvk'/fall?
    | QPizzaFifteenWord 'tomma:kvk'/fall?
    | QPizzaFourteenPointFiveWord 'tomma:kvk'/fall?

QPizzaSizeMedium/fall ->
    'millistór:lo'/fall
    | 'meðalstór:lo'/fall
    | QPizzaTwelveWord 'tomma:kvk'/fall?

QPizzaMediumWord/fall ->
    'miðstærð:kvk'/fall

QPizzaSizeSmall/fall ->
    'lítil:lo'/fall
    | QPizzaNineWord 'tomma:kvk'/fall?

QPizzaCrustType/fall ->
    QPizzaItalianWord/fall 
    | QPizzaClassicWord/fall

QPizzaRequestPleasantries ->
    QPizzaEgVil QPizzaKaupaFaraFaPanta?

QPizzaEgVil →
    "ég"? "vil"
    | "ég" "vill"
    | "mig" "langar" "að"
    | "mig" "langar" "í"
    | "ég" "ætla" "að"

QPizzaKaupaFaraFaPanta →
    "kaupa" "mér"?
    | "fá" "mér"?
    | "panta" "mér"?

QPizzaWord/fall →
    'pizza:kvk'/fall
    | 'pitsa:kvk'/fall
    | 'pítsa:kvk'/fall
    | 'flatbaka:kvk'/fall

QPizzaSixteenWord ->
    "16"
    | "sextán"

QPizzaFifteenWord ->
    "15"
    | "fimmtán"

QPizzaFourteenPointFiveWord ->
    QPizzaFourteenWord "komma" QPizzaFiveWord

QPizzaFourteenWord ->
    "14"
    | "fjórtán"

QPizzaFiveWord ->
    "5"
    | "fimm"

QPizzaTwelveWord ->
    "12"
    | "tólf"

QPizzaNineWord ->
    "9"
    | "níu"

QPizzaItalianWord/fall ->
    'ítalskur:lo'/fall

QPizzaClassicWord/fall ->
    'klassískur:lo'/fall

QPizzaCrustWord/fall ->
    'botn:kk'/fall

QPizzaNum/fall →
    # to is a declinable number word ('tveir/tvo/tveim/tveggja')
    # töl is an undeclinable number word ('sautján')
    # tala is a number ('17')
    to | töl | tala

QPizzaPepperoni/fall ->
    'pepperóní:hk'/fall
    | "pepperoni"
    | "pepperóni"
    | "pepperoní"

QPizzaOlive/fall ->
    'ólífa:kvk'/fall
    | 'ólíva:kvk'/fall

QPizzaMushroom/fall ->
    'sveppur:kk'/fall
    | 'Sveppi:kk'

QPizzaMargherita_nf ->
    "margaríta"
    | "margarita"

QPizzaMargherita/fallxnf ->
    "margarítu"
    | "margaritu"

QPizzaTokyo/fall ->
    'Tókýó:kvk'/fall
    | "Tókíó"
    | "Tokyo"
    | "tókýó"
    | "tókíó"
    | "tokyo"

QPizzaLargeBreadsticks/fall ->
    'stór:lo'_kvk_ft/fall? 'brauðstöng:kvk'_ft/fall
    | 'brauðstöng:kvk'_ft/fall 'stór:lo'_kvk_ft/fall

QPizzaSmallBreadsticks/fall ->
    'lítill:lo'_kvk_ft/fall 'brauðstöng:kvk'_ft/fall
    | 'brauðstöng:kvk'_ft/fall 'lítill:lo'_kvk_ft/fall

QPizzaCoke/fall ->
    QPizzaCokeWord/fall

QPizzaCokeZero/fall ->
    QPizzaCokeWord/fall "zero"
    | QPizzaCokeWord/fall "án" 'sykur:kk'_et_ef

QPizzaBlueCheese/fall ->
    'gráðaostasósa:kvk'_et/fall
    | 'gráðostasósa:kvk'_et/fall
    | 'gráðaostur:kk'_et/fall
    | 'gráðostur:kk'_et/fall

QPizzaCokeWord/fall ->
    'kók:kvk'_et/fall
    | "kóka-kóla"
    | "coke"
    | "coca-cola"