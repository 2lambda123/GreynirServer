# TODO: 2x of a topping. "Tvöfalt", "mikið", "extra"
# TODO: Ban more than two instances of a topping.
# TODO: Fix the toppings being a set. Doesn't handle "Ég vil skinku, ólífur og auka skinku."
# TODO: Add to PizzaRequestBare, start conversation with an order.

/þgf = þgf
/ef = ef

# Currently, the Hotword needs to be top-level, because the QPizza nonterminal is banned.
Query →
    QPizzaHotWord '?'?
    | QPizza '?'?

QPizza →
    QPizzaQuery

QPizzaQuery →
    QPizzaDialogue

# Hotwords are used to initialize the conversation.
QPizzaHotWord →
    QPizzaWord/nf # e.g. "Pítsa"
    | QPizzaRequestBare # e.g. "Ég vil panta pizzu."

# Doesn't allow for any specification, i.e. number of pizzas: "Ég vil panta tvær flatbökur."
QPizzaRequestBare ->
    QPizzaEgVil QPizzaKaupaFaraFaPanta? QPizzaWord/þf

QPizzaDialogue →
    QPizzaNumberAnswer #Answer to the question "How many pizzas do you want?"
    | QPizzaToppingsAnswer #Answer to the question "What toppings do you want?"
    | QPizzaSizeAnswer #Answer to the question "What size do you want?"
    | QPizzaCrustAnswer #Answer to the question "What kind of crust do you want?"

QPizzaNumberAnswer →
    QPizzaEgVil QPizzaKaupaFaraFaPanta? QPizzaNum/þf QPizzaWord/þf # e.g. "Ég vil kaupa tvær pítsur."
    | QPizzaNum/þf QPizzaWord/þf? # e.g. "Tvær pítsur."

QPizzaToppingsAnswer ->
    QPizzaEgVil? QPizzaToppingsList/þf QPizzaAPitsunaPhrase? # e.g. "Ég vil skinku, ólífur og pepperóní á pítsuna."
    | QPizzaEgVil? QPizzaWord/þf "með" QPizzaToppingsList/þgf # e.g. "Ég vil pizzu með ólífum og ananas."

# It is common to say "miðstærð af pítsu", which is handled separately here.
QPizzaSizeAnswer ->
    QPizzaEgVil? QPizzaSize/þf QPizzaWord/þf? # e.g. "Ég vil stóra pítsu."
    | QPizzaSize/nf # e.g. "Tólf tommur."
    | QPizzaEgVil? QPizzaMediumWord QPizzaAfPitsuPhrase # e.g. "Ég vil miðstærð af pítsu."

QPizzaCrustAnswer ->
    QPizzaEgVil? QPizzaCrustPhrase/þf QPizzaAPitsunaPhrase? # e.g. "Ég vil klassískan botn."
    | QPizzaCrustPhrase/nf # e.g. "Ítalskur botn."

QPizzaToppingsList/fall ->
    QPizzaToppingsWordWrapper/fall* 'og:st'? QPizzaToppingsWord/fall

QPizzaSize/fall ->
    QPizzaSizeLarge/fall
    | QPizzaSizeMedium/fall
    | QPizzaSizeSmall/fall

# This wrapper is necessary to prevent the module from reading toppings multiple times.
# Otherwise, it reads "ananas skinku og pepperóní" as "ananas" "skinka" "pepperóní" and "ananas skinka" (2022/07/20).
QPizzaToppingsWordWrapper/fall ->
    QPizzaToppingsWord/fall

# Toppings that are transcribed in different ways are in separate nonterminals for clarity.
# This also helps standardize the handling of each topping in the module, i.e. not reading "ólífa" and "ólíva" as separate toppings.
QPizzaToppingsWord/fall ->
    QPizzaMushroomWord/fall
    | QPizzaPepperoniWord/fall
    | 'ananas:kk'/fall
    | 'skinka:kvk'/fall
    | QPizzaOliveWord/fall

QPizzaCrustPhrase/fall ->
    QPizzaCrustType/fall QPizzaCrustWord/fall?

QPizzaAfPitsuPhrase ->
    "af" QPizzaWord/þgf

QPizzaAPitsunaPhrase ->
    "á" QPizzaWord/þgf

# A large pizza at Domino's is typically thought to be 16", some believe it to be 15".
# The actual size is 14.5".
QPizzaSizeLarge/fall ->
    'stór:lo'/fall
    | QPizzaSixteenWord 'tomma:kvk'/fall?
    | QPizzaFifteenWord 'tomma:kvk'/fall?
    | QPizzaFourteenPointFiveWord 'tomma:kvk'/fall?

QPizzaSizeMedium/fall ->
    'millistór:lo'/fall
    | 'meðalstór:lo'/fall
    | QPizzaTwelveWord 'tomma:kvk'/fall?

QPizzaMediumWord ->
    "miðstærð"

QPizzaSizeSmall/fall ->
    'lítil:lo'/fall
    | QPizzaNineWord 'tomma:kvk'/fall?

QPizzaCrustType/fall ->
    QPizzaItalianWord/fall 
    | QPizzaClassicWord/fall

QPizzaEgVil →
    "ég"? "vil"
    | "ég" "vill"
    | "mig" "langar" "að"
    | "mig" "langar" "í"

QPizzaKaupaFaraFaPanta →
    "kaupa" "mér"?
    | "fá" "mér"?
    | "panta" "mér"?

QPizzaWord/fall →
    'pizza:kvk'/fall
    | 'pitsa:kvk'/fall
    | 'pítsa:kvk'/fall
    | 'flatbaka:kvk'/fall

QPizzaSixteenWord ->
    "16"
    | "sextán"

QPizzaFifteenWord ->
    "15"
    | "fimmtán"

QPizzaFourteenPointFiveWord ->
    QPizzaFourteenWord "komma" QPizzaFiveWord

QPizzaFourteenWord ->
    "14"
    | "fjórtán"

QPizzaFiveWord ->
    "5"
    | "fimm"

QPizzaTwelveWord ->
    "12"
    | "tólf"

QPizzaNineWord ->
    "9"
    | "níu"

QPizzaItalianWord/fall ->
    'ítalskur:lo'/fall

QPizzaClassicWord/fall ->
    'klassískur:lo'/fall

QPizzaCrustWord/fall ->
    'botn:kk'/fall

QPizzaNum/fall →
    # to is a declinable number word ('tveir/tvo/tveim/tveggja')
    # töl is an undeclinable number word ('sautján')
    # tala is a number ('17')
    to | töl | tala

QPizzaPepperoniWord/fall ->
    'pepperóní:hk'/fall
    | "pepperoni"
    | "pepperóni"
    | "pepperoní"

QPizzaOliveWord/fall ->
    'ólífa:kvk'/fall
    | 'ólíva:kvk'/fall

QPizzaMushroomWord/fall ->
    'sveppur:kk'/fall
    | 'Sveppi:kk'
