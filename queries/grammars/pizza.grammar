# TODO: 2x of a topping. "Tvöfalt", "mikið", "extra"
# TODO: Ban more than two instances of a topping.
# TODO: Fix the toppings being a set. Doesn't handle "Ég vil skinku, ólífur og auka skinku."
# TODO: Add to PizzaRequestBare, start conversation with an order.
# TODO: Add the words for margherita to BinPackage
# TODO: Fix bug where "E_QUERY_NOT_UNDERSTOOD" is stored between trees in a single module.
# TODO: Fix composite answer limitation.

/þgf = þgf
/ef = ef

# Currently, the Hotword needs to be top-level, because the QPizza nonterminal is banned.
Query →
    QPizzaHotWord
    | QPizza

QPizza →
    QPizzaQuery '?'?

QPizzaQuery →
    QPizzaDialogue '?'?

# Hotwords are used to initialize the conversation.
QPizzaHotWord →
    QPizzaWord/nf '?'? # e.g. "Pítsa"
    | QPizzaRequestBare '?'? # e.g. "Ég vil panta pizzu."

# Doesn't allow for any order specification, e.g. the number of pizzas: "Ég vil panta tvær flatbökur."
QPizzaRequestBare ->
    QPizzaRequestPleasantries? QPizzaWord/þf
    | "rassgat"

QPizzaDialogue →
    QPizzaPrimeAnswer # Requests specifying a single thing.
    | QPizzaCompositeAnswerWrapper # Request specifying multiple things at once: size, toppings, crust.

QPizzaPrimeAnswer ->
    QPizzaNumberAnswer # Answer to the question "How many pizzas do you want?"
    | QPizzaMenuOrToppingsAnswer # Answer to the question "What do you want to order?"
    | QPizzaSizeAnswer # Answer to the question "What size do you want?"
    | QPizzaCrustAnswer # Answer to the question "What kind of crust do you want?"
    | QPizzaExtrasAnswer # Answer to the question "Do you want anything with your pizzas?"
    | QPizzaCancel # Request to cancel the order.
    | QPizzaStatus # Request for the status of the order.

QPizzaNumberAnswer →
    QPizzaRequestPleasantries? QPizzaNum/þf QPizzaWord/þf # e.g. "Ég vil kaupa tvær pítsur."
    | QPizzaNum/nf QPizzaWord/nf? # e.g. "Tvær pítsur."

QPizzaMenuOrToppingsAnswer ->
    QPizzaMenuAnswer # A request for a pizza off the menu.
    | QPizzaToppingsAnswer # A request for a custom pizza.

QPizzaMenuAnswer ->
    QPizzaRequestPleasantries? QPizzaMenuWords/þf # e.g. "Ég vil kjötveislu."
    | QPizzaMenuWords/nf # e.g. "Margaríta."

QPizzaToppingsAnswer ->
    QPizzaRequestPleasantries? QPizzaToppingsList/þf QPizzaAPitsunaPhrase? # e.g. "Ég vil skinku, ólífur og pepperóní á pítsuna."
    | QPizzaRequestPleasantries? QPizzaWord/þf? QPizzaMedToppings # e.g. "Ég vil pizzu með ólífum og ananas."

# This allows for "Ég vil miðstærð af kjötveislu" to make the composite answer simpler.
QPizzaSizeAnswer ->
    QPizzaRequestPleasantries? QPizzaSizePhrase/þf # e.g. "Ég vil stóra pítsu." or "Ég vil miðstærð af pítsu."
    | QPizzaSizePhrase/nf # e.g. "Tólf tommur."

QPizzaCrustAnswer ->
    QPizzaRequestPleasantries? QPizzaCrustPhrase/þf QPizzaAPitsunaPhrase? # e.g. "Ég vil klassískan botn á pítsuna."
    | QPizzaRequestPleasantries? QPizzaWord/þf QPizzaMedCrust # e.g. "Ég vil pizzu með ítölskum botni."
    | QPizzaCrustPhrase/nf # e.g. "Ítalskur botn."

QPizzaExtrasAnswer ->
    QPizzaRequestPleasantries? QPizzaExtraWords/þf QPizzaMedPitsunni? # e.g. "Ég vil brauðstangir með pizzunum."
    | QPizzaEgVil "líka" QPizzaExtraWords/þf QPizzaMedPitsunni? # e.g. "Ég vil líka kanilgott með pizzunum."
    | QPizzaExtraWords/nf # e.g. "Kók."

QPizzaCancel → 
    "ég" "hætti" "við"
    | QPizzaEgVil? "hætta" "við" 'pöntun:kvk'_et/þf?
    | "hætta" 'pöntun:kvk'_et/þgf?
    | "hættu"

QPizzaStatus →
    "staðan"
    | "hver" "er" "staðan" "á" 'pöntun:kvk'_et/þgf?
    | "hver" "er" "staðan"
    | "segðu" "mér" "stöðuna"
    | "hvernig" "er" "staðan"
    | "hvar" "var" "ég"
    | "hvert" "var" "ég" 'kominn'
    | "hvert" "var" "ég" 'kominn' "í" 'pöntun:kvk'_et/þgf?
    | "hver" "var" "staðan" "á"? 'pöntun:kvk'_et/þgf?
    | QPizzaEgVil? "halda" "áfram" "með" 'pöntun:kvk'_et/þf?

# Wrapper to handle multiple pizza requests at once, "Ég vil kjötveislu með ítölskum botni og stóra margarítu."
QPizzaCompositeAnswerWrapper →
    QPizzaCompositeAnswer QPizzaOgCompositeAnswer*

QPizzaCompositeAnswer ->
    QPizzaRequestPleasantries? QPizzaCompositeRest/þf # e.g. "Ég vil tólf tommu margarítu með ítölskum botni."
    | QPizzaCompositeRest/nf # e.g. "Tólf tommu pítsa með skinku ananas og með ítölskum botni'."

# Currently, "I want two large pizzas is not accepted here."
QPizzaCompositeRest/fall ->
    QPizzaNum/fall? QPizzaSizePhrase/fall QPizzaToppingsCrustPermutation # e.g. "Ég vil stóra pítsu með ólífum og ananas."
    | QPizzaNum/fall? QPizzaMenuWords/fall QPizzaToppingsCrustPermutation? # e.g. "Ég vil tvær margarítur með ítölskum botni."
    | QPizzaNum/fall QPizzaWord/fall QPizzaToppingsCrustPermutation # e.g. "Ég vil tvær margarítur með ítölskum botni."

QPizzaToppingsList/fall ->
    QPizzaToppingsWord/fall QPizzaOgMedToppings/fall*

QPizzaSize/fall ->
    QPizzaSizeLarge/fall
    | QPizzaSizeMedium/fall
    | QPizzaSizeSmall/fall

QPizzaOgCompositeAnswer ->
    "og:st"? QPizzaCompositeAnswer

QPizzaOgMedToppings/fall ->
    'og:st'? 'með:fs'? QPizzaToppingsWord/fall

# It is common to say "miðstærð af pítsu", which is handled separately here.
QPizzaSizePhrase/fall ->
    QPizzaSize/fall QPizzaOrMenuWord/fall?
    | QPizzaMediumWord/fall QPizzaAfPitsuPhrase?

QPizzaCrustPhrase/fall ->
    QPizzaCrustType/fall QPizzaCrustWord/fall?

QPizzaToppingsCrustPermutation ->
    QPizzaMedToppings "og:st"? 'með:fs'? QPizzaCrustPhrase/þgf?
    | QPizzaMedCrust "og:st"? 'með:fs'? QPizzaToppingsList/þgf?

QPizzaAfPitsuPhrase ->
    "af" QPizzaOrMenuWord/þgf

QPizzaAPitsunaPhrase ->
    "á" QPizzaWord/þf

QPizzaMedPitsunni ->
    "með" QPizzaWord/þgf

QPizzaMedToppings ->
    "með" QPizzaToppingsList/þgf

QPizzaMedCrust ->
    "með" QPizzaCrustPhrase/þgf

QPizzaOrMenuWord/fall ->
    QPizzaWord/fall
    | QPizzaMenuWords/fall

# Toppings that are transcribed in different ways are in separate nonterminals for clarity.
# This also helps standardize the handling of each topping in the module, i.e. not reading "ólífa" and "ólíva" as separate toppings.
QPizzaToppingsWord/fall ->
    QPizzaMushroom/fall
    | QPizzaPepperoni/fall
    | 'ananas:kk'/fall
    | 'skinka:kvk'/fall
    | QPizzaOlive/fall

QPizzaMenuWords/fall ->
    'prinsessa:kvk'/fall
    | 'dóttir:kvk'/fall
    | QPizzaMargherita/fall
    | 'kjöt-veisla:kvk'/fall
    | 'hvítlauksbrauð:hk'/fall
    | QPizzaTokyo/fall

QPizzaExtraWords/fall ->
    QPizzaSidesWords/fall
    | QPizzaDrinksWords/fall
    | QPizzaDipsWords/fall

QPizzaSidesWords/fall ->
    QPizzaLargeBreadsticks/fall 
    | QPizzaSmallBreadsticks/fall
    | 'lítill:lo'_kvk_ft/fall 'brauðstöng:kvk'_ft/fall
    | "ostagott"
    | "kanilgott"
    | "súkkulaðigott"
    | 'kartöflubátur:kk'_ft/fall
    | 'vængur:kk'_ft/fall

QPizzaDrinksWords/fall ->
    QPizzaCoke/fall 
    | QPizzaCokeZero/fall
    | "fanta"
    | 'toppur:kk'_et/fall
    | 'sítrónu-toppur:kk'_et/fall
    | 'appelsínu-svali:kk'_et/fall
    | 'epla-svali:kk'_et/fall
    | "monster"

QPizzaDipsWords/fall ->
    'hvítlauks-olía:kvk'_et/fall
    | 'hvítlauks-sósa:kvk'_et/fall
    | 'brauð-stanga-sósa:kvk'_et/fall
    | QPizzaBlueCheese/fall
    | 'sterkur:lo'_kvk_et/fall 'sósa:kvk'_et/fall
    | 'kokteilsósa:kvk'_et/fall
    | "súkkulaðiglassúr"
    | "glassúr"

# A large pizza at Domino's is typically thought to be 16", some believe it to be 15".
# The actual size is 14.5".
QPizzaSizeLarge/fall ->
    'stór:lo'/fall
    | QPizzaSixteenWord 'tomma:kvk'/ef?
    | QPizzaFifteenWord 'tomma:kvk'/ef?
    | QPizzaFourteenPointFiveWord 'tomma:kvk'/ef?

QPizzaSizeMedium/fall ->
    'millistór:lo'/fall
    | 'meðalstór:lo'/fall
    | QPizzaTwelveWord 'tomma:kvk'/ef?

QPizzaMediumWord/fall ->
    'mið-stærð:kvk'/fall

QPizzaSizeSmall/fall ->
    'lítil:lo'/fall
    | QPizzaNineWord 'tomma:kvk'/ef?

QPizzaCrustType/fall ->
    QPizzaItalianWord/fall 
    | QPizzaClassicWord/fall

QPizzaRequestPleasantries ->
    QPizzaEgVil QPizzaKaupaFaraFaPanta?

QPizzaEgVil →
    "ég"? "vil"
    | "ég" "vill"
    | "mig" "langar" "að"
    | "mig" "langar" "í"
    | "ég" "ætla" "að"

QPizzaKaupaFaraFaPanta →
    "kaupa" "mér"?
    | "fá" "mér"?
    | "panta" "mér"?

QPizzaWord/fall →
    'pizza:kvk'/fall
    | 'pitsa:kvk'/fall
    | 'pítsa:kvk'/fall
    | 'flatbaka:kvk'/fall

QPizzaSixteenWord ->
    "16"
    | "sextán"

QPizzaFifteenWord ->
    "15"
    | "fimmtán"

QPizzaFourteenPointFiveWord ->
    QPizzaFourteenWord "komma" QPizzaFiveWord

QPizzaFourteenWord ->
    "14"
    | "fjórtán"

QPizzaFiveWord ->
    "5"
    | "fimm"

QPizzaTwelveWord ->
    "12"
    | "tólf"

QPizzaNineWord ->
    "9"
    | "níu"

QPizzaItalianWord/fall ->
    'ítalskur:lo'/fall

QPizzaClassicWord/fall ->
    'klassískur:lo'/fall

QPizzaCrustWord/fall ->
    'botn:kk'/fall

QPizzaNum/fall →
    # to is a declinable number word ('tveir/tvo/tveim/tveggja')
    # töl is an undeclinable number word ('sautján')
    # tala is a number ('17')
    to | töl | tala

QPizzaPepperoni/fall ->
    'pepperóní:hk'/fall
    | "pepperoni"
    | "pepperóni"
    | "pepperoní"

QPizzaOlive/fall ->
    'ólífa:kvk'/fall
    | 'ólíva:kvk'/fall

QPizzaMushroom/fall ->
    'sveppur:kk'/fall
    | 'Sveppi:kk'

# "Margaríta" is not recognized by bin, so I hack the grammar here to make it work.
QPizzaMargherita_nf ->
    "margaríta"
    | "margarita"

QPizzaMargherita/fallxnf ->
    "margarítu"
    | "margaritu"

QPizzaTokyo/fall ->
    'Tókýó:kvk'/fall
    | "Tókíó"
    | "Tokyo"
    | "tókýó"
    | "tókíó"
    | "tokyo"

QPizzaLargeBreadsticks/fall ->
    'stór:lo'_kvk_ft/fall? 'brauð-stöng:kvk'_ft/fall
    | 'brauð-stöng:kvk'_ft/fall 'stór:lo'_kvk_ft/fall

QPizzaSmallBreadsticks/fall ->
    'lítill:lo'_kvk_ft/fall 'brauð-stöng:kvk'_ft/fall
    | 'brauð-stöng:kvk'_ft/fall 'lítill:lo'_kvk_ft/fall

QPizzaCoke/fall ->
    QPizzaCokeWord/fall

QPizzaCokeZero/fall ->
    QPizzaCokeWord/fall "zero"
    | QPizzaCokeWord/fall "án" 'sykur:kk'_et_ef

QPizzaBlueCheese/fall ->
    'gráðaosta-sósa:kvk'_et/fall
    | 'gráðosta-sósa:kvk'_et/fall
    | 'gráðaostur:kk'_et/fall
    | 'gráðostur:kk'_et/fall

QPizzaCokeWord/fall ->
    'kók:kvk'_et/fall
    | "kóka-kóla"
    | "coke"
    | "coca-cola"