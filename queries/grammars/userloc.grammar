# Grammar for the userloc query module

Query →
    QUserLocation

QUserLocation → QUserLocationQuery '?'?

QUserLocationQuery →
    QUserLocationCurrent | QUserLocationPostcode | QUserLocationCountry

QUserLocationCurrent →
    "hvar" "er" "ég" QULocEiginlega? QULocLocated? QULocInTheWorld? QULocNow?
    | "hvar" QULocInTheWorld "er" "ég" QULocEiginlega? QULocLocated? QULocNow?
    | "hvað" "er" "ég" QULocEiginlega? QULocLocated? QULocInTheWorld? QULocNow?
    | "veistu" "hvar" "ég" "er" QULocEiginlega? QULocInTheWorld? QULocNow?
    | "veist" "þú" "hvar" "ég" "er" QULocEiginlega? QULocInTheWorld? QULocNow?
    | "hver" "er" "staðsetning" "mín"? QULocEiginlega? QULocInTheWorld? QULocNow?
    | "hver" "er" "staðsetningin" "mín"? QULocEiginlega? QULocInTheWorld? QULocNow?
    | "hvar" "erum" "við" QULocEiginlega? QULocLocatedFemAndPlural? QULocInTheWorld? QULocNow?
    | "staðsetning" "mín"? QULocInTheWorld? QULocNow?
    | QULocWhichStreet QULocEiginlega? QULocLocated? QULocInTheWorld? QULocNow?

QUserLocationPostcode →
    "í" "hvaða" "póstnúmeri" "er" "ég" QULocEiginlega? QULocLocated? QULocNow?
    | "hvaða" "póstnúmeri" "er" "ég" QULocEiginlega? QULocLocated? "í" QULocNow?
    | "í" "hvaða" "póstnúmeri" "erum" "við" QULocEiginlega? QULocLocated? QULocNow?
    | "hvaða" "póstnúmeri" "erum" "við" QULocEiginlega? QULocLocated? "í" QULocNow?

QUserLocationCountry →
    QULocPreposition "hvaða" "landi" "er" "ég" QULocLocated? QULocNow?
    | QULocPreposition "hvaða" "landi" "erum" "við" QULocLocated? QULocNow?
    | "hvaða" "landi" "er" "ég" QULocLocated? QULocPreposition QULocNow?
    | "hvaða" "landi" "erum" "við" QULocLocated? QULocPreposition QULocNow?

QULocWhichStreet →
    QULocPreposition "hvaða" "götu" "er" "ég" QULocLocated? QULocNow?
    | QULocPreposition "hvaða" "götu" "erum" "við" QULocLocated? QULocNow?

QULocPreposition →
    "á" | "í"

QULocEiginlega →
    "eiginlega"

QULocLocated →
    QULocLocatedMasc | QULocLocatedFemAndPlural

QULocLocatedMasc →
    "staddur" | "staðsettur" | "niðurkominn" | "niður" "kominn"

QULocLocatedFemAndPlural →
    "stödd" | "staðsett" | "niðurkomin" | "niður" "komin"

QULocInTheWorld →
    "í" "heiminum"
    | "í" "veröldinni"
    | "á" "hnettinum"
    | "á" "jörðinni"
    | "á" "landinu"
    | "á" "Íslandi"
    | "á" "yfirborði" "jarðar"
    | "á" "jarðkringlunni"
    | "á" "plánetunni" "jörð"?

QULocNow →
    "nákvæmlega"? QULocNowGeneric

QULocNowGeneric →
    "nú" | "akkúrat"? "núna" | "eins" "og" "stendur" | "sem" "stendur"
    | "í" "augnablikinu" | "á" "þessari" "stundu" | "hér" "og" "nú"

$score(+35) QUserLocation
