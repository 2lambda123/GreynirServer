
/þf = þf
/þgf = þgf
/ef = ef

Query → QHue
QHue → QHueQuery '?'?
QHueQuery →
    QHueTurnOnLights
    | QHueTurnOffLights
    | QHueIncreaseBrightness
    | QHueDecreaseBrightness
    | QHueChangeColor
    | QHueChangeScene

# Some verbs
QHueKveiktu → "kveiktu" | "kveikja"
QHueSlökktu → "slökktu" | "slökkva"
QHueGerðu → "gerðu" | "gera"
QHueStilltuSettu → "stilltu" | "settu" | "stilla" | "setja"
QHueBreyttuSkiptu → "breyttu" | "skiptu" | "breyta" | "skipta"
QHueLáttu → "láttu" | "láta"
QHueHækkaðuAuktu → "hækkaðu" | "auktu" | "hækka" | "auka"
QHueLækkaðuMinnkaðu → "lækkaðu" | "minnkaðu" | "lækka" | "minnka"

# Commands for turning on lights
QHueTurnOnLights → QHueKveiktu QHueKveiktuX

# Commands for turning off lights
QHueTurnOffLights → QHueSlökktu QHueSlökktuX

QHueMeiri → "meiri" | "meira"

# Commands for increasing light brightness
QHueIncreaseBrightness →
    QHueHækkaðuAuktu QHueHækkaðuX
    | QHueGerðu QHueMeiri QHueBrightnessWord/þf QHueHvar
    | QHueGerðu QHueBrightnessWord/þf QHueMeiri QHueHvar?
    | QHueGerðu QHueBrightnessWord/þf QHueHvar QHueMeiri

QHueMinni → "minni" | "minna"

# Commands for decreasing light brightness
QHueDecreaseBrightness →
    QHueLækkaðuMinnkaðu QHueLækkaðuX
    | QHueGerðu QHueBrightnessWord/þf QHueMinni QHueHvar?
    | QHueGerðu QHueBrightnessWord/þf QHueHvar QHueMinni
    | QHueGerðu QHueMinni QHueBrightnessWord/þf QHueHvar

QHueÁLitinn → "á" "litinn" | "á" "lit"

# Commands for changing the current color
QHueChangeColor →
    QHueGerðu QHueLight/þf QHueHvar? QHueColorName/þf QHueÁLitinn?
    | QHueGerðu QHueLight/þf QHueColorName/þf QHueÁLitinn? QHueHvar
    | QHueLáttu QHueLight/þf QHueHvar? QHueVeraVerða QHueColorName/þf QHueÁLitinn?
    | QHueLáttu QHueLight/þf QHueVeraVerða QHueColorName/þf QHueÁLitinn? QHueHvar
    | QHueStilltuSettu "á" QHueNewColor/þf QHueHvar
    | QHueBreyttuSkiptu "yfir"? "í" QHueNewColor/þf QHueHvar
    | QHueBreyttuSkiptu "litnum" "á" QHueLight/þgf QHueHvar? "í" "litinn"? QHueColorName/þf

# Commands for changing the current scene
QHueChangeScene →
    QHueKveiktu "á" QHueNewScene/þgf QHueHvar?
    | QHueStilltuSettu "á" QHueNewScene/þf
    | QHueBreyttuSkiptu "yfir"? "í" QHueNewScene/þf

QHueKveiktuX →
    QHueLight/þf? QHueHvar    # ... ljósið í eldhúsinu
    | "á" QHueLight/þgf QHueHvar?    # ... á lampanum í stofunni

QHueSlökktuX →
    QHueLight/þf? QHueHvar    # ... ljósið í eldhúsinu
    | "á" QHueLight/þgf QHueHvar?    # ... á lampanum í stofunni

QHueHækkaðuX →
    QHueLjósTegund/þf QHueHvar?
    | QHueBrightnessSubject/þf QHueHvar?

QHueLækkaðuX →
    QHueLight/þf QHueHvar?
    | QHueBrightnessSubject/þf QHueHvar?

QHueGerðuX →
    QHueSubject/þf QHueHvar? QHueHvernigMake
    | QHueSubject/þf QHueHvernigMake QHueHvar?
    | QHueHvar? QHueSubject/þf QHueHvernigMake
    | QHueHvar? QHueHvernigMake QHueSubject/þf
    | QHueHvernigMake QHueSubject/þf QHueHvar?
    | QHueHvernigMake QHueHvar? QHueSubject/þf

# TODO: Add support for "stilltu rauðan lit á ljósið í eldhúsinu"
QHueSettuX →
    QHueSubject/þf QHueHvar? QHueHvernigSet
    | QHueSubject/þf QHueHvernigSet QHueHvar?
    | QHueHvar? QHueSubject/þf QHueHvernigSet
    | QHueHvar? QHueHvernigSet QHueSubject/þf
    | QHueHvernigSet QHueSubject/þf QHueHvar?
    | QHueHvernigSet QHueHvar? QHueSubject/þf

QHueBreyttuX →
    QHueSubjectOne/þgf QHueHvar? QHueHvernigChange
    | QHueSubjectOne/þgf QHueHvernigChange QHueHvar?
    | QHueHvar? QHueSubjectOne/þgf QHueHvernigChange
    | QHueHvar? QHueHvernigChange QHueSubjectOne/þgf
    | QHueHvernigChange QHueSubjectOne/þgf QHueHvar?
    | QHueHvernigChange QHueHvar? QHueSubjectOne/þgf

QHueLáttuX →
    QHueSubject/þf QHueHvar? QHueHvernigLet
    | QHueSubject/þf QHueHvernigLet QHueHvar?
    | QHueHvar? QHueSubject/þf QHueHvernigLet
    # | QHueHvar? QHueHvernigLet QHueSubject/þf
    # | QHueHvernigLet QHueSubject/þf QHueHvar?
    # | QHueHvernigLet QHueHvar? QHueSubject/þf

QHueSubject/fall →
    QHueSubjectOne/fall
    | QHueSubjectTwo/fall

# TODO: Decide whether LightSubject/þgf should be accepted
QHueSubjectOne/fall →
    QHueLight/fall
    | QHueColorSubject/fall
    | QHueBrightnessSubject/fall
    | QHueSceneSubject/fall

QHueSubjectTwo/fall →
    QHueGroupNameSubject/fall # á bara að styðja "gerðu eldhúsið rautt", "gerðu eldhúsið rómó" "gerðu eldhúsið bjartara", t.d.

QHueHvar →
    QHueLocationPreposition QHueGroupName/þgf

QHueHvernigMake →
    QHueAnnaðAndlag # gerðu litinn rauðan í eldhúsinu EÐA gerðu birtuna meiri í eldhúsinu
    | QHueAðHverju # gerðu litinn að rauðum í eldhúsinu
    | QHueÞannigAð

QHueHvernigSet →
    QHueÁHvað
    | QHueÞannigAð

QHueHvernigChange →
    QHueÍHvað
    | QHueÞannigAð

QHueHvernigLet →
    QHueVerða QHueSomethingOrSomehow
    | QHueVera QHueSomehow

QHueÞannigAð →
    "þannig" "að"? pfn_nf QHueBeOrBecomeSubjunctive QHueAnnaðAndlag

# I think these verbs only appear in these forms. 
# In which case these terminals should be deleted and a direct reference should be made in the relevant non-terminals.
QHueVeraVerða →
    QHueVera | QHueVerða

QHueVera →
    "vera"

QHueVerða →
    "verða"

QHueBeOrBecomeSubjunctive →
    "verði"
    | "sé"

QHueColorSubject/fall →
    QHueColorWord/fall QHueLight/ef?
    | QHueColorWord/fall "á" QHueLight/þgf

QHueBrightnessSubject/fall →
    QHueBrightnessWord/fall QHueLight/ef?
    | QHueBrightnessWord/fall "á" QHueLight/þgf

QHueSceneSubject/fall → QHueSceneWord/fall

QHueGroupNameSubject/fall → QHueGroupName/fall

QHueLocationPreposition →
    QHueLocationPrepositionFirstPart? QHueLocationPrepositionSecondPart

# The latter proverbs are grammatically incorrect, but common errors, both in speech and transcription.
# The list provided is taken from StefnuAtv in Greynir.grammar. That includes "aftur:ao", which is not applicable here.
QHueLocationPrepositionFirstPart →
    StaðarAtv
    | "fram:ao"
    | "inn:ao"
    | "niður:ao"
    | "upp:ao"
    | "út:ao"

QHueLocationPrepositionSecondPart →
    "á" | "í"

QHueGroupName/fall →
    no/fall #| QHueBanwords/fall
    # | sérnafn/fall

QHueLightName/fall →
    no/fall #| QHueBanwords/fall
    # | sérnafn/fall

QHueColorName/fall →
    {color_names}

QHueSceneName →
    no | lo #| QHueBanwords/fall
    # | sérnafn/fall

QHueAnnaðAndlag →
    QHueNewSetting/nf
    | QHueSpyrjaHuldu/nf

QHueAðHverju →
    "að" QHueNewSetting/þgf

QHueÁHvað →
    "á" QHueNewSetting/þf

QHueÍHvað →
    "í" QHueNewSetting/þf

QHueÁHverju →
    "á" QHueLight/þgf
    # | "á" QHueNewSetting/þgf

QHueSomethingOrSomehow →
    QHueAnnaðAndlag
    | QHueAðHverju

QHueSomehow →
    QHueAnnaðAndlag
    | QHueÞannigAð

QHueLight/fall →
    QHueLjósTegund/fall
    | QHueLightName/fall

QHueLjósTegund/fall →
    'ljós'/fall
    | 'lampi'/fall
    # | 'útiljós'/fall
    # | 'leslampi'/fall
    # | 'borðlampi'/fall
    # | 'gólflampi'/fall
    # | 'lýsing'/fall
    # | 'birta'/fall
    # | 'Birta'/fall

QHueColorWord/fall →
    'litur'/fall
    | 'litblær'/fall
    | 'blær'/fall

QHueBrightnessWords/fall →
    'bjartur'/fall
    | QHueBrightnessWord/fall

QHueBrightnessWord/fall →
    'birta:kvk'/fall
    | 'Birta'/fall
    | 'birtustig'/fall

QHueSceneWord/fall →
    'sena'/fall
    | 'stemning'/fall
    | 'stemming'/fall
    | 'stemmning'/fall
    # | 'sina'/fall
    # | 'Sena'/fall
    # | "Sena"

# Need to ask Hulda how this works.
QHueSpyrjaHuldu/fall →
    # QHueHuldaColor/fall
    QHueHuldaBrightness/fall
    # | QHueHuldaScene/fall
    | QHueHuldaTemp/fall

# Do I need a "new light state" non-terminal?
QHueNewSetting/fall →
    QHueNewColor/fall
    | QHueNewBrightness/fall
    | QHueNewScene/fall

# TODO: Missing "meira dimmt"
QHueHuldaBrightness/fall →
    QHueMoreBrighterOrHigher/fall QHueBrightnessWords/fall?
    | QHueLessDarkerOrLower/fall QHueBrightnessWords/fall?

QHueHuldaTemp/fall →
    QHueWarmer/fall
    | QHueCooler/fall 

# TODO: Unsure about whether to include /fall after QHueColorName
QHueNewColor/fall →
    QHueColorWord/fall QHueColorName/fall
    | QHueColorName/fall QHueColorWord/fall?

QHueNewBrightness/fall →
    'sá'/fall? QHueBrightestOrDarkest/fall
    | QHueBrightestOrDarkest/fall QHueBrightnessOrSettingWord/fall

QHueNewScene/fall →
    QHueSceneWord/fall QHueSceneName
    | QHueSceneName QHueSceneWord/fall?

QHueMoreBrighterOrHigher/fall →
    'mikill:lo'_mst/fall
    | 'bjartur:lo'_mst/fall
    | 'ljós:lo'_mst/fall
    | 'hár:lo'_mst/fall

QHueLessDarkerOrLower/fall →
    'lítill:lo'_mst/fall
    | 'dökkur:lo'_mst/fall
    | 'dimmur:lo'_mst/fall
    | 'lágur:lo'_mst/fall

QHueWarmer/fall →
    'hlýr:lo'_mst/fall
    | 'heitur:lo'_mst/fall
    | "hlýri"

QHueCooler/fall →
    'kaldur:lo'_mst/fall

QHueBrightestOrDarkest/fall →
    QHueBrightest/fall
    | QHueDarkest/fall

QHueBrightest/fall →
    'bjartur:lo'_evb
    | 'bjartur:lo'_esb
    | 'ljós:lo'_evb
    | 'ljós:lo'_esb

QHueDarkest/fall →
    'dimmur:lo'_evb
    | 'dimmur:lo'_esb
    | 'dökkur:lo'_evb
    | 'dökkur:lo'_esb

QHueBrightnessOrSettingWord/fall →
    QHueBrightnessWord/fall
    | QHueStilling/fall

QHueStilling/fall →
    'stilling'/fall

# Catching hotwords from iot_speakers
# QHueBanwords/fall →
#     QIoTSpeakerHotwords/fall

# $tag(keep) QHueBanwords/fall
# $tag(module_banwords) QHueBanwords/fall
