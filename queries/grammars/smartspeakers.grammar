# TODO: Fix music hardcoding
# TODO: Improve skip queries
# TODO: Check whether to force determinate form


Query → QIoTSpeaker '?'?
QIoTSpeaker → QPleasantries? QIoTSpeakerQuery QForMe?

QPleasantries → "værirðu" "til" "í" "að" | "gætirðu" "nokkuð"? | "vinsamlegast"
QForMe → "fyrir" "mig"

QIoTSpeakerQuery →
    QIoTSpeakerMakeVerb QIoTSpeakerMakeRest
    | QIoTSpeakerSetVerb QIoTSpeakerSetRest
    | QIoTSpeakerLetVerb QIoTSpeakerLetRest
    | QIoTSpeakerTurnOnOrOffVerb QIoTSpeakerTurnOrOffOnRest
    | QIoTSpeakerPlayOrPauseVerb QIoTSpeakerPlayOrPauseRest
    | QIoTSpeakerIncreaseOrDecreaseVerb QIoTSpeakerIncreaseOrDecreaseRest
    | QIoTSpeakerSkipVerb
    | QIoTSpeakerNewSetting/fall

QIoTSpeakerMakeVerb → "gerðu" | "gera"
QIoTSpeakerSetVerb → "settu" | "stilltu" | "láttu" 
QIoTSpeakerChangeVerb → "breyttu"
QIoTSpeakerLetVerb → "láttu"

QIoTSpeakerTurnOnOrOffVerb →
    QIoTSpeakerTurnOnVerb
    | QIoTSpeakerTurnOffVerb

QIoTSpeakerTurnOnVerb → "kveiktu" "aftur"? | "kveikja" "aftur"?

QIoTSpeakerTurnOffVerb → "slökktu" | "slökkva"

QIoTSpeakerPlayOrPauseVerb →
    QIoTSpeakerPlayVerb
    | QIoTSpeakerPauseVerb

QIoTSpeakerPlayVerb → "spilaðu" "áfram"? | "spila" "áfram"?

# "pásaðu" is not recognized by BÍN, but is relatively common in casual speech.
QIoTSpeakerPauseVerb →
    "stöðvaðu"
    | "stöðva"
    | "stoppaðu"
    | "stofnaðu"
    | "stoppa" "í"?
    | "pásaðu"
    | "pása"
    | "gerðu" "hlé" "á"

QIoTSpeakerIncreaseOrDecreaseVerb →
    QIoTSpeakerIncreaseVerb
    | QIoTSpeakerDecreaseVerb

QIoTSpeakerIncreaseVerb → "hækkaðu" | "auktu"

QIoTSpeakerDecreaseVerb → "lækkaðu" | "minnkaðu"

QIoTSpeakerSkipVerb →
    "skippaðu"
    | "skippa"
    | "skipaðu"
    | "næsta" "lag"
    | "ég" "vil" "næsta" "lag"
    | "settu" "á" "næsta" "lag"
    | "skiptu" "yfir" "í" "næsta" "lag"
    | "skipt" "yfir" "í" "næsta" "lag"

QIoTSpeakerMakeRest →
    QIoTSpeakerMusicOrSoundPhrase_þf QIoTSpeakerComparative_nf QIoTSpeakerHvar
    | QIoTSpeakerMusicOrSoundPhrase_þf QIoTSpeakerHvar? QIoTSpeakerComparative_nf
    | QIoTSpeakerMusicWord_þf QIoTSpeakerComparative_nf QIoTSpeakerHvarWithSpeaker
    | QIoTSpeakerSoundWord_þf QIoTSpeakerComparative_nf QIoTSpeakerHvarWithAppliance

QIoTSpeakerSetRest →
    QIoTSpeakerApplianceWord_þf? QIoTSpeakerAHvad QIoTSpeakerHvar
    | QIoTSpeakerApplianceWord_þf? QIoTSpeakerHvar? QIoTSpeakerAHvad

QIoTSpeakerLetRest →
    QIoTSpeakerBeOrBecome QIoTSpeakerMusicWord_nf QIoTSpeakerHvarWithSpeaker?
    | QIoTSpeakerMusicOrSoundPhrase_þf QIoTSpeakerHvar QIoTSpeakerBeOrBecome QIoTSpeakerComparative_nf
    | QIoTSpeakerMusicOrSoundPhrase_þf QIoTSpeakerBeOrBecome QIoTSpeakerComparative_nf QIoTSpeakerHvar?
    | QIoTSpeakerMusicWord_þf QIoTSpeakerBeOrBecome QIoTSpeakerComparative_nf QIoTSpeakerHvarWithSpeaker
    | QIoTSpeakerSoundWord_þf QIoTSpeakerBeOrBecome QIoTSpeakerComparative_nf QIoTSpeakerHvarWithAppliance

QIoTSpeakerTurnOrOffOnRest →
    QIoTSpeakerAHverju QIoTSpeakerHvarWithSpeaker?

QIoTSpeakerPlayOrPauseRest →
    QIoTSpeakerMusicWord_þf QIoTSpeakerHvarWithSpeaker?
    | "útvarpið" QIoTSpeakerHvarWithSpeaker?
    | "afspilun" QIoTSpeakerHvarWithSpeaker?
    | QIoTSpeakerNewRadio_þf QIoTSpeakerHvarWithSpeaker?
    | QIoTSpeakerNewNextOrPrevious_þf QIoTSpeakerHvarWithSpeaker?

QIoTSpeakerIncreaseOrDecreaseRest →
    QIoTSpeakerMusicOrSoundPhrase_þf QIoTSpeakerHvar?
    | "í" QIoTSpeakerMusicOrApplianceWord_þgf QIoTSpeakerHvar?
    | "í" QIoTSpeakerRadioStationName QIoTSpeakerHvar?

QIoTSpeakerComparative/fall →
    QIoTSpeakerMoreOrHigher/fall
    | QIoTSpeakerLessOrLower/fall

# Sometimes "á" is not registered by Embla
QIoTSpeakerAHvad →
    "yfir"? "á"? QIoTSpeakerMusicOrRadioWord_þf
    | "yfir"? "á"? QIoTSpeakerNewSetting_þf

QIoTSpeakerAHverju →
    "á" QIoTSpeakerMusicOrApplianceWord_þgf
    | "á" QIoTSpeakerNewRadio_þgf
    | "á" QIoTSpeakerMusicOrRadioWord_þgf
    | "á" QIoTSpeakerNewNextOrPrevious/fall

QIoTSpeakerNewSetting/fall →
    QIoTSpeakerNewRadio/fall
    | QIoTSpeakerNewPlayOrPause/fall
    | QIoTSpeakerNewNextOrPrevious/fall

QIoTSpeakerNewRadio/fall →
    QIoTSpeakerRadioStationWord/fall? QIoTSpeakerRadioStationName

QIoTSpeakerNewPlayOrPause/fall →
    QIoTSpeakerNewPlay/fall
    | QIoTSpeakerNewPause/fall

QIoTSpeakerNewPlay/fall →
    "play"
    | "plei"
    | "pley"

QIoTSpeakerNewPause/fall →
    'pása'/fall
    | 'stopp'/fall

QIoTSpeakerNewNextOrPrevious/fall →
    QIoTSpeakerNewNext/fall
    | QIoTSpeakerNewPrevious/fall

QIoTSpeakerNewNext/fall →
    'næstur:lo'_kvk/fall 'lag:hk'_et/fall
    | 'lag:hk'_et_gr/fall "á" "eftir"
    # | "næsta" "lagið"

QIoTSpeakerNewPrevious/fall →
    'seinastur:lo'_kvk/fall 'lag:hk'_et/fall
    | 'síðastur:lo'_kvk/fall 'lag:hk'_et/fall
    | 'fyrri:lo'_kvk/fall 'lag:hk'_et/fall
    | 'lag:hk'_et_gr/fall "á" "undan"

QIoTSpeakerMusicOrSoundPhrase/fall →
    QIoTSpeakerMusicPhrase/fall
    | QIoTSpeakerSoundPhrase/fall

QIoTSpeakerMusicPhrase/fall →
    QIoTSpeakerMusicWord/fall QIoTSpeakerApplianceWord_ef?
    | QIoTSpeakerMusicWord/fall "í" QIoTSpeakerApplianceWord_þgf

QIoTSpeakerSoundPhrase/fall →
    QIoTSpeakerSoundWord/fall? QIoTSpeakerMusicOrApplianceWord_ef?
    | QIoTSpeakerSoundWord/fall "í" QIoTSpeakerMusicOrApplianceWord_þgf?

QIoTSpeakerSoundWord/fall →
    QIoTSpeakerVolumeWord/fall
    | QIoTSpeakerNoiseWord/fall

QIoTSpeakerMusicOrApplianceWord/fall →
    QIoTSpeakerMusicWord/fall
    | QIoTSpeakerApplianceWord/fall

QIoTSpeakerRadioStationWord/fall →
    'útvarpsstöð'/fall | 'stöð:kvk'/fall

QIoTSpeakerMusicWord/fall →
    'tónlist'/fall

QIoTSpeakerVolumeWord/fall →
    'hljóð'/fall
    | 'hljóðstyrkur'/fall
    | 'ljóð'/fall

QIoTSpeakerNoiseWord/fall →
    'læti'/fall
    | 'hávaði'/fall

QIoTSpeakerApplianceWord/fall →
    QIoTSpeakerSpeakerWord/fall
    | QIoTSpeakerRadioWord/fall

QIoTSpeakerMusicOrRadioWord/fall →
    QIoTSpeakerMusicWord/fall
    | QIoTSpeakerRadioWord/fall

QIoTSpeakerSpeakerWord/fall →
    'hátalari'/fall

QIoTSpeakerRadioWord/fall →
    'útvarp'/fall

QIoTSpeakerMoreOrHigher/fall →
    'mikill:lo'_mst/fall
    | 'hár:lo'_mst/fall

QIoTSpeakerLessOrLower/fall →
    'lítill:lo'_mst/fall
    | 'lágur:lo'_mst/fall

QIoTSpeakerHvarWithAppliance →
    "í" QIoTSpeakerApplianceWord_þgf QIoTSpeakerHvar
    > QIoTSpeakerHvar

QIoTSpeakerHvarWithSpeaker →
    "í" QIoTSpeakerSpeakerWord_þgf QIoTSpeakerHvar
    > QIoTSpeakerHvar

QIoTSpeakerHvar →
    QIoTSpeakerLocationPreposition QIoTSpeakerGroupName_þgf
    > QIoTSpeakerAllGroups

QIoTSpeakerLocationPreposition →
    QIoTSpeakerLocationPrepositionFirstPart? QIoTSpeakerLocationPrepositionSecondPart

# The latter proverbs are grammatically incorrect, but common errors, both in speech and transcription.
# The list provided is taken from StefnuAtv in Greynir.grammar. That includes "aftur:ao", which is not applicable here.
QIoTSpeakerLocationPrepositionFirstPart →
    StaðarAtv
    | "fram:ao"
    | "inn:ao"
    | "niður:ao"
    | "upp:ao"
    | "út:ao"

QIoTSpeakerLocationPrepositionSecondPart →
    "á" | "í"

QIoTSpeakerGroupName/fall →
    no/fall
    | sérnafn/fall

QIoTSpeakerAllGroups →
    QIoTSpeakerLocationPreposition QIoTSpeakerHouse_þgf
    | QIoTSpeakerEverywhere 

QIoTSpeakerHouse/fall →
    'allur:fn'_et_hk/fall? 'hús'_et/fall

QIoTSpeakerEverywhere →
    "alls_staðar"
    | "alstaðar"
    | "allstaðar"
    | "allsstaðar"
    | "alsstaðar"

QIoTSpeakerBeOrBecome →
    QIoTSpeakerBe
    | QIoTSpeakerBecome

QIoTSpeakerBe → "vera"

QIoTSpeakerBecome → "verða"

QIoTSpeakerRadioStationName →
    QIoTSpeakerBylgjan
    | QIoTSpeakerGullbylgjan
    | QIoTSpeakerLettbylgjan
    | QIoTSpeakerIslenskaBylgjan
    | QIoTSpeaker80sBylgjan
    | QIoTSpeakerRas1
    | QIoTSpeakerRas2
    | QIoTSpeakerRondo
    | QIoTSpeakerUtvarpSaga
    | QIoTSpeakerXid
    | QIoTSpeakerFm957
    | QIoTSpeakerK100
    | QIoTSpeakerRetro
    | QIoTSpeakerKissFm
    | QIoTSpeakerUtvarp101
    | QIoTSpeakerApparatid
    | QIoTSpeakerFmExtra
    | QIoTSpeakerUtvarpSudurland
    | QIoTSpeakerFlashbackWord
    | QIoTSpeaker70sFlashback
    | QIoTSpeaker80sFlashback
    | QIoTSpeaker90sFlashback

QIoTSpeakerBylgjan →
    "Bylgjan"
    | "Bylgjuna"
    | "Bylgjunni"
    | "Bylgjunnar"
    | "bylgjan"
    | "bylgjuna"
    | "bylgjunni"
    | "bylgjunnar"

QIoTSpeakerUtvarpSaga →
    "Útvarp" "Saga"
    | "Útvarp" "Sögu"
    | "Útvarpi" "Sögu"
    | "Útvarp" "Sögu"
    | "Útvarps" "Sögu"
    | "útvarp" "saga"
    | "útvarp" "sögu"
    | "útvarpi" "sögu"
    | "útvarp" "sögu"
    | "útvarps" "sögu"
    | "útvarpssaga"
    | "útvarpssögu"

QIoTSpeakerGullbylgjan →
    "Gullbylgjan"
    | "Gullbylgjuna"
    | "Gullbylgjunni"
    | "Gullbylgjunnar"
    | "gullbylgjan"
    | "gullbylgjuna"
    | "gullbylgjunni"
    | "gullbylgjunnar"
    | "gull" "bylgjan"
    | "gull" "bylgjuna"
    | "gull" "bylgjunni"
    | "gull" "bylgjunnar"

QIoTSpeakerLettbylgjan →
    "Léttbylgjan"
    | "Léttbylgjuna"
    | "Léttbylgjunni"
    | "Léttbylgjunnar"
    | "léttbylgjan"
    | "léttbylgjuna"
    | "léttbylgjunni"
    | "léttbylgjunnar"
    | "létt" "bylgjan"
    | "létt" "bylgjuna"
    | "létt" "bylgjunni"
    | "létt" "bylgjunnar"

QIoTSpeakerXid →
    "X-ið" "977"?
    | "X-inu" "977"?
    | "X-ins" "977"?
    | "x-ið" "977"?
    | "x-inu" "977"?
    | "x-ins" "977"?
    | "x" "977"
    | "x977"
    | "x" "níu" "sjö" "sjö"
    | "x-977"

QIoTSpeakerRas1 →
    "rás" "1"
    | "rás" "eitt"

QIoTSpeakerRas2 →
    "rás" "2"
    | "rás" "tvö"

QIoTSpeakerRondo →
    "rondo" "fm"?
    | "rondó" "fm"?
    | "róndó" "fm"?
    | "London"

QIoTSpeakerFm957 →
    "fm" "957"
    | "fm957"

QIoTSpeakerK100 →
    "k" "100"
    | "k" "hundrað"
    | "k100"
    | "k-hundrað"
    | "k-100"
    | "kk" "hundrað"
    | "kk" "100"

QIoTSpeakerRetro →
    "retro" "fm"?
    | "retró" "fm"?

QIoTSpeakerKissFm →
    "kiss" "fm"?

QIoTSpeakerFlashback →
    "flassbakk" "fm"?
    | "flass" "bakk" "fm"?

QIoTSpeakerUtvarp101 →
    "útvarp"? "101"
    | "útvarp"? "hundrað" "og" "einn"
    | "útvarp"? "hundrað" "og" "eitt"
    | "útvarp"? "hundrað" "einn"
    | "útvarp"? "hundrað" "1"

QIoTSpeaker80sBylgjan →
    QIoTSpeaker80s "Bylgjan"
    | QIoTSpeaker80s "Bylgjuna"
    | QIoTSpeaker80s "Bylgjunni"
    | QIoTSpeaker80s "Bylgjunnar"
    | QIoTSpeaker80s "bylgjan"
    | QIoTSpeaker80s "bylgjuna"
    | QIoTSpeaker80s "bylgjunni"
    | QIoTSpeaker80s "bylgjunnar"

QIoTSpeakerIslenskaBylgjan →
    "íslenska" "Bylgjan"
    | "íslensku" "Bylgjuna"
    | "íslensku" "Bylgjunni"
    | "íslensku" "Bylgjunnar"
    | "íslenska" "bylgjan"
    | "íslensku" "bylgjuna"
    | "íslensku" "bylgjunni"
    | "íslensku" "bylgjunnar"

QIoTSpeakerApparatid → "apparatið"

QIoTSpeakerFmExtra → "fm" "extra"

QIoTSpeakerUtvarpSudurland →
    "útvarp"? "suðurland"
    | "útvarp"? "suðurland"
    | "útvarps"? "suðurlands"

QIoTSpeaker70sFlashback →
    QIoTSpeaker70s QIoTSpeakerFlashbackWord

QIoTSpeaker80sFlashback →
    QIoTSpeaker80s QIoTSpeakerFlashbackWord

QIoTSpeaker90sFlashback →
    QIoTSpeaker90s QIoTSpeakerFlashbackWord

QIoTSpeakerFlashbackWord →
    "flassbakk"
    | "flass" "bakk"
    | "bakk"
    | "flaska"

QIoTSpeaker70s →
    "seventís"
    | "seventies"

QIoTSpeaker80s →
    "eitís"
    | "Eydís"
    | "eydís"
    | "Eidís"
    | "eidís"
    | "eighties"

QIoTSpeaker90s →
    "næntís"
    | "nineties"

# TODO: Fix the song hotword
# Hotwords to block other similarly phrased query modules from catching queries belonging to iot_speakers
# QIoTSpeakerHotwords/fall →
#     QIoTSpeakerMusicOrApplianceWord/fall
#     | QIoTSpeakerSoundWord/fall
#     | QIoTSpeakerRadioStationName
#     | 'lag:no'
#     | QIoTSpeakerNewSetting/fall