# ----------------------------------------------
#
# Query grammar for bus-related queries
#
# ----------------------------------------------

/þfþgf = þf þgf

# A plug-in query grammar always starts with the following,
# adding one or more query productions to the Query nonterminal

Query →
    QBusArrivalTime | QBusAnyArrivalTime | QBusNearestStop | QBusWhich

# By convention, names of nonterminals in query grammars should
# start with an uppercase Q

QBusNearestStop →

    "hvaða" QBusStop_kvk QBusVarEr QBusStopTail_kvk '?'?
    | "hvaða" QBusStop_hk QBusVarEr QBusStopTail_hk '?'?

    | "hvar" "er" "næsta"? QBusStop_kvk '?'?
    | "hver" "er" "næsta" QBusStop_kvk '?'?
    # Leyfa 'hvað er næsta stoppistöð' (algeng misheyrn)
    | "hvað" "er" "næsta" QBusStop_kvk '?'?

    | "hvar" "er" "næsta"? QBusStop_hk '?'?
    | "hvert" "er" "næsta" QBusStop_hk '?'?
    | "hvað" "er" "næsta" QBusStop_hk '?'?
    # Leyfa 'hver er næsta strætóstopp' (algeng misheyrn)
    | "hver" "er" "næsta" QBusStop_hk '?'?

    | "hvar" "stoppar" "strætó" '?'?

$score(+32) QBusNearestStop

QBusVarEr → "er" | "var"

QBusStop_kvk →
    "stoppistöð" "strætó"? | "stoppustöð" "strætó"? | "stoppustuð" "strætó"?
    | "biðstöð" "strætó"? | "strætóstöð"
    | "strætóstoppistöð" | "strætóstoppustöð"

QBusStop_hk →
    "strætóstopp" | "stopp" | "strætóskýli"

QBusStopTail →
    "næst" "mér"? | "styst" "í" "burtu" | "nálægt" "mér"?

QBusStopTail_kvk →
    QBusStopTail | "nálægust"

QBusStopTail_hk →
    QBusStopTail | "nálægast"

QBusNoun/fall/tala →
    'strætó:kk'/tala/fall
    | 'leið:kvk'/tala/fall
    | 'vagn:kk'/tala/fall
    | 'strætisvagn:kk'/tala/fall
    | "strædo" | "stræto"

# Hack to also match Vagn as a person name
# (the lemma terminal 'vagn:kk', used above, does not match person names)

QBusNoun_nf_et → 'Vagn'_nf_kk
QBusNoun_þf_et → 'Vagn'_þf_kk
QBusNoun_þgf_et → 'Vagn'_þgf_kk
QBusNoun_ef_et → 'Vagn'_ef_kk

$tag(keep) QBusNoun/fall/tala

QBusNounSingular_nf →
    QBusNoun_nf_et

QBusNounSingular_þf →
    QBusNoun_þf_et

QBusNounSingular_þgf →
    QBusNoun_þgf_et

QBusNounSingular_ef →
    QBusNoun_ef_et

QBusWhich →
    # 'Hvaða strætó stoppar þar/í Einarsnesi'?
    # 'Hvaða strætisvagnar stoppa þar/á Lækjartorgi'?
    "hvaða" QBusNoun_nf/tala QBusWhichTail/tala '?'?

$score(+32) QBusWhich

QBusWhichTail/tala →
    QBusWhichTailCorrect/tala
    | QBusWhichTailIncorrect/tala

QBusWhichTailCorrect/tala →
    'stoppa:so'_p3_gm_fh_nt/tala "í" QBusStopName_þgf
    | 'stoppa:so'_p3_gm_fh_nt/tala "á" QBusStopName_þgf
    | 'stoppa:so'_p3_gm_fh_nt/tala QBusStopThere

    | 'stöðva:so'_p3_gm_fh_nt/tala "í" QBusStopName_þgf
    | 'stöðva:so'_p3_gm_fh_nt/tala "á" QBusStopName_þgf
    | 'stöðva:so'_p3_gm_fh_nt/tala QBusStopThere

    | 'aka:so'_p3_gm_fh_nt/tala QBusAtStopCorrect_þf
    | 'aka:so'_p3_gm_fh_nt/tala QBusStopToThere

    | 'koma:so'_p3_gm_fh_nt/tala "á" QBusStopName_þf
    | 'koma:so'_p3_gm_fh_nt/tala "í" QBusStopName_þf
    | 'koma:so'_p3_gm_fh_nt/tala "til" QBusStopName_ef
    | 'koma:so'_p3_gm_fh_nt/tala QBusStopToThere

    | 'fara:so'_p3_gm_fh_nt/tala QBusAtStopCorrect_þf
    | 'fara:so'_p3_gm_fh_nt/tala QBusStopToThere

# It seems to be necessary to allow the nominal case
# also, because the Google ASR language model doesn't always
# include all cases for road names (such as 'Fríkirkjuveg')

QBusWhichTailIncorrect/tala →
    'stoppa:so'_p3_gm_fh_nt/tala "í" QBusStopName_nf
    | 'stoppa:so'_p3_gm_fh_nt/tala "á" QBusStopName_nf
    | 'stöðva:so'_p3_gm_fh_nt/tala "í" QBusStopName_nf
    | 'stöðva:so'_p3_gm_fh_nt/tala "á" QBusStopName_nf
    | 'aka:so'_p3_gm_fh_nt/tala "í" QBusStopName_nf
    | 'aka:so'_p3_gm_fh_nt/tala "á" QBusStopName_nf
    | 'aka:so'_p3_gm_fh_nt/tala "til" QBusStopName_nf
    | 'aka:so'_p3_gm_fh_nt/tala "frá" QBusStopName_nf
    | 'koma:so'_p3_gm_fh_nt/tala "á" QBusStopName_nf
    | 'koma:so'_p3_gm_fh_nt/tala "í" QBusStopName_nf
    | 'koma:so'_p3_gm_fh_nt/tala "til" QBusStopName_nf
    | 'fara:so'_p3_gm_fh_nt/tala "í" QBusStopName_nf
    | 'fara:so'_p3_gm_fh_nt/tala "á" QBusStopName_nf
    | 'fara:so'_p3_gm_fh_nt/tala "frá" QBusStopName_nf
    | 'fara:so'_p3_gm_fh_nt/tala "til" QBusStopName_nf

# Prefer the correct forms
$score(-20) QBusWhichTailIncorrect/tala

QBusStopThere →
    "þar"

QBusStopToThere →
    "þangað"

QBusStopName/fall →
    # A bus stop name can consist of two noun phrases,
    # such as 'Þórunnarstræti sjúkrahús'
    Nl/fall Nl/fall?

$score(+1) QBusStopName/fall

# Bus stops with prepositions denoting movement, taking an accusative argument:
# '[kemur] á Hlemm / þangað'
QBusAtStop_þf →
    QBusAtStopCorrect_þf | QBusAtStopIncorrect_þf | QBusStopToThere

# Bus stops with prepositions denoting placement, taking a dative argument:
# '[stoppar] á Hlemmi / þar'
QBusAtStop_þgf →
    QBusAtStopCorrect_þgf | QBusAtStopIncorrect_þgf | QBusStopThere

# Movement prepositions
QBusAtStopCorrect_þf →
    "í" QBusStopName_þf
    | "á" QBusStopName_þf
    | "frá" QBusStopName_þgf
    | "að" QBusStopName_þgf
    | "til" QBusStopName_ef

# Placement prepositions
QBusAtStopCorrect_þgf →
    "í" QBusStopName_þgf
    | "á" QBusStopName_þgf
    | "við" QBusStopName_þf
    | "hjá" QBusStopName_þgf
    | "frá" QBusStopName_þgf
    | "að" QBusStopName_þgf
    | "til" QBusStopName_ef

QBusAtStopIncorrect_þf →
    "í" QBusStopName_nf
    | "í" QBusStopName_þgf
    | "á" QBusStopName_nf
    | "á" QBusStopName_þgf
    | "frá" QBusStopName_nf
    | "til" QBusStopName_nf

QBusAtStopIncorrect_þgf →
    "í" QBusStopName_nf
    | "í" QBusStopName_þf
    | "á" QBusStopName_nf
    | "á" QBusStopName_þf
    | "frá" QBusStopName_nf
    | "til" QBusStopName_nf

# Prefer the correct forms
$score(-20) QBusAtStopIncorrect/þfþgf

QBusWhen → "hvenær" | "klukkan" "hvað"

QBusArrivalTime →

    # 'Hvenær kemur/fer/stoppar ásinn/sexan/tían/strætó númer tvö [næst] [á Hlemmi]?'
    QBusWhen QBusArrivalVerb/þfþgf QBus_nf "næst"? QBusAtStop/þfþgf? '?'?

    # 'Hvenær er [næst] von á fimmunni / vagni númer sex?'
    | QBusWhen "er" "næst"? "von" "á" QBus_þgf QBusAtStop_þf? '?'?

    # 'Hvenær má [næst] búast við leið þrettán?'
    | QBusWhen "má" "næst"? "búast" "við" QBus_þgf QBusAtStop_þf? '?'?

QBusAnyArrivalTime →
    # 'Hvenær kemur/fer/stoppar [næsti] strætó [á Hlemmi]?'
    QBusWhen QBusArrivalVerb/þfþgf "næsti"? QBusNounSingular_nf QBusAtStop/þfþgf? '?'?
    # 'Hvað er langt í [næsta] strætó [á Hlemm / á Hlemmi]?'
    | "hvað" "er" "langt" "í" "næsta"? QBusNounSingular_þf QBusAtStop/þfþgf? '?'?
    # 'Hvenær er von á [næsta] strætó [á Hlemm]?'
    | QBusWhen "er" "von" "á" "næsta"? QBusNounSingular_þgf QBusAtStop_þf? '?'?

QBusArrivalVerb → QBusArrivalVerb/þfþgf

# Movement: verbs control prepositions in accusative case
QBusArrivalVerb_þf → "kemur" | "fer"
# Placement: verbs control prepositions in dative case
QBusArrivalVerb_þgf → "stoppar" | "stöðvar"

$score(+32) QBusArrivalTime
$score(+16) QBusAnyArrivalTime

# We can specify a bus in different ways, which may require
# the bus identifier to be in different cases

QBus/fall →
    QBusWord/fall | QBusNumber/fall

QBusWord/fall →
    'ás:kk'_et_gr/fall
    | 'tvistur:kk'_et_gr/fall
    | 'þristur:kk'_et_gr/fall
    | 'fjarki:kk'_et_gr/fall
    | 'fimma:kvk'_et_gr/fall
    | 'sexa:kvk'_et_gr/fall
    | 'sjöa:kvk'_et_gr/fall
    | 'átta:kvk'_et_gr/fall
    | 'nía:kvk'_et_gr/fall
    | 'tía:kvk'_et_gr/fall
    | 'ellefa:kvk'_et_gr/fall
    | 'tólfa:kvk'_et_gr/fall

QBusNumber/fall →
    QBusNounSingular/fall QBusNr? QBusNumberWord

QBusNr → 'númer:hk'_et_nf | "nr"

QBusNumberWord →

    # to is a declinable number word ('tveir/tvo/tveim/tveggja')
    # töl is an undeclinable number word ('sautján')
    # tala is a number ('17')
    "eitt" | to_nf_ft_hk | töl | tala
